# Contributions from Arch:
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: hydro <hydro@freenet.de>

pkgname=libzen
pkgver=0.4.37
pkgrel=1
pkgdesc="shared library for libmediainfo and mediainfo"
arch=('x86_64')
url="https://mediaarea.net"
license=('BSD')
depends=('gcc-libs')
makedepends=('libtool' 'automake' 'autoconf')
source=(https://mediaarea.net/download/source/libzen/${pkgver}/${pkgname}_${pkgver}.tar.bz2)
sha256sums=('d6e9b7084bbb828536b47698e0c60e381cf0cefc4a8d712bb81dac826ccd9ac1')

build() {
  cd "$srcdir"/ZenLib/Project/GNU/Library
  [ -x configure ] || sh ./autogen.sh
  [ -f Makefile ] || ./configure --prefix=/usr --enable-shared
  make clean
  make
}

package() {
  cd "$srcdir"/ZenLib/Project/GNU/Library
  make DESTDIR="$pkgdir" install
  install -dm 755 "$pkgdir"/usr/include/ZenLib
  install -m 644 "$srcdir"/ZenLib/Source/ZenLib/*.h "$pkgdir"/usr/include/ZenLib
  for i in HTTP_Client Format/Html Format/Http; do
    install -dm0755 "$pkgdir"/usr/include/ZenLib/$i
    install -m0644 "$srcdir"/ZenLib/Source/ZenLib/$i/*.h "$pkgdir"/usr/include/ZenLib/$i
  done
  install -dm 755 "$pkgdir"/usr/lib/pkgconfig
  install -m 644 "$srcdir"/ZenLib/Project/GNU/Library/libzen.pc "$pkgdir"/usr/lib/pkgconfig
  sed -i -e 's|Version: |Version: '$pkgver'|g' "$pkgdir"/usr/lib/pkgconfig/libzen.pc
  install -D -m 644 "$srcdir"/ZenLib/License.txt "$pkgdir"/usr/share/licenses/libzen/License.txt
}
