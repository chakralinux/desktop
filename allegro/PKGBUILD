#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: Giuseppe Calà <jiveaxe@gmail.com>

pkgname=allegro
pkgver=5.0.5
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming."
arch=('i686' 'x86_64')
url="http://alleg.sourceforge.net/"
license=('ZLIB')
depends=('jack' 'libxpm' 'libxxf86vm' 'libxxf86dga' 'libxcursor' 'libpng')
# With the exception of CMake, all are there to enable plugins support (all plugins but DUMB).
makedepends=('cmake' 'flac' 'freetype2' 'libjpeg' 'libogg' 'libpng' 'libvorbis' 'openal' 'physfs' 'zlib')
source=(http://downloads.sourceforge.net/alleg/$pkgname-$pkgver.tar.gz)
md5sums=('4d879afe7062f7dc9b8913cba2e44bdf')

build() {
  cd $srcdir/$pkgname-$pkgver
  mkdir build && cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    ..
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/build
  make DESTDIR=$pkgdir install

  install -m755 -d $pkgdir/usr/share/licenses/$pkgname
  install -m644 ../LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/
}
