#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: Giuseppe Calà <jiveaxe@gmail.com>

pkgname=allegro
pkgver=5.0.8
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming."
arch=('i686' 'x86_64')
url="http://alleg.sourceforge.net/"
license=('ZLIB')
depends=('alsa-lib' 'flac' 'freetype2' 'glibc' 'jack' 'libgl' 'libjpeg' 'libogg' 'libpng' 'libvorbis' 'libx11' 'libxau' 'libxcb' 'libxcursor' 'libxdmcp' 'libxext' 'libxfixes' 'libxpm' 'libxrender' 'libxxf86vm' 'libxxf86dga' 'libxcursor' 'libpng' 'openal' 'physfs' 'zlib')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/alleg/${pkgname}-${pkgver}.tar.gz")
md5sums=('899a7d576afd18442a68063e56b06bd9')

build() {
  cd "${srcdir}"
  mkdir -p build && cd build

  cmake "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo

  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE.txt" \
                "${pkgdir}/usr/share/licenses/${pkgname}"
}
	
