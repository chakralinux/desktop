pkgname=smplayer-themes
pkgver=18.6.0
pkgrel=1
pkgdesc="Themes for smplayer"
arch=('any')
url="https://www.smplayer.info/"
license=('GPL')
depends=('smplayer')
makedepends=('optipng' 'qt')
source=(https://downloads.sourceforge.net/smplayer/$pkgname-$pkgver.tar.bz2)
sha256sums=('2d86f4497dbeb3d5602bee2e0b2ed6ebf44ea90424c0b643b2597b4df277a985')

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix invalid PNG icons to work with libpng 1.6
  find -name '*.png' -exec optipng -quiet -force -fix {} +
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}/usr" install
  install -dpm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dpm644 COPYING* ${pkgdir}/usr/share/licenses/${pkgname}/
  install -dpm755 ${pkgdir}/usr/share/doc/${pkgname}
  install -Dpm644 Changelog README.txt ${pkgdir}/usr/share/doc/${pkgname}/
}
