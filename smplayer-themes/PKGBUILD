pkgname=smplayer-themes
pkgver=15.6.0
pkgrel=1
pkgdesc="Themes for smplayer"
arch=('any')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('smplayer')
makedepends=('optipng')
source=(http://downloads.sourceforge.net/smplayer/$pkgname-$pkgver.tar.bz2)
md5sums=('d5c1889aea807a626c51dbb0a795a2a1')

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix invalid PNG icons to work with libpng 1.6
  find -name '*.png' -exec optipng -quiet -force -fix {} +
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}/usr" install
  install -dpm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dpm644 COPYING* ${pkgdir}/usr/share/licenses/${pkgname}/
  install -dpm755 ${pkgdir}/usr/share/doc/${pkgname}
  install -Dpm644 Changelog README.txt ${pkgdir}/usr/share/doc/${pkgname}/
}
