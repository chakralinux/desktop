pkgname=smplayer-themes
pkgver=17.3.0
pkgrel=1
pkgdesc="Themes for smplayer"
arch=('any')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('smplayer')
makedepends=('optipng' 'qt')
source=(http://downloads.sourceforge.net/smplayer/$pkgname-$pkgver.tar.bz2)
sha256sums=('d1b89be79eda5f13bab4cda3ff9fae1253de69bed4896910698d20170ff658cd')

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix invalid PNG icons to work with libpng 1.6
  find -name '*.png' -exec optipng -quiet -force -fix {} +
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}/usr" install
  install -dpm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dpm644 COPYING* ${pkgdir}/usr/share/licenses/${pkgname}/
  install -dpm755 ${pkgdir}/usr/share/doc/${pkgname}
  install -Dpm644 Changelog README.txt ${pkgdir}/usr/share/doc/${pkgname}/
}
