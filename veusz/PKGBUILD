# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=veusz
pkgver=1.22
pkgrel=1
pkgdesc="A scientific plotting package, designed to create publication-ready Postscript output"
arch=('x86_64')
url="http://home.gna.org/$pkgname/"
license=('GPL')
depends=('python3-pyqt4' 'python3-numpy' 'lapack' 'hicolor-icon-theme')
optdepends=('python-pyfits: for reading files in FITS format')
source=(http://download.gna.org/$pkgname/$pkgname-$pkgver.tar.gz
        veusz.desktop)
categories=('education' 'science')
install=veusz.install
screenshot=('http://home.gna.org/veusz/screenshots/linux_mainwin.png')
md5sums=('50efe8580a3a9f25aa514d801c2195a3'
         '1e526306463d2212e56290c9994a41a6')


build() {
  cd $srcdir/$pkgname-$pkgver
  python3 setup.py build 
}

package() {
  cd $srcdir/$pkgname-$pkgver
  python3 setup.py install --root=$pkgdir --prefix=/usr 
  for _i in 16 32 48 64 128; do
    install -D -m644 $srcdir/$pkgname-$pkgver/icons/veusz_$_i.png \
        $pkgdir/usr/share/icons/hicolor/${_i}x${_i}/apps/veusz.png
  done
  install -D -m644 $srcdir/veusz.desktop \
      $pkgdir/usr/share/applications/veusz.desktop
}

