pkgname=veusz
pkgver=3.0.1
pkgrel=1
pkgdesc="A scientific plotting package, designed to create publication-ready Postscript output"
arch=('x86_64')
url="https://veusz.github.io/"
license=('GPL')
depends=('python3-pyqt5' 'python3-numpy' 'python3-sip'  'openblas'  'lapack' 'hicolor-icon-theme')
optdepends=('python3-pyfits: for reading files in FITS format')
source=(https://github.com/veusz/veusz/archive/$pkgname-$pkgver.tar.gz
        veusz.desktop)
categories=('education' 'science')
screenshot=('http://home.gna.org/veusz/screenshots/linux_mainwin.png')
sha256sums=('76f4feb400be4ada538e832cce352211383dbdb530f41f379c9dbf28e3591db3'
            '33e17492c79986d0e7c545648fc50ab17d62f0207a73329689d0c4192a0d0b1d')

build() {
  cd $pkgname-$pkgname-$pkgver
  python3 setup.py build 
}

package() {
  cd $pkgname-$pkgname-$pkgver
  python3 setup.py install --root=$pkgdir --prefix=/usr 
  for _i in 16 32 48 64 128; do
    install -D -m644 "icons/veusz_$_i.png" \
        $pkgdir/usr/share/icons/hicolor/${_i}x${_i}/apps/veusz.png
  done
  install -D -m644 ../veusz.desktop \
      $pkgdir/usr/share/applications/veusz.desktop
}
