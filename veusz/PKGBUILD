pkgname=veusz
pkgver=1.23.1
pkgrel=1
pkgdesc="A scientific plotting package, designed to create publication-ready Postscript output"
arch=('x86_64')
url="http://gna.org/projects/veusz"
license=('GPL')
depends=('python3-pyqt4' 'python3-numpy' 'atlas-lapack' 'hicolor-icon-theme')
optdepends=('python-pyfits: for reading files in FITS format')
source=(http://download.gna.org/$pkgname/$pkgname-$pkgver.tar.gz
        veusz.desktop)
categories=('education' 'science')
install=veusz.install
screenshot=('http://home.gna.org/veusz/screenshots/linux_mainwin.png')
sha512sums=('2500f6c74589e4415dff663b2138675638f999b4fa911dcb7eaf7c44a38d609f9e7931f8a4bb9bcea1ca3b7f2b756c0c71f77ee944d2a22996ac102c4a80cc76'
	    'e301ae597a9310a67f3d5ad6051251ef9389313722a11dded4827414ef061a56301ba7ba602603e1c577e1d4816efd3fc76ee0712ec61f0112830486de599828')


build() {
  cd $srcdir/$pkgname-$pkgver
  python3 setup.py build 
}

package() {
  cd $srcdir/$pkgname-$pkgver
  python3 setup.py install --root=$pkgdir --prefix=/usr 
  for _i in 16 32 48 64 128; do
    install -D -m644 $srcdir/$pkgname-$pkgver/icons/veusz_$_i.png \
        $pkgdir/usr/share/icons/hicolor/${_i}x${_i}/apps/veusz.png
  done
  install -D -m644 $srcdir/veusz.desktop \
      $pkgdir/usr/share/applications/veusz.desktop
}

