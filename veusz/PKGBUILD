pkgname=veusz
pkgver=1.23.2
pkgrel=2
pkgdesc="A scientific plotting package, designed to create publication-ready Postscript output"
arch=('x86_64')
url="http://gna.org/projects/veusz"
license=('GPL')
depends=('python3-pyqt4' 'python3-numpy' 'python3-sip'  'openblas'  'lapack' 'hicolor-icon-theme')
optdepends=('python3-pyfits: for reading files in FITS format')
source=(http://download.gna.org/$pkgname/$pkgname-$pkgver.tar.gz
        veusz.desktop)
categories=('education' 'science')
install=veusz.install
screenshot=('http://home.gna.org/veusz/screenshots/linux_mainwin.png')
sha512sums=('4af8858cc5b6e33589c1f1833e2051d3af45203218f2f9866ab1d704be9cd605ec0f829bff70aa0ce7f078a35369d315f930b5bbd4ee3436330ff20e90057ca1'
'e301ae597a9310a67f3d5ad6051251ef9389313722a11dded4827414ef061a56301ba7ba602603e1c577e1d4816efd3fc76ee0712ec61f0112830486de599828')


build() {
  cd $srcdir/$pkgname-$pkgver
  python3 setup.py build 
}

package() {
  cd $srcdir/$pkgname-$pkgver
  python3 setup.py install --root=$pkgdir --prefix=/usr 
  for _i in 16 32 48 64 128; do
    install -D -m644 $srcdir/$pkgname-$pkgver/icons/veusz_$_i.png \
        $pkgdir/usr/share/icons/hicolor/${_i}x${_i}/apps/veusz.png
  done
  install -D -m644 $srcdir/veusz.desktop \
      $pkgdir/usr/share/applications/veusz.desktop
}

