# Contributor: Anke Boersma <abveritas@chakra-project.org>
# Maintainer: Bruce Liu <brli@users.sourceforge.net>

pkgname=qupzilla
pkgver=1.6.5
pkgrel=2
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
depends=('qtwebkit' 'qtwebkit-plugins' 'kdelibs')
optdepends=('bash-completion: bash completion support')
categories=('network') 
install=${pkgname}.install
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/qupzilla/archive/v${pkgver}.tar.gz"
	'qupzilla.patch'
        'theme-app-icon.patch'
	'start-white.png')
sha1sums=('52f41bc68ee9d6b38977510e587cb1e2e201135c'
          '6c0450b4684d3a001ee5c7f601ba82e35b7db945'
          'a782a0ee5191ce5e0177ff5b9f1d55c248b829ea'
          '6ec67993bda3054d4210cc08177b124f873774e3')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}/src/lib"
  patch -uNp1 -i ${srcdir}/qupzilla.patch

  ## note that the following patches are backward patch, should be removed at next(>1.6.x) release
  patch -p3 -i ${srcdir}/theme-app-icon.patch
}
         
build() {
  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  
  cd "${srcdir}/${pkgname}-${pkgver}/"
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/icons/start-white.png"
  
  # shell completion
  install -Dm644 linux/completion/_${pkgname} "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 linux/completion/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}

