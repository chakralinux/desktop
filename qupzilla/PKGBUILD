# Contributor: Anke Boersma <abveritas@chakra-project.org>
# Maintainer: Bruce Liu <brli@users.sourceforge.net>

_pkgname=QupZilla
pkgname=qupzilla
pkgver=1.8.6
pkgrel=1
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
depends=('qt5-base' 'qt5-webkit' 'qt5-script' 'kdelibs')
makedepends=('qt5-tools')
optdepends=('bash-completion: bash completion support')
categories=('network') 
install=${pkgname}.install
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/qupzilla/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.xz"
	'browserui.patch'
	'preference.patch'
	'searchmanager.patch'
	'start-white.png')
sha1sums=('e1164dd6afb1ee121c2d53acc95b1c7b7d51603e'
          'c3d9f661c3243764afc24c661678ad96d5c423f9'
          'ac27b9415e74f2b6771b9011b7e415f8086abf5a'
          '86ead03eb48b2eca44125755ff55f76eedde3b10'
          '6ec67993bda3054d4210cc08177b124f873774e3')

prepare() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  msg 'Applying browserUI patch...'
  patch -Np1 -i ${srcdir}/browserui.patch
  msg 'Applying preference patch'
  patch -Np1 -i ${srcdir}/preference.patch  
  msg 'Applying search engine patch...'
  patch -Np1 -i ${srcdir}/searchmanager.patch
}
         
build() {
  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  export ENABLE_OPACITY_EFFECT="true"
  
  cd "${srcdir}/${_pkgname}-${pkgver}/"
  /usr/lib/qt5/bin/qmake
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/"

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/pixmaps/start-white.png"
  
  # shell completion
  install -Dm644 linux/completion/_${pkgname} "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 linux/completion/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
