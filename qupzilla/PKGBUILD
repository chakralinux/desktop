# Contributor: Anke Boersma <abveritas@chakra-project.org>
# Maintainer: Bruce Liu <brli@users.sourceforge.net>

_pkgname=QupZilla
pkgbase=qupzilla
pkgname=('qupzilla' 'qupzilla-qt4')
pkgver=1.8.8
pkgrel=1
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
makedepends=('qt5-tools' 'qt5-webkit' 'qt5-script' 'kwallet' 'kdelibs' 'qt')
optdepends=('bash-completion: bash completion support')
install=${pkgbase}.install
categories=('network') 
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/qupzilla/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.xz"
        '0001-branded-speeddial.patch')
sha1sums=('1ec7c048243b57b29eb8cb3e574cb62606dd3a87'
          '1c64f02f0671d379b2bd7a98ba15bc38c919e3cd')

prepare() {
  msg 'Applying browserUI patch...'
  cd ${srcdir}/${_pkgname}-${pkgver}/src/lib/app/
  sed -e 's|"qupzilla:start"|"https://chakraos.org/?welcome"|' \
      -e 's|"showStatusBar", true|"showStatusBar", false|' \
      -e 's|"showMenubar", true|"showMenubar", false|g' -i browserwindow.cpp
  cd ${srcdir}/${_pkgname}-${pkgver}/src/lib/plugins/
  sed -e 's|"background", QString()|"file:///usr/share/pixmaps/start-white.png", QString()|' \
      -e 's|"backsize", "auto"|"backsize", "contain"|' -i speeddial.cpp
  patch -p4 -i ${srcdir}/0001-branded-speeddial.patch
  msg 'Applying preference patch'
  cd ${srcdir}/${_pkgname}-${pkgver}/src/lib/preferences/
  sed -e 's|"homepage", QUrl(QSL("qupzilla:start"))|"homepage", QUrl(QSL("https://chakraos.org/?welcome"))|' \
      -e 's|"showStatusBar", true|"showStatusBar", false|g' -i preferences.cpp
  msg 'Applying search engine patch...'
  cd ${srcdir}/${_pkgname}-${pkgver}/src/lib/opensearch/
  sed 's|https://duckduckgo.com/?q=%s&t=qupzilla|https://duckduckgo.com/?t=chakra&q=%s|' -i searchenginesmanager.cpp
  
  # split
  mkdir ${srcdir}/qt{4,5}
  cp -r ${srcdir}/${_pkgname}-${pkgver}/* ${srcdir}/qt4
  cp -r ${srcdir}/${_pkgname}-${pkgver}/* ${srcdir}/qt5
}
         
build() {
  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  export ENABLE_OPACITY_EFFECT="true"
  
  msg 'Qt4 build'
  cd "${srcdir}/qt4"
  qmake
  make
  
  msg 'Qt5 build'
  cd "${srcdir}/qt5"
  /usr/lib/qt5/bin/qmake
  make

}

package_qupzilla-qt4() {
  pkgdesc+=('(Qt4 build)')
  depends=('qt' 'qtwebkit-plugins' 'qtwebkit')
  optdepends=('kdelibs: kwallet integration')
  provides=("${pkgbase}")
  conflicts=("${pkgbase}")
  cd "${srcdir}/qt4"
  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/pixmaps/start-white.png"
}

package_qupzilla() {
  pkgdesc+=('(Qt5 build)')
  depends=('qt5-webkit' 'qt5-script')
  optdepends=('kwallet: kf5 kwallet integration')
  cd "${srcdir}/qt5"
  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/pixmaps/start-white.png"  
}

