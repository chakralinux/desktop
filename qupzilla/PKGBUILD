# Contributor: Anke Boersma <abveritas@chakra-project.org>
# Maintainer: Bruce Liu <brli@users.sourceforge.net>

pkgname=qupzilla
pkgver=1.6.4
pkgrel=1
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
depends=('qtwebkit' 'qtwebkit-plugins' 'kdelibs')
optdepends=('bash-completion: bash completion support')
categories=('network') 
install=${pkgname}.install
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/qupzilla/archive/v${pkgver}.tar.gz"
	'qupzilla.patch'
        'theme-app-icon.patch'
	'start-white.png')
sha256sums=('05f543ba78171e9280809cd3f309dda6a835c5ac128341830cd7b9b0a6010d0f'
            'f37afa5b9a3d52dfe640991fd28061a9ddcba895c649bc97bfb49aef29f3703c'
            '8a5d93463a7e78d9d5f048a51a240703a4ea97b23ce0d66b27992b007f961687'
            '5bdac9f329c5d240e9aa58d7960f5f8b4c372e8908e2cf1080a02619a4695f09')

prepare() {
  cd "${pkgname}-${pkgver}/src/lib"
  patch -uNp1 -i ${srcdir}/qupzilla.patch
  patch -p3 -i ${srcdir}/theme-app-icon.patch
 }
         
build() {
  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  
  cd "${pkgname}-${pkgver}"
  qmake QupZilla.pro
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/icons/start-white.png"
  
  # shell completion
  install -Dm644 linux/completion/_${pkgname} "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 linux/completion/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}

