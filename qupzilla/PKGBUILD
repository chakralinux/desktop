# Contributor: Anke Boersma <abveritas@chakra-project.org>
# Maintainer: Bruce Liu <brli@users.sourceforge.net>

pkgname=qupzilla
pkgver=1.6.1
pkgrel=2
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
depends=('qtwebkit' 'qtwebkit-plugins' 'kdelibs')
optdepends=('bash-completion: bash completion support')
categories=('network') 
install=${pkgname}.install
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/qupzilla/archive/v${pkgver}.tar.gz"
	'qupzilla.patch'
	'start-white.png')
sha256sums=('ab484ccf524ee5fc9341cd5adea746e8769821859037a7d81c606f81184610fd'
            'c69006400f534c8de5a5628133145e4ec0ce3eced354df1e5f7f0038d8154c1b'
            '5bdac9f329c5d240e9aa58d7960f5f8b4c372e8908e2cf1080a02619a4695f09')

prepare() {
  cd "${pkgname}-${pkgver}/src/lib"
  patch -uNp1 -i $srcdir/qupzilla.patch
 }
         
build() {
  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  
  cd "${pkgname}-${pkgver}"
  qmake QupZilla.pro
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/icons/start-white.png"
  
  # shell completion
  install -Dm644 linux/completion/_${pkgname} "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 linux/completion/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
