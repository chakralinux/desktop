# Contributor: Anke Boersma <abveritas@chakra-project.org>
# Maintainer: Bruce Liu <brli@users.sourceforge.net>

pkgname=qupzilla
pkgver=1.6.0
pkgrel=2
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
depends=('qtwebkit' 'qtwebkit-plugins' 'kdelibs')
optdepends=('bash-completion: bash completion support')
categories=('network') 
install=${pkgname}.install
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/qupzilla/archive/v${pkgver}.tar.gz"
	'qupzilla.patch'
	'start-white.png'
        'qupzilla.changelog')
sha256sums=('67a12326c55de32434b3381f0574bf1be5858ca9f20eaa513feb720fd8f67dc4'
	    '932f0b84eaebbbc1b7f09877c100130bde0c56606f0c11b4a1876a2d3b4981e9'
            '5bdac9f329c5d240e9aa58d7960f5f8b4c372e8908e2cf1080a02619a4695f09'
            '6f3b069561d2b5512f10618f6b986a2e075901112da4c33febb083bc7679a309')

prepare() {
  cd "${pkgname}-${pkgver}/src/lib"
  patch -uNp1 -i $srcdir/qupzilla.patch
 }
         
build() {
  cd "${pkgname}-${pkgver}"

  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  
  qmake QupZilla.pro
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/icons/start-white.png"
  
  # shell completion
  install -Dm644 linux/completion/_${pkgname} "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 linux/completion/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
