# Contributor: Anke Boersma <abveritas@chakra-project.org>
# Maintainer: Bruce Liu <brli@users.sourceforge.net>

pkgname=qupzilla
pkgver=1.6.5
pkgrel=1
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
depends=('qtwebkit' 'qtwebkit-plugins' 'kdelibs')
optdepends=('bash-completion: bash completion support')
categories=('network') 
install=${pkgname}.install
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/qupzilla/releases/download/v${pkgver}/QupZilla-${pkgver}.tar.xz"
	'qupzilla.patch'
        'theme-app-icon.patch'
        'kwallet.patch'
	'start-white.png')
sha1sums=('4f3869587cc6b0e2dda4c98ed5aaa5410fd80c91'
          '9765cb3693bdb95772ee17051ce780baf4d867ab'
          'a782a0ee5191ce5e0177ff5b9f1d55c248b829ea'
          'e320338f89027f0995ef94b0eaa4fc409a6682d9'
          '6ec67993bda3054d4210cc08177b124f873774e3')

prepare() {
  cd "${srcdir}/QupZilla-${pkgver}/src/lib"
  patch -uNp1 -i ${srcdir}/qupzilla.patch

  ## note that the following patches are backward patch, should be removed at next(>1.6.4) release
  patch -p3 -i ${srcdir}/theme-app-icon.patch
  #cd "${srcdir}/QupZilla-${pkgver}/src/plugins/KWalletPasswords"
  #patch -uN ${srcdir}/QupZilla-${pkgver}/src/plugins/KWalletPasswords/kwalletplugin.cpp ${srcdir}/kwallet.patch
 }
         
build() {
  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  
  cd "${srcdir}/QupZilla-${pkgver}/"
  qmake QupZilla.pro
  make
}

package() {
  cd "${srcdir}/QupZilla-${pkgver}/"

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/icons/start-white.png"
  
  # shell completion
  install -Dm644 linux/completion/_${pkgname} "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 linux/completion/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
