# Contributor: Anke Boersma <abveritas@chakra-project.org>
# Maintainer: Bruce Liu <brli@users.sourceforge.net>

pkgname=qupzilla
pkgver=1.6.3
pkgrel=2
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
depends=('qtwebkit' 'qtwebkit-plugins' 'kdelibs')
optdepends=('bash-completion: bash completion support')
categories=('network') 
install=${pkgname}.install
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/qupzilla/archive/v${pkgver}.tar.gz"
	'qupzilla.patch'
	'start-white.png')
sha256sums=('884cd5fb971450fa5363101859fc7306583d5ec4839858956e6900c305d4be3a'
            'ffaa0261d1d227922cae829a1b6471d36a02753f7a08f117952f2f301224e4b1'
            '5bdac9f329c5d240e9aa58d7960f5f8b4c372e8908e2cf1080a02619a4695f09')

prepare() {
  cd "${pkgname}-${pkgver}/src/lib"
  patch -uNp1 -i $srcdir/qupzilla.patch
 }
         
build() {
  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  
  cd "${pkgname}-${pkgver}"
  qmake QupZilla.pro
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/icons/start-white.png"
  
  # shell completion
  install -Dm644 linux/completion/_${pkgname} "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 linux/completion/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
