# Maintainer: Bruce Liu <brli@users.sourceforge.net>

_pkgname=QupZilla
pkgname='qupzilla'
pkgver=1.9.90
_commit=a16c21b
pkgrel=1
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
makedepends=('qt5-tools' 'kwallet')
depends=('qt5-webengine' 'qt5-script')
optdepends=('bash-completion: bash completion support'
            'kwallet: kf5 kwallet integration')
provides=('qupzilla-qt5')
conflicts=('qupzilla-qt5')
replaces=('qupzilla-qt5')
install=${pkgname}.install
categories=('network') 
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/QupZilla/tarball/master/QupZilla-qupzilla-${pkgver}.tar.gz"
        '0001-branded-speeddial.patch'
        'start-white.png')
sha1sums=('54f760f18880f9b453e12152a425a9c7449efd1d'
          '7ae942d114016e5971caffd8a5cef8e20495f127'
          '6ec67993bda3054d4210cc08177b124f873774e3')

prepare() {
  msg 'Applying browserUI patch...'
  cd "${srcdir}/${_pkgname}-${pkgname}-${_commit}/src/lib/app/"
  sed -e 's|"qupzilla:start"|"https://chakraos.org/?welcome"|' \
      -e 's|"showStatusBar", true|"showStatusBar", false|' \
      -e 's|"showMenubar", true|"showMenubar", false|g' -i browserwindow.cpp
  cd "${srcdir}/${_pkgname}-${pkgname}-${_commit}/src/lib/plugins/"
  sed -e 's|"background", QString()|"file:///usr/share/pixmaps/start-white.png", QString()|' \
      -e 's|"backsize", "auto"|"backsize", "contain"|' -i speeddial.cpp
  #patch -p4 -i ${srcdir}/0001-branded-speeddial.patch
  msg 'Applying preference patch'
  cd "${srcdir}/${_pkgname}-${pkgname}-${_commit}/src/lib/preferences/"
  sed -e 's|"homepage", QUrl(QSL("qupzilla:start"))|"homepage", QUrl(QSL("https://chakraos.org/?welcome"))|' \
      -e 's|"showStatusBar", true|"showStatusBar", false|g' -i preferences.cpp
  msg 'Applying search engine patch...'
  cd "${srcdir}/${_pkgname}-${pkgname}-${_commit}/src/lib/opensearch/"
  sed 's,\?q=\%s\&t=qupzilla,\?q=\%s\&t=chakra,g' -i searchenginesmanager.cpp
}
         
build() {
  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  export ENABLE_OPACITY_EFFECT="true"
  #export NO_X11="true"

  cd "${srcdir}/${_pkgname}-${pkgname}-${_commit}"
  /usr/lib/qt5/bin/qmake
  make

}

package() {
  cd "${srcdir}/${_pkgname}-${pkgname}-${_commit}"
  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/pixmaps/start-white.png"  
}
