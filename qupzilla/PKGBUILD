# Contributor: Anke Boersma <abveritas@chakra-project.org>
# Maintainer: Bruce Liu <brli@users.sourceforge.net>
_pkgname=QupZilla
pkgname=qupzilla
pkgver=1.8.0
pkgrel=2
pkgdesc="A very fast open source browser based on WebKit core"
url="http://www.qupzilla.com"
arch=('x86_64')
license=('GPL3')
depends=('qtwebkit' 'qtwebkit-plugins' 'kdelibs')
optdepends=('bash-completion: bash completion support')
categories=('network') 
install=${pkgname}.install
screenshot='http://www.qupzilla.com/screens/kde.png'
source=("https://github.com/QupZilla/qupzilla/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.xz"
	'qupzilla.patch'
	'start-white.png')
sha1sums=('96efcea416ea417a28c84a29a28a4481597afbac'
          '956649151977cd6cb89a2d84f8e187175c21cdca'
          '6ec67993bda3054d4210cc08177b124f873774e3')

prepare() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/qupzilla.patch
}
         
build() {
  export USE_WEBGL="true"
  export KDE_INTEGRATION="true"
  
  cd "${srcdir}/${_pkgname}-${pkgver}/"
  qmake
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/"

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/start-white.png" "${pkgdir}/usr/share/pixmaps/start-white.png"
  
  # shell completion
  install -Dm644 linux/completion/_${pkgname} "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 linux/completion/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
