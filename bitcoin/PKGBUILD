# Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/bitcoin

pkgbase=bitcoin
pkgname=('bitcoin-daemon' 'bitcoin-cli' 'bitcoin-qt')
pkgver=0.11.0
pkgrel=3
arch=('x86_64')
url="http://www.bitcoin.org/"
makedepends=('boost' 'qrencode' 'miniupnpc' 'protobuf' 'qt5-tools')
license=('MIT')
source=(http://bitcoin.org/bin/bitcoin-core-$pkgver/bitcoin-$pkgver.tar.gz
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/bitcoin-qt.desktop
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/share/pixmaps/bitcoin128.png
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/examples/bitcoin.conf
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/manpages/bitcoind.1
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/manpages/bitcoin.conf.5)
sha512sums=('5c6a1aa75dc4c8518b62dfa8f0b0a21850cc2e7b6a741cebcd9ec8c8eca32091aa172c6d9bcd1a4d22a9111d9486ce2f12e2ae0635e74cdb9d0cd9eadd7afa9c'
            '9ada8dacb1b47c80460f66799d81319d11af2c0ae670d81348561eb4a69d852fd463df98428dc3d60fb90d47eda401524fe33aad318d1a07044fb27385f9e306'
            '57613bb7caf28bb1aba0d76dd6e500ea29437f1f2442345183c57f988c0eecb51dfe8dc42ed964971569ea3a79d712740cf319176f111174e61688bc26e4d9c0'
            'b45741863c93eadfd4a38f207679c02b485642c376d5907f50441e69ee94139f12ac0b77696402021c6637a7c205a23cc389a16ab4d59db039cf69d0bee87587'
            '0b798fba30e50a6ead4a12fa4c0a3bf93e40a738f76a4d71f8595f885e6d026fe1d2f03ea100315f2013bd0214bc2e6431c833d3b77fe3b04e99f4371660854c'
            'd9a0686e6931701a851b75ea700cfa68bdf8b09fd1faae365ab3803a1deb278bece7d7c88e6e4f8bd735774cc4d6e29ee18db7e3450faffa50f8fa5ac6b876e3')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr \
              --with-incompatible-bdb \
              --enable-hardening \
              --with-gui=qt5
  make
}

package_bitcoin-qt() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - Qt"
  depends=('boost-libs' 'qt5-base' 'miniupnpc' 'qrencode' 'protobuf')
  install=bitcoin-qt.install
  provides=('bitcoin')
  conflicts=('bitcoin')
  replaces=('bitcoin')
  
  cd "$pkgbase-$pkgver"
  install -Dm755 src/qt/bitcoin-qt "$pkgdir"/usr/bin/bitcoin-qt
  install -Dm644 "$srcdir"/bitcoin-qt.desktop \
    "$pkgdir"/usr/share/applications/bitcoin.desktop
  install -Dm644 "$srcdir"/bitcoin128.png \
    "$pkgdir"/usr/share/pixmaps/bitcoin128.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_bitcoin-daemon() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - daemon"
  depends=('boost-libs' 'miniupnpc' 'openssl')

  cd "$pkgbase-$pkgver"
  install -Dm755 src/bitcoind "$pkgdir"/usr/bin/bitcoind
  install -Dm644 "$srcdir"/bitcoin.conf \
    "$pkgdir/usr/share/doc/$pkgname/examples/bitcoin.conf"
  install -Dm644 "$srcdir"/bitcoind.1 \
    "$pkgdir"/usr/share/man/man1/bitcoind.1
  install -Dm644 "$srcdir"/bitcoin.conf.5 \
    "$pkgdir"/usr/share/man/man5/bitcoin.conf.5
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_bitcoin-cli() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - RPC client"
  depends=('boost-libs' 'openssl')

  cd "$pkgbase-$pkgver"
  install -Dm755 src/bitcoin-cli "$pkgdir"/usr/bin/bitcoin-cli
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
