#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor: shahid <helllamer@gmail.com>

pkgname=bitcoin
pkgver=0.5.2
pkgrel=1
pkgdesc="Bitcoin is a peer-to-peer network based digital currency."
arch=('i686' 'x86_64')
url="http://www.bitcoin.org/"
depends=('qt' 'libpng' 'expat' 'gcc-libs' 'boost-libs' 'miniupnpc' 'openssl')
makedepends=('boost' 'gcc' 'make' 'automoc4')
conflicts=('bitcoin-bin' 'bitcoin-git' 'bitcoin-daemon' 'bitcoin-cli')
license=('MIT')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-linux.tar.gz"
	"${pkgname}.desktop")
md5sums=('6d40f645ddc042de26d64e83656f38e0'
	 '77139ce8a40f46bd7d7ad0a743672113')
s1=$srcdir/${pkgname}-${pkgver}-linux
src_qt=$s1/src
src_d=$s1/src/src/
makefile_unix=makefile.unix

build() {
	cd $src_d

	msg "Patching $makefile_unix..."
	sed -i $makefile_unix \
		-e 's/\(-DNOPCH\)/\1 -DBOOST_FILESYSTEM_VERSION=2/' \
		-e 's/-Bstatic/--as-needed ${LDFLAGS}/g' \
		-e 's/\(USE_UPNP:=\)0/\11/' \
		-e 's/$(DEBUGFLAGS)//g' \
		-e 's/CXXFLAGS/CPPFLAGS/g' \
		-e 's/-O[0-3]/${CXXFLAGS}/g'

	# make bitcoind
	make $MAKEFLAGS -f $makefile_unix bitcoind

	# and make qt gui
	cd $src_qt
	qmake
	make $MAKEFLAGS
}


package() {
	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/share/pixmaps
	mkdir -p $pkgdir/usr/share/applications

	# get compiled binaries
	install -D -m755 $src_qt/bitcoin-qt $pkgdir/usr/bin/
	install -D -m755 $src_d/bitcoind $pkgdir/usr/bin/
	
	# add icon and .desktop file to pkg
	install -D -m644 $src_qt/src/qt/res/icons/bitcoin.png $pkgdir/usr/share/pixmaps/
	install -D -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/ 
}
