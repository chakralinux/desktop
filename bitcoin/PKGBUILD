# Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/bitcoin

pkgbase=bitcoin
pkgname=('bitcoin-daemon' 'bitcoin-cli' 'bitcoin-qt')
pkgver=0.12.0
pkgrel=1
arch=('x86_64')
url="http://www.bitcoin.org/"
makedepends=('boost' 'qrencode' 'miniupnpc' 'protobuf' 'qt5-tools' 'libevent')
license=('MIT')
source=(http://bitcoin.org/bin/bitcoin-core-$pkgver/bitcoin-$pkgver.tar.gz
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/bitcoin-qt.desktop
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/share/pixmaps/bitcoin128.png
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/examples/bitcoin.conf
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/manpages/bitcoind.1
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/manpages/bitcoin.conf.5)
sha512sums=('b7bd91bdac369c6d949ae5487801d98cc6db8779d73922d49013bbf9e7fb98c6bf03af87adde27b1b8e2664ad7e931daa1f8d65f6e84d13ea2d043432769e02b'
            '9ada8dacb1b47c80460f66799d81319d11af2c0ae670d81348561eb4a69d852fd463df98428dc3d60fb90d47eda401524fe33aad318d1a07044fb27385f9e306'
            '57613bb7caf28bb1aba0d76dd6e500ea29437f1f2442345183c57f988c0eecb51dfe8dc42ed964971569ea3a79d712740cf319176f111174e61688bc26e4d9c0'
            '736f7c975004cd8cd5409fb29e986ea28888f07086ec618c704b41855feea92959b9d00880ebc769dc52430dc6638a20424491ac9ae9405ffc8c228c3de2af02'
            '53cbf4694e33dd08caafe1f9e4d55729fa17a02881082f7095f665431ea654d5665255dab05209a5a7ef3b269220b2d84368762eb4ec2c35ecf11c6d92fb41b1'
            '1dd9dfdfb530d1bfb20bc84590034ab6902f25714b3c5bfa2418ad27515bc895fa16968880ec326641f0e58f9364e666764972b5a8782acb2e7494207d629888')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr \
              --with-incompatible-bdb \
              --enable-hardening \
              --with-gui=qt5
  make
}

package_bitcoin-qt() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - Qt"
  depends=('boost-libs' 'qt5-base' 'miniupnpc' 'qrencode' 'protobuf')
  install=bitcoin-qt.install
  provides=('bitcoin')
  conflicts=('bitcoin')
  replaces=('bitcoin')
  
  cd "$pkgbase-$pkgver"
  install -Dm755 src/qt/bitcoin-qt "$pkgdir"/usr/bin/bitcoin-qt
  install -Dm644 "$srcdir"/bitcoin-qt.desktop \
    "$pkgdir"/usr/share/applications/bitcoin.desktop
  install -Dm644 "$srcdir"/bitcoin128.png \
    "$pkgdir"/usr/share/pixmaps/bitcoin128.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_bitcoin-daemon() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - daemon"
  depends=('boost-libs' 'miniupnpc' 'libevent')

  cd "$pkgbase-$pkgver"
  install -Dm755 src/bitcoind "$pkgdir"/usr/bin/bitcoind
  install -Dm644 "$srcdir"/bitcoin.conf \
    "$pkgdir/usr/share/doc/$pkgname/examples/bitcoin.conf"
  install -Dm644 "$srcdir"/bitcoind.1 \
    "$pkgdir"/usr/share/man/man1/bitcoind.1
  install -Dm644 "$srcdir"/bitcoin.conf.5 \
    "$pkgdir"/usr/share/man/man5/bitcoin.conf.5
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_bitcoin-cli() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - RPC client"
  depends=('boost-libs' 'libevent')

  cd "$pkgbase-$pkgver"
  install -Dm755 src/bitcoin-cli "$pkgdir"/usr/bin/bitcoin-cli
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
