# Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/bitcoin

pkgbase=bitcoin
pkgname=('bitcoin-daemon' 'bitcoin-cli' 'bitcoin-qt')
pkgver=0.10.2
pkgrel=1
arch=('x86_64')
url="http://www.bitcoin.org/"
makedepends=('boost' 'automoc4' 'qrencode' 'miniupnpc' 'protobuf')
license=('MIT')
source=(http://bitcoin.org/bin/bitcoin-core-$pkgver/bitcoin-$pkgver.tar.gz
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/bitcoin-qt.desktop
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/share/pixmaps/bitcoin128.png
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/examples/bitcoin.conf
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/manpages/bitcoind.1
	https://raw.github.com/bitcoin/bitcoin/v$pkgver/contrib/debian/manpages/bitcoin.conf.5)
sha512sums=('0cd0592f660343cadc07809afb41eb08a477f6a5cb158a632f7ca4f7a7f7a505e60ec9058d47165c826024b802f1ce85cbd53dc2a11bb1019c0ecceaec14519d'
            '9ada8dacb1b47c80460f66799d81319d11af2c0ae670d81348561eb4a69d852fd463df98428dc3d60fb90d47eda401524fe33aad318d1a07044fb27385f9e306'
            '57613bb7caf28bb1aba0d76dd6e500ea29437f1f2442345183c57f988c0eecb51dfe8dc42ed964971569ea3a79d712740cf319176f111174e61688bc26e4d9c0'
            '373c7d5ec6a99ad6464ac3119ff687dbecdf1a09c5d254a174eb1f6424658fa5573651e1d54670e7c36bba311283e76903ba9e43a766d802097470dea123d3a3'
            'ab91011ebd4cde3d258f6147583a6361bdc5c98aab251fed3d6d126c65a3bfe4278db799aa03492f11e8ea85202773a09e6cb03941fe8a29fceeff2d62df77ad'
            'd9a0686e6931701a851b75ea700cfa68bdf8b09fd1faae365ab3803a1deb278bece7d7c88e6e4f8bd735774cc4d6e29ee18db7e3450faffa50f8fa5ac6b876e3')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr --with-incompatible-bdb --with-gui=qt4
  make
}

package_bitcoin-qt() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - Qt"
  depends=('boost-libs' 'qt' 'miniupnpc' 'qrencode' 'protobuf')
  install=bitcoin-qt.install
  provides=('bitcoin')
  conflicts=('bitcoin')
  replaces=('bitcoin')
  
  cd "$pkgbase-$pkgver"
  install -Dm755 src/qt/bitcoin-qt "$pkgdir"/usr/bin/bitcoin-qt
  install -Dm644 "$srcdir"/bitcoin-qt.desktop \
    "$pkgdir"/usr/share/applications/bitcoin.desktop
  install -Dm644 "$srcdir"/bitcoin128.png \
    "$pkgdir"/usr/share/pixmaps/bitcoin128.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_bitcoin-daemon() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - daemon"
  depends=('boost-libs' 'miniupnpc' 'openssl')

  cd "$pkgbase-$pkgver"
  install -Dm755 src/bitcoind "$pkgdir"/usr/bin/bitcoind
  install -Dm644 "$srcdir"/bitcoin.conf \
    "$pkgdir/usr/share/doc/$pkgname/examples/bitcoin.conf"
  install -Dm644 "$srcdir"/bitcoind.1 \
    "$pkgdir"/usr/share/man/man1/bitcoind.1
  install -Dm644 "$srcdir"/bitcoin.conf.5 \
    "$pkgdir"/usr/share/man/man5/bitcoin.conf.5
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_bitcoin-cli() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - RPC client"
  depends=('boost-libs' 'openssl')

  cd "$pkgbase-$pkgver"
  install -Dm755 src/bitcoin-cli "$pkgdir"/usr/bin/bitcoin-cli
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}