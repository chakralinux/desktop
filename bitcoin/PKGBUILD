# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor: shahid <helllamer@gmail.com>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>

pkgname=bitcoin
pkgver=0.8.5
pkgrel=1
pkgdesc="Bitcoin is a peer-to-peer network based digital currency."
arch=('x86_64')
url="http://www.bitcoin.org/"
screenshot="http://c.fsdn.com/con/app/proj/bitcoin/screenshots/203604"
depends=('boost-libs' 'qt' 'miniupnpc' 'qrencode' 'db')
makedepends=('boost' 'automoc4')
conflicts=('bitcoin-git')
replaces=('bitcoin-git')
license=('MIT')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-linux.tar.gz")
sha256sums=('84543f10de5e82ce6e88dd5a501db37c6327edf79a2a04f29199c24843e71f63')

build() {
  cd ${pkgbase}-${pkgver}-linux/src

  # and make qt gui (USE_UPNP default activated)
  qmake USE_QRCODE=1
  make

  # make bitcoind
  make -f makefile.unix -C src  CXXFLAGS="$CXXFLAGS"
}

package() {
  cd ${pkgbase}-${pkgver}-linux/src
  
  # Install bitcoin-qt
  install -Dm755 bitcoin-qt "${pkgdir}"/usr/bin/bitcoin-qt
  install -Dm644 contrib/debian/bitcoin-qt.desktop \
    "${pkgdir}"/usr/share/applications/bitcoin.desktop
  install -Dm644 share/pixmaps/bitcoin128.png \
    "${pkgdir}"/usr/share/pixmaps/bitcoin128.png

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # Install bitcoin deamon
  install -Dm755 src/bitcoind "${pkgdir}"/usr/bin/bitcoind
  install -Dm644 contrib/debian/examples/bitcoin.conf \
    "${pkgdir}/usr/share/doc/${pkgname}/examples/bitcoin.conf"
  install -Dm644 contrib/debian/manpages/bitcoind.1 \
    "${pkgdir}"/usr/share/man/man1/bitcoind.1
  install -Dm644 contrib/debian/manpages/bitcoin.conf.5 \
    "${pkgdir}"/usr/share/man/man5/bitcoin.conf.5
    
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
