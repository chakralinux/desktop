pkgname=wireshark
pkgver=2.6.5
pkgrel=1
pkgdesc='A free network protocol analyzer for Unix/Linux and Windows'
arch=('x86_64')
license=('GPL2')
depends=('libpcap' 'libcap' 'krb5' 'glib2' 'desktop-file-utils' 'hicolor-icon-theme' 'libssh'
         'lua52' 'libgcrypt' 'qt5-base' 'qt5-multimedia' 'harfbuzz' 'portaudio' 'bash' 'gnutls' 'geoip')
makedepends=('bison' 'flex' 'qt5-tools' 'python2')
url='http://www.wireshark.org/'
screenshot='https://blog.wireshark.org/wp-content/uploads/2015/11/Main-window-2.0.0rc2.png'
install=${pkgname}.install
source=(http://www.wireshark.org/download/src/${pkgname}-${pkgver}.tar.xz
        wireshark.sysusers
        do_not_use_svn_version.patch)
sha256sums=('93155b798544b2f07693920f4ac1b531c952965ee4eb1d98419961240177438a'
            'df07748152443c7d727bd51cd57af950c345b7141986b4f0e476cd6aa3623ac4'
            '3981bb46f592e319f9c679c5e215ae0af828044a3b64d49d2cf0305ecacf4691')

prepare() {
  cd ${pkgname}-${pkgver}

  # Avoid ugly "svn rev unknown from unknown" version string
  patch -p1 < "${srcdir}/do_not_use_svn_version.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  
  ./autogen.sh 
  ./configure \
      --prefix=/usr \
      --with-qt=5 \
      --with-ssl \
      --with-pcap \
      --with-libcap \
      --with-zlib \
      --with-lua \
      --with-krb5 \
      --with-portaudio \
      --with-geoip
  make all
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  # wireshark uid group is 150
  install -Dm644 "${srcdir}/wireshark.sysusers" "${pkgdir}/usr/lib/sysusers.d/wireshark.conf"
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"

  # headers
  install -Dm 644 *.h -t "${pkgdir}/usr/include/${pkgname}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes epan/wmem wiretap wsutil; do
    install -Dm 644 ${d}/*.h -t "${pkgdir}/usr/include/${pkgname}/${d}"
  done

  install -Dm 755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  install -Dm 644 wireshark.desktop -t "${pkgdir}/usr/share/applications"
  rm "${pkgdir}/usr/share/applications/wireshark-gtk.desktop"
}
