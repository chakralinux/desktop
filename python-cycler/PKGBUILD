pkgbase=python-cycler
pkgname=('python3-cycler' 'python2-cycler')
pkgver=0.10.0
pkgrel=2
pkgdesc="Composable style cycles"
arch=('any')
license=('BSD')
url="http://github.com/matplotlib/cycler"
makedepends=('python3-setuptools' 'python2-setuptools' 'python3-six' 'python2-six')
source=("https://github.com/matplotlib/cycler/archive/v$pkgver.tar.gz")
sha1sums=('c7591a281ddd0a30c542d04ea8ff671f1dc5503f')

prepare() {
  cp -a cycler-$pkgver{,-py2}
}

build() {
  cd "$srcdir/cycler-$pkgver"
  python3 setup.py build

  cd "$srcdir/cycler-$pkgver-py2"
  python2 setup.py build
}

package_python3-cycler() {
  depends=('python3-six')

  cd cycler-$pkgver
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cycler() {
  depends=('python2-six')

  cd cycler-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
