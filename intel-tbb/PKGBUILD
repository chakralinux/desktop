# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

pkgname=intel-tbb
pkgver=3.0_20101215
pkgrel=1
pkgdesc='An award-winning C++ runtime library that abstracts the low-level threading details necessary for optimal multi-core performance.'
arch=('i686' 'x86_64')
url='http://www.threadingbuildingblocks.org/'
license=('GPL')
source=("http://www.threadingbuildingblocks.org/uploads/77/164/3.0%20Update%205/tbb30_20101215oss_src.tgz")
md5sums=('d1f65b7ba8bafda5a8616dfc8159ea05')

build() {
  cd tbb${pkgver/\./}oss

  make
}

package() {
  cd tbb${pkgver/\./}oss

  install -d ${pkgdir}/usr/lib
  install -m755 build/linux_*/*.so* ${pkgdir}/usr/lib

  install -d ${pkgdir}/usr/include
  cp -pr include/tbb ${pkgdir}/usr/include
}
