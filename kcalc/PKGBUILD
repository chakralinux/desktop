# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdeutils-kcalc/
# Include global configuration
source ../kdeapps.conf

pkgname="kcalc"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=2
pkgdesc="Scientific calculator"
url="https://www.kde.org/applications/utilities/kcalc"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
depends=('kinit')
makedepends=('extra-cmake-modules' 'python3' 'kdoctools')
conflicts=('kdeutils-kcalc')
replaces=('kdeutils-kcalc')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeutils')
install=kdeutils.install
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        "qt5.6.patch")
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`
            "9c8367e31b8f91cb1ea852dc09b20d19492c09010159b536caceb95950986d9b")

prepare() {
  mkdir -p build
 
 # Workaround for null size window with Qt 5.6 https://bugs.kde.org/show_bug.cgi?id=360105
  cd $pkgname-$pkgver
  patch -p1 -i ../qt5.6.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
