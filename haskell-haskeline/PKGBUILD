#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=haskell-haskeline
_basename=haskell
_pkgname=haskeline
pkgver=0.6.4.3
ghcver=7.0.3
pkgrel=1
pkgdesc="A command-line interface for user input, written in Haskell"
url="http://hackage.haskell.org/package/haskeline"
license=('BSD')
arch=('i686' 'x86_64')
depends=(ghc=$ghcver haskell-utf8-string haskell-mtl haskell-terminfo)
install=$pkgname.install
source=(http://hackage.haskell.org/packages/archive/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.gz)
md5sums=('93d2f523608341cd56293edda00c4623')

build() {
  cd $srcdir/$_pkgname-$pkgver

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script

  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
  cd $srcdir/$_pkgname-$pkgver

  install -Dm744 register.sh	$pkgdir/usr/share/$_basename/$pkgname/register.sh
  install -Dm744 unregister.sh	$pkgdir/usr/share/$_basename/$pkgname/unregister.sh
  install -dm755 $pkgdir/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/$pkgname/html $pkgdir/usr/share/doc/ghc/html/libraries/$_pkgname
  
  runhaskell Setup copy --destdir=$pkgdir

  # Legal.
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/license.txt
  rm -f $pkgdir/usr/share/doc/$pkgname/LICENSE
}
