# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="kdeutils-ark"
_pkgname="ark"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=3
pkgdesc="Archiver for various compression formats"
url="http://www.kde.org"
license=('GPL'
         'LGPL'
         'FDL')
options=('docs'
         '!header'
         'debug'
         'log')
depends=("kde-runtime>=${_kdever}"
         "kde-baseapps>=${_kdever}"
         'libarchive=3.1.2'
         'qjson')
optdepends=('p7zip'
            'zip'
            'unzip'
            'unrar')
conflicts=("kdeutils-common"
           "kdeutils-doc")
groups=("kde"
        "kdeutils"
        "kde-uninstall")
categories=('utils')
provides=('ark')
install=kdeutils.install
makedepends=('pkg-config'
             'cmake'
             'automoc4'
             "kdelibs>=${_kdever}"
             "kde-baseapps>=${_kdever}"
             "kde-workspace=4.11.7" 
             'libarchive'
             'p7zip'
             'zip'
             'unzip'
             'unrar'
             'qjson'
             'docbook-xsl')
source=($_mirror/${_pkgname}-$_kdever.tar.xz)
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
