


# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=('kde-baseapps-kwrite')
_pkgname=kate
pkgver=${_kdever}
pkgrel=1
url="http://kde.org/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
pkgdesc="Text Editor"
depends=("kde-runtime>=${_kdever}" 'kdesdk-kate')
makedepends=("kdelibs>=${_kdever}" 'cmake' 'automoc4' 'docbook-xsl' "kdebindings-pykde4>=${_kdever}")
groups=('kde' 'kde-baseapps' 'kde-uninstall')
provides=('kdebase-kwrite' 'kwrite')
replaces=('kdebase-kwrite')
install='kde-baseapps-kwrite.install'
source=("$_mirror/${_pkgname}-$_kdever.tar.xz"
        'pkgbuild-syntax-highlight.patch')
sha1sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`
          '80c324c9db81c07a02374e7a092d75e0bdd6b4ea')

options=('docs' '!header' 'debug' 'log')

build() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	patch -p1 -i "${srcdir}"/pkgbuild-syntax-highlight.patch

        rm -rf build
        mkdir build && cd build      

	msg "starting build ..."
	cmake .. -DCMAKE_BUILD_TYPE=${_build_type} \
	         -DCMAKE_INSTALL_PREFIX=${_installprefix} \
		 -DBUILD_KTEXTEDITOR=OFF \
		 -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' 
	make
}

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}/build/kwrite
	make DESTDIR="${pkgdir}" install

	cd ${srcdir}/${_pkgname}-${pkgver}/build/doc/kwrite
	make DESTDIR="${pkgdir}" install
}
