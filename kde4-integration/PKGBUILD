pkgbase="kde-workspace"
pkgname=("kde4-integration")
arch=('x86_64')
pkgver=4.11.22
pkgrel=2
pkgdesc=("Qt4/KDE4 integration")
url="http://www.kde.org"
license=('GPL')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebindings-python2' "xcb-util-image" "xcb-util-renderutil" "xcb-util-keysyms" 'libkactivities4' 'xorg-xkb-utils'
             'libxcomposite' 'libxss' 'qimageblitz' 'libxft' 'libxdamage' 'libxrandr' 'libxres')
depends=("kdelibs")
options=('debug')
groups=('kde' 'kde-uninstall' 'kde-minimal')
source=("http://download.kde.org/stable/applications/15.08.0/src/kde-workspace-4.11.22.tar.xz")

sha256sums=('f035334e843d67ee88551ae9e6c5f64bf7b1edfe311b12501575fe74be0b03b7')

prepare() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    mkdir build
}

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}/build"

    CXXFLAGS="${CXXFLAGS} -I /usr/include/freetype2" cmake -Wno-dev .. \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DWITH_Xmms=OFF \
        -DCMAKE_SKIP_RPATH=ON \
        -DWITH_CkConnector=OFF \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make -C qguiplatformplugin_kde
}


package() { 
    cd "${srcdir}/${pkgbase}-${pkgver}/build"
    make -C qguiplatformplugin_kde DESTDIR=${pkgdir} install
}

