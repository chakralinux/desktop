pkgname=qbittorrent
pkgver=3.3.10
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt using the good libtorrent library"
arch=('x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt5-base' 'libtorrent-rasterbar>=1.0.6' 'xdg-utils' 'boost-libs' 'python3' 'geoip-database')
makedepends=('boost' 'geoip' 'qt5-tools')
categories=('network')
screenshot=('http://sourceforge.net/projects/qbittorrent/screenshots/242189.jpg')
install=qbittorrent.install
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'qbittorrent-libtorrent-1.1.1.patch')
sha256sums=('2c657ceacdc84033da044a0a9ecba7e0fdb35858324dc097546f989166f1a8d2'
            'c7642755dbc72567e04326b362eb862a62fc225984934d08fd808727d3bd6578')

prepare() {
  cd $pkgbase-$pkgver

  patch -Np1 -i "$srcdir/qbittorrent-libtorrent-1.1.1.patch"
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make INSTALL_ROOT=${pkgdir} install
    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING
}
