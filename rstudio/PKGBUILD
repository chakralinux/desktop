# Maintainer: Francesco Marinucci <franzmari [at] chakra-project[dot]it>

pkgname=rstudio
_pkgname=rstudio-rstudio
pkgver=0.98.507
_pkgver=ee098fe
pkgrel=1
pkgdesc="A new integrated development environment (IDE) for R - The Desktop Client"
arch=('x86_64')
url="http://www.rstudio.org/"
license=('AGPL')
depends=('boost-libs' 'java-environment' 'qtwebkit' 'r')
makedepends=('apache-ant' 'boost' 'bzip2' 'cmake' 'git' 'icu' 'openssl' 'pam' 'unzip' 'wget' 'libcups')
provides=('rstudio-desktop')
categories=('programming')
source=("https://github.com/$pkgname/$pkgname/tarball/v$pkgver")
md5sums=('6e223c600dcc02855c37ec0cda0c97f5')

build() {
  cd $srcdir/$_pkgname-$_pkgver/dependencies/common
  sh install-gwt
  sh install-dictionaries
  sh install-mathjax

  # Configure and build.
  cd $srcdir/$_pkgname-$_pkgver

  mkdir build 
  cd build
  unset LDFLAGS
  cmake \
    -DRSTUDIO_TARGET=Desktop \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr/local/lib/$pkgname \
    ..
}

package() {
  cd $srcdir/$_pkgname-$_pkgver/build
  make DESTDIR=$pkgdir install
}
