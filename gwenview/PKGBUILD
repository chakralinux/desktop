# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/gwenview

pkgname=gwenview
pkgver=18.12.1
pkgrel=1
pkgdesc="A fast and easy to use image viewer for KDE"
arch=('x86_64')
url="http://kde.org/applications/graphics/gwenview/"
screenshot="https://www.kde.org/images/screenshots/gwenview.png"
license=('GPL' 'LGPL' 'FDL')
depends=('kded' 'kactivities-frameworks' 'libkdcraw' 'libkipi' 'baloo-kf5' 'lcms2' 'exiv2')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegraphics')
provides=('kdegraphics-gwenview')
replaces=('kdegraphics-gwenview')
conflicts=('kdegraphics-gwenview')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d5095aea3bd2c8c8661a69b276fd5a536133696789b56a21ba4ce96b7d543906'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
  -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
