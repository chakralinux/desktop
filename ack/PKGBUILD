pkgname=ack
pkgver=2.17.02
_ver=${pkgver%.*}_${pkgver##*.}
pkgrel=1
pkgdesc="A Perl-based grep replacement, aimed at programmers with large trees of heterogeneous source code"
arch=('x86_64')
url="http://betterthangrep.com/"
license=('GPL' 'PerlArtistic')
depends=('perl-file-next')
options=('!emptydirs')
categories=('programming')
source=(https://github.com/petdance/ack2/archive/${_ver}.tar.gz)
sha256sums=('4aa7e67b87bb8850fb2b2a8bf036e384bfa5fe0388ac483acf1ce430dc5d4fb4')

build() {
  cd "$srcdir/ack2-$_ver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/ack2-$_ver"
  make DESTDIR="$pkgdir" install
}
