# Maintainer (x86_64): Manuel Tortosa <manutortosa@chakra-project@org>

pkgname=qhull
pkgver=2012.1
pkgrel=1
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=('x86_64')
url="http://www.qhull.org/"
license=('custom')
depends=('glibc')
makedepends=('cmake')
source=(http://www.qhull.org/download/$pkgname-$pkgver-src.tgz)
options=('!libtool')
sha1sums=('60f61580e1d6fbbd28e6df2ff625c98d15b5fbc6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ -d build ] || mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DDOC_INSTALL_DIR=/usr/share/doc \
	-DLIB_INSTALL_DIR=/usr/lib ..
  make
}

package() { 
  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR=$pkgdir install

  # install manpages
  install -Dm644 $srcdir/$pkgname-$pkgver/html/rbox.man $pkgdir/usr/share/man/man1/rbox.1
  install -Dm644 $srcdir/$pkgname-$pkgver/html/qhull.man $pkgdir/usr/share/man/man1/qhull.1

  # Install license
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

