pkgname=dblatex
pkgver=0.3.5
pkgrel=1
pkgdesc="DocBook (XML and SGML) to DVI, PDF, PostScript converter using latex"
arch=('x86_64')
url="http://dblatex.sourceforge.net/"
license=(GPL)
depends=('docbook-xml' 'docbook-xsl' 'latex-epic' 'libxslt' 'python2' 'texlive-core' 'texlive-htmlxml' 'texlive-latexextra')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.bz2"
	dblatex-0.3.5-build-fix.patch)

sha512sums=('ea667015c9a5452aea5ffbc62dabf0b136a19dd8ecc45cb91094ae0ac190f6dd2d7ecf94aaef4fc96d6ffea69f48d197b76da773a06ddff89550480153fa88b4'
	 '851292bf592a56520131a5675e481a425eac6752ff524f12ccaa903444663767a77b09661f7c1833b98574ba1e797da0903a96a980f4fab07047d1bae64cfdd6')

package() {
  cd $srcdir/$pkgname-$pkgver

  patch -Np0 -i ../dblatex-0.3.5-build-fix.patch
  
  python2 setup.py install \
    --root $pkgdir \
    --catalogs=/etc/xml/docbook
  
  # Python 2 fix.
  sed -i -e "s,${pkgdir},," -e "s,#!/usr/bin/env python,#!/usr/bin/env python2," $pkgdir/usr/bin/dblatex

  # Texlive fix. Without it, ‘ghc’ package documentation won’t compile.
  # NOTE: see http://www.haskell.org/pipermail/glasgow-haskell-users/2011-March/020199.html
  sed -i -e "s#<xsl:param name=\"texlive.version\">2010</xsl:param>#<xsl:param name=\"texlive.version\">2009</xsl:param>#" $pkgdir/usr/share/$pkgname/xsl/param.xsl
}
