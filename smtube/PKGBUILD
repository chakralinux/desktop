pkgname=smtube
pkgver=18.1.0
pkgrel=1
pkgdesc="Search and download videos from YouTube"
arch=('x86_64')
url="https://www.smtube.org/"
license=('GPL')
depends=('qt5-script' 'qt5-webkit' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('qt5-tools')
optdepends=('mplayer: play videos with MPlayer'
            'smplayer: play videos with SMPlayer'
            'dragon: play videos with Dragon Player'
            'vlc: play videos with VLC'
            'mpv: play videos with MPV'
            'youtube-dl: download videos')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/smtube/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('a1a970af6c4833c65ecad4c468574b903f601372e9eabf5e5daeb398135a82eb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make QMAKE=qmake-qt5 LRELEASE=lrelease-qt5 PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
