# Include global configuration

pkgname=kfind
pkgver=18.12.2
pkgrel=1
pkgdesc="Find Files/Folders"
arch=(x86_64)
url="https://www.kde.org/applications/utilities/kfind/"
license=(LGPL)
depends=(kio kfilemetadata)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kde-baseapps-kfind)
replaces=(kde-baseapps-kfind)
options=(docs debug)
groups=(kde kde-uninstall kde-applications kde-baseapps)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('59281180b995331bef1243e9b56a62a9a3c2702de19ccb081faf9ddf375a8600'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
