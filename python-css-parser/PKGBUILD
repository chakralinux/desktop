# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=css-parser
pkgbase='python-css-parser'
pkgname=('python3-css-parser' 'python2-css-parser')
pkgver=1.0.4
pkgrel=1
pkgdesc="A CSS Cascading Style Sheets library for Python"
arch=('x86_64')
url="https://github.com/ebook-utils/${_pkgname}"
license=('LGPL')
makedepends=('python2-setuptools' 'python3-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('3781128e55c1e72aa00369873e214f683e3f42944ae83d399697adb7d2966f68fb500c39982845ce9cd06078e4021838d82b06f99d90938069371da8a89a0857'
            'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

build() {
    cd $_pkgname-$pkgver

    python3 setup.py build
    python2 setup.py build
}

package_python3-css-parser() {
    depends=('python3')

    cd $_pkgname-$pkgver
    python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-css-parser() {
    depends=('python2')

    cd $_pkgname-$pkgver
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
