# Original Arch Linux Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=chkrootkit
pkgver=0.49
pkgrel=1
pkgdesc="Locally checks for signs of a rootkit."
arch=('i686' 'x86_64')
url="http://www.chkrootkit.org"
depends=('sh' 'net-tools')
license=('BSD')
source=("ftp://ftp.pangeia.com.br/pub/seg/pac/${pkgname}.tar.gz"
        'chkrootkit.cron'
        'fix-tools-path.patch'
        'backslashes.patch'
        'kallsyms.patch')
md5sums=('304d840d52840689e0ab0af56d6d3a18'
         'f4b6494270f708bf016e087104681739'
         '858b9e4fe1c38ca712f24f0ca1535c1e'
         'd6e43518968607682a7eed86123891ec'
         'a1afdd938b09c11a5200aad0a8abed9d')
sha1sums=('cec1a3c482b95b20d3a946b07fffb23290abc4a6'
          '6dda90abf779b6f5c3bacd638e1231f34635575d'
          '8d488aa47d7a74755eee94158bbe66c84061592e'
          '8a81ed7a109ad087685ad362ffe82e64d6997d83'
          '295d729516d31fa7e810bd6e61296e1da827915d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/var/adm|/var/log|' check_wtmpx.c chklastlog.c chkutmp.c chkwtmp.c
  patch -p0 -i "${srcdir}/fix-tools-path.patch"
  patch -p1 -i "${srcdir}/backslashes.patch"
  patch -p1 -i "${srcdir}/kallsyms.patch"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  for i in check_wtmpx chkdirs chklastlog chkproc chkrootkit \
           chkutmp chkwtmp ifpromisc strings-static ; do
    install -D -m755 "${i}" "${pkgdir}/usr/sbin/${i}"
  done
  install -D -m744 "${srcdir}/chkrootkit.cron" "${pkgdir}/etc/cron.weekly/chkrootkit"
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
