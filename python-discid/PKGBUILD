# Maintainer:  Ram-Z <ram-z[at]chakraos[dot]org>
# Contributor: Johannes Dewender	 arch at JonnyJD dot net
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgbase=python-discid
pkgver=1.1.1
pkgrel=2
_pkgname=discid
pkgname=('python3-discid' 'python2-discid')
pkgdesc='Python binding of MusicBrainz libdiscid'
url='https://python-discid.readthedocs.org/'
license=('LGPL3')
arch=('any')
depends=('libdiscid')
makedepends=('python3' 'python2')
validpgpkeys=('8BB230F6C5C308B31FE7C82568304838CC686607')
_magic='a2/76/463785b1715b461c2fc0dad73b115e6ca061dbd768154b88242974d04a9f'
source=("https://pypi.python.org/packages/$_magic/$_pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('78a3bf6c8377fdbe3d85e914a209ff97aa43e35605779639847b510ced31f7b9'
            'SKIP')

check() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py test
}

package_python3-discid() {
	depends+=('python')
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-discid() {
	depends+=('python2')
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python2 setup.py install --root="${pkgdir}" --optimize=1
}
