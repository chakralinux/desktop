# Maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>

## NOTE: when you update Haskell packages you'll have to update the entire stack to not break it ##

_hkgname=dataenc
pkgname=haskell-dataenc
pkgver=0.14.0.7
pkgrel=1
pkgdesc="Data encoding library"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc')
options=('strip' 'staticlibs')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('28491649dbeafb9512b4ea187cc6fddf')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure \
        -O \
        -p \
        --enable-split-objs \
        --enable-shared \
        --prefix="/usr" \
        --docdir="/usr/share/doc/haskell-dataenc" \
        --libsubdir='$compiler/site-local/$pkgid'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    
    # Documentation
    ln -s "/usr/share/doc/haskell-dataenc/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/dataenc"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/haskell-dataenc/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/haskell-dataenc/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
