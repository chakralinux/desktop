#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=opal
pkgver=3.10.9
pkgrel=1
pkgdesc="Open Phone Abstraction Library"
arch=('x86_64')
url="http://www.opalvoip.org/"
license=('MPL')
depends=('gsm' 'libtheora' 'ptlib' 'silc-toolkit' 'speex' 'x264')
makedepends=('ffmpeg')
changelog=ChangeLog
source=("http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        "$pkgname-ffmpeg.diff")
md5sums=('bb8eeb28321035663738e4fc8d6eb852'
         'b3655f313db17dfea44cbabe4cf850f5')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Patches.
  patch -Np1 -i "${srcdir}/$pkgname-ffmpeg.diff"

  CXXFLAGS="$CXXFLAGS -fpermissive" \
  ./configure \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  chmod 644 "${pkgdir}/usr/lib/libopal_s.a"
}
