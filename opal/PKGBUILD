# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=opal
pkgver=3.12.8
pkgrel=1
pkgdesc="Open Phone Abstraction Library"
arch=('x86_64')
url="http://www.opalvoip.org/"
license=('MPL')
depends=('gsm' 'libtheora' 'ptlib' 'silc-toolkit' 'speex' 'x264')
makedepends=('ffmpeg-compat')
source=(http://downloads.sourceforge.net/opalvoip/$pkgname-$pkgver.tar.bz2)
md5sums=('db928c30ae0de43b6063a17dc6fe5106')

build() {
  cd $srcdir/$pkgname-$pkgver

  CXXFLAGS="$CXXFLAGS -fpermissive" \
  ./configure \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  chmod 644 "${pkgdir}/usr/lib/libopal_s.a"
}
