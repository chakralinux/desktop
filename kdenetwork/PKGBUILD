#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

#
# package info
#
pkgbase=('kdenetwork')
pkgname=('kdenetwork-filesharing'
         'kdenetwork-kdnssd'
         'kdenetwork-kget'
         'kdenetwork-kopete'
         'kdenetwork-kppp'
         'kdenetwork-krdc'
         'kdenetwork-krfb')

arch=('i686' 'x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="split package"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' '!splithdr' 'splitdbg' 'log')

makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'speex' 'ortp' 'libotr' 'qca-ossl' 
	     "kde-workspace>=${_kdever}" "kde-baseapps>=${_kdever}" 'libvncserver' 'libmsn' 'libmms'
	     'ppp' 'libv4l' 'libidn' 'rdesktop' 'qimageblitz' 'libxss' 'libxdamage' 'libgadu' 
	     'telepathy-qt4' 'meanwhile' 'mediastreamer' 'kwebkitpart>=0.9.6' 'libktorrent>=1.0.5' 'msilbc' 'docbook-xsl')

source=("$_mirror/${pkgbase}-$_kdever.tar.xz"
        'lisa.daemon')

md5sums=(`grep ${pkgbase}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '66069c83fd5fa0df5d743d4486799033')  # lisa.daemon


#
# build function
#
build() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DWITH_Xmms=OFF \
		-DWITH_Decibel=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make || return 1
}

#
# split-install functions
#
package_kdenetwork-filesharing() {
	pkgdesc='Konqueror properties dialog plugin to share a directory with the local network'
	depends=("kdelibs>=${_kdever}" 'smbclient')
        conflicts=("kdenetwork-common" "kdenetwork-doc")
	groups=("kde" "${pkgbase}" "kde-uninstall")
	conflicts=("kdenetwork-common")
	categories=('network')
	splitdirs="filesharing"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-kdnssd() {
	pkgdesc='Monitors the network for DNS-SD services'
	depends=("kdelibs>=${_kdever}")
	conflicts=("kdenetwork-common" "kdenetwork-doc")
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('network')

	splitdirs="kdnssd"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-kget() {
	pkgdesc="Download manager"
	url="http://kde.org/applications/internet/kget/"
	depends=("kde-workspace>=${_kdever}" 'qca' 'kwebkitpart>=0.9.6' 'libktorrent>=1.0.4' 'libmms')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('network')
        conflicts=("kdenetwork-common" "kdenetwork-doc")
	install=${pkgbase}.install

	splitdirs="kget doc/kget"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-kopete() {
	pkgdesc="Kopete, the instant messenger"
	url="http://kde.org/applications/internet/kopete/"
	depends=("kde-runtime>=${_kdever}" "kdepimlibs>=${_kdever}"
	         'qca-ossl' 'libotr' 'libmsn' 'libidn' 'qimageblitz' 'libxss' 'libv4l' 'libgadu' 'meanwhile' 'mediastreamer' 'msilbc')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('communication')
        conflicts=("kdenetwork-common" "kdenetwork-doc")
	install=${pkgbase}.install

	splitdirs="kopete doc/kopete"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-kppp() {
	pkgdesc="PPP Client"
	url="http://kde.org/applications/internet/kppp/"
	depends=("kde-runtime>=${_kdever}" 'ppp')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('network')
        conflicts=("kdenetwork-common" "kdenetwork-doc")
	install=${pkgbase}.install

	splitdirs="kppp doc/kppp"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-krdc() {
	pkgdesc="Desktop Sharing Client"
	url="http://kde.org/applications/internet/krdc/"
	depends=("kde-runtime>=${_kdever}" 'libvncserver' 'rdesktop' 'telepathy-qt4')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('network')
        conflicts=("kdenetwork-common" "kdenetwork-doc")
	install=${pkgbase}.install

	splitdirs="krdc doc/krdc"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-krfb() {
	pkgdesc="Desktop Sharing Server"
	# note on libxdamage:
	#       not detected by namcap because libgl depends on it
	#       but nvidia providing libgl does not depend on libxdamage
	depends=("kde-runtime>=${_kdever}" "kdelibs>=${_kdever}" 'libvncserver' 'libxdamage')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('network')
        conflicts=("kdenetwork-common" "kdenetwork-doc")
	install=${pkgbase}.install

	splitdirs="krfb doc/krfb"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}
