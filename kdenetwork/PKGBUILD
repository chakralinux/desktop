#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

#
# package info
#
pkgbase="kdenetwork"
pkgname=('kdenetwork-common'
         'kdenetwork-doc'
         'kdenetwork-kget'
         'kdenetwork-kopete'
         'kdenetwork-kppp'
         'kdenetwork-krdc'
         'kdenetwork-krfb')

arch=('i686' 'x86_64')
pkgver=${_kdever}
pkgrel=1

pkgdesc="split package"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')

options=('docs' '!splithdr' 'splitdbg' 'log')

makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'speex' 'ortp' 'libotr'
             'qca-ossl' "kdebase-workspace>=${_kdever}" 
             "kdebase>=${_kdever}" 'libvncserver' 'libmsn'
             'ppp' 'libv4l' 'libidn' 'rdesktop' 'qimageblitz' 'libxss' 'libxdamage' 'ilbc' 'msilbc' 
             'libgadu' 'telepathy-qt4' 'meanwhile' 'mediastreamer>=2.7.3' 'kwebkitpart>=0.9.6' 'libktorrent>=1.0.5')

source=($_mirror/${pkgbase}-$_kdever.tar.bz2 \
        'lisa.daemon')
md5sums=(`grep ${pkgbase}-$_kdever.tar.bz2 ../kde-sc.md5 | cut -d" " -f1`
         '66069c83fd5fa0df5d743d4486799033')  # lisa.daemon


#
# build function
#
build()
{
	cd ${srcdir}/${pkgbase}-${pkgver}

	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DWITH_Xmms=OFF \
		-DWITH_Decibel=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make || return 1
}



#
# split-install functions
#
package_kdenetwork-common()
{
	pkgdesc="KDE Network - Common files and libraries"
	depends=("kdelibs>=${_kdever}" 'smbclient')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	install=${pkgbase}.install

	# FIXME strigi-analyzer
	splitdirs="filesharing kdnssd" 
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install

		done

	# install lisa network daemon
	install -D -m 755 ${srcdir}/lisa.daemon ${pkgdir}/etc/rc.d/kde-lisa

	# include our patches into the package
#	ls -1 ${startdir}/*.patch &>/dev/null 2>&1
#	if [ "$?" = "0" ]; then
#		warning "incuding patches into package"
#		mkdir -p ${pkgdir}/usr/share/kdemod/patches/${pkgbase} &>/dev/null
#		for i in ${startdir}/*.patch; do
#			msg "$i"
#			cp $i ${pkgdir}/usr/share/kdemod/patches/${pkgbase}/ &>/dev/null
#		done
#	else
#		warning "no patches found, skipping to include them into the package..."
#	fi
}

package_kdenetwork-doc()
{
	pkgdesc="KDE Network - Documentation"
	groups=("kde" "${pkgbase}" "kde-doc" "kde-uninstall")

	splitdirs="doc"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-kget()
{
	pkgdesc="Download manager"
	depends=("${pkgbase}-common>=${_kdever}" "kdebase-workspace>=${_kdever}" 'qca' 'kwebkitpart>=0.9.6' 'libktorrent>=1.0.4')
	provides=('kget')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	install=${pkgbase}.install

	splitdirs="kget"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-kopete()
{
	pkgdesc="Kopete, the instant messenger"
	depends=("kdebase-runtime>=${_kdever}" "kdepimlibs>=${_kdever}"
	         'qca-ossl' 'libotr' 'libmsn' 'libidn' 'qimageblitz' 'libxss' 'v4l-utils' 'libgadu' 'meanwhile' 'mediastreamer' 'msilbc')
	provides=('kopete')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	install=${pkgbase}.install

	splitdirs="kopete"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-kppp()
{
	pkgdesc="PPP Client"
	depends=("${pkgbase}-common>=${_kdever}" 'ppp')
	provides=('kppp')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	install=${pkgbase}.install

	splitdirs="kppp"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-krdc()
{
	pkgdesc="Desktop Sharing Client"
	depends=("${pkgbase}-common>=${_kdever}" 'libvncserver' 'rdesktop' 'telepathy-qt4')
	provides=('krdc')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	install=${pkgbase}.install

	splitdirs="krdc"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdenetwork-krfb()
{
	pkgdesc="Desktop Sharing Server"
	# note on libxdamage:
	#       not detected by namcap because libgl depends on it
	#       but nvidia providing libgl does not depend on libxdamage
	depends=("kdebase-runtime>=${_kdever}" "kdelibs>=${_kdever}" 'libvncserver' 'libxdamage')
	provides=('krfb')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	install=${pkgbase}.install

	splitdirs="krfb"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}
