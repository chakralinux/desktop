# Include global configuration
source ../kdeapps.conf

pkgbase=('kdewebdev')
pkgname=('kdewebdev-kfilereplace'
         'kdewebdev-kimagemapeditor'
         'kdewebdev-kommander'
         'kdewebdev-klinkstatus')
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
url="http://www.kde.org/"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'ruby' "kdepimlibs4"
             "kde-runtime" 'tidyhtml' "kdepim-runtime>=${_libsver}" 'kdoctools' 'boost')
source=($_mirror/${pkgbase}-$_kdever.tar.xz)
sha256sums=(`grep ${pkgbase}-$_kdever.tar.xz  ../checksums.txt | cut -d" " -f1`)

build() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package_kdewebdev-kfilereplace() {
    pkgdesc="Utility that replace some strings with others in a lot of files in an only operation"
    depends=("kde-runtime" "kdelibs>=${_libsver}")
    groups=("kde" "${pkgbase}" "kde-uninstall")
    categories=('utils')
    conflicts=('kdewebdev-common' Â'kdewebdev-doc')
    install=${pkgbase}.install

    splitdirs="kfilereplace doc/kfilereplace"
    for i in ${splitdirs} ; do
        cd ${srcdir}/${pkgbase}-${pkgver}/${i}
        make DESTDIR=${pkgdir} install
    done
}

package_kdewebdev-kimagemapeditor() {
    pkgdesc="Image map editor"
    depends=("kde-runtime" "kdelibs>=${_libsver}")
    conflicts=('kdewebdev-common' 'kdewebdev-doc')
    groups=("kde" "${pkgbase}" "kde-uninstall")
    categories=('graphics')
    install=${pkgbase}.install

    splitdirs="kimagemapeditor doc/kimagemapeditor"
    for i in ${splitdirs} ; do
        cd ${srcdir}/${pkgbase}-${pkgver}/${i}
        make DESTDIR=${pkgdir} install
    done
}

package_kdewebdev-kommander() {
    pkgdesc="Visual application builder"
    depends=("kde-runtime" "kdelibs>=${_libsver}")
    conflicts=('kdewebdev-common' 'kdewebdev-doc')
    groups=("kde" "${pkgbase}" "kde-uninstall")
    categories=('programming')
    install=${pkgbase}.install

    splitdirs="kommander" # FIXME: doc/kommander
    for i in ${splitdirs} ; do
        cd ${srcdir}/${pkgbase}-${pkgver}/${i}
        make DESTDIR=${pkgdir} install
    done
}

package_kdewebdev-klinkstatus() {
    pkgdesc="Link checking utility"
    depends=("kde-runtime" "kdelibs>=${_libsver}" 'tidyhtml' "kdepimlibs4") # "kdepim-runtime>=${_kdever}"
    conflicts=('kdewebdev-common' 'kdewebdev-doc')
    groups=("kde" "${pkgbase}" "kde-uninstall")
    categories=('utils')
    install=${pkgbase}.install

    splitdirs="klinkstatus doc/klinkstatus"
    for i in ${splitdirs} ; do
        cd ${srcdir}/${pkgbase}-${pkgver}/${i}
        make DESTDIR=${pkgdir} install
    done
}
