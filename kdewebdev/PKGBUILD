#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf



# original name (used for the source and pkgnames)
_origname=kdewebdev



#
# package info
#
pkgbase=('kdewebdev')
pkgname=('kdewebdev-common'
         'kdewebdev-doc'
         'kdewebdev-kfilereplace'
         'kdewebdev-kimagemapeditor'
         'kdewebdev-kommander'
         'kdewebdev-klinkstatus')

arch=('i686' 'x86_64')
pkgver=${_kdever}
pkgrel=1

pkgdesc="split package"
url="http://www.kde.org/"
license=('GPL' 'LGPL' 'FDL')

options=('docs' '!splithdr')

makedepends=('pkgconfig' 'cmake' 'automoc4' 'ruby' "kdepimlibs>=${_kdever}"
             "kdebase-runtime>=${_kdever}" 'tidyhtml' "kdepim-runtime>=${_kdever}")

source=($_mirror/${_origname}-$_kdever.tar.bz2)
md5sums=('f3e958278ab8dd2defce029b5f61da87')  # kdewebdev-4.4.4.tar.bz2



#
# build function
#
build()
{
	cd ${srcdir}/${_origname}-${pkgver}

	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make || return 1
}



#
# split-install functions
#
package_kdewebdev-common()
{
	pkgdesc="KDE webdev - Common files and libraries"
	provides=("${_origname}=${_kdever}")
	conflicts=("kdemod-${_origname}-common")
	groups=("kde-complete" "${_origname}" "kde-uninstall")
	install=${_origname}.install

	# include our patches into the package
	ls -1 ${startdir}/*.patch &>/dev/null 2>&1
	if [ "$?" = "0" ]; then
		warning "incuding patches into package"
		mkdir -p ${pkgdir}/usr/share/kdemod/patches/${_origname} &>/dev/null
		for i in ${startdir}/*.patch; do
			msg "$i"
			cp $i ${pkgdir}/usr/share/kdemod/patches/${_origname}/ &>/dev/null
		done
	else
		warning "no patches found, skipping to include them into the package..."
	fi
}

package_kdewebdev-doc()
{
	pkgdesc="KDE webdev - Documentation"
	depends=("${_origname}-common>=${_kdever}")
	conflicts=("kdemod-${_origname}-doc")
	groups=("kde-complete" "${_origname}" "kde-doc" "kde-uninstall")

	splitdirs="doc"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${_origname}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdewebdev-kfilereplace()
{
	pkgdesc="Utility that replace some strings with others in a lot of files in an only operation"
	depends=("${_origname}-common>=${_kdever}" "kdebase-runtime>=${_kdever}"
		 "kdelibs>=${_kdever}")
	conflicts=("kdemod-${_origname}-kfilereplace")
	groups=("kde-complete" "${_origname}" "kde-uninstall")
	install=${_origname}.install

	splitdirs="kfilereplace"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${_origname}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdewebdev-kimagemapeditor()
{
	pkgdesc="Image map editor"
	depends=("${_origname}-common>=${_kdever}" "kdebase-runtime>=${_kdever}"
		 "kdelibs>=${_kdever}")
	conflicts=("kdemod-${_origname}-kimagemapeditor")
	groups=("kde-complete" "${_origname}" "kde-uninstall")
	install=${_origname}.install

	splitdirs="kimagemapeditor"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${_origname}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdewebdev-kommander()
{
	pkgdesc="Kommander, the visual application builder"
	depends=("${_origname}-common>=${_kdever}" "kdebase-runtime>=${_kdever}"
		 "kdelibs>=${_kdever}")
	conflicts=("kdemod-${_origname}-kommander")
	groups=("kde-complete" "${_origname}" "kde-uninstall")
	replaces=('kdewebdev-kommander-devel')
	install=${_origname}.install

	splitdirs="kommander"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${_origname}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdewebdev-klinkstatus()
{
	pkgdesc="A Link checker utility"
	depends=("${_origname}-common>=${_kdever}" "kdebase-runtime>=${_kdever}"
		 "kdelibs>=${_kdever}" 'tidyhtml' "kdepim-runtime>=${_kdever}")
	conflicts=("kdemod-${_origname}-klinkstatus")
	groups=("kde-complete" "${_origname}" "kde-uninstall")
	install=${_origname}.install

	splitdirs="klinkstatus"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${_origname}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

