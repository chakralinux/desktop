#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Andrea Fagiani <andfagiani {at} gmail {dot} com>

pkgname=libxft-ubuntu
_pkgname=libxft
_pkgname2=libXft
_pkgname3=xft
pkgver=2.3.1
_pkgver=2.3.1-1
pkgrel=1
pkgdesc="FreeType-based font drawing library for X, with Ubuntu's LCD rendering patches."
arch=('x86_64')
url="https://launchpad.net/ubuntu/natty/+source/xft"
license=('GPL')
depends=('fontconfig-ubuntu>=2.8.0' 'freetype2-ubuntu' 'libxrender' 'sh')
conflicts=("${_pkgname}")
provides=("${_pkgname}=${pkgver}")
source=("http://xorg.freedesktop.org/releases/individual/lib/$_pkgname2-$pkgver.tar.bz2"
        "http://archive.ubuntu.com/ubuntu/pool/main/x/xft/${_pkgname3}_$_pkgver.diff.gz")
sha256sums=('7fce32b92dcb7b2869bed567af2abc7bbad0d5d6fcf471b8a3e137964a31bbbd'
            'd8b26c364338a1a77290e4c91c0a9410a5982f0dc6fe070ba84158ac0c3942c0')

prepare() {
  cd ${_pkgname2}-${pkgver}
  # Patches.
  patch -Np1 -i ${srcdir}/${_pkgname3}_${_pkgver}.diff
  #for FILE in $(cat debian/patches/series) ; do
  #  [[ $FILE = "001_no_export_freetype.diff" ]] && continue
  #  patch -Np1 -i debian/patches/$FILE
  #done
}

build() {
  cd ${_pkgname2}-${pkgver}

  # Configure and build.
  ./configure \
    --disable-static \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd ${_pkgname2}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # License.
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
