#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Andrea Fagiani <andfagiani {at} gmail {dot} com>

pkgname=libxft-ubuntu
_pkgname=libxft
_pkgname2=libXft
_pkgname3=xft
pkgver=2.2.0
_pkgver=2.1.14-2ubuntu1
pkgrel=1
pkgdesc="FreeType-based font drawing library for X, with Ubuntu's LCD rendering patches."
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntu/natty/+source/xft"
license=('GPL')
depends=('fontconfig-ubuntu>=2.6.0' 'freetype2-ubuntu' 'libxrender' 'sh')
conflicts=($_pkgname)
provides=("$_pkgname=$pkgver")
source=(http://xorg.freedesktop.org/releases/individual/lib/$_pkgname2-$pkgver.tar.bz2
        http://archive.ubuntu.com/ubuntu/pool/main/x/xft/${_pkgname3}_$_pkgver.diff.gz)
md5sums=('cce3c327258116493b753f157e0360c7'
         'b965b9575673ebed2a86992b4096731d')

build() {
  cd $srcdir/$_pkgname2-$pkgver

  # Patches.
  patch -Np1 -i $srcdir/${_pkgname3}_$_pkgver.diff
  for FILE in $(cat debian/patches/series) ; do
    [[ $FILE = "001_no_export_freetype.diff" ]] && continue
    patch -Np1 -i debian/patches/$FILE
  done

  # Configure and build.
  ./configure \
    --disable-static \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$_pkgname2-$pkgver
  make DESTDIR=$pkgdir install

  # License.
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/license.txt
}
