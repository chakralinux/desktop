# Include global configuration

pkgname=kmix
pkgver=18.04.3
pkgrel=1
pkgdesc="KDE volume control program"
url='https://kde.org/applications/multimedia/kmix/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kinit libcanberra hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
conflicts=(kdemultimedia-kmix)
replaces=(kdemultimedia-kmix)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('61bb02ccbef20168229be722be68f697e60555f9a352e699c6ad6b462ef18e3d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
