pkgname=winff
pkgver=1.5.4
pkgrel=3
pkgdesc="WinFF is a GUI for the command line video converter, FFMPEG. It will convert most any video file that FFmpeg will convert. WinFF does multiple files in multiple formats at one time."
arch=('x86_64')
url="http://winff.org/"
license=('GPL')
depends=('fpc-qtbindings' 'ffmpeg')
makedepends=('lazarus' 'fpc' 'git' 'tar' 'gendesk')
install='winff.install'
source=("git://github.com/WinFF/winff.git#commit=86c3560848")
sha512sums=('SKIP')
screenshot="http://winff.org/images/2013/screenshot-1.5-ubuntu.jpg"

prepare() {
  gendesk -n -f \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --genericname 'Video converter' \
    --categories 'AudioVideo;AudioVideoEditing;Qt'
}

build() {
  cd ${srcdir}/${pkgname}/${pkgname}
  lazbuild --lazarusdir=/usr/lib/lazarus winff.lpr --ws=qt
}

package() {
  cd ${srcdir}/${pkgname}/${pkgname}
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -d "$pkgdir/usr/share/winff/"
  find . -name "*.xml" \
    -exec install -Dm644 {} "$pkgdir/usr/share/winff/" \; &> /dev/null
  install -Dm644 "$srcdir/winff.desktop" \
    "$pkgdir/usr/share/applications/winff.desktop"
  install -Dm644 'winff-icons/48x48/winff.png' \
    "$pkgdir/usr/share/pixmaps/winff.png"
  for size in 16x16 24x24 32x32 48x48; do
    install -Dm644 "winff-icons/$size/winff.png" \
      "$pkgdir/usr/share/icons/hicolor/$size/apps/winff.png"
  done
  install -dm755 "$pkgdir/usr/share/winff/languages/"
  find . -name "*.po" \
    -exec install -Dm644 {} "$pkgdir/usr/share/winff/languages/" \; &> /dev/null
  rm -f 'presets.xml'
}
