# Include global configuration

pkgname=sweeper
pkgver=18.04.2
pkgrel=1
pkgdesc='System Cleaner'
url='https://kde.org/applications/utilities/sweeper'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kactivities-stats kio)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kdeutils)
conflicts=(kdeutils-sweeper)
replaces=(kdeutils-sweeper)
provides=(kdeutils-sweeper)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c3fe45ff54260bb27e5589ba70f131168ef8806d141d0b0b20dffa360f50e1b7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
