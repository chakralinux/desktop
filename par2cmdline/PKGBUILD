# Maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>


pkgname=par2cmdline
pkgver=0.6.5
pkgrel=1
pkgdesc='A PAR 2.0 compatible file verification and repair tool'
url='https://github.com/BlackIkeEagle/par2cmdline'
license=('GPL2')
depends=('gcc-libs')
arch=('x86_64')
source=("$pkgname-$pkgver.tar.gz::https://github.com/BlackIkeEagle/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('727d668fa932dda6a013248dc8b73e6c8805ccfa45fed6bf8d99b718bbaf44a1')

build() {
	cd "$pkgname-$pkgver"
	aclocal
	automake --add-missing
	autoconf
	./configure --prefix=/usr
  make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
} 
