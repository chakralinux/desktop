# Part of chakra-project.org
#
# Maintainer: Francesco Marinucci <franzmari[at]chakra-project[dot]it>
#

pkgname=par2cmdline
pkgver=0.6.4
pkgrel=1
pkgdesc='A PAR 2.0 compatible file verification and repair tool'
url='https://github.com/BlackIkeEagle/par2cmdline'
license=('GPL2')
depends=('gcc-libs')
arch=('x86_64')
source=("$pkgname-$pkgver.tar.gz::https://github.com/BlackIkeEagle/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('431da50fdd794881bad7e1b0dee234e8102d7065f53abd7ce1e86be758810116')

build() {
	cd "$pkgname-$pkgver"
	aclocal
	automake --add-missing
	autoconf
	./configure --prefix=/usr
  make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
} 
