# Maintainer: UtG  <utg[dot]chakra.linux[at]gmail[dot]com>

pkgname=openimageio
_pkgname=oiio
pkgver=1.4.9
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications."
arch=(x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb')
makedepends=('cmake' 'qt4' 'python2' 'boost' 'mesa')
optdepends=('qt: iv image viewer'
            'python2: bindings support')
source=("https://github.com/OpenImageIO/oiio/archive/Release-${pkgver}.tar.gz")
sha1sums=('88584312ef95d5dd27b147ca4f49ce5a6aa54223')

build() {
  cd $_pkgname-Release-$pkgver

  [[ -d build ]] && rm -r build
  mkdir build && cd build

    cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYLIB_INSTALL_DIR=usr/lib/python2.7/site-packages \
      -DUSE_EXTERNAL_TBB=ON \
      -DOIIO_BUILD_TESTS=OFF \
      ..
  make
}

package() {
  cd $_pkgname-Release-$pkgver/build

  make DESTDIR="$pkgdir" install

  # license
  cd ..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

