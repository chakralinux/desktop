pkgname=openimageio
pkgver=1.8.15
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications."
arch=('x86_64')
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb' 'openssl' 'libpng' 'ptex')
makedepends=('git' 'cmake' 'qt4' 'python2' 'python3' 'boost' 'mesa' 'freetype2' 'fontconfig' 'libxrender')
optdepends=('qt4: iv image viewer'
            'python2: bindings support'
            'python3: bindings support'
            'ttf-droid: font support')
source=("git+https://github.com/OpenImageIO/oiio#tag=Release-$pkgver")
sha1sums=('SKIP')

build() {
  cd oiio

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
      -DUSE_PYTHON=ON \
      -DUSE_PYTHON3=ON \
      -DUSE_OPENSSL=ON \
      -DUSE_PTEX=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DPYLIB3_INSTALL_DIR=lib/python3.6/site-packages \
      -DOIIO_BUILD_TESTS=ON \
      -DOIIO_BUILD_TOOLS=ON \
      -DSTOP_ON_WARNING=OFF

  make
}

package() {
  cd oiio/build

  make DESTDIR="$pkgdir" install

  # license
  cd ..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
