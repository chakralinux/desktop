pkgname=openimageio
pkgver=1.6.16
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications."
arch=('x86_64')
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb'
         'ffmpeg' 'openssl' 'libraw' 'openjpeg' 'libwebp')
makedepends=('cmake' 'qt' 'python2' 'boost' 'mesa')
optdepends=('qt: iv image viewer'
            'python2: bindings support')
source=("https://github.com/OpenImageIO/oiio/archive/Release-$pkgver.tar.gz")
sha1sums=('688e1716d9660fd8d14e48105a2a207eeddc30e3')

build() {
  cd oiio-Release-$pkgver

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DUSE_OPENSSL=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DOIIO_BUILD_TESTS=OFF \
      ..
  make
}

package() {
  cd oiio-Release-$pkgver/build

  make DESTDIR="$pkgdir" install

  # license
  cd ..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
