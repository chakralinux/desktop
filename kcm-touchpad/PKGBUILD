# Maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>

pkgname=kcm-touchpad
pkgver=1.1
pkgrel=1
pkgdesc='KCM, daemon and applet for touchpad'
arch=('x86_64')
url='https://projects.kde.org/projects/playground/utils/kcm-touchpad'
license=('GPL')
depends=('kdelibs' 'xf86-input-synaptics' 'libxcb' 'libx11')
makedepends=('cmake' 'automoc4' 'xorg-server')
install="${pkgname}.install"
source=("https://github.com/sanya-m/kde-touchpad-config/archive/v${pkgver}.tar.gz")
md5sums=('e8905e7888f3a6f002338fdf1417a5b5')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kde-touchpad-config-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
