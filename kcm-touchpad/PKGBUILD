# Maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>

pkgname=kcm-touchpad
pkgver=1.1
pkgrel=1
pkgdesc='KCM, daemon and applet for touchpad'
arch=('x86_64')
url='https://projects.kde.org/projects/playground/utils/kcm-touchpad'
license=('GPL')
depends=('kde-workspace' 'xf86-input-synaptics' 'libxcb' 'libx11' 'libxi')
makedepends=('cmake' 'automoc4' 'xorg-server' 'pkg-config')
install="${pkgname}.install"
source=("https://github.com/sanya-m/kde-touchpad-config/archive/v${pkgver}.tar.gz"
        'xlibbackend.diff'
        'x11.diff')
md5sums=('e8905e7888f3a6f002338fdf1417a5b5'
         '3599c3fa2c903b079e0da4e9b2794ac7'
         '26064c12b71becad9ed27d53098a8caf')

         
prepare() {
  cd kde-touchpad-config-${pkgver}
  patch -p0 -i ${srcdir}/xlibbackend.diff
  patch -p0 -i ${srcdir}/x11.diff
}

build() {
  mkdir build
  cd build
  cmake ../kde-touchpad-config-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
