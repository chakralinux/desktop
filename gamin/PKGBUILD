#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Pulphix <crimea.v@libero.it>

pkgname=gamin
pkgver=0.1.10
pkgrel=1
pkgdesc='File and directory monitoring system defined to be a subset of the FAM (File Alteration Monitor).'
url='http://www.gnome.org/~veillard/gamin'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('python2')
optdepends=('python2: for the Python module.')
provides=('fam')
conflicts=('fam')
source=("http://www.gnome.org/~veillard/$pkgname/sources/$pkgname-$pkgver.tar.gz")
md5sums=('b4ec549e57da470c04edd5ec2876a028')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Python 2.
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' python/gamin.py

  ./configure \
    --disable-debug \
    --disable-debug-api \
    --disable-static \
    --libexecdir=/usr/lib/gamin \
    --prefix=/usr \
    --with-python=/usr/bin/python2 \
    --with-threads
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
