pkgname=kipi-plugins
pkgver=5.9.0
pkgrel=5
epoch=1
pkgdesc="A collection of plugins extending the KDE graphics and image applications as digiKam"
url="https://www.digikam.org/"
arch=('x86_64')
license=('GPL')
options=("debug")
depends=('kio' 'libkipi')
makedepends=('extra-cmake-modules' 'libksane' 'liblqr' 'doxygen' 'lensfun' 'eigen' 'boost' 'kdoctools' 'opencv'
             'kfilemetadata' 'akonadi-contacts' 'qtav' 'threadweaver' 'kcalcore' 'knotifyconfig' 'libmediawiki' 'libkvkontakte'
             'glu' 'qt5-xmlpatterns' 'exiv2' 'marble' 'jasper' 'eigen3')
optdepends=('qt5-xmlpatterns: rajce.net plugin'
              'libmediawiki: MediaWiki Export plugin'
              'libkvkontakte: VKontakte.ru Exporter plugin')
source=("https://download.kde.org/stable/digikam/digikam-$pkgver.tar.xz")
sha1sums=('f98f9311cc52173192db6ebba101231cf24af633')

prepare() {
  mkdir -p build
}

build() {

  cd build

  cmake ../digikam-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_TESTING=OFF \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_INTERNALMYSQL=ON \
    -DENABLE_APPSTYLES=ON
  make -C extra
}

package() {
  
  


  cd build/extra
  make DESTDIR=$pkgdir install
  cd $srcdir/build/po
  make DESTDIR=$pkgdir install

  # Provided by digikam
  rm $pkgdir/usr/share/locale/*/LC_MESSAGES/digikam.mo
}
 
