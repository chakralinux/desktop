pkgname=gammaray
pkgver=2.5.1
pkgrel=1
pkgdesc="tool to poke around in a Qt-application and also to manipulate the application to some extent"
arch=('x86_64')
url="http://www.kdab.com/gammaray"
license=('GPL')
depends=('qt5-base>=5.6' 'qt5-location' 'qt5-xmlpatterns' 'qt5-script' 'qt5-svg'
         'qt5-declarative' 'qt5-webkit' 'qt5-3d' 'qt5-connectivity' 'kcoreaddons'
         'git' 'qt5-graphicaleffects' 'qt5-tools' 'graphviz' 'kdstatemachineeditor')
makedepends=("cmake" 'doxygen' 'perl') # perl for pod2man
source=("https://github.com/KDAB/GammaRay/releases/download/v${pkgver}/gammaray-${pkgver}.tar.gz")
sha256sums=('fd493d4b53fdd05f288f7a8ae0f414faa38c5626269643eec64a0a858e854c61')


prepare()
{
  cd "$srcdir/${pkgname}-$pkgver"
  sed -i 's/enable_testing()//' ./CMakeLists.txt
  mkdir -p build
}

build() {
  cd "$srcdir/${pkgname}-$pkgver/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package()
{
 cd "$srcdir/$pkgname-$pkgver/build"
 make DESTDIR=${pkgdir} install
}
