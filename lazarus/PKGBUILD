pkgname=lazarus
pkgver=1.8.0
pkgrel=1
_pkgsubver=0
pkgdesc='Delphi-like IDE for FreePascal'
url='http://www.lazarus.freepascal.org/'
license=('GPL2' 'MPL' 'custom:modifiedLGPL')
arch=('x86_64')
depends=('fpc>=3.0.0' 'fpc-src>=3.0.0' 'qt5pas' 'libxslt')
provides=(lazarus-qt5)
options=('!emptydirs' '!makeflags')
categories=('programming')
install='lazarus.install'
source=("https://downloads.sourceforge.net/project/lazarus/Lazarus%20Zip%20_%20GZip/Lazarus%20${pkgver}/lazarus-$pkgver.tar.gz")
sha512sums=('dd5e02e9c0a057cf9a6b2c15605969cf4cb743aa78fc81bfdc7c566434127893cf32bab5bd1729f79b51c611bf9ae015bea8d774e0544ead42a064f1224ffd7b')

build() {
  cd $srcdir/${pkgname}
  make FPC=/usr/bin/fpc LCL_PLATFORM=qt5 bigide
}

package() {
  cd $srcdir/${pkgname}
  # skip the 'make install' mess completely and do everything manually
  mkdir -p $pkgdir/usr/lib/lazarus $pkgdir/usr/bin $pkgdir/usr/share/man/man1 $pkgdir/usr/share/doc
  rsync -a \
    --exclude="CVS"     --exclude=".cvsignore" \
    --exclude="*.ppw"   --exclude="*.ppl" \
    --exclude="*.ow"    --exclude="*.a"\
    --exclude="*.rst"   --exclude=".#*" \
    --exclude="*.~*"    --exclude="*.bak" \
    --exclude="*.orig"  --exclude="*.rej" \
    --exclude=".xvpics" \
    --exclude="killme*" --exclude=".gdb_hist*" \
    --exclude="debian"  --exclude="COPYING*" \
    --exclude="*.app"   --exclude="tools/install" \
    . "$pkgdir"/usr/lib/lazarus

  ln -s /usr/lib/lazarus/lazbuild "$pkgdir"/usr/bin/lazbuild
  cp -R install/man/man1/* "$pkgdir"/usr/share/man/man1/
  mv "$pkgdir"/usr/lib/lazarus/docs "$pkgdir"/usr/share/doc/lazarus
  mkdir -p "$pkgdir"/usr/lib/lazarus/docs
  ln -s /usr/share/doc/lazarus/chm "$pkgdir"/usr/lib/lazarus/docs/html
  ln -s /usr/share/doc/lazarus/lazdoc.css "$pkgdir"/usr/lib/lazarus/docs/lazdoc.css
  rm -r "$pkgdir"/usr/lib/lazarus/install

  # license files: /usr/lib/lazarus/COPYING*
  install -D -m644 COPYING.modifiedLGPL.txt $pkgdir/usr/share/licenses/${pkgname}/COPYING.modifiedLGPL

  # install qt binaries
  install -Dm755 lazarus $pkgdir/usr/lib/lazarus/lazarus
  install -Dm755 startlazarus $pkgdir/usr/lib/lazarus/startlazarus
  install -dm755 $pkgdir/usr/bin
  ln -s /usr/lib/lazarus/lazarus $pkgdir/usr/bin/lazarus
  ln -s /usr/lib/lazarus/startlazarus $pkgdir/usr/bin/startlazarus

  # make 'desktop-file-validate' happy and fix missing .png icon
  sed -e 's|\(Categories\).*|\1=IDE;Development;|' \
    -e 's|\.png|\.xpm|' -i install/lazarus.desktop
  install -Dm644 install/lazarus.desktop $pkgdir/usr/share/applications/lazarus.desktop
  install -Dm644 images/icons/${pkgname}256x256.png $pkgdir/usr/share/pixmaps/lazarus.png
}
