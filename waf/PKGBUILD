pkgname=waf
pkgver=1.9.9
pkgrel=1
pkgdesc='General-purpose build system modelled after Scons'
url='http://waf.io/'
arch=('any')
license=('BSD')
depends=('python3')
makedepends=('unzip')
provides=('python3-waf')
options=('!emptydirs')
source=("https://github.com/waf-project/waf/archive/waf-$pkgver.tar.gz")
sha256sums=('09a05e179faed8d3ae09e4302132fe957be1848d455b7da4f2c71ed3927a4243')

prepare() {
  cd "$pkgname-$pkgname-$pkgver"

  # Extracting license
  head -n 30 waf-light | tail -n 25 > LICENSE
  # Python 3 fix
  sed -i '0,/env python/s//python3/' waf-light
}

build() {
  cd "$pkgname-$pkgname-$pkgver"
  ./waf-light \
    configure --prefix=/usr \
    build --make-waf --tools='compat,compat15,ocaml,go,cython,scala,erlang,cuda,gcj,boost,pep8,eclipse'
}

package() {
  cd "$pkgname-$pkgname-$pkgver"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 waf "$pkgdir/usr/bin/waf"

  local waflib="$pkgdir/usr/lib/waf3-$pkgver-$(grep -aPom1 '(?<=^REVISION=")[[:xdigit:]]*(?="$)' "$pkgdir/usr/bin/waf")"
  install -dm755 "$waflib"
  unzip -d "$waflib" zip/waflib.zip

  # compile all python sources for once to be used after installation.
  python3 -m compileall "$waflib"
}
