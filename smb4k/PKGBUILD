pkgname=smb4k
pkgver=2.0.0
pkgrel=1
pkgdesc="A KDE program that browses samba shares"
arch=('x86_64')
url="http://sourceforge.net/projects/smb4k/"
license=('GPL')
depends=(kparts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python3 plasma-framework)
source=("https://sourceforge.net/projects/smb4k/files/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('7252df36cb3695daac416689a836741a')

prepare() {
  mkdir -p build

  # Fix doc cmake conflicts
  sed -e '/kdoctools_install/d' -i $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RELEASE \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF

  make VERBOSE=1 
}
package() {
  cd build

  make DESTDIR=$pkgdir install
}
