# Maintainer: UtG <utg.chakra.linux[at]gmail[dot]com>
# Contributor: Neophytos Kolokotronis <tetris4@gmail.com>

pkgbase=qtcurve
pkgname=('qtcurve-utils' 'qtcurve-kde4') #'qtcurve-qt5')
pkgver=1.8.18
pkgrel=3
arch=('x86_64')
license=('GPL')
pkgdesc='A configurable set of widget styles for Qt and KDE'
url='https://github.com/QtCurve/qtcurve'
makedepends=('cmake' 'automoc4' 'qt' 'kde-workspace' 'qt5-svg' 'qt5-quickcontrols' 'qt5-x11extras' 'libx11' 'libxcb')
categories=('system')
groups=("${pkgbase}")
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
md5sums=('422d1876d944bb278855f320eda19368')

prepare(){
  cd ${srcdir}/${pkgbase}-${pkgver}
  [[ -e build ]] && rm -rf build
  mkdir build
}

build() {
  msg 'Making KDE4 modules'
  cd ${srcdir}/${pkgbase}-${pkgver}/build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DENABLE_GTK2=Off \
           -DENABLE_QT5=Off
  make
}

package_qtcurve-utils() {
  depends=('libx11' 'libxau' 'libxdmcp')
  
  cd ${srcdir}/${pkgbase}-${pkgver}/build/lib/utils
  make DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/build/po
  make DESTDIR="${pkgdir}" install
}

package_qtcurve-kde4() {
  depends=('qtcurve-utils' 'kde-workspace')

  cd ${srcdir}/${pkgbase}-${pkgver}/build/qt4
  make DESTDIR="${pkgdir}" install
}
