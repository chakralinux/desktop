# Maintainer: UtG <utg.chakra.linux[at]gmail[dot]com>
# Contributor: Neophytos Kolokotronis <tetris4@gmail.com>

pkgbase=qtcurve
pkgname=('qtcurve-utils' 'qtcurve-kde4') #'qtcurve-gtk2' )
pkgver=1.8.18
pkgrel=1
arch=('x86_64')
license=('GPL')
pkgdesc='A configurable set of widget styles for KDE' #and gtk2
url='https://github.com/QtCurve/qtcurve'
depends=('kde-workspace')
makedepends=('cmake' 'automoc4' 'qt' 'kde-workspace') #'gtk2')
categories=('system')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
md5sums=('422d1876d944bb278855f320eda19368')

build() {
  cd $pkgbase-$pkgver
  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DENABLE_GTK2=false \
           -DENABLE_QT5=false
  make
}

package_qtcurve-utils() {
  depends=('libx11')
  cd $pkgbase-$pkgver/build/lib/utils
  make DESTDIR="$pkgdir" install
}

package_qtcurve-kde4() {
  depends=('qtcurve-utils' 'kde-workspace')
  provides=qtcurve
  replaces=qtcurve
  conflicts=qtcurve
  
  cd $pkgbase-$pkgver/build/qt4
  make DESTDIR="$pkgdir" install
  cd ../po
  make DESTDIR="$pkgdir" install
}




