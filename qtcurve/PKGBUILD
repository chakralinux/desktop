# Maintainer: UtG <utg.chakra.linux[at]gmail[dot]com>
# Contributor: Neophytos Kolokotronis <tetris4@gmail.com>
#              Drake Justice <djustice@chakraos.org>

pkgbase=qtcurve
pkgname=('qtcurve-utils' 'qtcurve-qt5' 'qtcurve-kde4')
pkgver=1.8.18
pkgrel=8
pkgdesc='A configurable set of widget styles for KDE and Gtk'
arch=('x86_64')
url='https://github.com/QtCurve/qtcurve'
license=('LGPL')
groups=('qtcurve')
makedepends=('cmake' 'automoc4' 'qt' 'qt5-svg' 'qt5-x11extras' 'kdelibs')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        "https://github.com/QtCurve/qtcurve/commit/323402d8b99502300f65e909c61153cbd5d5d997.diff")
md5sums=('422d1876d944bb278855f320eda19368'
         '632c1dfa26fcc5f5d45fe082c9738f3b')

prepare() {
  cd $pkgbase-$pkgver

  # fix FS#40524 - Qt 5.3 build fix
  patch -p1 -i ../${source[1]##*/}

  # prepare for build
  [[ -e build ]] && rm -rf build
  mkdir build
}

build() {
  cd $pkgbase-$pkgver/build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DENABLE_GTK2=Off
  make
}

package_qtcurve-utils() {
  depends=('libx11')
  cd ${srcdir}/${pkgbase}-${pkgver}/build/lib/utils
  make DESTDIR="$pkgdir" install
  cd ${srcdir}/${pkgbase}-${pkgver}/build/po
  make DESTDIR="${pkgdir}" install
}

package_qtcurve-kde4() {
  replaces=('qtcurve')
  depends=('qtcurve-utils' 'kdelibs')

  cd ${pkgbase}-${pkgver}/build
  make -C qt4 DESTDIR="${pkgdir}" install
}

package_qtcurve-qt5() {
  depends=('qtcurve-utils' 'qt5-svg' 'qt5-x11extras' 'qt5-base')
  
  cd ${srcdir}/${pkgbase}-${pkgver}/build/qt5
  make DESTDIR="${pkgdir}" install
}
