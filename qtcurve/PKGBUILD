# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/qtcurve

pkgbase=qtcurve
pkgname=('qtcurve-utils' 'qtcurve-kde4') #'qtcurve-gtk2' )
pkgver=1.8.17
pkgrel=1
arch=('x86_64')
license=('GPL')
pkgdesc='A configurable set of widget styles for KDE' #and gtk2
url='https://github.com/QtCurve/qtcurve'
depends=('kde-workspace')
makedepends=('cmake' 'automoc4' 'qt' 'kde-workspace') #'gtk2')
categories=('system')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
md5sums=('c4f2e9bc296398b42bfc132b88a3b989')

build() {
  cd $pkgbase-$pkgver
  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DENABLE_GTK2=false \
           -DENABLE_QT5=false
  make
}

package_qtcurve-utils() {
  depends=('libx11')
  cd $pkgbase-$pkgver/build/qtcurve-utils
  make DESTDIR="$pkgdir" install
}

package_qtcurve-kde4() {
  depends=('qtcurve-utils' 'kde-workspace')
  provides=qtcurve
  replaces=qtcurve
  conflicts=qtcurve
  
  cd $pkgbase-$pkgver/build/qt4
  make DESTDIR="$pkgdir" install
  cd ../po
  make DESTDIR="$pkgdir" install
}




