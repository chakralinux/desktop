pkgbase=qtcurve
pkgname=('qtcurve-utils' 'qtcurve-kf5' 'qtcurve-qt4')
pkgver=1.8.18.r247.d126bd5
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk'
arch=('x86_64')
url='https://github.com/QtCurve/qtcurve'
license=('LGPL')
groups=('qtcurve')
makedepends=('qt' 'qt5-svg' 'qt5-x11extras' 'extra-cmake-modules' 'kdelibs4support' 'kdoctools')
source=('git://anongit.kde.org/qtcurve.git#commit=d126bd5')
sha1sums=('SKIP')

pkgver() {
  cd $srcdir/$pkgbase 
  printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd $srcdir/$pkgbase

  # prepare for build
  [[ -e build ]] && rm -rf build
  mkdir build
}

build() {
  cd $srcdir/$pkgbase/build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DENABLE_GTK2=Off \
           -DQTC_QT4_ENABLE_KDE=false
  make
}

package_qtcurve-utils() {
  depends=('libx11')
  cd ${srcdir}/${pkgbase}/build/lib/utils
  make DESTDIR="$pkgdir" install
}

package_qtcurve-qt4() {
  depends=('qtcurve-utils' 'qt')

  cd $srcdir/${pkgbase}/build
  make -C qt4 DESTDIR="${pkgdir}" install
}

package_qtcurve-kf5() {
  depends=('qtcurve-utils' 'qt5-svg' 'qt5-x11extras' 'qt5-base' 'plasma-workspace')
  
  cd ${srcdir}/${pkgbase}/build/qt5
  make DESTDIR="${pkgdir}" install
}
