# Maintainer: Jeff Huang <s8321414<at>gmail<dot>com>
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.co

pkgname=mkvtoolnix
pkgver=7.7.0
pkgrel=1
pkgdesc="Set of tools to create, edit and inspect Matroska files."
arch=('x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('flac' 'libmatroska' 'libogg' 'libvorbis' 'qt5-base' 'xdg-utils' 'libebml' 'zlib' 'boost-libs' 'expat')
optdepends=('flac: for FLAC support (Free Lossless Audio Codec)'
            'lzo2: for additional compression capabilities'
            'bzip2: for additional compression capabilities'
            'file: for automatic content type detection'
            'curl: for online update checks')
makedepends=('boost' 'ruby')
categories=('multimedia')
install=${pkgname}.install
source=("http://www.bunkus.org/videotools/$pkgname/sources/$pkgname-$pkgver.tar.xz")
sha1sums=('9f24c02f8f0e4e40162dd5e5a305f2226186a046')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
  ./configure \
    --enable-qt \
    --prefix=/usr \
    --with-boost-libdir=/usr/lib
  rake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/bin/mkvinfo
  install -Dm755 src/mkvinfo "${pkgdir}"/usr/bin/mkvinfo
}
