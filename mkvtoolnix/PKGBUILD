pkgbase=mkvtoolnix
pkgname=('mkvtoolnix-cli' 'mkvtoolnix-gui')
pkgver=21.0.0
pkgrel=1
pkgdesc="Set of tools to create, edit and inspect Matroska files."
arch=('x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
optdepends=('flac: for FLAC support (Free Lossless Audio Codec)'
            'lzo2: for additional compression capabilities'
            'bzip2: for additional compression capabilities'
            'file: for automatic content type detection'
            'curl: for online update checks')
makedepends=('flac' 'libmatroska' 'libogg' 'libvorbis' 'qt5-base' 'xdg-utils' 'libebml' 
             'zlib' 'boost-libs' 'expat' 'boost' 'ruby' 'libxslt' 'docbook-xsl' 'po4a'
             'qt5-multimedia' 'libebml' 'cmark')
categories=('multimedia')
source=("http://www.bunkus.org/videotools/${pkgbase}/sources/${pkgbase}-${pkgver}.tar.xz")
sha256sums=('ad6d1d0ba40b153c155d5b26425e88531b24bb9969bac0341040e48cbb77c391')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Disable automagic curl dep used for online update checking
  sed -i -e '/curl/d' configure.ac
  export CURL_CFLAGS="" CURL_LIBS=""

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=3"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=3"

  autoreconf -vfi
}

build() {
  cd mkvtoolnix-${pkgver}

  ./configure --prefix='/usr' \
              --without-curl \
              --disable-qt
  rake apps:mkvinfo $MAKEFLAGS
  mv src/mkvinfo{,-cli}

  ./configure --prefix='/usr' \
              --without-curl
  rake $MAKEFLAGS
}

package_mkvtoolnix-cli() {
  pkgdesc="Set of tools to create, edit and inspect Matroska files - CLI version"
  depends=('libmatroska' 'expat' 'flac' 'libvorbis' 'file' 'boost-libs' 'lzo2')
  provides=('mkvtoolnix')
  conflicts=('mkvtoolnix')
  replaces=('mkvtoolnix')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  
  rake DESTDIR="${pkgdir}" install
  install -m 755 src/mkvinfo-cli "${pkgdir}"/usr/bin/mkvinfo
  rm -rf "${pkgdir}"/usr/share/{applications,icons,mime}
  find "${pkgdir}" -name mkvtoolnix-gui* -delete
}

package_mkvtoolnix-gui() {
  pkgdesc="Set of tools to create, edit and inspect Matroska files - Qt GUI"
  depends=("mkvtoolnix-cli=${pkgver}" 'qt5-base' 'qt5-multimedia' 'xdg-utils' 'desktop-file-utils' 'cmark')
  install=mkvtoolnix.install

  cd "${srcdir}/${pkgbase}-${pkgver}"
  
  rake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/locale
  for t in mkv{extract,info,merge,propedit}; do
    find "${pkgdir}" -name $t* -delete
  done
  
  # Fix file conflicts with mkvtoolnix-cli
  rm -rf ${pkgdir}/usr/share/mkvtoolnix/sounds

#  install -Dm755 src/mkvinfo "${pkgdir}"/usr/bin/mkvinfo-gui
#  sed -ri 's/^(Exec=mkvinfo)/\1-gui/' "${pkgdir}"/usr/share/applications/mkvinfo.desktop
}
