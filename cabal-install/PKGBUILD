# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributor: Stephen McIntosh <stephenmac7[at]gmail[dot]com>

pkgname=cabal-install
pkgver=0.14.0
pkgrel=2
pkgdesc='The command-line interface for Cabal and Hackage'
url='http://hackage.haskell.org/package/cabal-install'
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('haskell-http' 'haskell-zlib' 'haskell-mtl' 'haskell-network' 'haskell-random')
depends=('ghc' 'gmp' 'zlib')
source=(http://hackage.haskell.org/packages/archive/cabal-install/$pkgver/cabal-install-$pkgver.tar.gz)
md5sums=('638514bd1a5792d75866481852148ae5')

build() {
    cd cabal-install-${pkgver}
    runhaskell Setup configure --enable-shared --enable-split-objs --enable-executable-stripping --prefix=/usr
    runhaskell Setup build
}

package() {
    cd cabal-install-${pkgver}
    runhaskell Setup copy --destdir=$pkgdir

    # rm license installed to wrong location
    rm -rf ${pkgdir}/usr/share/doc

    install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
    
    # add bash completion
    install -d ${pkgdir}/usr/share/bash-completion/completions
    install -m644 bash-completion/cabal \
        ${pkgdir}/usr/share/bash-completion/completions
}
