# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
 
pkgname=cabal-install
pkgver=1.22.6.0
pkgrel=1
pkgdesc="The command-line interface for Cabal and Hackage."
url="http://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('x86_64')
makedepends=('ghc' 'haskell-random' 'haskell-stm')
depends=('gmp' 'zlib')
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz")
sha512sums=('155227a0bc29f8d6c9ec9846f1dbd956234853b1301fa15ab81aaee654abba8bfbd19cbb608008427b1272bff7aa397830011855b85d0a13deb9ebd1485cab14')
 
# the bootstrap script was broken on several releases
# build with a cabal sandbox to avoid unnecessary patching & headaches
build() {
  cd cabal-install-${pkgver}
 
  ./bootstrap.sh
}
 
package() {
  cd cabal-install-${pkgver}
 
  install -d ${pkgdir}/usr/bin
  install -m755 dist/build/cabal/cabal \
    ${pkgdir}/usr/bin
 
 
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}
 
  # add bash completion
  install -d ${pkgdir}/usr/share/bash-completion/completions
  install -m644 bash-completion/cabal \
    ${pkgdir}/usr/share/bash-completion/completions
}