# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=1.16.0.2
pkgrel=1
pkgdesc='The command-line interface for Cabal and Hackage'
url='http://hackage.haskell.org/package/cabal-install'
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('haskell-http=4000.2.8-1'
             'haskell-zlib=0.5.4.1-1'
             'haskell-mtl=2.1.2-3'
             'haskell-network=2.4.1.2-1'
             'haskell-random=1.0.1.1-5')
depends=('ghc' 'gmp' 'zlib')
source=(http://hackage.haskell.org/packages/archive/cabal-install/$pkgver/cabal-install-$pkgver.tar.gz)
md5sums=('638514bd1a5792d75866481852148ae5')

build() {
    cd cabal-install-${pkgver}
    runhaskell Setup configure \
        --enable-shared \
        --enable-split-objs \
        --enable-executable-stripping \
        --prefix=/usr
    runhaskell Setup build
}

package() {
    cd cabal-install-${pkgver}
    runhaskell Setup copy \
        --destdir=$pkgdir

    # Delete license installed to wrong location
    rm -rf ${pkgdir}/usr/share/doc

    install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

    # Add bash completion
    install -d ${pkgdir}/usr/share/bash-completion/completions
    install -m644 bash-completion/cabal \
        ${pkgdir}/usr/share/bash-completion/completions
}
