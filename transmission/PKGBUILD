# maintainer abveritas@chakra-project.org
# contributor : Stephen McIntosh <stephenmac7@gmail.com>
# Maintainer: Bruce Liu <bril@users.sourceforge.net>

pkgbase=transmission
pkgname=('transmission-cli' 'transmission')
pkgver=2.83
pkgrel=1
arch=('x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
makedepends=('intltool' 'curl' 'pkgconfig' 'desktop-file-utils' 'qt' 'libevent' 'gettext' 'qt5-base' 'miniupnpc')
source=("http://mirrors.m0k.org/transmission/files/${pkgbase}-${pkgver}.tar.xz"
        'transmission.tmpfiles')
md5sums=('4620cfbfefee2ce55a6fa12c3ec330a7'
         '23f2320361ad54373c3a4551ef634fe8')

build() {
  msg 'Configure source code...'
  cd "${srcdir}/${pkgbase}-${pkgver}"

  autoreconf -fi 
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  ./configure --prefix=/usr \
              --enable-nls \
              --enable-static=no \
              --localedir=/usr/share/transmission/i18n
  make
#  msg 'Compiling Qt4 part...'
#  cd "${srcdir}/${pkgbase}-${pkgver}/qt"
#  qmake qtr.pro
#  make
  msg 'Compiling Qt5 part...'
  cd "${srcdir}/${pkgbase}-${pkgver}/qt"
  /usr/lib/qt5/bin/qmake qtr.pro
  make
}

package_transmission-cli() {
  pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
  depends=('curl' 'libevent' 'miniupnpc')
  categories=('network')
  backup=('etc/conf.d/transmissiond')
  install=transmission-cli.install
  
  cd "${srcdir}/${pkgbase}-${pkgver}"

  for dir in daemon cli web utils
  do
      make -C "${dir}" DESTDIR="${pkgdir}" install
  done

  install -D -m644 "daemon/transmission-daemon.service" "$pkgdir/usr/lib/systemd/system/transmission.service"
  install -D -m644 "$srcdir/transmission.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/transmission.conf"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-cli/COPYING"
}

#package_transmission-qt() {
#  pkgdesc="Fast, easy, and free BitTorrent client (Qt GUI)"
#  depends=('curl' 'qt' 'libevent' 'miniupnpc')
#  optdepends=('transmission-cli: daemon and web support')
#  categories=('network')
#  screenshot=('http://www.transmissionbt.com/images/screenshots/Qt-Large.jpg')
#  provides=("${pkgbase}")
#  conflicts=("${pkgbase}")
#  replaces=("${pkgbase}")
#  install=transmission-qt.install

#  cd "${srcdir}/${pkgbase}-${pkgver}"

#  make -C qt INSTALL_ROOT="${pkgdir}"/usr install
#
#  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-qt/COPYING"
#  install -D -m644 qt/icons/transmission.png "${pkgdir}/usr/share/pixmaps/transmission-qt.png"
#  install -D -m644 qt/transmission-qt.desktop "${pkgdir}/usr/share/applications/transmission-qt.desktop"
  
#  install -dm755 ${pkgdir}/usr/share/icons/hicolor
#  cp -r qt/icons/hicolor/* ${pkgdir}/usr/share/icons/hicolor/
#}

package_transmission() {
  pkgdesc="Fast, easy, and free BitTorrent client (Qt5 GUI)"
  depends=('curl' 'qt5-base' 'libevent' 'miniupnpc')
  optdepends=('transmission-cli: daemon and web support')
  categories=('network')
  provides=("${pkgbase}" 'transmission-qt')
  conflicts=("${pkgbase}" 'transmission-qt')
  replaces=("${pkgbase}" 'transmission-qt5' 'transmission-qt')
  screenshot=('http://www.transmissionbt.com/images/screenshots/Qt-Large.jpg')
  install=transmission.install

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make -C qt INSTALL_ROOT="${pkgdir}"/usr install

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-qt/COPYING"
  install -D -m644 qt/icons/transmission.png "${pkgdir}/usr/share/pixmaps/transmission-qt.png"
  install -D -m644 qt/transmission-qt.desktop "${pkgdir}/usr/share/applications/transmission-qt.desktop"
  
  install -dm755 ${pkgdir}/usr/share/icons/hicolor
  cp -r qt/icons/hicolor/* ${pkgdir}/usr/share/icons/hicolor/
}
