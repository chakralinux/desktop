pkgbase=transmission
pkgname=('transmission-cli' 'transmission')
pkgver=2.93
pkgrel=1
arch=('x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
makedepends=('intltool' 'curl' 'pkgconfig' 'desktop-file-utils' 'libevent' 'gettext' 'qt5-tools' 'miniupnpc')
source=("https://github.com/transmission/transmission-releases/raw/master/transmission-${pkgver}.tar.xz"
        'transmission-cli.sysusers'
        'transmission-cli.tmpfiles')
md5sums=('a1b8113ebc3402787312ecb443d9d3c1'
         '6836ecf77e8c96738259e61b4ec0813e'
         '73f17e39f3996768bb1d1e25a2f3fed1')

build() {
  msg 'Configure source code...'
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-nls \
              --enable-static=no
  make

  msg 'Compiling Qt5 part...'
  cd "${srcdir}/${pkgbase}-${pkgver}/qt"
  /usr/lib/qt5/bin/qmake qtr.pro
  make
  lrelease-qt5 ${srcdir}/${pkgbase}-${pkgver}/qt/translations/*.ts
}

package_transmission-cli() {
  pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
  depends=('curl' 'libevent' 'miniupnpc')
  categories=('network')
  backup=('etc/conf.d/transmissiond')
  install=transmission-cli.install
  
  cd "${srcdir}/${pkgbase}-${pkgver}"

  for dir in daemon cli web utils
  do
      make -C "${dir}" DESTDIR="${pkgdir}" install
  done

  install -D -m644 "daemon/transmission-daemon.service" "$pkgdir/usr/lib/systemd/system/transmission.service"
  install -D -m644 "$srcdir/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/transmission.conf"
  install -D -m644 "$srcdir/transmission-cli.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/transmission.conf"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-cli/COPYING"
}

package_transmission() {
  pkgdesc="Fast, easy, and free BitTorrent client (Qt5 GUI)"
  depends=('curl' 'qt5-base' 'libevent' 'miniupnpc' 'libxkbcommon')
  optdepends=('transmission-cli: daemon and web support')
  categories=('network')
  provides=('transmission-qt' 'transmission-qt5')
  conflicts=('transmission-qt' 'transmission-qt5')
  replaces=('transmission-qt' 'transmission-qt5')
  screenshot=('http://www.transmissionbt.com/images/screenshots/Qt-Large.jpg')
  install=transmission.install

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make -C qt INSTALL_ROOT="${pkgdir}"/usr install

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-qt/COPYING"
  install -D -m644 qt/icons/transmission.png "${pkgdir}/usr/share/pixmaps/transmission-qt.png"
  install -D -m644 qt/transmission-qt.desktop "${pkgdir}/usr/share/applications/transmission-qt.desktop"
  
  install -dm755 ${pkgdir}/usr/share/icons/hicolor
  cp -r qt/icons/hicolor/* ${pkgdir}/usr/share/icons/hicolor/

  install -dm755 ${pkgdir}/usr/share/transmission/i18n
  for i in $(ls ${srcdir}/${pkgbase}-${pkgver}/qt/translations/ | grep qm); do 
    install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/qt/translations/${i} ${pkgdir}/usr/share/qt5/translations/${i}
  done
}
