# Contributor : Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kdeclarative
pkgver=${KFVersion}
pkgrel=1
pkgdesc='KDeclarative'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdeclarative'
license=('LGPL')
depends=('kio')
makedepends=('extra-cmake-modules')
checkdepends=("cmake")
groups=('kf5')
options=("debug")
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('1581842bd5824156a600d099a24c5505')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib 
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
