#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

#
# package info
#
pkgbase=('kdemultimedia')
pkgname=('kdemultimedia-common'
         'kdemultimedia-dragonplayer'
         'kdemultimedia-juk'
         'kdemultimedia-kmix'
         'kdemultimedia-kscd'
         'kdemultimedia-thumbnailers')

arch=('i686' 'x86_64')
pkgver=${_kdever}
pkgrel=1

pkgdesc="split package"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')

options=('docs' '!splithdr' 'splitdbg' 'log')

makedepends=('ffmpeg' 'pkgconfig' 'cmake' 'automoc4' "kdelibs>=${_kdever}" "kde-runtime>=${_kdever}"
             'cdparanoia' 'mplayer' 'libpulse' 'musicbrainz>=3.0.3' 'taglib' 'automoc4' 'docbook-xsl')

source=($_mirror/${pkgbase}-$_kdever.tar.xz
	http://kenai.com/projects/kde-odf-thumbnail/downloads/download/1.0.0/kde-odf-thumbnail-1.0.0.tar.gz
	http://kde-apps.org/CONTENT/content-files/107128-GimpResourcesThumbnailer-2.1.0.tar.gz
	http://kde-apps.org/CONTENT/content-files/114885-AudioThumbs-0.1.tar.gz
	mplayerthumbs-horizontal.patch
	sprocket-large.png
	sprocket-medium.png
	sprocket-small.png)

md5sums=(`grep ${pkgbase}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'edf694939f4b5d5e97c3fe2f91e7cec3'  # kde-odf-thumbnail-1.0.0.tar.gz
         '5e3022ddcf3876e07d4b9870e1806b48'  # 107128-GimpResourcesThumbnailer-2.1.0.tar.gz
         '07c3039ddf5e88ba0ab290570f43749d'  # 114885-AudioThumbs-0.1.tar.gz
         '00ff1e8a1e4c1e39e3d1146f1d08036c'  # mplayerthumbs-horizontal.patch
         '6eeba61989d0e6904fe2c18bb01a3775'  # sprocket-large.png
         'ebb3b4c68ee69e128faf5ef0b37aaeae'  # sprocket-medium.png
         '450cf7bf2ac02b1efe75156d9bb887f6')  # sprocket-small.png

#
# build function
#
build() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	msg "applying patches ..."
	patch -Np1 -i ${srcdir}/mplayerthumbs-horizontal.patch 
        
	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DWITH_PulseAudio=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make VERBOSE=1 || return 1

	pushd $srcdir/kde-odf-thumbnail-1.0.0
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
	popd

	pushd $srcdir/GimpResourcesThumbnailer-2.1.0
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
	popd

	pushd $srcdir/AudioThumbs-0.1
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
	popd
}

#
# split-install functions
#
package_kdemultimedia-common() {
	pkgdesc="KDE multimedia - Common files and libraries"
	depends=("kdelibs>=${_kdever}" "kde-runtime>=${_kdever}" 'flac' 'musicbrainz>=3.0.3' 'cdparanoia' 'taglib')
        conflicts=('kdemultimedia-doc')
	groups=("kde" "kdemultimedia" "kde-uninstall")
        optdepends=('pulseaudio: pulseaudio support')
	install=${pkgbase}.install

	splitdirs="kioslave libkcddb libkcompactdisc strigi-analyzer doc/kioslave doc/kcontrol"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install 
		done

	# include our patches into the package
	ls -1 ${startdir}/*.patch &>/dev/null 2>&1
	if [ "$?" = "0" ]; then
		warning "incuding patches into package"
		mkdir -p ${pkgdir}/usr/share/chakra/patches/${pkgbase} &>/dev/null
		for i in ${startdir}/*.patch; do
			msg "$i"
			cp $i ${pkgdir}/usr/share/chakra/patches/${pkgbase}/ &>/dev/null
		done
	else
		warning "no patches found, skipping to include them into the package..."
	fi
}

package_kdemultimedia-dragonplayer() {
	pkgdesc="Simple video player"
	depends=("${pkgbase}-common>=${_kdever}")
	conflicts=('kdemultimedia-doc')
	groups=("kde" "${pkgbase}" "kde-uninstall")
        provides=('dragonplayer')
        categories=('multimedia')
	install=${pkgbase}.install

	splitdirs="dragonplayer doc/dragonplayer"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install
		done
}

package_kdemultimedia-juk() {
	pkgdesc="An audio player with music database"
	depends=("${pkgbase}-common>=${_kdever}")
        conflicts=('kdemultimedia-doc')
	groups=("kde" "${pkgbase}" "kde-uninstall")
        provides=('juk')
        categories=('multimedia')
	install=${pkgbase}.install

	splitdirs="juk doc/juk"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install 
		done
}

package_kdemultimedia-kmix() {
	pkgdesc="Audio mixer"
	depends=("${pkgbase}-common>=${_kdever}")
        conflicts=('kdemultimedia-doc')
	groups=("kde" "${pkgbase}" "kde-uninstall")
        optdepends=('pulseaudio: pulseaudio support')
        provides=('kmix')
        categories=('multimedia')
	install=${pkgbase}.install

	splitdirs="kmix doc/kmix"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install 
		done
}

package_kdemultimedia-kscd() {
	pkgdesc="Audio CD player"
	depends=("${pkgbase}-common>=${_kdever}" 'musicbrainz')
	groups=("kde" "${pkgbase}" "kde-uninstall")
        provides=('kscd')
        categories=('multimedia')
	install=${pkgbase}.install

	splitdirs="kscd"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install 
		done
}

package_kdemultimedia-thumbnailers() {
	pkgdesc='KDE Thumbnailers for several formats'
	depends=("${pkgbase}-common>=${_kdever}" 'mplayer') 
	provides=("${pkgbase}-mplayerthumbs=${_kdever}" 'mplayerthumbs')
        groups=("kde" "${pkgbase}" "kde-uninstall")
	install=${pkgbase}.install

	splitdirs="mplayerthumbs ffmpegthumbs"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install
		done

	install -D -m644 ${srcdir}/sprocket-large.png ${pkgdir}/usr/share/apps/videothumbnail/sprocket-large.png
	install -D -m644 ${srcdir}/sprocket-medium.png ${pkgdir}/usr/share/apps/videothumbnail/sprocket-medium.png
	install -D -m644 ${srcdir}/sprocket-small.png ${pkgdir}/usr/share/apps/videothumbnail/sprocket-small.png

	cd $srcdir/kde-odf-thumbnail-1.0.0
	make DESTDIR=${pkgdir} install 

	cd $srcdir/GimpResourcesThumbnailer-2.1.0
	make DESTDIR=${pkgdir} install 

	cd $srcdir/AudioThumbs-0.1
	make DESTDIR=${pkgdir} install 
}
