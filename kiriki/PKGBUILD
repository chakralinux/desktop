# Include global configuration

pkgname=kiriki
pkgver=18.08.3
pkgrel=1
pkgdesc="An addictive and fun dice game"
url="http://kde.org/applications/games/kiriki/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkdegames' 'hicolor-icon-theme' 'knotifications')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegames')
conflicts=('kdegames-kiriki')
replaces=('kdegames-kiriki')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a0f4e0e5df8ba3dcaed4651e04321a0650691c56177c695b8de439fed776f37a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
