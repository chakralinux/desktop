# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegames-kpatience/
# Include global configuration

pkgname=kpat
pkgver=18.12.2
pkgrel=1
pkgdesc="Collection of various patience games"
arch=('x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=(libkdegames freecell-solver hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegames')
replaces=('kdegames-kpat')
conflicts=('kdegames-kpat')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('66fe916cb5b93cebdbe94a439917e6098761d62434276c389b2c399fee4c2b0a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
