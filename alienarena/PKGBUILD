#
# Games Packages for Chakra, part of chakra-project.org
#
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=alienarena
pkgver=7.53
pkgrel=1
_verdate=20111227
pkgdesc="Multiplayer retro sci-fi deathmatch game"
arch=('i686' 'x86_64')
url='http://icculus.org/alienarena/rpa/'
license=('GPL' 'LGPL' 'custom:Non-free data')
depends=('curl' 'freetype2' 'libgl' 'libjpeg' 'libvorbis' 'libxxf86dga' 'libxxf86vm' 'mesa' 'ode' 'sdl' 'openal')
categories=('games')
source=(http://icculus.org/alienarena/Files/alienarena-$pkgver-linux${_verdate}.tar.gz
	'alienarena.desktop')
md5sums=('2ccf9f1e2781c3bc005dbdb1b9954a33'
         '7bc3fba786eb379fbcb3c9aecf106811')
 
build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mv -f "$pkgdir/usr/bin/crx" "$pkgdir/usr/bin/$pkgname"
  mv -f "$pkgdir/usr/bin/crx-ded" "$pkgdir/usr/bin/$pkgname-ded"
  install -D -m644 "$srcdir/alienarena.desktop" "${pkgdir}/usr/share/applications/alienarena.desktop"
}
