#
# Games Packages for Chakra, part of chakra-project.org
#
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=alienarena
pkgver=7.60.1
pkgrel=1
_verdate=20120823
pkgdesc="Multiplayer retro sci-fi deathmatch game"
arch=('i686' 'x86_64')
url='http://icculus.org/alienarena/rpa/'
license=('GPL' 'LGPL' 'custom:Non-free data')
depends=('curl' 'freetype2' 'libgl' 'libjpeg' 'libvorbis' 'libxxf86dga' 'libxxf86vm' 'mesa' 'ode' 'sdl' 'openal')
categories=('games')
source=("http://icculus.org/alienarena/Files/alienarena-${pkgver}-linux${_verdate}.tar.gz"
        'alienarena.desktop')
sha256sums=('a53fcc6d1fe54217bf9f9e4ca5500a615686737da1919e2b52847ba879531301'
            '1b872db6d5544e5537592ba19d08c1016ba1816e134f2ebbf0266a9468c753e4')
 
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/pixmaps"
  mv -f "${pkgdir}/usr/share/icons/alienarena.png" "${pkgdir}/usr/share/pixmaps/"
  rm -r "${pkgdir}/usr/share/icons"
  install -D -m644 "${srcdir}/alienarena.desktop" "${pkgdir}/usr/share/applications/alienarena.desktop"
}
