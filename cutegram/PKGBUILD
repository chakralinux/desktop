pkgname=cutegram
_pkgname=Cutegram
pkgver=2.9.5+de+49+ge4898120
pkgrel=1
pkgdesc="A different telegram client from Aseman team"
arch=('x86_64')
url="http://aseman.co/en/products/cutegram/"
license=('GPL')
depends=('libqtelegram-ae' 'telegramqml' 'aseman-qt-tools' 'qt5-declarative')
optdepends=('gst-plugins-good: for audio and notification support'
            'gst-plugins-bad: for audio support')
source=("${pkgname}::git+https://github.com/Aseman-Land/Cutegram.git#commit=e489812"
        '0001-remove-error-dialog.patch')
sha1sums=('SKIP'
          '3dc80cb6e95ba413871fa53b1240fc900d2b0b32')

pkgver() {
  cd ${srcdir}/${pkgname}
  git describe --long | sed 's/\([^-]*-g\)/\1/;s/-/+/g' | sed 's/.stable//g' | sed 's/v//g'
}

prepare() {
  cd "${srcdir}/${pkgname}"
  
  patch -Np1 -i $srcdir/0001-remove-error-dialog.patch
  # change the heading application
  sed 's,qmlscene,qmlscene-qt5,g' -i cutegram.pro
  mkdir -p build
}

build() {
  cd "${srcdir}/${pkgname}/build"
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DEFINES+=WEBENGINE_ASEMAN_WEBGRABBER CONFIG+=binaryMode ..
  make
}

package() {
  cd "${srcdir}/${pkgname}/build"
  make INSTALL_ROOT="${pkgdir}" install
}
