pkgname=cutegram
_pkgname=Cutegram
pkgver=2.9.5
pkgrel=1
pkgdesc="A different telegram client from Aseman team"
arch=('x86_64')
url="http://aseman.co/en/products/cutegram/"
license=('GPL')
depends=('libqtelegram-ae' 'telegramqml' 'aseman-qt-tools')
optdepends=('gst-plugins-good: for audio and notification support'
            'gst-plugins-bad: for audio support')
source=("https://github.com/Aseman-Land/Cutegram/archive/v$pkgver-dev.tar.gz")
sha1sums=('792c0ac9075638657f1939550508281a8400fde6')

prepare() {
  cd "${srcdir}/${_pkgname}-$pkgver-dev"
  mkdir -p build

  # modify desktop file for binaryMode
  sed -i "s|qmlscene /usr/share/cutegram/3.0/main.qml|/usr/bin/cutegram|" share/Cutegram.desktop
}

build() {
  cd "${srcdir}/${_pkgname}-$pkgver-dev/build"
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DEFINES+=WEBENGINE_ASEMAN_WEBGRABBER CONFIG+=binaryMode .. 
  make
}

package() {
  cd "${srcdir}/${_pkgname}-$pkgver-dev/build"
  make INSTALL_ROOT="${pkgdir}" install

  # Remove fonts
  rm -rf ${pkgdir}/usr/share/cutegram/files/fonts/
}
