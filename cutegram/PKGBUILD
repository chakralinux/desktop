pkgname=cutegram
_pkgname=Cutegram
pkgver=2.7.0
pkgrel=2
pkgdesc="A different telegram client from Aseman team"
arch=('x86_64')
url="http://aseman.co/en/products/cutegram/"
license=('GPL')
depends=('qt5-base' 'qt5-declarative' 'qt5-multimedia' 'qt5-quick1'
         'qt5-graphicaleffects' 'qt5-quickcontrols' 'qt5-webengine' 'qt5-imageformats'
	 'gstreamer' 'libqtelegram-ae>=6.0' 'telegramqml')
source=("https://github.com/Aseman-Land/Cutegram/archive/v$pkgver-stable.tar.gz"
        '0001-Add-Chakra-specific-search-option.patch')
sha1sums=('53008ca4c61e06f211b0948dc5733b77e30d6945'
          'bcd6d726359a83619a9743283a58990e055556f5')

prepare() {
  cd "${srcdir}/${_pkgname}-$pkgver-stable"
  mkdir -p build

  # Add chakra-specific search engine option
  patch -Np1 -i ${srcdir}/0001-Add-Chakra-specific-search-option.patch
}

build() {
  cd "${srcdir}/${_pkgname}-$pkgver-stable/build"
  qmake-qt5 -r .. PREFIX=/usr DEFINES+=WEBENGINE_ASEMAN_WEBGRABBER
  make
}

package() {
  cd "${srcdir}/${_pkgname}-$pkgver-stable/build"
  make INSTALL_ROOT="${pkgdir}" install

  # Remove fonts
  rm -rf ${pkgdir}/usr/share/cutegram/files/fonts/
}
