pkgname=cutegram
_pkgname=Cutegram
pkgver=2.9.5
pkgrel=3
pkgdesc="A different telegram client from Aseman team"
arch=('x86_64')
url="http://aseman.co/en/products/cutegram/"
license=('GPL')
depends=('libqtelegram-ae' 'telegramqml' 'aseman-qt-tools')
optdepends=('gst-plugins-good: for audio and notification support'
            'gst-plugins-bad: for audio support')
source=("${pkgname}"::"git+https://github.com/Aseman-Land/Cutegram.git#commit=20690bd6b47e0cb56787e9f0e60c7c99de492c91")
sha1sums=('SKIP')

prepare() {
  cd "${srcdir}/${pkgname}"

  # modify desktop file for binaryMode
  sed -i "s|qmlscene /usr/share/cutegram/3.0/main.qml|/usr/bin/cutegram|" share/Cutegram.desktop
  mkdir -p build
}

build() {
  cd "${srcdir}/${pkgname}/build"
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DEFINES+=WEBENGINE_ASEMAN_WEBGRABBER CONFIG+=binaryMode ..
  make
}

package() {
  cd "${srcdir}/${pkgname}/build"

  install -Dm755 cutegram "${pkgdir}/usr/bin/cutegram"
  install -Dm644 ../share/Cutegram.desktop "${pkgdir}/usr/share/applications/Cutegram.desktop"
}
