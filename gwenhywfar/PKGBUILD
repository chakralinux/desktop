# Arch contribution https://www.archlinux.org/packages/community/x86_64/gwenhywfar/

pkgname=gwenhywfar
pkgver=4.20.0
_releasenumber=208
pkgrel=1
pkgdesc="OS abstraction functions for various projects"
arch=('x86_64')
url="http://www.aquamaniac.de"
license=("LGPL")
depends=('openssl' 'qt5-base' 'gnutls' 'libgcrypt' 'libgpg-error')
source=(gwenhywfar-$pkgver.tar.gz::"https://www.aquamaniac.de/sites/download/download.php?package=01&release=${_releasenumber}&file=02&dummy=gwenhywfar-$pkgver.tar.gz")
sha256sums=('5a88daabba1388f9528590aab5de527a12dd44a7da4572ce48469a29911b0fb0')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --enable-ssl \
	      --with-guis="qt5" \
	      --with-qt5-qmake=/usr/lib/qt5/bin/qmake \
	      --with-qt5-moc=/usr/lib/qt5/bin/moc \
	      --with-qt5-uic=/usr/lib/qt5/bin/uic
  sed -i 's|gnutls_transport_set_lowat||g' src/sio/syncio_tls.c
  make
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  
  make DESTDIR=$pkgdir install
}
