pkgbase=python-mpmath
pkgname=('python2-mpmath' 'python3-mpmath')
pkgver=1.0.0
pkgrel=1
pkgdesc='Python library for arbitrary-precision floating-point arithmetic.'
arch=('any')
url='http://mpmath.org/'
license=('BSD')
makedepends=('python3-setuptools' 'python2-setuptools')
source=("http://mpmath.org/files/mpmath-${pkgver}.tar.gz")
sha1sums=('667569ac2d71708613f66c6a094b0cb74e82b430')

prepare(){
  cp -R mpmath-$pkgver mpmath2-$pkgver
}

build() {
  cd mpmath2-$pkgver  
  python2 setup.py build

  cd ../mpmath-$pkgver
  python3 setup.py build
}

package_python2-mpmath() {
  depends=('python2-gmpy2')
  cd mpmath2-${pkgver}

  find -name '*.py' | xargs sed -e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
                                -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i

  python2 setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}

package_python3-mpmath() {
  depends=('python3-gmpy2')
  cd mpmath-${pkgver}  

  python3 setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}

