pkgname=qt4-fsarchiver
pkgver=0.6.19.15
_pkgver=0.6.19-15
pkgrel=1
pkgdesc='GUI for fsarchiver'
arch=('x86_64')
url='http://sourceforge.net/projects/qt4-fsarchiver/'
license=('GPL')
depends=('fsarchiver' 'kde-runtime')
makedepends=('qt')
categories=('utils')
screenshot=('http://media.cdn.ubuntu-de.org/wiki/attachments/02/04/verzeichnis.jpg')
source=("http://sourceforge.net/projects/${pkgname}/files/source/${pkgname}-${_pkgver}.tar.gz"
	"qt4-fsarchiver.png")
install="${pkgname}.install"
sha512sums=('fb082290d52d1efe5e01c96cb120724714084b74f4033553c5f3bf2974cb607595f9e4cd5f16d2d4210d7b9d6a3c51be59b6176568b4e41d4eb61cb1e90ab6a8'
	    '713cf469ea2165b6038e581ec42d7bf6486e39cc9a64f6b1eae9b2e9d69fdc1a5987e34cd8717a23435ded4e5f33784202ffc01c30ed3153192dfab46dcbbb7b')

build() {
	cd "${srcdir}/${pkgname}"
	qmake
	make
}

package() {
  cd "${srcdir}/${pkgname}"
  make INSTALL_ROOT="${pkgdir}" install
  
  install -d ${pkgdir}/usr/share/pixmaps
  install -d ${pkgdir}/usr/share/qt/translations
  rm -R ${pkgdir}/usr/share/qt4
  install -D ${srcdir}/${pkgname}.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
  rm -R ${pkgdir}/usr/share/app-install
  install -D -m644 ${srcdir}/${pkgname}/translations/*.qm ${pkgdir}/usr/share/qt/translations
	
  # workaround for fixed destination path in qmake project
  install -d ${pkgdir}/usr/bin
  mv ${pkgdir}/usr/sbin/findsmb-qt ${pkgdir}/usr/bin/
  mv ${pkgdir}/usr/sbin/qt4-fsarchiver ${pkgdir}/usr/bin/
  rm -rf ${pkgdir}/usr/sbin
  
  sed s/Terminal=true/Terminal=false/ -i ${pkgdir}/usr/share/applications/kde-qt4-fsarchiver.desktop
  sed s/sudo/kdesu/ -i ${pkgdir}/usr/share/applications/kde-qt4-fsarchiver.desktop
  sed -i s!/usr/share/app-install/icons/harddrive.png!qt4-fsarchiver.png! \
  ${pkgdir}/usr/share/applications/kde-qt4-fsarchiver.desktop
  rm ${pkgdir}/usr/share/applications/mate-qt4-fsarchiver.desktop
  rm ${pkgdir}/usr/share/applications/gnome-qt4-fsarchiver.desktop
	
}
