# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdemultimedia-dragonplayer/
# Include global configuration
source ../kdeapps.conf

pkgname=kdemultimedia-dragonplayer
_pkgname=dragon
pkgver=${_kdever}
pkgrel=1
pkgdesc="A simple multimedia player"
url='http://kde.org/applications/multimedia/dragonplayer/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=("kde-runtime")
makedepends=('cmake' 'automoc4' 'kdoctools')
groups=("kde" "kdemultimedia" "kde-uninstall")
provides=('dragonplayer')
options=('docs' 'debug')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
