# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=tellico
pkgver=2.3.10
pkgrel=1
pkgdesc="A collection manager for KDE"
arch=('x86_64')
url="http://tellico-project.org/"
license=('GPL')
depends=('kde-workspace' 'yaz' 'exempi' 'libksane' 'taglib'
         'libkcddb' 'poppler-qt' 'qjson' 'kdemultimedia-audiocd-kio')
makedepends=('automoc4' 'cmake' 'docbook-xsl')
categories=('office')
screenshot=('http://tellico-project.org/img/main_screen-2.0.jpg')
install=$pkgname.install
source=("http://tellico-project.org/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('06b29f88fe9883ddf9550ce541a0c180')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # fix python 2.7 path
  find "$pkgdir" -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
