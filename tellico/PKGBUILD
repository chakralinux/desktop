#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=tellico
pkgver=2.3.6
pkgrel=2
pkgdesc="A collection manager for KDE"
arch=('i686' 'x86_64')
url="http://tellico-project.org/"
license=('GPL')
depends=('kde-workspace' 'yaz' 'exempi' 'libksane' 'taglib'
         'libkcddb' 'poppler-qt' 'qjson')
makedepends=('automoc4' 'cmake' 'docbook-xsl')
categories=('office')
screenshot=('http://tellico-project.org/img/main_screen-2.0.jpg')
install=$pkgname.install
source=("http://tellico-project.org/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a547c3cff6559012bf2292cec5a543ed')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # fix python 2.7 path
  find "$pkgdir" -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
