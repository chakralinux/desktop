pkgname=tellico
pkgver=3.0.2
pkgrel=1
pkgdesc="A collection manager for KDE"
arch=('x86_64')
url="http://tellico-project.org/"
license=('GPL')
depends=('exempi' 'kio' 'libksane' 'libkcddb' 'libcddb' 'libdiscid' 'hicolor-icon-theme' 'kfilemetadata'
         'knewstuff' 'kxmlgui' 'kitemmodels' 'python2' 'solid' 'khtml' 'yaz' 'taglib' 'poppler-qt5' 'qt5-base')
makedepends=('cmake' 'extra-cmake-modules' 'kdoctools' 'libkcddb')
categories=('office')
screenshot=('http://tellico-project.org/img/main_screen-2.0.jpg')
install=$pkgname.install
source=(http://tellico-project.org/files/${pkgname}-${pkgver}.tar.xz)
md5sums=('7f20d59485163a753b8ce4b773062857')

build() {
  cd "${srcdir}"

  mkdir -p build && cd build

  cmake ../${pkgname}-${pkgver} -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

# fix python 2.7 path
  find "${pkgdir}" -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
