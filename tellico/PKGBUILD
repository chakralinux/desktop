# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=tellico
pkgver=2.3.10
pkgrel=2
pkgdesc="A collection manager for KDE"
arch=('x86_64')
url="http://tellico-project.org/"
license=('GPL')
depends=('kde-runtime' 'qimageblitz' 'kdepimlibs' 'yaz' 'exempi' 'libksane' 'taglib' 'libkcddb' 'poppler-qt4' 'kdemultimedia-audiocd-kio')
makedepends=('automoc4' 'cmake' 'docbook-xsl')
categories=('office')
screenshot=('http://tellico-project.org/img/main_screen-2.0.jpg')
install=$pkgname.install
source=(http://tellico-project.org/files/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e916101f86b6f843b85be42e8545aa7441cd496cbb9b81590e2e0bd03a58faaa')

build() {
  cd "${srcdir}"

  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DWITH_Nepomuk=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

# fix python 2.7 path
  find "${pkgdir}" -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
