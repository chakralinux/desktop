# Include global configuration

pkgname=ksnakeduel
pkgver=18.07.80
pkgrel=1
pkgdesc="A simple snake duel game"
arch=('x86_64')
url='http://kde.org/applications/games/ksnakeduel/'
screenshot='https://www.kde.org/images/screenshots/ksnakeduel.png'
license=('GPL' 'LGPL' 'FDL')
groups=('kdegames' 'kde' 'kde-applications')
depends=(libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegames-ksnakeduel)
replaces=(kdegames-ksnakeduel)
provides=(kdegames-ksnakeduel)
options=('docs' 'debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('360653e5099f3b0fae3245b985a62bba04d52ed174741d4194e294c7fceebd17'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
