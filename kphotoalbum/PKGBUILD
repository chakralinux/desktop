pkgname=kphotoalbum
pkgver=5.4
pkgrel=1
pkgdesc="KDE Photo Album, picture collection manager"
url="http://www.kphotoalbum.org/"
arch=('x86_64')
license=('GPL')
depends=('libkipi' 'libkdcraw' 'libkface' 'libkgeomap' 'exiv2')
makedepends=('extra-cmake-modules' 'python3' 'plasma-framework' 'phonon-qt5' 'kdoctools' 'kparts' 'libkexiv2')
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('d031ce9eec4c16d628e1152cee4ab6aeef5194ce6517dd7b6493e60d45ad852d'
            'SKIP')
validpgpkeys=('8A53F0CB9F752E6F4F72FEC6F8DECD0DDF0F74A8') # Tobias Leupold <tobias.leupold@gmx.de>

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
