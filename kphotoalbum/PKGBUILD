pkgname=kphotoalbum
pkgver=5.3
pkgrel=1
pkgdesc="KDE Photo Album, picture collection manager"
url="http://www.kphotoalbum.org/"
arch=('x86_64')
license=('GPL')
depends=('libkipi' 'libkdcraw' 'libkface' 'libkgeomap' 'exiv2')
makedepends=('extra-cmake-modules' 'python3' 'plasma-framework' 'phonon-qt5' 'kdoctools' 'kparts' 'libkexiv2')
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('c1175489c70902511d7fa7c261a65d4dd19a3a651a1811edea3034e36fda88d1'
            'SKIP')
validpgpkeys=('8A53F0CB9F752E6F4F72FEC6F8DECD0DDF0F74A8') # Tobias Leupold <tobias.leupold@gmx.de>

build() {
  mkdir -p build
  cd build

  # Gentoo #623566
  export LDFLAGS+=" -fuse-ld=gold"

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
