# Maintainer:   Manuel Tortosa <manutortosa@chakra-project@org>
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdegraphics-ksnapshot
_pkgname=ksnapshot
pkgver=${_kdever}
pkgrel=3
pkgdesc='Screen capture program'
url='http://kde.org/applications/graphics/ksnapshot/'
arch=('x86_64')
license=('GPL'
         'LGPL'
         'FDL')
depends=("kdelibs>=${_kdever}"
         "kdegraphics-libkipi>=${_kdever}")
makedepends=('cmake'
             'automoc4')
optdepends=('kipi-plugins')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)
install=${pkgname}.install
groups=('kde'
        'kdegraphics'
        'kde-uninstall')
categories=('graphics')
options=('docs'
         '!header'
         'debug'
         'log')

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
