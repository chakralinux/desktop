# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>
#               Andrea Scarpino <andrea@archlinux.org>
#               birdflesh <antkoul@gmail.com>

pkgname=libkfbapi
pkgver=1.0
pkgrel=9
pkgdesc="KDE library for accessing Facebook services"
arch=('x86_64')
url='https://projects.kde.org/projects/extragear/libs/libkfbapi'
license=('GPL')
depends=('kdepimlibs')
makedepends=('cmake'
             'automoc4'
             'boost')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.bz2")
sha512sums=('d9587752008aeecd061e27fa1415920b938cc71d117d59673ea9f0fe985be68fdf682ed9caf5caf8528b9486ad44d832b06e59c545a349aad1416f49df7d6a85')

prepare() {
    mkdir "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake "../$pkgname-$pkgver" \
        -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}
