#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com> 

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=urbanterror
_pkgname=iourbanterror
_pkgname2=ioUrbanTerror
pkgver=4.1
pkgrel=1
pkgdesc="A team-based tactical shooter based on the Quake 3 Engine."
arch=('i686' 'x86_64')
url="http://www.urbanterror.net"
license=('GPL2')
depends=('sdl' 'openal' 'curl' 'urbanterror-data')
makedepends=('mesa')
categories=('games')
source=("http://ftp.snt.utwente.nl/pub/games/$pkgname/$_pkgname/source/complete/${_pkgname2}Source_2007_12_20.zip" \
	"${pkgname}_home.patch" "default-curl_lib.patch" \
	"cmd.c.patch" "g_cmds.c.patch" "qcommon.h.patch" "sv_client.c.patch" "sv_main.c.patch" \
	"${pkgname}.sh" "${pkgname}-server.sh" "${pkgname}.desktop" "${pkgname}.png")
md5sums=('4dbe5a03a10f23138f42b5e0dc39598b'
         'a1d992453aa64108c0b7b8304484c21f'
         'e917e9fe1210598885665e13a40c11d8'
         '0e5cc83478b8d37f59740620e5eccd21'
         '1120e5f2a3a5944f219a432e5eb9bcaa'
         'a12059253d555fdb169d2717899b47e6'
         '57241e730301a78d1b13de5b9cc9f68a'
         '8b24a2aa5446e9c6ccbc04e844b189ab'
         '1871dee6da522731cf86748cabed9706'
         '57e013a7999a3b0fb7277e8276e28410'
         '08a99f4d7ad63024bc886e118ddcbc0f'
         'f9a57d898df73f43c6a85c8d8cc455ba')

build() {
  # Patch home.
  cd $srcdir
  patch -Np0 -i ${pkgname}_home.patch

  # Patch master servers.
  sed -i 's/master.urbanterror.net/master.urbanterror.info/g' \
    $srcdir/ioUrbanTerrorServerSource/code/qcommon/qcommon.h \
    $srcdir/ioUrbanTerrorClientSource/code/qcommon/qcommon.h
  sed -i 's/master2.urbanterror.net/master2.urbanterror.info/g' \
    $srcdir/ioUrbanTerrorServerSource/code/qcommon/qcommon.h \
    $srcdir/ioUrbanTerrorClientSource/code/qcommon/qcommon.h

  # Patch and compile client.
  cd $srcdir/ioUrbanTerrorClientSource
  patch -Np0 -i $srcdir/default-curl_lib.patch
  make
  
  # Patch and compile server.
  cd $srcdir/ioUrbanTerrorServerSource
  patch -Np1 -i $srcdir/cmd.c.patch
  patch -Np1 -i $srcdir/g_cmds.c.patch
  patch -Np1 -i $srcdir/qcommon.h.patch
  patch -Np1 -i $srcdir/sv_client.c.patch
  patch -Np1 -i $srcdir/sv_main.c.patch
  make
}

package() {
  install -d $pkgdir/usr/share/urbanterror

  cd $pkgdir/usr/share/urbanterror

  # Copy binaries.
  install -m755 $srcdir/ioUrbanTerrorClientSource/build/release-linux-*/ioUrbanTerror.* ioUrbanTerror
  install -m755 $srcdir/ioUrbanTerrorServerSource/build/release-linux-*/ioUrTded.* ioUrTded

  # Copy desktop launcher.
  install -Dm644 $srcdir/urbanterror.desktop $pkgdir/usr/share/applications/urbanterror.desktop
  install -Dm644 $srcdir/urbanterror.png $pkgdir/usr/share/pixmaps/urbanterror.png

  # Copy launch scripts.
  install -Dm755 $srcdir/urbanterror.sh $pkgdir/usr/bin/urbanterror
  install -Dm755 $srcdir/urbanterror-server.sh $pkgdir/usr/bin/urbanterror-server
}
