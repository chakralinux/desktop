# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetjärn (totte) <totte@tott.es>
# Contributor: Rémy Oudompheng <remy@archlinux.org>
#              Hugo Doria <hugo@archlinux.org>

pkgname=namcap
pkgver=3.2.5
pkgrel=1
pkgdesc="A Pacman package analyzer"
arch=('x86_64')
url="http://projects.archlinux.org/namcap.git/"
license=('GPL')
depends=('python3' 'pyalpm>=0.5' 'licenses'
         'binutils' 'elfutils' 'python3-pyelftools')
makedepends=('python3-distribute')
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha1sums=('5fb93f267766aaf9b399387af374f624849caf25')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    python3 setup.py build
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}
    env PARSE_PKGBUILD_PATH=${srcdir}/${pkgname}-${pkgver} \
        PATH=${srcdir}/${pkgname}-${pkgver}:$PATH \
        python3 setup.py test
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    python3 setup.py install --root=${pkgdir}
}
