# Include global configuration

pkgname=kigo
pkgver=18.08.2
pkgrel=1
pkgdesc="An open-source implementation of the popular Go game"
arch=('x86_64')
url='http://kde.org/applications/games/kigo/'
screenshot='https://www.kde.org/images/screenshots/kigo.png'
license=('GPL' 'LGPL' 'FDL')
depends=(libkdegames gnugo hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools knotifyconfig)
groups=('kdegames' 'kde' 'kde-applications')
conflicts=(kdegames-kigo)
replaces=(kdegames-kigo)
provides=(kdegames-kigo)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('be35e9b96b422e0dd239ac8d306a10c28cbab77588189ae317d26a3492a0839f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
