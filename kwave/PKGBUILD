#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=kwave
pkgver=0.8.8
pkgrel=1
pkgdesc="A sound editor for KDE"
arch=('i686' 'x86_64')
url="http://kwave.sourceforge.net/"
license=('GPL')
depends=('audiofile>=0.3.0' 'flac' 'fftw' 'kdelibs' 'libvorbis' 'id3lib' 'libsamplerate' 'libmad')
makedepends=('cmake' 'kdesdk-poxml' 'gettext' 'imagemagick' 'automoc4' 'docbook-xsl')
categories=('multimedia')
screenshot=('http://kde-apps.org/CONTENT/content-pre1/11781-1.png')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-1.tar.bz2)
md5sums=('09f266c807a0276bd57573ebb90ace24')

build() {
  cd "${srcdir}"
  mkdir build
  cd build

  cmake "../${pkgname}-${pkgver}" -DCMAKE_INSTALL_PREFIX=/usr \
  -DLIB_INSTALL_DIR=/usr/lib \
  -DCMAKE_BUILD_TYPE=Release \
  -DWITH_PULSEAUDIO=OFF \
  -DWITH_PHONON=OFF \
  -DWITH_MP3=ON 
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
