# Include global configuration

pkgname=kwave
pkgver=18.08.1
pkgrel=1
pkgdesc="A sound editor for KDE"
arch=(x86_64)
url="http://kwave.sourceforge.net"
license=(GPL)
depends=(qt5-multimedia kio audiofile libmad libsamplerate hicolor-icon-theme id3lib fftw opus)
makedepends=(extra-cmake-modules kdoctools doxygen python librsvg)
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdemultimedia')
screenshot=('http://kde-apps.org/CONTENT/content-pre1/11781-1.png')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('e6c4fbfb7e49c4f71ac79cc4deccf13c88eff744b4ad46b4e1ebc888dcfd5dbf'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"  \
    -DWITH_PULSEAUDIO=OFF \
    -DWITH_PHONON=OFF \
    -DWITH_MP3=ON
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
