pkgname=mysql-connector-c++
pkgver=1.1.9
pkgrel=1
pkgdesc='A MySQL database connector for C++'
arch=('x86_64')
url='http://dev.mysql.com/doc/connector-cpp/en/'
license=('GPL')
depends=('libmariadbclient')
makedepends=('cmake' 'boost')
source=("https://cdn.mysql.com/Downloads/Connector-C++/${pkgname}-${pkgver}.tar.gz"
	'0001-mysql-connector-c++-mariadb-api.patch')
sha256sums=('3e31847a69a4e5c113b7c483731317ec4533858e3195d3a85026a0e2f509d2e4'
            '1694ead0b9c9cb7803a76f56e3871b4f64f045a07fa390cf18bc15be798035ee')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}/"

	patch -Np1 < "${srcdir}/0001-mysql-connector-c++-mariadb-api.patch"
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}/"

	cmake . -Wno-dev \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib \
		-DMYSQLCPPCONN_BUILD_EXAMPLES=OFF \
		-DMYSQL_LIB=/usr/lib/libmysqlclient.so
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}/"

	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/{COPYING,README,Licenses_for_Third-Party_Components.txt,lib/libmysqlcppconn-static.a}
}
