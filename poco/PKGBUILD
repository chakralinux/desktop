# Maintainer: Adrian Chaves Fernandez (Gallaecio) <adriyetichaves@gmail.com>

pkgname=poco
pkgver=1.4.6.4
_pkgver=1.4.6
_pkgver2=1.4.6p4
pkgrel=1
pkgdesc="C++ class libraries for network-centric, portable applications, complete edition"
arch=('x86_64')
url="http://www.pocoproject.org/"
license=('custom:boost')
depends=('unixodbc' 'libmariadbclient' 'openssl')
makedepends=('gcc' 'make' 'unixodbc' 'libmariadbclient' 'openssl' 'chrpath')
source=(
  
"${pkgname}-${pkgver}-all.tar.gz"::"http://pocoproject.org/releases/${pkgname}-${_pkgver}/${pkgname}-${_pkgver2}-all.tar.gz"
)
sha256sums=('a0453cd88a8360036b6d474c9ca428299c183bc66a119509273b8a5b7db42848')

build()
{
  cd "${srcdir}/${pkgname}-${_pkgver2}-all"
  ./configure --prefix=/usr --no-samples --no-tests
  make ODBCLIBDIR="/usr/lib"
}

package()
{
  cd "${srcdir}/${pkgname}-${_pkgver2}-all"
  make ODBCLIBDIR="/usr/lib" DESTDIR="${pkgdir}" install
  install -Dm644 'LICENSE' "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # remove rpath information from binaries
  chrpath -d "${pkgdir}/usr/bin/cpspc"
  chrpath -d "${pkgdir}/usr/bin/cpspcd"
  chrpath -d "${pkgdir}/usr/bin/f2cpspd"
  chrpath -d "${pkgdir}/usr/bin/f2cpsp"
 
#  remove debugging libraries
#  rm "${pkgdir}/usr/lib/libPoco"*"d.so"*
}
