# Maintainer: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kactivities-frameworks-git
pkgver=r589.3a63096
pkgrel=1
pkgdesc="Core components for the KDE's Activities"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
depends=('qt5-declarative' 'boost-libs' 'kf5umbrella' 'kdbusaddons' 'kwindowsystem' 'kservice')
makedepends=('extra-cmake-modules' 'git' 'boost')
options=("debug")
source=("git://anongit.kde.org/kactivities.git#tag=v$pkgver")
md5sums=('SKIP')

pkgver() {
  cd kactivities
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
