#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=dumb
pkgver=0.9.3
pkgrel=1
pkgdesc="An IT, XM, S3M and MOD player library."
arch=('i686' 'x86_64')
license=('custom:dumb' 'GPL')
url="http://dumb.sourceforge.net/"
depends=('allegro' 'glibc')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('f48da5b990aa8aa822d3b6a951baf5c2')

build() {
  cd $srcdir/$pkgname-$pkgver
  [ $CARCH = "x86_64" ] && export CFLAGS="$CFLAGS -fPIC"
  
  # Create the directories which the Makefile somehow fails to create
  install -d lib/unix/{debug,release}

  # Make with Allegro support
  # Upstream's make process is interactive, and we can't simply pass
  # a here-document to the make command.  The best way is to write our
  # own config.txt, circumventing the interactive configuration step,
  # as is done in Gentoo's ebuild.
  cat << EOF > make/config.txt
include make/unix.inc
ALL_TARGETS := core core-examples core-headers
ALL_TARGETS += allegro allegro-examples allegro-headers
PREFIX := "$pkgdir/usr"
EOF

  # Upstream's makefile ignores our CFLAGS.  They put -I directives in
  # their own CFLAGS, so we can't just say
  # make CFLAGS=...
  # Gentoo's ebuild overrides another of their makefile variables, named
  # OFLAGS.
  make OFLAGS="${CFLAGS}"
}

package() {
  cd $srcdir/$pkgname-$pkgver

  # Create directories
  install -d "${pkgdir}"/usr/{lib,bin,include}
  install -D -m644 licence.txt \
    $pkgdir/usr/share/licenses/$pkgname/licence.txt

  make install
}
