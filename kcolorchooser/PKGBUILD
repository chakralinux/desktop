# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegraphics-kcolorchooser/
# Include global configuration

pkgname=kcolorchooser
pkgver=18.07.90
pkgrel=1
pkgdesc='Colour chooser'
url='http://kde.org/applications/graphics/kcolorchooser/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=(kio libgphoto2)
makedepends=(extra-cmake-modules)
groups=(kde kde-applications kdegraphics kde-uninstall)
conflicts=(kdegraphics-kcolorchooser)
replaces=(kdegraphics-kcolorchooser)
options=('docs' 'debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f74b1e847700528c3ee4458352eeaa2276aa991f7c5fceac232a6a15264e9f63'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
