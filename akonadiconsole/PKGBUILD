# Include global configuration

pkgname=akonadiconsole
pkgver=18.12.0
pkgrel=1
pkgdesc="Akonadi management and debugging console"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(hicolor-icon-theme calendarsupport messagelib)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin boost python3)
options=(docs debug)
groups=(kde kde-uninstall kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('65c53c7b938dca80919b97b3329d3764a722e7f3e8f3c6378f8505155e42e8e1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
