pkgname=emacs-nox
pkgver=25.3
pkgrel=1
pkgdesc='The Emacs Editor, without X11 support'
arch=('x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus' 'ncurses' 'perl' 'gnutls')
provides=('emacs')
conflicts=('emacs' 'emacs-cvs')
options=('docs')
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-${pkgver}.tar.xz)
md5sums=('05da38e18da75c98d48ad69b5ab74e71')

build() {
  cd $srcdir/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-x --without-sound
  make
}

package() {
  cd $srcdir/emacs-$pkgver
  make DESTDIR=${pkgdir} install

  #remove conflict with ctags package
  mv $pkgdir/usr/bin/{ctags,ctags.emacs}
  mv $pkgdir/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}
  
  #fix all the 777 perms on directories
  find $pkgdir/usr/share/emacs/$pkgver -type d -exec chmod 755 {} \;
  #fix user/root permissions on usr/share files
  find $pkgdir/usr/share/emacs/$pkgver -exec chown root.root {} \;
  #remove .desktop file and icons
  rm -rf $pkgdir/usr/share/{applications,icons}

  # fix perms on /var/games
  chmod 775 $pkgdir/var/games
  chmod 775 $pkgdir/var/games/emacs
  chmod 664 $pkgdir/var/games/emacs/*
  chown -R root:games $pkgdir/var/games

  # fix conflict with texinfo
  rm $pkgdir/usr/share/info/info.info.gz
}
