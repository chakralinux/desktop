pkgname=emacs-nox
pkgver=26.1
pkgrel=2
pkgdesc='The Emacs Editor, without X11 support'
arch=('x86_64')
url="https://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus' 'ncurses' 'perl' 'gnutls')
provides=('emacs')
conflicts=('emacs' 'emacs-cvs')
options=('docs')
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-${pkgver}.tar.xz)
md5sums=('649ec46965a8b842bdb2cbf7764c2a9a')

build() {
  cd $srcdir/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-x --without-sound
  make
}

package() {
  cd $srcdir/emacs-$pkgver
  make DESTDIR=${pkgdir} install

  #remove conflict with ctags package
  mv $pkgdir/usr/bin/{ctags,ctags.emacs}
  mv $pkgdir/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

  #fix user/root permissions on usr/share files
  find $pkgdir/usr/share/emacs/$pkgver -exec chown root.root {} \;
  #remove .desktop file and icons
  rm -rf $pkgdir/usr/share/{applications,icons}
}
