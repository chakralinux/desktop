#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=emacs-nox
pkgver=23.3
pkgrel=1
pkgdesc="The Emacs Editor, without X11 support"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL')
replaces=('cedet')
depends=('dbus-core' 'ncurses' 'perl')
provides=('emacs')
conflicts=('emacs' 'emacs-cvs')
options=('docs')
categories=('office')
changelog=$pkgname.changelog
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-${pkgver}.tar.gz)
sha256sums=('0038248a4299708a8717f2f5ba48f1c0f15e95ffc5fcfb08cbe5c3146757e34c')

build() {
  cd ${srcdir}/emacs-$pkgver

#gcc 4.5 Workaround: http://gcc.gnu.org/bugzilla/show_bug.cgi?id=43904
  CFLAGS="${CFLAGS} -fno-optimize-sibling-calls"\
 ./configure --prefix=/usr --without-x --without-sound

#we don't want to use /usr/libexec
  sed -i "s|\"/usr/libexec/emacs.*$|\"/usr/lib/emacs/$pkgver\"|g" src/epaths.h
#  mv newepaths.h src/epaths.h || return 1

  make libexecdir=/usr/lib archlibdir=/usr/lib/emacs/${pkgver}
  make prefix=${pkgdir}/usr libexecdir=${pkgdir}/usr/lib \
    archlibdir=${pkgdir}/usr/lib/emacs/${pkgver} install

#remove conflict with ctags package
  mv ${pkgdir}/usr/bin/{ctags,ctags.emacs}
  mv ${pkgdir}/usr/bin/{etags,etags.emacs}
  mv ${pkgdir}/usr/share/man/man1/{etags.1,etags.emacs.1}
  mv ${pkgdir}/usr/share/man/man1/{ctags.1,ctags.emacs.1}
#fix all the 777 perms on directories
  find ${pkgdir}/usr/share/emacs/$pkgver -type d -exec chmod 755 {} \;
#fix user/root permissions on usr/share files
  find ${pkgdir}/usr/share/emacs/$pkgver -exec chown root.root {} \;
#remove empty files
  rm -rf ${pkgdir}/usr/var
#remove .desktop file and icons
  rm -rf ${pkgdir}/usr/share/{applications,icons}

#get rid of the package's info directory, install-info adds entries for us at install-time
  rm ${pkgdir}/usr/share/info/dir
  gzip -9nf ${pkgdir}/usr/share/info/*
}