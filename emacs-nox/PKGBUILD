#
# Apps packages for Chakra, part of www.chakra-project.org
#
# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: M Tortosa <manutortosa@chakra-project.org>

pkgname=emacs-nox
pkgver=24.3
pkgrel=3
pkgdesc='The extensible, customizable, self-documenting real-time display editor (without X11 support)'
arch=('x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus-core' 'ncurses' 'perl')
provides=('emacs')
conflicts=('emacs' 'emacs-cvs')
options=('docs')
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-${pkgver}.tar.gz)
sha512sums=('d20441025efd4931ef64cc2bb18eddc9')

build() {
    cd ${srcdir}/emacs-$pkgver

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --without-x \
        --without-sound
    make
}

package() {
    cd ${srcdir}/emacs-$pkgver
    make DESTDIR=${pkgdir} install

    # Remove conflict with ctags package
    mv ${pkgdir}/usr/bin/{ctags,ctags.emacs}
    mv ${pkgdir}/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}
    
    # Fix all the 777 perms on directories
    find ${pkgdir}/usr/share/emacs/$pkgver -type d -exec chmod 755 {} \;
    
    # Fix user/root permissions on usr/share files
    find ${pkgdir}/usr/share/emacs/$pkgver -exec chown root.root {} \;
    
    # Remove .desktop file and icons
    rm -rf ${pkgdir}/usr/share/{applications,icons}

    # Fix perms on /var/games
    chmod 775 ${pkgdir}/var/games
    chmod 775 ${pkgdir}/var/games/emacs
    chmod 664 ${pkgdir}/var/games/emacs/*
    chown -R root:games ${pkgdir}/var/games

    # Fix conflict with texinfo
    rm ${pkgdir}/usr/share/info/info.info.gz
}
