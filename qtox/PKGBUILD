pkgname=qtox
_pkgname=qTox
pkgver=1.16.3
pkgrel=2
pkgdesc='Powerful Tox client written in C++/Qt that follows the Tox design guidelines'
arch=('x86_64')
url='https://github.com/tux3/qTox'
screenshot="https://camo.githubusercontent.com/22edee6a50bd284bd985ed7b219d57130e5241c8/687474703a2f2f692e696d6775722e636f6d2f363641524247432e706e67"
license=('GPL3')
depends=('libxss' 'desktop-file-utils' 'openal' 'ffmpeg' 'qrencode' 'qt5-svg' 'sqlcipher' 'toxcore' 'libexif' 'qt5-base')
makedepends=('qt5-tools' 'cmake' 'check' 'pkg-config')
provides=('qtox')
conflicts=('qtox-git')
replaces=('qtox-git')
source=("https://github.com/$_pkgname/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('a857266ffcf32b1963e3a4428501299ce19f3c087087583115dc01e41934fc31d52ea54d781fa93ec7e21f6e3303b4e22383ecaaa7f1f1dbf32d740db73aca4c')

build() {
  cd $_pkgname-$pkgver
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
	       -DENABLE_GTK_SYSTRAY=False
  make
}

check() {
  cd $_pkgname-$pkgver
  cd build
  make test
}

package() {
  # executable
  cd $_pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
}
