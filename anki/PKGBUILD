#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: Julien Humbert <julroy67 at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch at gmail dot com>
# Contributor: Timm Preetz <timm at preetz dot us>
# Contributor: Michael 'manveru' Fellinger <m.fellinger at gmail dot com>
# Contributor: Dave Pretty <david dot pretty at gmail dot com>

pkgname=anki
pkgver=1.2.9
pkgrel=1
pkgdesc='Helps you remember facts (like words/phrases in a foreign language) efficiently.'
url='http://ankisrs.net/'
license=(GPL3)
arch=('any')
depends=('beautiful-soup' 'pyqt' 'python-pysqlite' 'python-simplejson' 'python-sqlalchemy')
optdepends=('python-matplotlib: show graphs.'
	    'sox: audio recording.'
	    'pyaudio: audio recording.')
makedepends=('desktop-file-utils' 'setuptools')
source=(http://anki.googlecode.com/files/$pkgname-$pkgver.tgz)
md5sums=('3b32c40c589565ebd5b349f8e0d36967')

package() {
  cd $srcdir/$pkgname-$pkgver

  export PYTHONPATH=$srcdir/$pkgname-$pkgver/libanki
  
  (
    cd libanki \
    && python2 setup.py install --root $pkgdir
  )
  python2 setup.py install --root $pkgdir
  
  # Desktop integration.
  desktop-file-install --dir $pkgdir/usr/share/applications $pkgname.desktop
  install -D -m 644 icons/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
