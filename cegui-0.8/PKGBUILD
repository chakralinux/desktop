# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org> 
# Contributor: William Rea <sillywilly@gmail.com>, 
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>

pkgname=cegui-0.8
pkgver=0.8.3
pkgrel=4
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines"
arch=('x86_64')
url="http://crayzedsgui.sourceforge.net"
license=("MIT")
depends=('pcre' 'glew' 'expat' 'freetype2' 'libxml2' 'devil' 'freeglut' 'lua' 'silly' 'glfw2')
makedepends=('cmake' 'python2' 'doxygen' 'ogre' 'boost' 'graphviz' 'irrlicht' 'mesa' 'mercurial' 'toluapp')
optdepends=("python2: python bindings"
            "ogre: ogre module"
            "gtk2: gtk2 module"
            "irrlicht: irrlicht module")
provides=('cegui')
conflicts=('cegui')
replaces=('cegui')
source=("hg+https://bitbucket.org/cegui/cegui#tag=v0-8-3"
        "freetype.patch")
md5sums=('SKIP'
         '4c5f81f109f36718045bb4ba5ba25602')

build() {
  cd "$srcdir/cegui"

  patch -Np1 < "$srcdir/freetype.patch"
  sed -i "s/lib64/lib/g" CMakeLists.txt

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCEGUI_LIB_INSTALL_DIR=lib \
      -DCEGUI_BUILD_PYTHON_MODULES=OFF # currently broken :(
      # -PYTHON_EXECUTABLE=/usr/bin/python2

  make
  make html
}

package() {
  cd "$srcdir/cegui"/build
 
  make DESTDIR="${pkgdir}" install

  install -Dm644 ../doc/COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
