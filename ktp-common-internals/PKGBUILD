# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/telepathy-kde-common-internals/
# Include global configuration

pkgname=ktp-common-internals
pkgver=18.04.2
pkgrel=1
pkgdesc="Common libraries for telepathy-kde"
arch=('x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('hicolor-icon-theme' 'kaccounts-integration' 'knotifyconfig' 'kpeople' 'ktexteditor' 'telepathy-logger-qt' 'libotr'
         'telepathy-accounts-signon')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'doxygen')
groups=('kde-telepathy')
install=${pkgname}.install
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6209a0f524ce575a7f6cbb7754050e41e0ac6d484b8cc174812743de110db18e'
                        'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
