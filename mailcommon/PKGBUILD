# Include global configuration

pkgname=mailcommon
pkgver=18.08.3
pkgrel=1
pkgdesc="Lib which provides support for mail apps"
arch=('x86_64')
url='https://github.com/KDE/mailcommon'
license=('LGPL')
depends=('akonadi' 'ki18n' 'kmime' 'kcodecs' 'kiconthemes' 'karchive' 'kitemviews'
         'kwindowsystem' 'kxmlgui' 'messagelib' 'libkdepim' 'pimcommon' 'mailimporter' 'kmailtransport'
         'kldap' 'phonon-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools')
groups=(kde kde-applications kdepim kde-uninstall)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('17ab7cad37a0bac8ae4baa075241571571a35b743b52c640717b9cf54a9382e8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
