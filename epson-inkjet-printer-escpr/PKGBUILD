#Contributions from AUR: https://aur.archlinux.org/packages/epson-inkjet-printer-escpr/

pkgname=epson-inkjet-printer-escpr
pkgver=1.6.11
pkgrel=1
pkgdesc="Epson Inkjet Printer Driver (ESC/P-R) for Linux"
arch=('x86_64')
url="http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX"
license=('GPL2')
depends=('cups' 'ghostscript')
source=('https://download3.ebz.epson.net/dsc/f/03/00/05/36/34/7b858fe7440558b9864dde0a6d353821ddb6f310/epson-inkjet-printer-escpr-1.6.11-1lsb3.2.tar.gz'
        'bug_x86_64.patch')
sha256sums=('367f2503b533ffacae9b9f529f629daaf9f4f8068615ae3d77cb5a4e13bbe39d'
            '277bc9afe581d2c434217db646e20ecfd1b6045bdcce861f2f08992ccf854084')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i "${srcdir}/bug_x86_64.patch"

}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --with-cupsfilterdir=/usr/lib/cups/filter \
              --with-cupsppddir=/usr/share/ppd
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

