#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=cegui
_pkgname=CEGUI
pkgver=0.8.2
_pkgver=0.8
pkgrel=1
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines."
arch=('i686' 'x86_64')
url="http://crayzedsgui.sourceforge.net"
license=("MIT")
depends=('boost-libs' 'devil' 'expat' 'freeglut' 'freetype2' 'glew' 'glfw' 'irrlicht' 'libxml2' 'lua' 'ogre' 'pcre' 'python2' 'silly' 'toluapp')
makedepends=('boost' 'cmake' 'doxygen' 'graphviz' 'irrlicht' 'mesa' 'ogre' 'python2')
optdepends=("python2: python bindings"
            "ogre: ogre module"
            "irrlicht: irrlicht module")
source=("http://downloads.sourceforge.net/crayzedsgui/CEGUI Mk-2/${pkgname}-${pkgver}.tar.gz")
md5sums=('f72951f0cc0374bb2294035cb639bb83')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i "s/lib64/lib/g" CMakeLists.txt
  sed -i "s/lib32/lib/g" CMakeLists.txt
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCEGUI_LIB_INSTALL_DIR=lib \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    ..

  make -j5
  make html
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ../doc/COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
