#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=cegui
_pkgname=CEGUI
pkgver=0.7.7
pkgrel=2
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines."
arch=('i686' 'x86_64')
url="http://crayzedsgui.sourceforge.net"
license=("MIT")
depends=('boost-libs' 'devil' 'expat' 'freeglut' 'freetype2' 'glew' 'irrlicht' 'libxml2' 'lua' 'ogre' 'pcre' 'python2' 'silly' 'toluapp')
makedepends=('boost' 'doxygen' 'graphviz')
options=(!libtool)
source=("http://downloads.sourceforge.net/crayzedsgui/${_pkgname}-${pkgver}.tar.gz")
md5sums=('8b83577f86eaa1581765dd155c7c8f24')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  
  # Typo fixed in Irrlicht 1.8.0, whereas CEGUI 0.7.7 was based on Irrlicht 1.7.x.
  sed -e "s#texureBlend#textureBlend#" -i cegui/src/RendererModules/Irrlicht/CEGUIIrrlichtGeometryBuffer.cpp

  ./bootstrap
  ./configure \
    --enable-null-renderer \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Documentation.
  cd doc/doxygen && doxygen
  cd .. && make DESTDIR=${pkgdir} install-html

  # License.
  install -D -m644 "${srcdir}/${_pkgname}-${pkgver}/doc/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
