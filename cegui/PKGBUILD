#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=cegui
_pkgname=CEGUI
pkgver=0.7.6
pkgrel=4
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines."
arch=('i686' 'x86_64')
url="http://crayzedsgui.sourceforge.net"
license=("MIT")
depends=('boost-libs' 'devil' 'expat' 'freeglut' 'freetype2' 'glew' 'irrlicht' 'libxml2' 'lua' 'ogre' 'pcre' 'python2' 'silly' 'toluapp')
makedepends=('boost' 'doxygen' 'graphviz')
options=(!libtool)
source=("http://downloads.sourceforge.net/crayzedsgui/${_pkgname}-${pkgver}.tar.gz"
        
"cegui-0.7.6-python-detection.patch::http://projects.archlinux.org/svntogit/community.git/plain/trunk/cegui-0.7.6-python-detection.patch?h=packages/cegui")
md5sums=('7ddb5145dc94fb7daf9aea1d30a6ffa3'
         '4600775f78b7349621a8a314ade8c902')
build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -Np0 < "${srcdir}/cegui-0.7.6-python-detection.patch"

  ./bootstrap
  ./configure \
    --enable-null-renderer \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Documentation.
  cd doc/doxygen && doxygen
  cd .. && make DESTDIR=${pkgdir} install-html

  # License.
  install -D -m644 "${srcdir}/${_pkgname}-${pkgver}/doc/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
