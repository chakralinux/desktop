# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegraphics-kolourpaint/
# Include global configuration

pkgname=kolourpaint
pkgver=18.08.3
pkgrel=1
pkgdesc='A simple painting program to quickly create raster images'
url='http://kde.org/applications/graphics/kolourpaint/'
arch=('x86_64')
license=(GPL LGPL FDL)
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegraphics')
depends=(kdelibs4support libksane hicolor-icon-theme libkexiv2)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools)
options=('debug')
conflicts=(kdegraphics-kolourpaint)
replaces=(kdegraphics-kolourpaint)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('207d344db75e13f1d72050e329dba3d8a48227c22c93e916741f76ae742fb44b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
