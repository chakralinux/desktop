#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgbase=t-engine4
_pkgbase=t-engine
pkgname=('t-engine4' 'tome')
pkgver=1.0.0_beta31
_pkgver=1.0.0beta31
pkgrel=1
arch=('i686' 'x86_64')
url="http://tome.te4.org/"
license=('GPL3')
makedepends=('premake')
source=("http://te4.org/dl/$_pkgbase/$pkgbase-src-$_pkgver.tar.bz2")
md5sums=('d05d0e32cfe545456e38614ea110210e')

build() {
  cd $srcdir/$pkgbase-src-$_pkgver
  premake gmake
  make
}

package_t-engine4() {
  pkgdesc="Roguelike game engine operating in Lua and available for all major platforms."
  depends=('mesa' 'openal' 'sdl' 'sdl_image' 'sdl_mixer' 'sdl_ttf')

  cd $srcdir/$pkgbase-src-$_pkgver

  install -d $pkgdir/usr/share/$pkgbase

  cp -r bootstrap/ game/ bin/Debug/t-engine \
      $pkgdir/usr/share/$pkgbase
  rm -R $pkgdir/usr/share/$pkgbase/game/modules/tome

  # Execution script.
  install -d $pkgdir/usr/bin
  echo "#!/bin/bash"		 > $pkgdir/usr/bin/$pkgname
  echo "cd /usr/share/$pkgbase"	>> $pkgdir/usr/bin/$pkgname
  echo "exec ./t-engine \$*"	>> $pkgdir/usr/bin/$pkgname
  chmod +x $pkgdir/usr/bin/$pkgname

  # License.
  install -d $pkgdir/usr/share/licenses/$pkgname
  cp -r COPYING CREDITS \
      $pkgdir/usr/share/licenses/$pkgname
}

package_tome() {
  pkgdesc="Single-player, role-playing roguelike game set in the world of Eyal."
  depends=('t-engine4')

  cd $srcdir/$pkgbase-src-$_pkgver

  install -d $pkgdir/usr/share/$pkgbase/game/modules

  cp -r game/modules/tome \
      $pkgdir/usr/share/$pkgbase/game/modules/

  # Execution script.
  install -d $pkgdir/usr/bin
  echo "#!/bin/bash"		 > $pkgdir/usr/bin/$pkgname
  echo "cd /usr/share/$pkgbase"	>> $pkgdir/usr/bin/$pkgname
  echo "exec ./t-engine \$*"	>> $pkgdir/usr/bin/$pkgname
  chmod +x $pkgdir/usr/bin/$pkgname

  # License.
  install -d $pkgdir/usr/share/licenses/$pkgname
  cp -r COPYING CREDITS \
      $pkgdir/usr/share/licenses/$pkgname
}
