# contribution from Arch https://www.archlinux.org/packages/extra/x86_64/conky/

pkgname=conky
pkgver=1.10.1
pkgrel=4
pkgdesc="An advanced, highly configurable system monitor for X based on torsmo"
arch=('x86_64')
url="https://github.com/brndnmtthws/conky"
license=('custom')
replaces=('torsmo')
depends=('glib2' 'curl' 'lua' 'wireless_tools' 'libxml2' 'libxft' 'libxdamage' 'imlib2')
makedepends=('cmake' 'docbook2x' 'docbook-xml' 'man-db')
categories=('utils')
options=('!strip' 'debug')
source=("https://github.com/brndnmtthws/conky/archive/v${pkgver}.tar.gz"
	'ascii.patch'
	'curl.patch'
        'lua53.patch'
        'ipv6.patch')
sha256sums=('b4e66a71e8211d02b8dbe8cd4873af209af25326e63afd90c90ba9416c79cf21'
            '4c91fb4925b62f53255b3777d812ff6736a8d20692066d901c49d885050b9552'
            '415460524c6e22b3ed534df9bab1c0bdfcadfd67c59282372c5c6e4f47ab2d9e'
            '50844ccdd2b1f8e1b4b110d8b9069dc35af11650163013e2b98cc2e59aeda569'
            '09e13fc56fe87cba6fafe27c1215dd2f8b1db7930ee5ebb7d076ec6ca8335012')


prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
#	patch -p1 -i ../ascii.patch # db2x_manxml fails on non-ascii chars
#	patch -p1 -i ../lua53.patch # lua_gettable returns an int in lua-5.3
#	patch -p1 -i ../ipv6.patch # https://bugs.archlinux.org/task/45626
#	patch -p1 -i ../curl.patch # https://github.com/bagder/curl/issues/342
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	cmake \
		-D CMAKE_BUILD_TYPE=Release \
		-D MAINTAINER_MODE=ON \
		-D BUILD_CURL=ON \
		-D BUILD_XDBE=ON \
		-D BUILD_IMLIB2=ON \
		-D BUILD_RSS=ON \
		-D BUILD_WEATHER_METAR=ON \
		-D BUILD_WEATHER_XOAP=ON \
		-D BUILD_WLAN=ON \
		-D CMAKE_INSTALL_PREFIX=/usr \
		.

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 extras/vim/syntax/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax/conkyrc.vim
	install -Dm644 extras/vim/ftdetect/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/ftdetect/conkyrc.vim
}

