#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=crypto++
_pkgname=cryptopp
pkgver=5.6.3
_pkgver=${pkgver//./}
pkgrel=1
pkgdesc="C++ class library of cryptographic schemes."
arch=('i686' 'x86_64')
url="http://www.cryptopp.com"
license=('custom')
makedepends=('unzip')
source=($url/$_pkgname$_pkgver.zip
        libcrypto++.pc)
md5sums=('3c5b70e2ec98b7a24988734446242d07'
         '46de3847c59ad16456fa863eb33e73b2')

# The code below has been copied from the Arch Linux PKGBUILD.
build() {
  sed -i -e 's/^CXXFLAGS/#CXXFLAGS/' GNUmakefile
  export CXXFLAGS="${CXXFLAGS} -DNDEBUG -fPIC"
  make -f GNUmakefile  
  make libcryptopp.so
}

package() {
  install -d ${pkgdir}/usr/{lib/pkgconfig,include/cryptopp}
  install -m644 *.h ${pkgdir}/usr/include/cryptopp/
  install -m644 libcryptopp.so "${pkgdir}/usr/lib/libcryptopp.so"
  install -m644 ${srcdir}/libcrypto++.pc "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
