#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=crypto++
_pkgname=cryptopp
pkgver=5.6.1
_pkgver=561
pkgrel=3
pkgdesc="C++ class library of cryptographic schemes."
arch=('i686' 'x86_64')
url="http://www.cryptopp.com"
license=('custom')
makedepends=('unzip')
source=("${url}/${_pkgname}${_pkgver}.zip"
        'libcrypto++.pc'
        'fedora-cryptopp-5.6.1-gcc-4.7.0.patch')
md5sums=('96cbeba0907562b077e26bcffb483828'
         '2337c6edbc552d9142cae8e0854715e7'
         '2175eb9fa4efec0d5362e9fb8ff575d3')

build() {
  cd "${srcdir}"

  # Patch for GCC 4.7 compiling issues - remove this when the
  # fix is released upstream.
  patch -Np1 -i "${srcdir}/fedora-cryptopp-5.6.1-gcc-4.7.0.patch"

  sed -i -e 's/^CXXFLAGS/#CXXFLAGS/' GNUmakefile
  export CXXFLAGS="${CXXFLAGS} -DNDEBUG -fPIC"
  make -f GNUmakefile
  make libcryptopp.so
}

package() {
  install -d "${pkgdir}/usr"/{lib/pkgconfig,include/cryptopp}
  install -m644 *.h "${pkgdir}/usr/include/${_pkgname}"
  install -m644 libcryptopp.a "${pkgdir}/usr/lib"
  install -m644 libcryptopp.so "${pkgdir}/usr/lib"
  install -m644 "${srcdir}/libcrypto++.pc" "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"

  # License:
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
