pkgname=kpmcore
pkgver=3.0.0
pkgrel=1
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('x86_64')
url="http://kde.org/applications/system/kdepartitionmanager/"
license=('GPL2')
depends=('kio' 'parted' 'libatasmart' 'util-linux')
makedepends=('extra-cmake-modules' 'python3')
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfs-3g: NTFS support'
	    'dosfstools: FAT32 support'
            'btrfs-progs: btrfs support')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('5f98cb2dc97b00af6d848f3df6e68d406d8202e7bcffe7a2092d0f47adfe799a')

build() {
  cd ${pkgname}-${pkgver}

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
