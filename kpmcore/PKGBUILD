pkgname=kpmcore
pkgver=3.2.0
pkgrel=1
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('x86_64')
url="http://kde.org/applications/system/kdepartitionmanager/"
license=('GPL2')
depends=('kio' 'parted' 'libatasmart' 'util-linux' 'kservice' 'kwidgetsaddons')
makedepends=('extra-cmake-modules' 'python3')
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfs-3g: NTFS support'
	    'dosfstools: FAT32 support'
	    'f2fs-tools: F2FS support'
            'btrfs-progs: btrfs support'
            'exfat-utils: exFAT support'
            'nilfs-utils: nilfs support')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('2b38c2bbc3d9a47cd4bfa2e8d2ad1bd7d791383dfe479506456b52a99ff899cb')

build() {
  cd ${pkgname}-${pkgver}

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
