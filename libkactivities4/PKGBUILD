pkgname='libkactivities4'
pkgver=4.13.3 # there is no 4.14 version!
pkgrel=7
arch=('x86_64')
pkgdesc='API for using and interacting with Activities'
url='http://kde.org/'
license=('GPL'
         'FDL')
depends=("kdelibs>=${_kdever}")
makedepends=('cmake'
             'automoc4')
provides=('kactivities')
replaces=('kactivities<=4.13.3-3')
conflicts=('kactivities<=4.13.3-3')

options=('debug')
source=(http://download.kde.org/Attic/${pkgver}/src/kactivities-$pkgver.tar.xz)
sha256sums=("c7cb9d023f6e5cd01d76568c3590303ea3ecb4ebe9535b31862957846f5e898a")

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm "${pkgdir}"/usr/bin/kactivitymanagerd
  rmdir "${pkgdir}"/usr/bin
}
