#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/plasma-desktop
source ../plasma.conf

pkgname=plasma-desktop
pkgver=${PVersion}
pkgrel=3
pkgdesc='KDE Plasma Desktop'
arch=('x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('qt5-graphicaleffects' 'powerdevil' 'polkit-kde-agent-frameworks' 'libcanberra'
         'kwin' 'libusb-compat' 'libxkbfile' 'kmenuedit' 'systemsettings' 'ksysguard' 'breeze' 'krunner'
         #sddm can be removed as a dependency in the future, only added here to automate the switch to Plasma 5
         'sddm-kcm')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'baloo-kf5')
optdepends=('plasma-nm: Network manager applet')
checkdepends=("cmake")
replaces=('kde-workspace')
conflicts=('kde-workspace' 'kde-baseapps-kdepasswd')
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PSubVersion}.tar.xz")
sha256sums=( $(getSum ${pkgname}) )
install=${pkgname}.install

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
