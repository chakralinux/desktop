#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=globulation2
_pkgname=glob2
pkgver=0.9.4.4
pkgrel=2
pkgdesc="RTS game which minimizes micro-management by automatically assigning tasks to units."
arch=('i686' 'x86_64')
url="http://www.globulation2.org/"
license=('GPL3')
depends=('boost-libs' 'glu' 'sdl_net' 'sdl_image' 'sdl_ttf' 'libvorbis' 'speex' 'libgl' 'fribidi' 'desktop-file-utils')
makedepends=('scons' 'boost' 'mesa' 'patch')
categories=('games')
install=$pkgname.install
source=("http://dl.sv.nongnu.org/releases/$_pkgname/0.9.4/$_pkgname-$pkgver.tar.gz"
	"$pkgname-0.9.4.1-gcc44.patch"
	"fix-ftbfs-gcc49.diff")
md5sums=('94c527325f355a29a2807f8f18a6e6a8'
         'b13cb1f73ece23bfc6413773678d583e'
         'a7f2a53de593c60a49d3ca26dac79ecc')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -p0 -i "$srcdir/$pkgname-0.9.4.1-gcc44.patch"
  patch -p1 -i "$srcdir/fix-ftbfs-gcc49.diff"
}


build() {
  cd $srcdir/$_pkgname-$pkgver
  scons LINKFLAGS="-lboost_system" BINDIR=$pkgdir/usr/bin INSTALLDIR=$pkgdir/usr/share
}

package () {
  cd $srcdir/$_pkgname-$pkgver
  scons install

  find $pkgdir/usr/share -type f -exec chmod 644 '{}' \;
  install -Dm644 data/icons/glob2-icon-48x48.png $pkgdir/usr/share/pixmaps/glob2-icon-48x48.png
}
