pkgname=kile
pkgver=2.9.92
pkgrel=1
pkgdesc="A user friendly TeX/LaTeX frontend for KDE."
arch=("x86_64")
license=('GPL2')
url="https://kile.sourceforge.net/"
screenshot="http://kile.sourceforge.net/images/screenshots/snap_structureview.png"
depends=('ktexteditor' 'kinit' 'okular' 'khtml' 'texlive-core')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('konsole: embedded terminal')
categories=('office')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
        kile-local-url.patch)
sha256sums=('177372dc25b1d109e037a7dbfc64b5dab2efe538320c87f4a8ceada21e9097f2'
            '2cea79a285fe8b481d64088a87b0e6bccd1091a9ac08c934cdead7dcb7bd1fa3')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kile-local-url.patch # Fix opening local URLs
}

build() {
  cd ${srcdir}
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=$pkgdir install
}
