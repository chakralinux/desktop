# Include global configuration

pkgname="kwalletmanager"
arch=('x86_64')
pkgver=18.08.2
pkgrel=1
pkgdesc="The KDE wallet system"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
depends=('kcoreaddons' 'kauth' 'kwallet' 'kservice' 'kcmutils' 'kdelibs4support' 'ki18n' 'kxmlgui' 'kconfig' 'kconfigwidgets' 'kdbusaddons')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
conflicts=('kdeutils-kwallet')
replaces=('kdeutils-kwallet')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeutils')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b807453fba5da9676b8965f157a990f340cf95eb85f5d77f75f2e0b454113f85'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
