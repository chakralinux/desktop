# Maintainer: Chaoting Liu <brli@chakraos.org>

pkgname=xembed-sni-proxy
pkgver=1.2.ded1538
pkgrel=1
pkgdesc="Convert XEmbed system tray icons to SNI icons (Experimental build)"
arch=('x86_64')
url="https://github.com/davidedmundson/${pkgname}"
license=('GPL' 'LGPL')
depends=(qt5-{base,x11extras} kwindowsystem)
makedepends=('extra-cmake-modules' 'git')
source=("git+${url}.git#commit=ded1538")
sha256sums=('SKIP')

pkgver() {
    cd ${pkgname}
    printf "1.2.$(git rev-parse --short HEAD)"
}

prepare() {
    if [ -d build ] ; then
        rm build -rf
    fi
}

build() {
    mkdir build && cd build
    cmake ../${pkgname} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_INSTALL_DIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DSYSCONF_INSTALL_DIR=/etc
    make  
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
