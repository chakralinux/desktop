#
# Chakra Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=freeimage
pkgver=3.15.0
pkgrel=1
pkgdesc="Library project for developers who would like to support popular graphics image formats."
arch=('i686' 'x86_64')
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=('gcc-libs')
makedepends=('hd2u')
categories=('graphics')
source=("http://downloads.sourceforge.net/project/freeimage/Source%20Distribution/${pkgver}/FreeImage${pkgver//./}.zip"
        'gcc4.5_ln.patch')
md5sums=('3b4f08e4985b269beb29a2fced1ef888'
         'f85279b2572a0a9e03775909cd9cd759')

build() {
  cp -r FreeImage FreeImagefip

  cd FreeImage
  patch -Np1 < ${srcdir}/gcc4.5_ln.patch
  make

  cd ${srcdir}/FreeImagefip
  patch -Np1 < ${srcdir}/gcc4.5_ln.patch
  make -f Makefile.fip 
}

package() {
  cd FreeImage
  make DESTDIR=${pkgdir} install 

  cd ${srcdir}/FreeImagefip
  make -f Makefile.fip DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/FreeImage/license-fi.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/license-fi.txt

  ln -s libfreeimage-${pkgver}.so ${pkgdir}/usr/lib/libfreeimage.so
  ln -s libfreeimageplus-${pkgver}.so ${pkgdir}/usr/lib/libfreeimageplus.so
}
