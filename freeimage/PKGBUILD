#
# Chakra Packages for Chakra, part of chakra-project.org
#
# Maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=freeimage
pkgver=3.15.4
pkgrel=1
pkgdesc="Library project for developers who would like to support popular graphics image formats."
arch=('x86_64')
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=('gcc-libs')
makedepends=('hd2u')
categories=('programming')
source=("http://downloads.sourceforge.net/project/freeimage/Source%20Distribution/${pkgver}/FreeImage${pkgver//./}.zip"
        'ImfAutoArray_cstring.patch')
md5sums=('9f9a3b2c3c1b4fd24fe479e8aaee86b1'
         '8153b95f571e43547e2be2b6f129df92')

build() {
  cp -r FreeImage FreeImagefip

  # added -fpermissive option to hide compile errors like notified here
  # http://sourceforge.net/p/freeimage/discussion/36110/thread/93d4ecd7/?limit=50
  export CFLAGS="$CFLAGS -fPIC -fexceptions -fpermissive -fvisibility=hidden -DNO_LCMS"
  export CXXFLAGS="$CXXFLAGS -fPIC -fexceptions -fpermissive -fvisibility=hidden -DNO_LCMS" 

  patch -Np1 < "$srcdir/ImfAutoArray_cstring.patch"
  
  cd FreeImage
  make

  cd ${srcdir}/FreeImagefip
  make -f Makefile.fip 
}

package() {
  cd FreeImage
  make DESTDIR=$pkgdir install 

  cd $srcdir/FreeImagefip
  make -f Makefile.fip DESTDIR=$pkgdir install

  # License.
  install -D -m644 $srcdir/FreeImage/license-fi.txt \
    $pkgdir/usr/share/licenses/$pkgname/license-fi.txt
}
