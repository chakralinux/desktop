pkgname=feh
pkgver=2.24
pkgrel=1
pkgdesc='Fast and light imlib2-based image viewer'
arch=('x86_64')
url="https://feh.finalrewind.org/"
license=('custom:MIT')
depends=('imlib2' 'curl' 'libxinerama' 'libexif')
optdepends=('imagemagick: support more file formats')
makedepends=('libxt')
categories=('graphics')
source=("${url}${pkgname}-${pkgver}.tar.bz2")
sha256sums=('98172dc548dddc6653c5b4193072775ee219d589dcd9ea3b1bf1d432675f1891')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PREFIX=/usr exif=1 help=1 stat64=1
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	
	# .desktop isn't needed since feh is a terminal app
	rm ${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
