pkgname=feh
pkgver=2.13.1
pkgrel=1
pkgdesc='Fast and light imlib2-based image viewer'
arch=('x86_64')
url="http://feh.finalrewind.org/"
license=('custom:MIT')
depends=('imlib2' 'curl' 'libxinerama' 'libexif')
optdepends=('perl: feh-cam, webcam wrapper for feh'
            'imagemagick: support more file formats')
makedepends=('libxt')
categories=('graphics')
source=("${url}${pkgname}-${pkgver}.tar.bz2")
sha512sums=('60f2d691c24495fc126f868d3a4b3e43c7a41d0a3ff9ecfcec66a26b62012ef68bbbab897398c1d054d8eaa747baa180c8a41ed77e750071fde4e57a058b179b')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PREFIX=/usr exif=1 help=1 stat64=1
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	
	# .desktop isn't needed since feh is a terminal app
	rm ${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
