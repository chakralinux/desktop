# Maintainer:   Manuel Tortosa <manutortosa@chakra-project@org>
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdegraphics-kolourpaint
_pkgname=kolourpaint
pkgver=${_kdever}
pkgrel=3
pkgdesc='A simple painting program to quickly create raster images'
url='http://kde.org/applications/graphics/kolourpaint/'
arch=('x86_64')
license=('GPL'
         'LGPL'
         'FDL')
depends=("kde-runtime>=${_kdever}"
         'qimageblitz'
         "kdegraphics-libkexiv2>=${_kdever}"
         "kdegraphics-libkdcraw>=${_kdever}" 
         "kdegraphics-ksaneplugin>=${_kdever}"
         "kdegraphics-svgpart>=${_kdever}"
         "kdegraphics-kgamma>=${_kdever}")
makedepends=('cmake'
             'automoc4'
             'docbook-xsl')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)
install=${pkgname}.install
groups=('kde'
        'kdegraphics'
        'kde-uninstall')
categories=('graphics')
provides=('kolourpaint')
options=('docs'
         '!header'
         'debug'
         'log')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
