# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegraphics-kolourpaint/
# Include global configuration
source ../kdeapps.conf

pkgname=kdegraphics-kolourpaint
_pkgname=kolourpaint
pkgver=${_kdever}
pkgrel=1
pkgdesc='A simple painting program to quickly create raster images'
url='http://kde.org/applications/graphics/kolourpaint/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=("kde-runtime"
         'qimageblitz'
         "kdegraphics-libkexiv2>=${_kdever}"
         "kdegraphics-libkdcraw>=${_kdever}" 
         "kdegraphics-ksaneplugin>=${_kdever}"
         "kdegraphics-svgpart>=${_kdever}"
         "kdegraphics-kgamma>=${_kdever}")
makedepends=('cmake' 'automoc4' 'docbook-xsl')
groups=('kde' 'kdegraphics' 'kde-uninstall')
categories=('graphics')
provides=('kolourpaint')
options=('docs' 'debug')
install=${pkgname}.install
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE4_BUILD_TESTS=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
