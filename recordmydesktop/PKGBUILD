# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=recordmydesktop
pkgver=0.3.8.1
pkgrel=6
pkgdesc="Produces a OGG encapsulated Theora/Vorbis recording of your desktop"
arch=('x86_64')
url="http://recordmydesktop.sourceforge.net"
license=('GPL')
depends=('alsa-lib' 'zlib' 'libvorbis' 'libxdamage' 'libxext' 'libtheora' 'libsm' 'jack')
makedepends=('jack-audio-connection-kit')
categories=('multimedia')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
	shmstr-to-shmproto.patch
	recordmydesktop-bitrate.patch)
md5sums=('6998b165540598965499bd99d8aa0eef'
         '211a1773f223b3f281520aa1056395e6'
         'b0794a769efd287e9e8c5fb8c3299b35')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 < ${srcdir}/shmstr-to-shmproto.patch
  patch -p1 < ${srcdir}/recordmydesktop-bitrate.patch
}
         
build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --enable-jack=yes LIBS="`pkg-config --libs jack`" \
	CFLAGS="`pkg-config --cflags jack` -DHAVE_LIBJACK"
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
