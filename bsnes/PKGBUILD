# Apps Packages for Chakra, part of chakra-project.org
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>

pkgname=bsnes
pkgver=091
pkgrel=2
pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy"
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
screenshot=('http://byuu.org/bsnes/user-guide/images/main-window-loaded.png')
license=('GPL3')
depends=('libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt>=4.7.0')
makedepends=('mesa')
install='bsnes.install'
changelog='bsnes.changelog'
source=("http://${pkgname}.googlecode.com/files/${pkgname}_v${pkgver}-source.tar.xz")
md5sums=('ba53212c56990e83d7770393c4e2d173')

# Build the accuracy profile (you can also choose 'performance' or 'compatibility')
__profile='accuracy'

build() {
  cd "${srcdir}/${pkgname}_v${pkgver}-source/${pkgname}"
        
    # Don't use sudo
    find . -name "Makefile" -exec sed -e 's/sudo install/install/' -i {} \;

    # Fix building with QT >= 4.8.0.
    moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp

    # Compile bsnes.
    make flags="$CXXFLAGS -I. -DPROFILE_${__profile^^}" compiler=gcc platform=x profile=${__profile} phoenix=qt
  
    # Compile purify.
    cd "${srcdir}/${pkgname}_v${pkgver}-source/purify"
    moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp
    sed -e 's|link := -s|link := -s -lX11|' -i 'Makefile'
    make compiler=gcc platform=x phoenix=qt
}

package() {
  cd "${srcdir}/${pkgname}_v${pkgver}-source/${pkgname}"
  make install profile=${__profile} DESTDIR="${pkgdir}" prefix=/usr
  
  # Install purify.
  cd "${srcdir}/${pkgname}_v${pkgver}-source/purify"
  install -Dm 755 purify ${pkgdir}/usr/bin/$pkgname-purify

  # Install the shaders
  install -d "${pkgdir}/usr/share/${pkgname}/shaders"
  install -Dm644 "${srcdir}/${pkgname}_v${pkgver}-source"/shaders/*.shader "${pkgdir}/usr/share/${pkgname}/shaders"

  # Install the user-profile into /usr/share/bsnes/profile.
  install --directory "${pkgdir}/usr/share/${pkgname}/profile"
  cp -R "${srcdir}/${pkgname}_v${pkgver}-source/${pkgname}"/profile/* "${pkgdir}/usr/share/${pkgname}/profile"
}
