#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=bsnes
pkgver=085
_pkgver=v$pkgver
pkgrel=2
pkgdesc="Super Nintendo Entertainment System™ (SNES) emulator focused on accuracy."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL2')
depends=('libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt')
makedepends=('mesa' 'libpulse')
source=("http://bsnes.googlecode.com/files/${pkgname}_$_pkgver-source.tar.bz2")
md5sums=('2419710087ba28c894e5aa1c3c41b6e7')

# Build profile, to choose between: accuracy, compatibility or performance.
_profile='performance'

build() {
  cd $srcdir/${pkgname}_$_pkgver-source/$pkgname

 # Don't copy the cheat file.
  sed -e '/mkdir -p ~\/.config\/bsnes/{N;N;d}' -i "ui/Makefile" 

  make compiler=gcc platform=x profile=$_profile phoenix=qt
}

package() {
  cd $srcdir/${pkgname}_$_pkgver-source/$pkgname
  make install profile=$_profile DESTDIR=$pkgdir prefix=/usr
}
