# Arch contribution https://www.archlinux.org/packages/community/x86_64/drumstick/

pkgname=drumstick
pkgver=1.0.2
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=('x86_64')
url="http://drumstick.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'fluidsynth' 'hicolor-icon-theme' 'shared-mime-info' 'qt5-svg')
makedepends=('cmake' 'doxygen' 'docbook-xsl')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2")
sha256sums=('34aab5812c35ba16f8e606632a56c38879f470e3e395d580412c73ef937f8750')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DLIB_SUFFIX=
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}
