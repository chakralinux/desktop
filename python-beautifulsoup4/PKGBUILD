pkgbase=python-beautifulsoup4
pkgname=('python2-beautifulsoup4' 'python3-beautifulsoup4')
pkgver=4.4.0
pkgrel=2
pkgdesc="A Python HTML/XML parser designed for quick turnaround projects like screen-scraping."
arch=('x86_64')
url="http://www.crummy.com/software/BeautifulSoup/index.html"
license=('PSF')
makedepends=('python2-setuptools' 'python3-setuptools')
source=("http://www.crummy.com/software/BeautifulSoup/bs4/download/${pkgver%.*}/${pkgbase#*-}-${pkgver}.tar.gz")
sha512sums=('812abb09099779ecb9c41ce55963ad42973eef00193e28e59dd907715fd6bc2b470b07f80bbe2838b790be252935eb81357a8cfb59808865d2fb62479476eb2f')
 
build() {
  cd "${srcdir}"
  cp -a beautifulsoup4-${pkgver}{,-python2}

  # Build python 3 module
  cd "${srcdir}/beautifulsoup4-${pkgver}"
  python3 setup.py build

  # Build python 2 module
  cd "${srcdir}/beautifulsoup4-${pkgver}-python2"
  python2 setup.py build
}

package_python2-beautifulsoup4() {
  optdepends=('python2-chardet: universal encoding detector'
              'python2-lxml: pythonic binding for the libxml2 and libxslt libraries')

  cd "${srcdir}/beautifulsoup4-${pkgver}-python2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  rm -rf ${pkgdir}/usr/lib/python2.7/site-packages/bs4/tests
}

package_python3-beautifulsoup4() {
  optdepends=('python3-chardet: universal encoding detector'
              'python3-lxml: pythonic binding for the libxml2 and libxslt libraries')

  cd "${srcdir}/beautifulsoup4-${pkgver}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  rm -rf ${pkgdir}/usr/lib/python3.3/site-packages/bs4/{__pycache__,tests,builder/__pycache__}
}
