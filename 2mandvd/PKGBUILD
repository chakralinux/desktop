#
# Chakra Packages for Chakra, part of chakra-project.org
#

pkgname=2mandvd
pkgver=1.8.4
pkgrel=4
pkgdesc="DVD Authoring Maker In Linux Environment."
url="http://2mandvd.tuxfamily.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('qt>=4.6' 'sox' 'mencoder' 'perl' 'netpbm' 'transcode' 'ffmpegthumbnailer' 'dvdauthor' 'mjpegtools' 'ttf-dejavu' 'dvd+rw-tools' 'exif' 'python-opengl')
optdepends=('xine-ui: to preview the result')
categories=('multimedia')
screenshot=('http://i1-linux.softpedia-static.com/screenshots/2ManDVD_1.jpg')
source=("http://download.tuxfamily.org/2mandvd/2ManDVD-${pkgver}.tar.gz"
        "ffmpeg-0.11.diff")
sha1sums=('3ed5b79e88c05159ac05cb40a39a702e3c400c79'
          '2870ea186ecfa4a894618418249fa28f184dbf89')

build() {
  cd "${srcdir}"
  
  # Patch 2ManDVD for ffmpeg
  patch -p0 -i "${srcdir}/ffmpeg-0.11.diff"

  # Prepare the build directory and configure the project
  rm -rf build && mkdir -p build
  cd build

  qmake "${srcdir}/2ManDVD/2ManDVD.pro"

  # Fix those damn absolute paths!
  sed -e 's,chmod -R 755 /usr/share/2ManDVD,chmod -R 755 \$(INSTALL_ROOT)/usr/share/2ManDVD,g' \
      -i Makefile

  # Actually compile it
  make
}

package(){
  cd "${srcdir}/build"

  make INSTALL_ROOT="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
