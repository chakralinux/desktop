#maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=2mandvd
pkgver=1.8.5
pkgrel=1
pkgdesc="DVD Authoring Maker In Linux Environment."
url="http://2mandvd.tuxfamily.org/"
license=('GPL')
arch=('x86_64')
depends=('qt' 'mesa' 'sox' 'mplayer' 'perl' 'netpbm' 'transcode' 'ffmpegthumbnailer' 'dvdauthor' 'mjpegtools' 'ttf-dejavu' 'dvd+rw-tools' 'exif' 
'python-opengl')
optdepends=('xine-ui: to preview the result')
categories=('multimedia')
screenshot=('http://i1-linux.softpedia-static.com/screenshots/2ManDVD_1.jpg')
source=("http://download.tuxfamily.org/2mandvd/2ManDVD-${pkgver}.tar.gz"
        "ffmpeg-0.11.diff")
sha1sums=('7f7f1ccf34ea8df1f24f6f28be36ea61012e03bd'
          'e8bfd4adb11f5641a55719ff7d346f061281e8b2')

build() {
  cd "${srcdir}"
  
  # Patch 2ManDVD for ffmpeg
  patch -p0 -i "${srcdir}/ffmpeg-0.11.diff"

  # Prepare the build directory and configure the project
  rm -rf build && mkdir -p build
  cd build

  qmake "${srcdir}/2ManDVD/2ManDVD.pro"

  # Fix those damn absolute paths!
  sed -e 's,chmod -R 755 /usr/share/2ManDVD,chmod -R 755 \$(INSTALL_ROOT)/usr/share/2ManDVD,g' \
      -i Makefile

  # Actually compile it
  make
}

package(){
  cd "${srcdir}/build"

  make INSTALL_ROOT="${pkgdir}" install
}
