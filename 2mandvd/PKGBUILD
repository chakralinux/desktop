#
# Apps Packages for Chakra, part of chakra-project.org
#

pkgname=2mandvd
pkgver=1.8.4
pkgrel=2
pkgdesc="DVD Authoring Maker In Linux Environnement"
url="http://2mandvd.tuxfamily.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('qt>=4.6' 'sox' 'mplayer' 'perl' 'netpbm' 'transcode' 'ffmpegthumbnailer' 'dvdauthor' 'mjpegtools' 'ttf-dejavu' 'dvd+rw-tools' 'exif' 'python-opengl')
optdepends=('xine-ui: to preview the result')
categories=('multimedia')
screenshot=('http://i1-linux.softpedia-static.com/screenshots/2ManDVD_1.jpg')
source=("http://download.tuxfamily.org/${pkgname}/2ManDVD-${pkgver}.tar.gz")
md5sums=('2e362b6d963b0698ee4911d28865d50d')

build() {
  cd "${srcdir}"

  # Prepare the build directory and configure the project
  rm -rf build && mkdir -p build
  cd build

  qmake "${srcdir}/2ManDVD/2ManDVD.pro"

  # Fix those damn absolute paths!
  sed -e 's,chmod -R 755 /usr/share/2ManDVD,chmod -R 755 \$(INSTALL_ROOT)/usr/share/2ManDVD,g' \
      -i Makefile

  # Actually compile it
  make
}

package() {
  cd "${srcdir}/build"
  make install INSTALL_ROOT="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
