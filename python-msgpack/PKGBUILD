# Contribution from Arch:
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Sébastien "Seblu" Luttringer

pkgbase=python-msgpack
pkgname=('python3-msgpack' 'python2-msgpack')
pkgver=0.4.8
pkgrel=2
url='https://github.com/msgpack/msgpack-python'
arch=('x86_64')
license=('Apache')
makedepends=('python3-cython' 'python2-cython' 'python3-setuptools' 'python2-setuptools' 'python3' 'python2')
source=("https://pypi.io/packages/source/m/msgpack-python/msgpack-python-$pkgver.tar.gz")
md5sums=('dcd854fb41ee7584ebbf35e049e6be98')

build() {
  cd msgpack-python-$pkgver
  python3 setup.py build --build-lib=build/python3
  python2 setup.py build --build-lib=build/python2
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

package_python3-msgpack() {
  pkgdesc='MessagePack serializer implementation for Python3'
  depends=('python3')

  cd msgpack-python-$pkgver
  python3 setup.py build --build-lib=build/python3 \
                  install --root="$pkgdir" --optimize=1
}

package_python2-msgpack() {
  pkgdesc='MessagePack serializer implementation for Python2'
  depends=('python2')

  cd msgpack-python-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
}
