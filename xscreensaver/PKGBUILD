# Part of the X.org group
# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# Contributor: abveritas@chakra-project.org
pkgname=xscreensaver
pkgver=5.26
pkgrel=1
pkgdesc="Screen saver and locker for the X Window System"
arch=('x86_64')
url="http://www.jwz.org/xscreensaver/"
license=('BSD')
depends=('mesa' 'pam' 'xorg-res-utils' 'libxmu' 'libjpeg-turbo')
makedepends=('bc' 'libxpm')
backup=('etc/pam.d/xscreensaver')
source=(http://www.jwz.org/xscreensaver/${pkgname}-${pkgver}.tar.gz
        add-electricsheep.diff
        xscreensaver.pam
        LICENSE)
sha1sums=('8055822b661733e68550872a4ae6b6129c0b73fc'
          'a68407b91ebff2629f1928d4e909d3565116906c'
          '106635aa1aae51d6f0668b1853f6c49a4fe9d3d8'
          '4209ea586b204fd1d81c382a0522c654f9fd9134')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "${srcdir}/add-electricsheep.diff" 
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --with-x-app-defaults=/usr/share/X11/app-defaults \
    --with-pam --without-motif --without-gtk --with-gl \
    --without-gle --with-xpm --with-jpeg 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install_prefix="${pkgdir}" install 
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" 
  install -D -m644 ../xscreensaver.pam "${pkgdir}/etc/pam.d/xscreensaver" 
  chmod 755 "${pkgdir}/usr/bin/xscreensaver" 
  #echo "NotShowIn=KDE;" >> "${pkgdir}/usr/share/applications/xscreensaver-properties.desktop" 
}
