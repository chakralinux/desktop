pkgname=obs-studio
pkgver=21.0.2
pkgrel=2
pkgdesc="Free, open source software for live streaming and recording"
arch=('x86_64')
url="https://obsproject.com"
license=('GPL2')
depends=('ffmpeg' 'jansson' 'libxinerama' 'libxkbcommon-x11'
         'qt5-x11extras' 'curl' 'hicolor-icon-theme' 'jack')
makedepends=('cmake' 'libfdk-aac' 'libxcomposite' 'x264' 'vlc' 'swig' 'python3' 'luajit')
optdepends=('libfdk-aac: FDK AAC codec support'
            'libxcomposite: XComposite capture support'
            'vlc: VLC plugin'
            'luajit: scripting support'
            'python3: scripting support')
install=$pkgname.install
source=("$pkgname-$pkgver::https://github.com/jp9000/obs-studio/archive/$pkgver.tar.gz")
sha256sums=('b8faa5f50b2139204275fe4ce8b3a3caadc2a66f1562ff3df367cb40562e9bc8')

prepare() {
  cd $pkgname-$pkgver

  mkdir -p build
}

build() {
  cd $pkgname-$pkgver/build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
	-DOBS_VERSION_OVERRIDE="$pkgver-$pkgrel" ..

  make
}

package() {
  cd $pkgname-$pkgver/build

  make install DESTDIR="$pkgdir"
}
