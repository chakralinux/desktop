pkgname=obs-studio
pkgver=18.0.2
pkgrel=1
pkgdesc="Free and open source software for video recording and live streaming."
arch=('x86_64')
url="https://github.com/jp9000/obs-studio"
license=('GPL2')
depends=('ffmpeg' 'libxinerama' 'libxkbcommon-x11'
         'qt5-x11extras' 'curl' 'hicolor-icon-theme' 'jack')
makedepends=('cmake' 'libfdk-aac' 'libxcomposite' 'x264' 'vlc')
optdepends=('libfdk-aac: FDK AAC codec support'
            'libxcomposite: XComposite capture support'
            'vlc: VLC plugin')
install=$pkgname.install
source=("$pkgname-$pkgver::https://github.com/jp9000/obs-studio/archive/$pkgver.tar.gz")
sha256sums=('1cfbad3439b1d581b4f5b47792ccc4dae231acaf37178808811ff4e2b68feb0a')

prepare() {
  cd $pkgname-$pkgver

  mkdir -p build
}

build() {
  cd $pkgname-$pkgver/build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
	-DOBS_VERSION_OVERRIDE="$pkgver-$pkgrel" ..

  make
}

package() {
  cd $pkgname-$pkgver/build

  make install DESTDIR="$pkgdir"
}
