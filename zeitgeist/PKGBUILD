#
# Platform Packages for Chakra, part of chakraos.org
#
# maintainer almack

pkgname=zeitgeist
pkgver=0.9.15
pkgrel=2
pkgdesc="A service which logs the users's activities and events and makes relevant information available to other applications"
arch=('x86_64')
url="http://zeitgeist-project.com/"
license=('GPL')
depends=(json-glib telepathy-glib)
makedepends=(intltool gobject-introspection vala raptor python2-rdflib gnome-common git)
source=("$pkgname::git://anongit.freedesktop.org/$pkgname/$pkgname#commit=d2c966f")
md5sums=('SKIP')

prepare() {
  cd $pkgname

  sed -i 's/python -/$PYTHON -/' configure.ac
  sed -i '1s/python$/python2/' data/ontology2code

  mkdir m4
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-datahub
  make
}

check() {
  cd $pkgname
  # Log test fails in chroot, as we have no X and no proper dbus
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
