# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdesdk-lokalize/
# Include global configuration

pkgname="lokalize"
arch=('x86_64')
pkgver=18.04.3
pkgrel=1
pkgdesc="Computer aided translation system"
screenshot="http://www.kde.org/images/screenshots/lokalize.png"
depends=('kross')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
provides=('kdesdk-lokalize')
conflicts=('kdesdk-lokalize')
replaces=('kdesdk-lokalize')
groups=('kde' 'kde-devel' 'kde-uninstall' 'kdesdk')
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('84498ef61fde93b8f195febc2d9a0316c7645bac11e4931323e3aaf101c74a14'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install 
}
