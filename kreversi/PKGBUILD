# Include global configuration

pkgname=kreversi
pkgver=18.12.0
pkgrel=1
pkgdesc="A simple one player strategy game played against the computer"
arch=('x86_64')
url='http://kde.org/applications/games/kreversi/'
screenshot='https://www.kde.org/images/screenshots/kreversi.png'
license=('GPL' 'LGPL' 'FDL')
groups=('kdegames' 'kde' 'kde-applications')
depends=(libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules)
conflicts=(kdegames-kreversi)
replaces=(kdegames-kreversi)
provides=(kdegames-kreversi)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('eb4a3bd0653bdd0707b4f1f58e4bc80be0df8b4d7fbe4c17a2272b8ad49db712'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
