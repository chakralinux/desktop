# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdegraphics-gwenview
_pkgname=gwenview
pkgver=${_kdever}
pkgrel=1
pkgdesc="A fast and easy to use image viewer for KDE"
arch=('x86_64')
url="http://kde.org/applications/graphics/gwenview/"
license=('GPL' 'LGPL' 'FDL')
depends=("kde-runtime>=${_kdever}" "kdegraphics-libkipi>=${_kdever}" "lcms2")
makedepends=('cmake' 'automoc4' "kde-baseapps-konqueror>=${_kdever}" 'pkg-config')
optdepends=('kdegraphics-kamera: to work directly with cameras')
install=${pkgname}.install
groups=("kde" "kdegraphics" "kde-uninstall")
categories=('graphics')
provides=('gwenview')
options=('docs' '!header' 'debug' 'log')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
