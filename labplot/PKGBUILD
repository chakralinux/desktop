source ../kdeapps.conf

pkgname=labplot
pkgver=2.4.0
pkgrel=1
pkgdesc="A free software data analysis and visualization application build on top of KDE SC"
arch=('x86_64')
url='http://labplot.sourceforge.net/'
license=('GPL')
depends=('hdf5' 'netcdf' 'gsl' 'qt5-base' 'qt5-svg' 'karchive' 'ki18n' 'kwidgetsaddons' 
         'kxmlgui' 'kio' 'kdelibs4support' 'kdoctools' 'kcompletion' 'kconfigwidgets'
         'kcoreaddons' 'ktextwidgets' 'kio' 'knewstuff' 'fftw' 'cantor' 'cfitsio')
makedepends=('cmake' 'extra-cmake-modules')
conflicts=('labplot2-svn')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}-kf5.tar.xz"
        fix-build.patch)
sha256sums=('86198247ff0d274c85607cdb07a0c4ef19149e4f4bf6c9d2e6e8249134e26bb2'
            'e1a6e69cfde87602102b24324dcb829897bd0e7ce74dc08e04c10f5cec81121b')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}-kf5
  patch -Np1 -i "$srcdir/fix-build.patch"
}
 
build() { 
  cd "${srcdir}"/${pkgname}-${pkgver}-kf5  
  cmake_kf5
  make 
}
 
package() {
  cd "${srcdir}"/${pkgname}-${pkgver}-kf5
  make DESTDIR="${pkgdir}" install
  
  #fix .desktop exec line
  sed -i s!'Exec=labplot2 -caption %c %i %f'!'Exec=labplot2'! \
  ${pkgdir}/usr/share/applications/org.kde.labplot2.desktop
}
