source ../kdeapps.conf

pkgname=labplot
pkgver=2.5.0
pkgrel=3
pkgdesc="A free software data analysis and visualization application build on top of KDE SC"
arch=('x86_64')
url='http://labplot.sourceforge.net/'
license=('GPL')
depends=('hdf5' 'netcdf' 'gsl' 'qt5-base' 'qt5-svg' 'karchive' 'ki18n' 'kwidgetsaddons' 
         'kxmlgui' 'kio' 'kdelibs4support' 'kdoctools' 'kcompletion' 'kconfigwidgets'
         'kcoreaddons' 'ktextwidgets' 'kio' 'knewstuff' 'fftw' 'cantor' 'cfitsio' 'qt5-serialport')
makedepends=('cmake' 'extra-cmake-modules')
conflicts=('labplot2-svn')
source=("http://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('f1ef2d95a4d4f18902e38cd1f2f79d041d4eeed1eb7f6284ec9a6a6954792225')

prepare() {
  cd $pkgname-$pkgver
  mkdir -p build
}
 
build() { 
  cd $pkgname-$pkgver/build  
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make 
}
 
package() {
  cd $pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
  
  #fix .desktop exec line
  sed -i s!'Exec=labplot2 -caption %c %i %f'!'Exec=labplot2'! \
  ${pkgdir}/usr/share/applications/org.kde.labplot2.desktop
}
