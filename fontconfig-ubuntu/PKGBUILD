#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Andrea Fagiani <andfagiani {at} gmail {dot} com>

pkgname=fontconfig-ubuntu
_pkgname=fontconfig
pkgver=2.8.0
_pkgver=$pkgver-2ubuntu1
pkgrel=1
pkgdesc="A library for configuring and customizing font access, with Ubuntu's LCD rendering patches."
arch=('i686' 'x86_64')
url="http://packages.ubuntu.com/lucid/fontconfig"
license=('MIT')
depends=('expat>=2.0.1' 'freetype2-ubuntu>=2.3.11')
conflicts=($_pkgname)
provides=("$_pkgname=$pkgver")
options=('!libtool')
install=fontconfig.install
source=(http://archive.ubuntu.com/ubuntu/pool/main/f/$_pkgname/${_pkgname}_$pkgver.orig.tar.gz
        http://archive.ubuntu.com/ubuntu/pool/main/f/$_pkgname/${_pkgname}_$_pkgver.diff.gz)
md5sums=('77e15a92006ddc2adbb06f840d591c0e'
         'ea8666fd1906bf101b9cfc42bf325f94')

build() {
  cd $srcdir/$_pkgname-$pkgver

  # Patches.
  patch -Np1 -i $srcdir/${_pkgname}_$_pkgver.diff || return 1
  for FILE in $(cat debian/patches/series) ; do
     patch -Np1 -i debian/patches/$FILE || return 1
  done

  # Enable Position Independent Code for prelinking
  export CFLAGS="${CFLAGS} -fPIC"

  # Configure and build.
  ./configure \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  # Installation.
  make DESTDIR=$pkgdir install

  # Remove configuration files.
  rm -f ${pkgdir}/etc/fonts/conf.d/*.conf

  # License.
  install -Dm0644 COPYING $pkgdir/usr/share/licenses/$pkgname/license.txt

  # Documentation.
  install -Dm0644 debian/changelog $pkgdir/usr/share/doc/$pkgname/ChangeLog
}

