#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Andrea Fagiani <andfagiani {at} gmail {dot} com>

pkgname=fontconfig-ubuntu
_pkgname=fontconfig
pkgver=2.10.93
_pkgver=$pkgver-2ubuntu1
pkgrel=1
pkgdesc="A library for configuring and customizing font access, with Ubuntu's LCD rendering patches."
arch=('x86_64')
url="http://packages.ubuntu.com/lucid/fontconfig"
license=('MIT')
depends=('expat' 'freetype2-ubuntu')
conflicts=($_pkgname)
provides=("$_pkgname=$pkgver")
options=('!libtool')
install=fontconfig.install
source=(http://archive.ubuntu.com/ubuntu/pool/main/f/$_pkgname/${_pkgname}_$pkgver.orig.tar.bz2)
md5sums=('0f8e5c63c2d6931626185c02802375a3')

build() {
  cd $srcdir/$_pkgname-$pkgver

  # Enable Position Independent Code for prelinking
  export CFLAGS="${CFLAGS} -fPIC"

  # Configure and build.
  ./configure \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  # Installation.
  make DESTDIR=$pkgdir install

  # Remove configuration files.
  rm -f ${pkgdir}/etc/fonts/conf.d/*.conf

  # License.
  install -Dm0644 COPYING $pkgdir/usr/share/licenses/$pkgname/license.txt

  # Documentation.
  install -Dm0644 $srcdir/$_pkgname-$pkgver/ChangeLog $pkgdir/usr/share/doc/$pkgname/ChangeLog
}

