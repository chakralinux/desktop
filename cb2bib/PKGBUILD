pkgname=cb2bib
pkgver=1.9.6
pkgrel=1
pkgdesc="A tool for parsing clipboard data into BibTeX bibliographic database files"
arch=('x86_64')
url="http://www.molspaces.com/cb2bib/"
license=('GPL3')
depends=('desktop-file-utils' 'lzo2' 'qt5-base' 'qt5-x11extras' 'qt5-webkit')
makedepends=('qt5-tools')
optdepends=('openssl: network reference query support'
            'perl-exiftool: meta data support'
            'texlive-latex3: file correctness checking; bib2pdf printing'
            'xpdf: pdftotext support')
categories=('office')
install=cb2bib.install
source=(http://www.molspaces.com/dl/progs/$pkgname-$pkgver.tar.gz)
sha256sums=('a2715a670ad405d6599b237d058cccf9e0dbe47070ee8e5d7f799c24ea4715af')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix /usr --qmakepath /usr/bin/qmake-qt5
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install
}
