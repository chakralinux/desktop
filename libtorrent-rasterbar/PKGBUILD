pkgname=libtorrent-rasterbar
pkgver=1.0.4
pkgrel=1
pkgdesc="A C++ library that aims to be a good alternative to all the other bittorrent implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs' 'python2' 'geoip')
makedepends=('boost')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/libtorrent/$pkgname-$pkgver.tar.gz)
sha512sums=('1917d1207171d23dbcbbb17f0c3afdadcec059e8e603e2009bb72a610be598f2ff5a5ebdfc5a1b0e07563a05e774b395e24e04fabb17847b6a757f47f41a2313')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --enable-python-binding \
      --with-libgeoip=system
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
