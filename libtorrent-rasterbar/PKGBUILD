pkgname=libtorrent-rasterbar
pkgver=1.1.2
pkgrel=1
pkgdesc="A C++ library that aims to be a good alternative to all the other bittorrent implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs' 'python2' 'geoip' 'websocketpp')
makedepends=('boost' 'cmake')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/releases/download/libtorrent-1_1_2/$pkgname-$pkgver.tar.gz)
sha256sums=('45e55f2374507bb67a9b9125f6b8d1c45e6cd33849c62b1d9cb56657354e359a')

build() {
  cd $pkgname-$pkgver
  CXXFLAGS="${CXXFLAGS} -std=c++11"
  PYTHON=/usr/bin/python2 \
  ./configure \
    --prefix=/usr \
    --enable-python-binding \
    --with-libiconv
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
  install -D COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
