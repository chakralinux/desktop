# Maintainer: Fabian Kosmale <0inkane@googlemail.com>
# Contributor: arlx_ignacy <ziphims@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=python3-pyside
pkgver=1.1.2
pkgrel=1
pkgdesc='CPython Qt bindings generated using the Shiboken generator. (Python 3 build)'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://www.pyside.org'
depends=('python3-shiboken>=1.1.2' 'python3' 'qt>=4.8' 'phonon' 'mesa')
makedepends=('cmake' 'automoc4')
replaces=('pyside-py3')
source=("http://qt-project.org/uploads/pyside/pyside-qt4.8+1.1.2.tar.bz2"
        'cmake_fixes.patch')
md5sums=('59c8653734b020b0128b82adc98ee704'
         '4a538e43dcfcd6c3bd67b8915f0d9769')

build(){
    cd $srcdir/pyside-qt4.8+${pkgver}
    patch -p1 -N < ../cmake_fixes.patch
    mkdir -p build && cd build
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
             -DCMAKE_BUILD_TYPE=Release  \
             -DBUILD_TESTS=OFF  \
             -DUSE_PYTHON3=1 \
             -Dpyside_SUFFIX=-python3 
    make
}


package() {
    cd "$srcdir/pyside-qt4.8+${pkgver}/build/"
    make DESTDIR=$pkgdir install
}
