# Include global configuration

pkgname="kmousetool"
arch=('x86_64')
pkgver=18.07.90
pkgrel=1
pkgdesc="Automatic mouse clicker"
url="https://kde.org/applications/utilities/$pkgname"
license=('GPL' 'FDL')
groups=("kdeaccessibility" 'kde' 'kde-applications')
depends=(knotifications kxmlgui hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python3 libxt)
conflicts=(kdeaccessibility-kmousetool)
provides=(kdeaccessibility-kmousetool)
replaces=(kdeaccessibility-kmousetool)
options=('docs' 'debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('482f2efbf5bd53af4aeac45bb259a7b1c2570af6ab8c6311793da7eb5140729b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
