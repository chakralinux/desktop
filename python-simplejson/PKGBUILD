# Maintainer: Manuel "UtG" <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgbase=python-simplejson
pkgname=('python2-simplejson' 'python3-simplejson')
pkgver=3.8.2
pkgrel=1
pkgdesc="Simple, fast, extensible JSON encoder/decoder for Python"
license=('MIT')
arch=('x86_64')
url='http://undefined.org/python/#simplejson'
makedepends=('python2-setuptools' 'python3-setuptools')
source=("git://github.com/simplejson/simplejson.git#tag=v$pkgver")
md5sums=('SKIP')

package_python2-simplejson() {
  depends=('python2')
  replaces=(python-simplejson)
  provides=(python-simplejson)
  
  cd simplejson
  python2 setup.py install --root=${pkgdir}

  # License
  install -Dm644 "$srcdir/simplejson/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python3-simplejson() {
  depends=('python3')

  cd simplejson
  python3 setup.py install --root="$pkgdir"
  
  # License
  install -Dm644 "$srcdir/simplejson/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
