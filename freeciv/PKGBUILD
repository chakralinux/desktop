pkgname=freeciv
pkgver=2.5.11
pkgrel=1
pkgdesc="Multiplayer clone of Civilizationâ„¢."
arch=('x86_64')
url="http://www.freeciv.org/"
license=('GPL2')
depends=('bzip2' 'freetype2' 'readline' 'sqlite3' 'curl' 'libltdl' 'sdl' 'sdl_mixer' 'qt5-base')
makedepends=('qt5-tools')
categories=('games')
backup=('etc/freeciv/database.lua')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('4c9c526952fe977cb4b302b8ccf75798fd066c6dde670f72f677fe4964259aad')

build() {
  cd $srcdir/$pkgname-$pkgver
  export MOCCMD="/usr/lib/qt5/bin/moc"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --enable-client=qt \
    --enable-fcdb=sqlite3 \
    --enable-aimodules \
    --enable-shared
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  install -dm 755 $pkgdir/etc
  make DESTDIR=$pkgdir install

  # Link to Qt client.
  cd $pkgdir/usr/bin
  ln -s freeciv-qt freeciv
}
