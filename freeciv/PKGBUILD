# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=freeciv
pkgver=2.4.3
pkgrel=1
pkgdesc="Multiplayer clone of Civilization™."
arch=('x86_64')
url="http://www.freeciv.org/"
license=('GPL2')
depends=('bzip2' 'freetype2' 'readline' 'sdl' 'sdl_image' 'sdl_mixer')
changelog=ChangeLog
categories=('games')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('437f9a4aadb80619a7c5ae9643142b8b')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-client=sdl \
    --enable-shared
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  install -dm 755 $pkgdir/etc
  make DESTDIR=$pkgdir install

  # Link to SDL client.
  cd $pkgdir/usr/bin
  ln -s freeciv-sdl freeciv
}
