# Maintainer: Jeff Huang <s8321414[at]gmail[dot]com>
# Contributer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=freeciv
pkgver=2.5.0
pkgrel=2
pkgdesc="Multiplayer clone of Civilization™."
arch=('x86_64')
url="http://www.freeciv.org/"
license=('GPL2')
depends=('bzip2' 'freetype2' 'readline' 'qt5-base' 'sqlite3' 'curl' 'libltdl')
changelog=ChangeLog
categories=('games')
backup=('etc/freeciv/database.lua')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('8a61ecd986853200326711446c573f1b')

build() {
  cd $srcdir/$pkgname-$pkgver
  export MOCCMD="/usr/lib/qt5/bin/moc"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --enable-client=qt \
    --enable-fcdb=sqlite3 \
    --enable-aimodules \
    --enable-shared
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  install -dm 755 $pkgdir/etc
  make DESTDIR=$pkgdir install

  # Link to Qt client.
  cd $pkgdir/usr/bin
  ln -s freeciv-qt freeciv
}
