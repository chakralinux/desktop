# Include global configuration
source ../kdeapps.conf

pkgname=kdev-python
pkgver=$_kdevver
pkgrel=1
pkgdesc="Python language and documentation plugin for KDevelop"
arch=('x86_64')
url="http://www.kdevelop.org/"
license=(GPL)
depends=(kdevelop python3 knewstuff threadweaver)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/stable/kdevelop/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=("`grep ${pkgname}-${pkgver}.tar.xz  ../kdevelop.checksums | cut -d " " -f1`"
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake  ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
