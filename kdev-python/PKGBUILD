# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# contributor: Jakub Kozisek <nodevel at gmail dot com>

pkgname=kdev-python
pkgver=1.5.1
_branch=stable
pkgrel=1
pkgdesc="KDevelop plugin that starts a script by opening a webbrowser"
arch=('x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop')
makedepends=('cmake' 'automoc4' 'kdevelop-pg-qt')
source=("http://master.kde.org/${_branch}/kdevelop/${pkgname}/${pkgver}/src/${pkgname}-v${pkgver}.tar.bz2")
md5sums=('d163e18ad75489f8ba0f0736b43f6642')

build() {
    cd "${srcdir}"
    mkdir build
    cd build 
    cmake ../${pkgname}-v${pkgver} \
        -DCMAKE_BUILD_TYPE=RELWITHDEBINFO \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
        -DCMAKE_INSTALL_PREFIX=/usr

    make parser
    make
}

package() {
    cd "${srcdir}"/build

    make DESTDIR="${pkgdir}" install
}
