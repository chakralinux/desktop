#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=rosegarden
pkgver=11.11.1
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('i686' 'x86_64')
url="http://www.rosegardenmusic.com/"
license=('GPL')
depends=('liblrdf' 'dssi' 'fftw' 'lirc-utils'
         'perl' 'qt' 'shared-mime-info')
makedepends=('pkgconfig' 'imake')
optdepends=('lilypond: notation display'
            'cups: printing support'
            'okular: print preview, or any other PDF viewer'
            'timidity++: MIDI playback, or any other softsynth'
            'flac'
            'wavpack')
categories=('multimedia')
[ "$CARCH" = "i686" ] && optdepends=("${optdepends[@]}"
                                     'dssi-vst: win32 VST support')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('f3701c3a7f4cb284a3e59c731fbad5b1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # lrdf + raptor2 compatibility
  export lrdf_CFLAGS="$(pkg-config --cflags raptor2)"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

