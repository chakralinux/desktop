pkgname=wesnoth
pkgver=1.14.0
pkgrel=1
pkgdesc="A turn-based strategy game on a fantasy world."
arch=('x86_64')
license=('GPL2')
url="http://www.wesnoth.org/"
depends=('boost-libs' 'freetype2' 'fribidi' 'gcc-libs' 'icu' 'libvorbis' 'lua52' 'pango' 'sdl2_image' 'sdl2_mixer' 
         'sdl2_net' 'sdl2_ttf' 'dbus' 'python2')
makedepends=('boost' 'cmake')
conflicts=('wesnoth-dev' 'wesnoth-svn')
categories=('games')
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        "wesnothd.tmpfiles.conf"
        "wesnothd.service")
sha256sums=('7c81ca4a4896a34789514025b999daf028d846b70aa556e18fca9de047c1d126'
            '1ae908f0608e9600088d3175c2276923e7fdccf825850d3f6e607bc197987e70'
            'd314dbefc72d09f2e3b1db15c4dc20873771f26df2360b39b55481deaeba00db')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  mkdir -p build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_CAMPAIGN_SERVER=ON \
    -DENABLE_TOOLS=ON \
    -DENABLE_OMP=ON \
    -DCMAKE_BUILD_TYPE=RelWithDebugInfo \
    -DMANDIR=share/man \
    -DFIFO_DIR=/run/wesnothd \
    ..
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/build
  make DESTDIR=$pkgdir install
  
  install -Dm644 "$srcdir/wesnothd.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/wesnothd.conf"
  install -Dm644 "$srcdir/wesnothd.service" "$pkgdir/usr/lib/systemd/system/wesnothd.service"
}
