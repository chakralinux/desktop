# Original AUR Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=tiger
pkgver=3.2.3
pkgrel=1
pkgdesc="A security scanner, that checks computer for known problems. Can also use tripwire, aide and chkrootkit."
arch=('i686' 'x86_64')
url="http://www.nongnu.org/tiger/"
license=('GPL')
depends=('perl')
makedepends=('patch')
backup=('opt/tiger/config' 'etc/tiger/tigerrc' 'etc/tiger/cronrc')
source=("http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-makefile.diff")
md5sums=('f41076f645da9de937819bf6d516e546'
         'aadd12186c717dfe202d55d1192851c2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i ${srcdir}/$pkgname-makefile.diff

  install -d "${pkgdir}/usr/share/tiger" "${pkgdir}/usr/sbin"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --with-tigerhome=/usr/share/tiger \
              --with-tigerconfig=/etc/tiger \
              --with-tigerwork=/var/run/tiger \
              --with-tigerlog=/var/log/tiger \
              --with-tigerbin=/usr/sbin

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
