# Maintainer: Francesco Marinucci <framari [at] opmbx [dot] org>
# CCR Maintainer: ahjolinna <ahjolinna@ovi.com>

pkgname=lucene++
pkgver=3.0.7
pkgrel=3
pkgdesc='C++ port of the popular Java Lucene library'
arch=('x86_64')
url='https://github.com/luceneplusplus/LucenePlusPlus'
license=('GPL' 'Apache')
depends=('boost-libs')
makedepends=('boost' 'cmake' 'subversion')
source=("$pkgname-$pkgver.tar.gz::https://github.com/luceneplusplus/LucenePlusPlus/archive/rel_${pkgver}.tar.gz"
        "boost.patch")
md5sums=('e260c1ff9c10af7070faee29f325f955'
         'e1124c9fc9d971b6a12135257992b15c')

prepare() {
  mkdir build
  cd LucenePlusPlus-rel_${pkgver}
  patch -Np1 -i ${srcdir}/boost.patch
}

build() {
  cd build

  cmake ../LucenePlusPlus-rel_${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib \
    -DBoost_LIBRARY_DIR=/usr/lib \
    -DENABLE_CUSTOM_ALLOCATOR='FALSE'

  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
