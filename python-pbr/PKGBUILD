
pkgbase=python-pbr
pkgname=(python3-pbr python2-pbr)
pkgver=3.1.1
pkgrel=1
pkgdesc="Python Build Reasonableness"
arch=('any')
url=https://pypi.python.org/pypi/pbr
license=(Apache)
makedepends=(python2-setuptools python3-setuptools git)
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack-dev/pbr/archive/$pkgver.tar.gz")
sha256sums=('cf742ca9b8e28fff579cc2d6d7be740a43679b2fba5603627ea7881c5d663ad5')

prepare() {
    cp -a ${pkgname#*-}-$pkgver python2-pbr-$pkgver
    find python2-pbr-$pkgver -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
    export SKIP_PIP_INSTALL=1
    cd ${pkgname#*-}-$pkgver/
    python3 setup.py build
    cd $srcdir/python2-pbr-$pkgver
    python2 setup.py build
}

package_python3-pbr() {
    cd ${pkgname#*-}-$pkgver/
    depends=(python3-pip)
    python3 setup.py install --prefix=/usr --root="$pkgdir"
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
package_python2-pbr() {
    cd $pkgname-$pkgver/
    depends=(python2-pip)
    python2 setup.py install --prefix=/usr --root="$pkgdir"
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    mv $pkgdir/usr/bin/pbr{,2}
}
