# Maintainer: Manuel 'UtG' <utg[dot]chakra.linux[at]gmail[dot]com>

pkgname=python2-xcaplib
pkgver=1.0.17
pkgrel=1
pkgdesc="Client for managing full or partial XML documents on XCAP servers (RFC 4825)"
license=('custom:MIT')
arch=('x86_64')
url="http://download.ag-projects.com/SipClient"
depends=('python2-application' 'python2-lxml')
source=("http://download.ag-projects.com/SipClient/${pkgname/2}-$pkgver.tar.gz")
md5sums=('6d839fbf5dde9952dd261c96605b9e44')

build() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  #python2 fix
  sed -i "s|^#!.*python$|&2|" `grep -rl "^#!.*python" "$pkgdir"`

  # bash completion
  install -Dm644 bash_completion.d/xcapclient \
    "$pkgdir/usr/share/bash-completion/completions/xcapclient"

  # license
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

