pkgname=debianutils
pkgver=4.8.1
pkgrel=1
pkgdesc="Utility programs from Debian."
url="http://packages.debian.org/source/sid/debianutils"
arch=('x86_64')
license=('GPL')
depends=('glibc')
categories=('utils')
source=("http://ftp.de.debian.org/debian/pool/main/d/debianutils/debianutils_${pkgver}.tar.xz")
sha256sums=('2c395c0bdcfe89de30828b1d25cc5549ded5225a6d3625fbcb2cc0881ef5f026')

build() {
  cd "${srcdir}/${pkgname}-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-$pkgver"
  make DESTDIR="${pkgdir}" install

  msg "Removing files already provided by the \"which\" package..."
  rm "${pkgdir}/usr/bin/which"
  find "${pkgdir}/usr/share/man" -name 'which.1' -delete

  msg "Removing files already provided by the \"run-parts\" package..."
  rm "${pkgdir}/usr/bin/run-parts"
  find "${pkgdir}/usr/share/man" -name 'run-parts.8' -delete

  msg "Removing the \"installkernel\" utility, which is probably too Debian-specific to work here..."
  rm "${pkgdir}/usr/sbin/installkernel"
  find "${pkgdir}/usr/share/man" -name 'installkernel.8' -delete
}
