pkgname=debianutils
pkgver=4.5
pkgrel=1
pkgdesc="Utility programs from Debian."
url="http://packages.debian.org/source/sid/debianutils"
arch=('x86_64')
license=('GPL')
depends=('glibc')
categories=('utils')
source=("http://ftp.de.debian.org/debian/pool/main/d/debianutils/debianutils_${pkgver}.tar.xz")
sha512sums=('fc986b4e11b0514aba73d1104909f6fa267a51321f5c1253f6991b9a57d077f6bf1a879df6c4e9c9a6a77a046bc268e60e2be64edc893244670f607fa1360545')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  msg "Removing files already provided by the \"which\" package..."
  rm "${pkgdir}/usr/bin/which"
  find "${pkgdir}/usr/share/man" -name 'which.1' -delete

  msg "Removing files already provided by the \"run-parts\" package..."
  rm "${pkgdir}/usr/bin/run-parts"
  find "${pkgdir}/usr/share/man" -name 'run-parts.8' -delete

  msg "Removing the \"installkernel\" utility, which is probably too Debian-specific to work here..."
  rm "${pkgdir}/usr/sbin/installkernel"
  find "${pkgdir}/usr/share/man" -name 'installkernel.8' -delete
}

