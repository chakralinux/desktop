#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <jmc@chakra-project.org>

pkgname=debianutils
pkgver=4.4
pkgrel=1
pkgdesc="Utility programs from Debian."
url="http://packages.debian.org/source/sid/debianutils"
arch=('x86_64')
license=('GPL')
depends=('glibc')
categories=('utils')
source=("http://ftp.de.debian.org/debian/pool/main/d/debianutils/debianutils_${pkgver}.tar.gz")
md5sums=('c0cb076754d7f4eb1e3397d00916647f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  msg "Removing files already provided by the \"which\" package..."
  rm "${pkgdir}/usr/bin/which"
  find "${pkgdir}/usr/share/man" -name 'which.1' -delete

  msg "Removing files already provided by the \"run-parts\" package..."
  rm "${pkgdir}/usr/bin/run-parts"
  find "${pkgdir}/usr/share/man" -name 'run-parts.8' -delete

  msg "Removing the \"installkernel\" utility, which is probably too Debian-specific to work here..."
  rm "${pkgdir}/usr/sbin/installkernel"
  find "${pkgdir}/usr/share/man" -name 'installkernel.8' -delete
}

