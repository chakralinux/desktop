#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=librecad
_pkgname=LibreCAD
pkgver=1.0.2
_pkgtag=a5c34b2
pkgrel=2
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad"
arch=('x86_64')
url="http://www.librecad.org/"
license=('GPL3')
depends=('qt' 'boost' 'muparser')
categories=('graphics')
source=("${pkgname}-${pkgver}.tar.bz2::https://github.com/$_pkgname/$_pkgname/tarball/v${pkgver}"
	"http://aur.archlinux.org/packages/li/$pkgname/$pkgname.tar.gz")
md5sums=('afaa38dc54fc9a694b17aed19b1a3269'
         'e70c1d87fee314e7291a83db4e545285')

build() {
  cd "${srcdir}/$_pkgname-$_pkgname-${_pkgtag}"
  qmake
  make
}

package() {
  cd "${srcdir}/$_pkgname-$_pkgname-${_pkgtag}"

  # Executable.
  install -D -m 755 unix/$pkgname ${pkgdir}/usr/bin/$pkgname

  # Desktop Integration.
  install -D -m 644 "${srcdir}/$pkgname/$pkgname.desktop" "${pkgdir}/usr/share/applications/$pkgname.desktop"
  install -D -m 644 res/main/$pkgname.png "${pkgdir}/usr/share/pixmaps/$pkgname.png"

  # Resources.
  mkdir -p "${pkgdir}/usr/share/$pkgname/"
  cp -r unix/resources/{library,patterns,fonts,qm} "${pkgdir}/usr/share/$pkgname/"
}
