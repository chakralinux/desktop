pkgbase=python-pysocks
pkgname=(python3-pysocks python2-pysocks)
pkgver=1.6.7
pkgrel=1
pkgdesc="SOCKS4, SOCKS5 or HTTP proxy (Anorov fork PySocks replaces socksipy)"
arch=('any')
license=('BSD')
url="https://github.com/Anorov/PySocks"
makedepends=('python3-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Anorov/PySocks/archive/$pkgver.tar.gz")
sha512sums=('6b670b2246c2c3c8d0b5424f6fb65cbab1d030d58b15f99db9eb232ead5e853c52af637f3d429e3fe88c70792f3b4913219b6694fa0e81318cdd40c41c1b17d6')

prepare() {
  cp -a PySocks-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PySocks-$pkgver
  python3 setup.py build

  cd "$srcdir"/PySocks-$pkgver-py2
  python2 setup.py build
}

package_python3-pysocks() {
  depends=('python3')

  cd PySocks-$pkgver
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python2-pysocks() {
  depends=('python2')

  cd PySocks-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
