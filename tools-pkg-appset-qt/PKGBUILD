# Maintainer: Phil Miller <philm[at]chakra-project[dot]org>
# Committer: Simone Tobia <simone.tobia at gmail dot com>

pkgname=appset-qt
pkgver=0.2.4
pkgrel=5
pkgdesc="An advanced and feature rich Package Manager Frontend"
arch=('i686' 'x86_64')
url="http://appset.sourceforge.net/"
license=('GPL2')
depends=('qt>=4.7')
install=${pkgname}.install
source=(http://sourceforge.net/projects/appset/files/appset-qt/0.2/$pkgver/$pkgname-$pkgver-sources.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt-oxygen-icons.tar.xz
        http://chakra-project.org/sources/$pkgname/icons-rearrangement.patch
        http://chakra-project.org/sources/$pkgname/chakra.patch
        http://chakra-project.org/sources/$pkgname/minimum-size-920x540.patch
        http://chakra-project.org/sources/$pkgname/add-catalan.patch
        http://chakra-project.org/sources/$pkgname/appset-qt_de-0.2.4.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_ca-0.2.4.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_fr-0.2.4.tar.gz)
md5sums=(d440f2ba87183ba5babe29d2e2382bae
         87f5f278e3fe67ed2cd6165c2835d81b
         753f0a9bb8dc9a849faa7b0877b324fb 
         cb98674a96eadcc4dd3fddaefa293961
         f347027ba71bd60e314b90d19de0c412  
         c55efede964828899af5258fbf268f89  
         2f49ceac111711671a455f1484475045
         dfe1da52624c180f53d80122d956ab96
         db34218949c2c5e5d5b02ba06679d860)

build() {
  cd $srcdir/$pkgname-$pkgver-sources
  # copy oxygen icons
  cp -v ../*png Qt/AppSet-Qt

  # patch it for Chakra GNU/Linux
  patch -Np1 -i ${startdir}/src/chakra.patch
  patch -Np1 -i ${startdir}/src/icons-rearrangement.patch
  patch -Np1 -i ${startdir}/src/minimum-size-920x540.patch


  # add chakra translations (check upstream)
  patch -Np1 -i ${startdir}/src/add-catalan.patch
  cp -v ../appsettray-qt*.ts Qt/AppSetTray-Qt
  cp -v ../appset-qt*.ts Qt/AppSet-Qt

  qmake PREFIX=/usr -Wnone

  #localization
  lrelease Qt/AppSet-Qt/*.ts
  lrelease Qt/AppSetTray-Qt/*.ts

  make || return 1
}

package() {
    cd $srcdir/$pkgname-$pkgver-sources

    make INSTALL_ROOT="$pkgdir/" install

    #helper daemon
    install -m 755 -d "$pkgdir/etc/rc.d" || return 1
    install -m 755 "$srcdir/$pkgname-$pkgver-sources/AppSetHelper/appset-helper.sh" "$pkgdir/etc/rc.d/appset-helper"
    #clean arch stuff
    rm -r "$pkgdir/etc/appset/Arch"
}
