# Maintainer: Phil Miller <philm[at]chakra-project[dot]org>
# Committer: Simone Tobia <simone.tobia at gmail dot com>
# Translators: http://chakra-project.org/bbs/viewtopic.php?pid=29626#p29626

pkgname=appset-qt
pkgver=0.4.1pre
pkgrel=3
pkgdesc="An advanced and feature rich Package Manager Frontend"
arch=('i686' 'x86_64')
url="http://appset.sourceforge.net/"
license=('GPL2')
depends=('qt>=4.7')
optdepends=('ccr: For Community Repository support')
install=${pkgname}.install
source=(http://sourceforge.net/projects/appset/files/appset-qt/0.4/0.4.1/$pkgname-$pkgver-sources.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt-oxygen-icons.tar.xz
        http://chakra-project.org/sources/$pkgname/$pkgname-0.4.1pre-chakra.patch
        http://chakra-project.org/sources/$pkgname/appset-qt_ca-0.4.1.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_de-0.4.1.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_el-0.4.1.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_es-0.4.1.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_fr-0.4.1.tar.gz
        #http://chakra-project.org/sources/$pkgname/appset-qt_nl-0.4.0.tar.gz
        )
md5sums=(3f464e9adcc704fabd5cf02e01dba28a
         87f5f278e3fe67ed2cd6165c2835d81b
         4a1c4f5f31e754e48a9145421c08d900
         8c3833816b9270c175636a7bc7fce544 # appset-qt_ca.0.4.1.tar.gz
         275ce0fe7db802558340c5c234a66249 # appset-qt_de-0.4.1.tar.gz
         b9ffe714c1c1d76a9df7f0784c9e62d5 # appset-qt_el.0.4.1.tar.gz
         033278244a74b198f386766abbb9fc50 # appset-qt_es.0.4.1.tar.gz
         676f758716610442b0ef873c1d2974f1 # appset-qt_fr.0.4.1.tar.gz
         #b9238caf3c0bf3f7f2258c052d8addf6  #appset-qt_nl-0.4.0.tar.gz
         )

build() {
  cd $srcdir/$pkgname-$pkgver-sources
  # copy oxygen icons
  cp -v ../*png Qt/AppSet-Qt

  # patch it for Chakra GNU/Linux
  patch -Np1 -i ${startdir}/src/$pkgname-0.4.1pre-chakra.patch

  # add chakra translations (check upstream)
  #patch -Np1 -i ${startdir}/src/add-spanish.patch
  cp -v ../appsettray-qt*.ts Qt/AppSetTray-Qt
  cp -v ../appset-qt*.ts Qt/AppSet-Qt

  # check for updated strings
  cd Qt/AppSet-Qt
  lupdate *.cpp *.ui *.h -ts appset*-qt_*.ts
  cd ../AppSetTray-Qt
  lupdate *.cpp *.h -ts appsettay*-qt_*.ts
  cd ../..

  qmake PREFIX=/usr -Wnone

  #localization
  lrelease Qt/AppSet-Qt/*.ts
  lrelease Qt/AppSetTray-Qt/*.ts

  make || return 1
}

package() {
    cd $srcdir/$pkgname-$pkgver-sources

    make INSTALL_ROOT="$pkgdir/" install

    #helper daemon
    install -m 755 -d "$pkgdir/etc/rc.d" || return 1
    install -m 755 "$srcdir/$pkgname-$pkgver-sources/AppSetHelper/appset-helper.sh" "$pkgdir/etc/rc.d/appset-helper"
}
