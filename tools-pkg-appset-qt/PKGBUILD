# Maintainer: Phil Miller <philm[at]chakra-project[dot]org>
# Committer: Simone Tobia <simone.tobia at gmail dot com>
# Translators: http://chakra-project.org/bbs/viewtopic.php?pid=29626#p29626

pkgname=appset-qt
pkgver=0.4.0
pkgrel=0.5
pkgdesc="An advanced and feature rich Package Manager Frontend"
arch=('i686' 'x86_64')
url="http://appset.sourceforge.net/"
license=('GPL2')
depends=('qt>=4.7')
optdepends=('ccr: For Community Repository support')
install=${pkgname}.install
source=(http://sourceforge.net/projects/appset/files/appset-qt/0.4/$pkgver/$pkgname-$pkgver-sources.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt-oxygen-icons.tar.xz
        http://chakra-project.org/sources/$pkgname/$pkgname-0.4.0-chakra.patch
        http://chakra-project.org/sources/$pkgname/appset-qt_ca-0.4.0.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_de-0.4.0.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_el-0.4.0.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_es-0.3.4.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_fr-0.4.0.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_nl-0.4.0.tar.gz
        )
md5sums=(4d0500ae33c8250dfa24c9b696ba7e30
         87f5f278e3fe67ed2cd6165c2835d81b
         724e4cea62d3b95dd63573e75dffe783
         44ba1b52c438e05853fbaf202ae243b6  #appset-qt_ca-0.4.0.tar.gz
         3b5c5027609eef9b43ad0f98c137c094  #appset-qt_de-0.4.0.tar.gz
         84542e24121e93dc1c84b63c940c60e0  #appset-qt_el-0.4.0.tar.gz
         e9af00b142394ce29c538a0afdba2edb  #appset-qt_es-0.3.4.tar.gz
         f64d3552553bf28d12358aba2db4ca2a  #appset-qt_fr-0.4.0.tar.gz
         b9238caf3c0bf3f7f2258c052d8addf6  #appset-qt_nl-0.4.0.tar.gz
         )

build() {
  cd $srcdir/$pkgname-$pkgver-sources
  # copy oxygen icons
  cp -v ../*png Qt/AppSet-Qt

  # patch it for Chakra GNU/Linux
  patch -Np1 -i ${startdir}/src/$pkgname-0.4.0-chakra.patch

  # add chakra translations (check upstream)
  #patch -Np1 -i ${startdir}/src/add-spanish.patch
  cp -v ../appsettray-qt*.ts Qt/AppSetTray-Qt
  cp -v ../appset-qt*.ts Qt/AppSet-Qt

  # check for updated strings
  cd Qt/AppSet-Qt
  lupdate *.cpp *.ui *.h -ts appset*-qt_*.ts
  cd ../AppSetTray-Qt
  lupdate *.cpp *.h -ts appsettay*-qt_*.ts
  cd ../..

  qmake PREFIX=/usr -Wnone

  #localization
  lrelease Qt/AppSet-Qt/*.ts
  lrelease Qt/AppSetTray-Qt/*.ts

  make || return 1
}

package() {
    cd $srcdir/$pkgname-$pkgver-sources

    make INSTALL_ROOT="$pkgdir/" install

    #helper daemon
    install -m 755 -d "$pkgdir/etc/rc.d" || return 1
    install -m 755 "$srcdir/$pkgname-$pkgver-sources/AppSetHelper/appset-helper.sh" "$pkgdir/etc/rc.d/appset-helper"
}
