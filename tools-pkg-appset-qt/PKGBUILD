# Maintainer: Phil Miller <philm[at]chakra-project[dot]org>
# Committer: Simone Tobia <simone.tobia at gmail dot com>
# Translators: http://chakra-project.org/bbs/viewtopic.php?pid=29626#p29626

pkgname=appset-qt
pkgver=0.3.1
pkgrel=1
pkgdesc="An advanced and feature rich Package Manager Frontend"
arch=('i686' 'x86_64')
url="http://appset.sourceforge.net/"
license=('GPL2')
depends=('qt>=4.7')
optdepends=('kdebase-konqueror: For displaying Webpages in fullscreen')
install=${pkgname}.install
source=(http://sourceforge.net/projects/appset/files/appset-qt/0.3/$pkgver/$pkgname-$pkgver-sources.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt-oxygen-icons.tar.xz
        http://chakra-project.org/sources/$pkgname/$pkgname-$pkgver-chakra.patch
        http://chakra-project.org/sources/$pkgname/add-spanish.patch
        http://chakra-project.org/sources/$pkgname/appset-qt_es-0.3.0.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_nl-0.3.0.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_de-0.3.1.tar.gz)
md5sums=(9ae7e86884b716fbf338b8ecdad786e9
         87f5f278e3fe67ed2cd6165c2835d81b
         03a45d193324ba9aee983c5de25b9dfe 
         0bebc346942eec90bb6a99f2f58e477f
         0d4d86427bcbee1e4e4f53c6da55d31d
         67b182714c5e4e2abb13d454ca00b266
         9995c78c733e14a7945501d9a30f4d61)

build() {
  cd $srcdir/$pkgname-$pkgver-sources
  # copy oxygen icons
  cp -v ../*png Qt/AppSet-Qt

  # patch it for Chakra GNU/Linux
  patch -Np1 -i ${startdir}/src/$pkgname-$pkgver-chakra.patch

  # add chakra translations (check upstream)
  patch -Np1 -i ${startdir}/src/add-spanish.patch
  cp -v ../appsettray-qt*.ts Qt/AppSetTray-Qt
  cp -v ../appset-qt*.ts Qt/AppSet-Qt

  qmake PREFIX=/usr -Wnone

  #localization
  lrelease Qt/AppSet-Qt/*.ts
  lrelease Qt/AppSetTray-Qt/*.ts

  make || return 1
}

package() {
    cd $srcdir/$pkgname-$pkgver-sources

    make INSTALL_ROOT="$pkgdir/" install

    #helper daemon
    install -m 755 -d "$pkgdir/etc/rc.d" || return 1
    install -m 755 "$srcdir/$pkgname-$pkgver-sources/AppSetHelper/appset-helper.sh" "$pkgdir/etc/rc.d/appset-helper"
}
