# Maintainer: Phil Miller <philm[at]chakra-project[dot]org>
# Committer: Simone Tobia <simone.tobia at gmail dot com>

pkgname=appset-qt
pkgver=0.3.0
pkgrel=1
pkgdesc="An advanced and feature rich Package Manager Frontend"
arch=('i686' 'x86_64')
url="http://appset.sourceforge.net/"
license=('GPL2')
depends=('qt>=4.7')
optdepends=('kdebase-konqueror: For displaying Webpages in fullscreen')
install=${pkgname}.install
source=(http://sourceforge.net/projects/appset/files/appset-qt/0.3/$pkgver/$pkgname-$pkgver-sources.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt-oxygen-icons.tar.xz
        http://chakra-project.org/sources/$pkgname/$pkgname-$pkgver-chakra.patch
#        http://chakra-project.org/sources/$pkgname/add-other-languages.patch
        http://chakra-project.org/sources/$pkgname/appset-qt_fr-0.2.4-7.tar.gz
        http://chakra-project.org/sources/$pkgname/appset-qt_el-0.2.4-7.tar.gz)
md5sums=(130ca03294d8f8493382a1a0e4bdd43d
         87f5f278e3fe67ed2cd6165c2835d81b
         91a5c84e31cb0f412bb56539cdf428c1 
#         39d6f5669249c6b902434990ac753d0f
         4b1f0ca7f96a2333e0bef3b4ec11eb79
         ce3bd4bc2e462bb509486e34a4863ab3)

build() {
  cd $srcdir/$pkgname-$pkgver-sources
  # copy oxygen icons
  cp -v ../*png Qt/AppSet-Qt

  # patch it for Chakra GNU/Linux
  patch -Np1 -i ${startdir}/src/$pkgname-$pkgver-chakra.patch

  # add chakra translations (check upstream)
#  patch -Np1 -i ${startdir}/src/add-other-languages.patch
  cp -v ../appsettray-qt*.ts Qt/AppSetTray-Qt
  cp -v ../appset-qt*.ts Qt/AppSet-Qt

  qmake PREFIX=/usr -Wnone

  #localization
  lrelease Qt/AppSet-Qt/*.ts
  lrelease Qt/AppSetTray-Qt/*.ts

  make || return 1
}

package() {
    cd $srcdir/$pkgname-$pkgver-sources

    make INSTALL_ROOT="$pkgdir/" install

    #helper daemon
    install -m 755 -d "$pkgdir/etc/rc.d" || return 1
    install -m 755 "$srcdir/$pkgname-$pkgver-sources/AppSetHelper/appset-helper.sh" "$pkgdir/etc/rc.d/appset-helper"
}
