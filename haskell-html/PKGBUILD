#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Frenández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_hkgname=html
pkgname=haskell-html
pkgver=1.0.1.2
pkgrel=6
pkgdesc="HTML combinator library"
url="http://hackage.haskell.org/package/html"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=(ghc=6.12.3 sh)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/html/$pkgver/html-$pkgver.tar.gz)
install=haskell-html.install
md5sums=('2a7de5a2af9a2f80d39825d6a95ee445')

build() {
    cd ${srcdir}/html-$pkgver
    runhaskell Setup configure -p --enable-split-objs --enable-shared --prefix=/usr --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
}
package() {
    cd ${srcdir}/html-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/html
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
