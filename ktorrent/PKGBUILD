# Include global configuration
source ../kdeapps.conf

pkgname=ktorrent
pkgver=5.1.1
pkgrel=1
pkgdesc="A powerful BitTorrent client for KDE"
arch=('x86_64')
url='https://www.kde.org/applications/internet/ktorrent/'
license=('GPL2')
depends=('libktorrent' 'knotifyconfig' 'kcmutils' 'kross' 'geoip' 'kplotting')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'boost' 'taglib' 'plasma-workspace' 'kdnssd' 'syndication')
optdepends=('kdnssd: zeroconf plugin'
            'taglib: mediaplayer plugin'
            'syndication: rss/syndication plugin')
options=('debug')
source=("http://download.kde.org/stable/ktorrent/$pkgver/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha1sums=('1fd9f1e78071a8b846b1f8517fd9d5b146b703c0'
          'SKIP')
validpgpkeys=('1EE5A3205904BAA2B88C0A9D24FD31940095C0E1') # Andrius \xc5\xa0tikonas

prepare() {
  mkdir -p build
}

build() {
	cd build
	cmake_kf5 ../${pkgname}-${pkgver}
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
