# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kte-collaborative

pkgname=kte-collaborative
pkgver=0.2.0
pkgrel=1
pkgdesc="A plugin for KTextEditor which allows to edit text documents collaboratively"
arch=('x86_64')
url='https://projects.kde.org/projects/playground/network/kte-collaborative'
license=('GPL')
depends=('libqinfinity' 'ktp-common-internals')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-v${pkgver}.tar.xz")
md5sums=('e96718ed62b3bb315364fd03a4f2b463')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-v${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
