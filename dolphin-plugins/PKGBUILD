# Include global configuration

pkgname=dolphin-plugins
pkgver=18.04.2
pkgrel=1
pkgdesc='Dolphin plugins'
url="http://www.kde.org"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('dolphin')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kdesdk-dolphin-plugins')
replaces=('kdesdk-dolphin-plugins')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdesdk')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('db47a16f5fd8fb67940bb7104d75a139561346a8d4a28c65cab454c92458b886'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
