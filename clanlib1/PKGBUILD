#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=clanlib1
_pkgname=clanlib
pkgver=1.0.0
pkgrel=6
pkgdesc="A multi-platform game development library"
arch=('i686' 'x86_64')
url="http://clanlib.org/"
license=('zlib')
depends=('glu' 'alsa-lib' 'libjpeg' 'libmikmod' 'libpng' 'libvorbis' 'libxi' 'libxmu' 'mesa' 'sdl_gfx' 'freetype2' 'pcre' 'sqlite3')
makedepends=( 'doxygen' 'graphviz')
provides=('clanlib')
replaces=('clanlib')
conflicts=('clanlib')
options=('!libtool')
source=(http://pkgs.fedoraproject.org/repo/pkgs/ClanLib1/ClanLib-1.0.0.tgz/281e64a463155474d0f1270686286811/ClanLib-1.0.0.tgz
        $_pkgname-$pkgver-stdio.diff
        $_pkgname-$pkgver-libpng-1.5.diff)
md5sums=('281e64a463155474d0f1270686286811'
         '8266047805d8dba2f4bf67562c9f061d'
         '9979f8f736e5985dfb8ac25ee2492c53')

build() { 
  cd ${srcdir}/ClanLib-${pkgver}
  
  # Patches.
  patch -uNp1 -i $srcdir/$_pkgname-$pkgver-stdio.diff
  patch -uNp1 -i $srcdir/$_pkgname-$pkgver-libpng-1.5.diff # Source: http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=649552
  
  # Fix compilation errors
  sed -i "51i#include <cstdio>" Sources/GL/canvas_opengl.cpp
  sed -i "38i#include <cstdio>" Sources/GL/GLX/input_device_linuxevent.cpp
  sed -i "32i#include <cstdio>" Sources/GL/GLX/input_device_xinput.cpp
  
  export LINUX_JOYSTICK_TRUE=yes
  export LINUX_INPUT_TRUE=yes
  ./configure \
    --prefix=/usr 

  make
}

package() {
  cd ${srcdir}/ClanLib-${pkgver}

  make DESTDIR=${pkgdir} install

  # License.
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
