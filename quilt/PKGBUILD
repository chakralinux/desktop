#maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
#contributions from Arch: https://www.archlinux.org/packages/community/any/quilt/
pkgname=quilt
pkgver=0.60
pkgrel=1
pkgdesc='Manage a series of patches by keeping track of the changes each patch makes'
arch=('x86_64')
url='http://savannah.nongnu.org/projects/quilt'
license=('GPL2')
depends=('diffstat' 'gawk' 'patch' 'diffutils' 'gettext')
source=("http://savannah.nongnu.org/download/quilt/${pkgname}-${pkgver}.tar.gz")
md5sums=('058a08a9b626bdec9ec8c519dc92158c')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man --without-rpmbuild
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="$pkgdir/usr" mandir="$pkgdir/usr/share/man" install
  # move bash completion in right place
  install -d -m 755 "$pkgdir/usr/share/bash-completion"
  mv "$pkgdir/etc/bash_completion.d" "$pkgdir/usr/share/bash-completion/completions"
}

