pkgname=whois
pkgver=5.2.13
pkgrel=1
pkgdesc="The whois client by Marco d'Itri"
arch=('x86_64')
url="http://www.linux.it/~md/software/"
license=('GPL')
depends=('libidn')
makedepends=('perl')
backup=('etc/whois.conf')
source=("http://ftp.debian.org/debian/pool/main/w/$pkgname/whois_$pkgver.tar.xz")
sha512sums=('0c9028021415e69116e891696c86b26cbab17f861829fe5f9a5ed1528f7e6d8406890a8d1afdeb17e41856eaa076fe7417557034b20bb59109b9fc5970ad05c7')
 
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr \
       CFLAGS="$CFLAGS $CPPFLAGS" \
       CONFIG_FILE=/etc/whois.conf \
       HAVE_LIBIDN=1 \
       HAVE_ICONV=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr BASEDIR="${pkgdir}" install-whois
  install -Dm644 whois.conf "${pkgdir}/etc/whois.conf"
}
