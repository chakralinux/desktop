pkgname=whois
pkgver=5.2.12
pkgrel=1
pkgdesc="The whois client by Marco d'Itri"
arch=('x86_64')
url="http://www.linux.it/~md/software/"
license=('GPL')
depends=('libidn')
makedepends=('perl')
backup=('etc/whois.conf')
source=("http://ftp.debian.org/debian/pool/main/w/$pkgname/whois_$pkgver.tar.xz")
sha512sums=('df40e2e7cbd11d00984f865e122ad4ece882a6a45e2f75ad5ea24ce937b7e9452b01fd886f43bae55fd065a6556cfc4577e7126151b5fd43fa806c4b0c886ced')
 
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr \
       CFLAGS="$CFLAGS $CPPFLAGS" \
       CONFIG_FILE=/etc/whois.conf \
       HAVE_LIBIDN=1 \
       HAVE_ICONV=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr BASEDIR="${pkgdir}" install-whois
  install -D -m644 whois.conf "${pkgdir}/etc/whois.conf"
}
