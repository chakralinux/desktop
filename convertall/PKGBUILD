pkgname=convertall
pkgver=0.7.5
pkgrel=1
pkgdesc="Unit conversion program (Qt)"
arch=('x86_64')
url="https://convertall.bellz.org/"
license=('GPL')
depends=('python3-pyqt5' 'desktop-file-utils')
categories=('utils')
source=(https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha1sums=('eefe59873e4fc0d905ca79272796694107b08924')

package() {
  cd ConvertAll

  python3 install.py -b "${pkgdir}" -p /usr -i /usr/share/pixmaps/convertall
  rm "${pkgdir}/usr/share/doc/${pkgname}"/{INSTALL,LICENSE}

  install -Dm644 icons/convertall-icon.svg \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/convertall.svg"
  install -Dm644 icons/convertall_lg.png \
    "${pkgdir}/usr/share/icons/hicolor/64x64/apps/convertall.png"
  install -Dm644 icons/convertall_med.png \
    "${pkgdir}/usr/share/icons/hicolor/32x32/apps/convertall.png"
  install -Dm644 icons/convertall_sm.png \
    "${pkgdir}/usr/share/icons/hicolor/16x16/apps/convertall.png"
}
