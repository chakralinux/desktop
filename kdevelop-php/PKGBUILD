#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgbase=kdevelop-php
pkgname=('kdevelop-php' 'kdevelop-php-docs')
pkgver=1.4.0
_pkgver=4.4.0
pkgrel=1
pkgdesc="PHP plugin for Kdevelop"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=("kdevelop=${_pkgver}" "kdevelop-pg-qt")
makedepends=('cmake' 'automoc4')
categories=('programming')
options=('!makeflags')
source=("http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        "http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-docs-${pkgver}.tar.bz2")
sha1sums=('c899aae94755dbc3f4fb72edfcb5b01a9f0eca43'
          'd06df690ea70d02707388851b61077bea8f9db1d')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make

  cd "${srcdir}"
  mkdir build-docs
  cd build-docs
  # kdevelop-php-docs
  cmake ../${pkgname}-docs-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package_kdevelop-php() {
  optdepends=('kdevelop-php-docs: documentation')
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}

package_kdevelop-php-docs() {
  pkgdesc="Documentation for kdevelop-php"
  depends=("kdevelop-php=${pkgver}")
  cd "${srcdir}"/build-docs
  make DESTDIR="${pkgdir}" install
}
