# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgbase=kdevelop-php
pkgname=('kdevelop-php' 'kdevelop-php-docs')
pkgver=1.7.1
_pkgver=4.7.1
pkgrel=2
pkgdesc="PHP plugin for Kdevelop"
arch=('x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=("kdevelop=${_pkgver}" "kdevelop-pg-qt")
makedepends=('cmake' 'automoc4')
categories=('programming')
options=('!makeflags')
source=(http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgbase}-{,docs-}${pkgver}.tar.xz)
sha1sums=('384bb55b8878041b1979d1652f149195842fb5e2'
          'f0755ea6649ca501e45a17fc59c51ebcbef77267')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make

  cd "${srcdir}"
  mkdir build-docs
  cd build-docs
  # kdevelop-php-docs
  cmake ../${pkgname}-docs-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package_kdevelop-php() {
  optdepends=('kdevelop-php-docs: documentation')
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}

package_kdevelop-php-docs() {
  pkgdesc="Documentation for kdevelop-php"
  depends=("kdevelop-php=${pkgver}")
  cd "${srcdir}"/build-docs
  make DESTDIR="${pkgdir}" install
}
