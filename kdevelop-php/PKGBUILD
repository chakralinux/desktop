# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgbase=kdevelop-php
pkgname=('kdevelop-php' 'kdevelop-php-docs')
pkgver=1.5.2
_pkgver=4.5.2
pkgrel=1
pkgdesc="PHP plugin for Kdevelop"
arch=('x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=("kdevelop=${_pkgver}" "kdevelop-pg-qt")
makedepends=('cmake' 'automoc4')
categories=('programming')
options=('!makeflags')
source=("http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        "http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-docs-${pkgver}.tar.xz")
sha1sums=('784f76dd12ee0ab443fa0f9d05d0e3062aafdb38'
          '44e737743d0c9663912480508adac0ccbb826a56')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make

  cd "${srcdir}"
  mkdir build-docs
  cd build-docs
  # kdevelop-php-docs
  cmake ../${pkgname}-docs-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package_kdevelop-php() {
  optdepends=('kdevelop-php-docs: documentation')
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}

package_kdevelop-php-docs() {
  pkgdesc="Documentation for kdevelop-php"
  depends=("kdevelop-php=${pkgver}")
  cd "${srcdir}"/build-docs
  make DESTDIR="${pkgdir}" install
}
