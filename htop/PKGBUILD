#
# Chakra Packages for Chakra, part of chakra-project.org
#

pkgname=htop
pkgver=2.0.2
pkgrel=2
pkgdesc="Interactive process viewer"
arch=('x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
source=("http://hisham.hm/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('7d354d904bad591a931ad57e99fea84a')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  sed -i 's|ncursesw/curses.h|curses.h|' RichString.h RichString.c configure 
  sed -i 's|python|python2|' scripts/MakeHeader.py
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-unicode \
      --enable-openvz \
      --enable-vserver \
      --enable-cgroup

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
