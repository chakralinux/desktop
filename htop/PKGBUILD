pkgname=htop
pkgver=2.1.0
pkgrel=1
pkgdesc="Interactive process viewer"
arch=('x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
optdepends=('lsof: show files opened by a process'
            'strace: attach to a running process')
source=("http://hisham.hm/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('f262b66ad6c194782f4d3a80627e84c8')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  sed -i 's|ncursesw/curses.h|curses.h|' RichString.h RichString.c configure 
  sed -i 's|python|python2|' scripts/MakeHeader.py
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-unicode \
      --enable-openvz \
      --enable-vserver \
      --enable-cgroup

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
