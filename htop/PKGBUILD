pkgname=htop
pkgver=2.2.0
pkgrel=1
pkgdesc="Interactive process viewer"
arch=('x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
optdepends=('lsof: show files opened by a process'
            'strace: attach to a running process')
source=("http://hisham.hm/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz"
        '0001-fix-option-string.patch')
md5sums=('0d816b6beed31edc75babcfbf863ffa8'
         'fbeab356b156f00e5b9838d4f6bad37b')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  patch -Np1 < "${srcdir}"/0001-fix-option-string.patch

  sed -i 's|ncursesw/curses.h|curses.h|' RichString.h RichString.c configure 
  sed -i 's|python|python2|' scripts/MakeHeader.py
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-unicode \
      --enable-openvz \
      --enable-vserver \
      --enable-cgroup \
      --enable-delayacct

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
