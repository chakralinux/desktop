#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=htop
pkgver=0.9
pkgrel=2
pkgdesc="Interactive process viewer"
arch=('i686' 'x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/htop/${pkgname}-${pkgver}.tar.gz)
md5sums=('7c5507f35f363f3f40183a2ba3c561f8')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	sed -i 's|ncursesw/curses.h|curses.h|' RichString.h RichString.c configure 
	sed -i 's|python|python2|' scripts/MakeHeader.py
	./configure --prefix=/usr --enable-unicode --enable-openvz \
	    --enable-vserver 
	make 
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install 
}
