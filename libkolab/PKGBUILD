# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>
#               Andrea Scarpino <andrea@archlinux.org>

pkgname=libkolab
pkgver=0.5.2
pkgrel=1
pkgdesc='Advanced Kolab Object Handling Library'
url='http://git.kolab.org/libkolab/'
arch=('x86_64')
license=('GPL')
depends=('libkolabxml'
         'kdepimlibs')
makedepends=('cmake'
             'automoc4'
             'boost')
source=("http://mirror.kolabsys.com/pub/releases/${pkgname}-${pkgver}.tar.gz"{,.gpg})
md5sums=('f33fcdc6852b9d045cb2be98ef3907ed'
         '35014787170d0c61df56c99059be95ed')

prepare() {
    mkdir -p "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake "../${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}
