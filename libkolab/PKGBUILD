# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>
#               Andrea Scarpino <andrea@archlinux.org>

pkgname=libkolab
pkgver=0.5.0
pkgrel=4
pkgdesc='Advanced Kolab Object Handling Library'
url='http://git.kolab.org/libkolab/'
arch=('x86_64')
license=('GPL')
depends=('libkolabxml'
         'kdepimlibs')
makedepends=('cmake'
             'automoc4'
             'boost')
source=("http://mirror.kolabsys.com/pub/releases/${pkgname}-${pkgver}.tar.gz"{,.gpg})
sha512sums=('09dc3493ad32dc6f59d0ee0abc4faef32bfb9bb3c69d94440f67a30f5cac3456427ffffdfab1604fb4f1c68b0d754399ef99ef2be06894646586d0501562b917'
            '094aa012584a639f641bc48707d9690e1368041bea2484001b60ad127310581fa5121aecffc70a27addf7ad40362b8b8f326aa12e540e3dc80dd299bf6014bc8')

prepare() {
    mkdir "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake "../${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}
