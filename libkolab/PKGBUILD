
pkgname=libkolab
pkgver=1.0.1
pkgrel=1
pkgdesc='Advanced Kolab Object Handling Library'
url='http://git.kolab.org/libkolab/'
arch=('x86_64')
license=('GPL')
depends=('libkolabxml' 'kmime' 'kcontacts' 'kcalutils' 'akonadi-client' 'akonadi-notes')
makedepends=('cmake' 'kdoctools' 'boost' 'swig')
source=("http://mirror.kolabsys.com/pub/releases/${pkgname}-${pkgver}.tar.gz"{,.gpg}
        Fix_build_with_Qt_5.5.patch)
sha256sums=('dd1ac7a596253bce97e5cb0f7ce05362fc2cbc908635c30d96b1c9f76cd6a824'
            '112c6993c1296512cf54875d0e0df83ee47dcb2e318d3916b5c3b4832b2b731f'
            'c14ca5af7e014c1db49f39dcd7eb34e1ad8b6edf901d36b308e648eb47a7a6fc')

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/Fix_build_with_Qt_5.5.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
