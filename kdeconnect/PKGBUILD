source ../kdeapps.conf

pkgname=kdeconnect
pkgver=1.3.3
pkgrel=1
epoch=1
pkgdesc='Adds communication between KDE and your smartphone'
arch=(x86_64)
url='https://community.kde.org/KDEConnect'
license=(GPL)
depends=(hicolor-icon-theme kcmutils kwayland libfakekey qca-qt5)
makedepends=(extra-cmake-modules kdoctools python3 qt5-declarative)
optdepends=('sshfs: remote filesystem browser'
            'kde-cli-tools: configuration UI'
            'qt5-declarative: QML bindings')
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-kde-$pkgver.tar.xz")
sha256sums=('4f3f7709255757233bd6406acb0bff9c30e0e2a36737154ce63afb1a78054ced')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-kde-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
