# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributions from AUR: https://aur.archlinux.org/packages/kdeconnect/

# The section below can be adjusted to suit your needs
# What type of build do you want? Default is Release.
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
 
_buildtype='Release'
  
pkgname=kdeconnect
pkgver=0.4.2
#_pkgver=0.4
pkgrel=2
pkgdesc='KDE Connect - kded and kcm'
url='https://albertvaka.wordpress.com/2013/08/05/introducing-kde-connect/'
license=('GPL')
arch=('x86_64')
depends=('kdelibs' 'qjson' 'qca-ossl')
makedepends=('automoc4' 'cmake')
provides=('kdeconnect')
conflicts=('kdeconnect-git')
install=kdeconnect.install
screenshot=('http://albertvaka.files.wordpress.com/2013/08/kcm.png')
source=("http://download.kde.org/unstable/kdeconnect/${pkgver}/src/${pkgname}-kde-${pkgver}.tar.xz"
'plasma_icon.patch')
md5sums=('96320feda9805296c72e1e10578b903e'
	'b89f0b4e1d38bb62848c0f0fc5e119b0')
 
if [[ ! ${_buildtype} == 'Release' ]] && [[ ! ${_buildtype} == 'release' ]]; then
  options=(debug)
fi
 
prepare() {
  if [[ -e ${pkgname}-kde-${pkgver}-build ]]; then rm -rf ${pkgname}-build; fi
  mkdir ${pkgname}-kde-${pkgver}-build

  cd ${pkgname}-kde-${pkgver}
  # Plasmoid: Changed IconWidget -> SvgItem to enable themes to change the icon
  # ToDo: remove this patch with version > 0.4.2
  # https://projects.kde.org/projects/playground/base/kdeconnect-kde/repository/revisions/00632aaa6a02e1cef9b9b267467106d79ab71d39/diff/plasmoid/package/contents/ui/CompactRepresentation.qml
  patch -Np1 -i ${srcdir}/plasma_icon.patch
}
 
build() {
  cd ${pkgname}-kde-${pkgver}-build
 
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        ../${pkgname}-kde-${pkgver}
  make
}
 
package() {
  cd ${pkgname}-kde-${pkgver}-build
  make DESTDIR=${pkgdir} install
}
