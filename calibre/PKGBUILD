#
# Apps Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=calibre
pkgver=0.7.59
pkgrel=1
pkgdesc="Ebook management application"
arch=('i686' 'x86_64') 
url="http://calibre.kovidgoyal.net/"
license=('GPL3')
depends=('python2' 'python-imaging' 'libusb' 'pyqt' 'python-mechanize'
         'imagemagick' 'dbus-python' 'python-lxml' 'beautiful-soup' 'python-dateutil'
         'python-cssutils-beta' 'desktop-file-utils' 'python-dnspython' 
         'podofo>=0.8.1-6' 'xdg-utils' 'cherrypy' 'python-pypdf' 'unrar' 'chmlib' 'poppler' 'poppler-qt' 'icu' 'libwmf')
makedepends=('python-pycountry')
optdepends=('ipython: to use calibre-debug')
install=calibre.install
source=(http://calibre-ebook.googlecode.com/files/${pkgname}-${pkgver}.tar.gz
        desktop_integration.patch)
md5sums=('78ebed5248758b9be1d135ae018b8dfe'
         'bcc538a3b004429bf8f5a0ac1d89a37f')

build() {
  cd "${pkgname}"

  rm -rf src/{cherrypy,pyPdf}
  sed -i -e "s/ldflags = shlex.split(ldflags)/ldflags = shlex.split(ldflags) + ['-fPIC']/" setup/extensions.py
  sed -i -e 's:\(#!/usr/bin/env[ ]\+python$\|#!/usr/bin/python$\):\12:g' \
    $(find . -regex ".*.py\|.*.recipe")

  python setup.py build || return 1
  python setup.py resources || return 1
  python setup.py translations || return 1
}

package() {
  cd ${pkgname}
  
  patch -Np1 -i $srcdir/desktop_integration.patch || return 1

  # More on desktop integration (e.g. enforce chakra defaults)
  sed -i -e "/self.create_uninstaller()/,/os.rmdir(config_dir)/d" \
      -e "s|self.opts.staging_sharedir, 'man/man1'|self.opts.staging_root, 'usr/share/man/man1'|" \
      -e "s|manpath, prog+'.1'+__appname__+'.bz2'|manpath, prog+'.1'+'.bz2'|" \
      -e "s|old_udev = '/etc|old_udev = '${pkgdir}/etc|" \
      -e "s/^Name=calibre/Name=Calibre/g" src/calibre/linux.py

  # Fix the environment module location
  sed -i -e "s|(prefix=.*)|(prefix='$pkgdir/usr')|g" setup/install.py

  mkdir -p ${pkgdir}/usr/lib/python2.7/site-packages
  python setup.py install --root=${pkgdir}/ --prefix=/usr \
    --staging-bindir=${pkgdir}/usr/bin \
    --staging-libdir=${pkgdir}/usr/lib \
    --staging-sharedir=${pkgdir}/usr/share

  find ${pkgdir} -type d -empty -delete

  # Decompress the man pages so makepkg will do it for us.
  for decom in ${pkgdir}/usr/share/man/man1/*.bz2; do
    bzip2 -d ${decom}
  done
}
