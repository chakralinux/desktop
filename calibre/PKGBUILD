# maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>

pkgname=calibre
pkgver=1.48.0
pkgrel=2
pkgdesc="Ebook management application"
arch=('x86_64')
url="http://calibre.kovidgoyal.net/"
license=('GPL3')
depends=('python2' 'python2-imaging' 'libusb' 'python2-pyqt4' 'python-mechanize'
         'imagemagick' 'python2-dbus' 'python-lxml' 'python2-beautifulsoup' 'python2-dateutil'
         'python2-cssutils' 'desktop-file-utils' 'python2-dns' 'libmtp'
         'podofo' 'xdg-utils' 'cherrypy' 'python2-pdf' 'unrar' 'chmlib' 'poppler'
         'poppler-qt' 'icu' 'libwmf' 'libffi' 'python2-six' 'python2-netifaces' 'python2-cssselect'
         'python2-apsw')
makedepends=('python2-pycountry' 'qt-private-headers')
#optdepends=('ipython2: to use calibre-debug')
screenshot=('https://lh5.googleusercontent.com/_lG58t3XWd3c/TcNwiXkAx_I/AAAAAAAAAEc/M34I4uY9UUY/s800/gui.jpeg')
install=${pkgname}.install
source=("http://download.calibre-ebook.com/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e704090f6611c5e374252c3f1b58e077ccd3170a3ea39893e5bf687287abf472')

prepare() {
  cd ${pkgname}

  # Remove unneded files an libs
  rm -rf src/cherrypy
  #       src/six.py \
  #       src/html5lib \
  #       src/chardet
  rm -rf resources/${pkgname}-portable.*
  mkdir -p src/calibre/plugins

  sed -i -e "s/shlex.split(ldflags)/& + ['-fPIC']/" \
    setup/extensions.py

  sed -i -e 's:\(#!/usr/bin/env[ ]\+python$\|#!/usr/bin/python$\):\12:g' \
    $(find . -regex ".*.py\|.*.recipe")
  
  # Desktop integration (e.g. enforce arch defaults)
  sed -e "/self.create_uninstaller()/,/os.rmdir(config_dir)/d" \
      -e "/\(cc('xdg-icon-resource\|self.icon_resources.append\|'128'))\)/d" \
      -e "/render_img/ s/\('calibre-.*\.png'\)/os.path.join(dir, \1)/g" \
      -e "/dir, 'calibre-lrf.png'/i \
\                dir = os.path.join(self.opts.staging_sharedir,'../pixmaps')\n\
\                os.mkdir(dir)" \
      -e "/f = open/ s/\('calibre-.*\.desktop'\)/os.path.join(dir, \1)/g" \
      -e "/dir, 'calibre-lrfviewer.desktop'/i \
\                dir = os.path.join(self.opts.staging_sharedir,'../applications')\n\
\                os.mkdir(dir)" \
      -e "s/'ctc-posml'/'text' not in mt and 'pdf' not in mt and 'xhtml'/" \
      -e "s/^Name=calibre/Name=Calibre/g" \
      -i  src/calibre/linux.py
}

build() {
  cd ${pkgname}
  
  LANG=en_US.UTF-8 python2 setup.py build
  LANG=en_US.UTF-8 python2 setup.py translations
}

package() {
  cd ${pkgname}

  # Fix the environment module location
  sed -i -e "s|(prefix=.*)|(prefix='${pkgdir}/usr')|g" setup/install.py

  install -d "${pkgdir}/usr/lib/python2.7/site-packages"
  install -d "${pkgdir}/usr/share/zsh/site-functions"
  
  LANG=en_US.UTF-8 python2 setup.py install \
    --root="${pkgdir}" \
    --prefix=/usr \
    --staging-bindir="${pkgdir}/usr/bin" \
    --staging-libdir="${pkgdir}/usr/lib" \
    --staging-sharedir="${pkgdir}/usr/share"
    
  python2 -m compileall "${pkgdir}/usr/lib/calibre/"
  python2 -O -m compileall "${pkgdir}/usr/lib/calibre/"
}
