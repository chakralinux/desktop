#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

#
# package info
#
pkgbase=('kdeadmin')
pkgname=('kdeadmin-strigi-analyzer'
         'kdeadmin-kcron'
         'kdeadmin-kuser'
         'kdeadmin-ksystemlog'
         'kdeadmin-system-config-printer')

arch=('i686' 'x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="split package"
url="http://www.kde.org/"
license=('GPL' 'LGPL' 'FDL')
install=kdeadmin.install
options=('docs' '!splithdr' 'splitdbg' 'log')

makedepends=('pkgconfig' 'cmake' 'automoc4' "kdepimlibs>=${_kdever}"  "kde-runtime>=${_kdever}"
	     "kdebindings-pykde4>=${_kdever}" 'system-config-printer-common' "kdepim-runtime>=${_kdever}" "docbook-xsl")

source=($_mirror/${pkgbase}-$_kdever.tar.xz)
md5sums=(`grep ${pkgbase}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

#
# build function
#
build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	
	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make
}

#
# split-install functions
#
package_kdeadmin-strigi-analyzer() {
	pkgdesc="KDE Administrative Tools - Common files and libraries"
	depends=("kde-runtime>=${_kdever}")
        provides=('kdeadmin-common')
	replaces=('kdeadmin-common')
	groups=("${pkgbase}" "kde" "kde-uninstall")
	categories=('system')

	splitdirs="strigi-analyzer"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done

}

package_kdeadmin-kcron() {
	pkgdesc="Crontab editor"
	depends=("kdeadmin-strigi-analyzer>=${_kdever}")
	groups=("${pkgbase}" "kde" "kde-uninstall")
	conflicts=("kdeadmin-doc")
	categories=('system')

	splitdirs="kcron doc/kcron"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdeadmin-kdat() {
	pkgdesc="Tar-based tape archiver"
	depends=("kdeadmin-strigi-analyzer>=${_kdever}")
	groups=("${pkgbase}" "kde" "kde-uninstall")
	categories=('system')

	splitdirs="kdat"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdeadmin-ksystemlog() {
	pkgdesc="Log viewer"
	depends=("kdeadmin-strigi-analyzer>=${_kdever}")
	groups=("${pkgbase}" "kde" "kde-uninstall")
	conflicts=("kdeadmin-doc")
	categories=('system')

	splitdirs="ksystemlog doc/ksystemlog"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdeadmin-kuser() {
	pkgdesc="Manage users and groups"
	depends=("kdeadmin-strigi-analyzer>=${_kdever}")
	groups=("${pkgbase}" "kde" "kde-minimal" "kde-uninstall")
	conflicts=("kdeadmin-doc")
	categories=('system')

	splitdirs="kuser doc/kuser"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdeadmin-system-config-printer() {
	pkgdesc="Printer configuration"
	depends=("kdeadmin-strigi-analyzer>=${_kdever}" "system-config-printer" "kdebindings-pykde4>=${_kdever}" "pygobject")
	groups=("${pkgbase}" "kde" "kde-uninstall")
	categories=('system')

	splitdirs="system-config-printer-kde"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done

	msg "fixing i18n ..."
	#fix locations
	sed -i -e 's~special_choice=_("Automatic rotation")),~special_choice=i18n("Automatic rotation")),~g' ${pkgdir}/usr/share/apps/system-config-printer-kde/system-config-printer-kde.py
}
