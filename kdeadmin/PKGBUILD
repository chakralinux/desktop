#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# maintainer Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=('kdeadmin')
pkgname=('kdeadmin-kcron'
         'kdeadmin-kuser'
         'kdeadmin-ksystemlog')

arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="split package"
url="http://www.kde.org/"
license=('GPL' 'LGPL' 'FDL')
install=kdeadmin.install
options=('docs' '!splithdr' 'splitdbg' 'log')

makedepends=('pkgconfig' 'cmake' 'automoc4' "kdepimlibs>=${_kdever}"  "kde-runtime>=${_kdever}"
	     "kdebindings-pykde4>=${_kdever}" "kdepim-runtime>=${_kdever}" "docbook-xsl")

source=("$_mirror/${pkgbase}-$_kdever.tar.xz" 
        "syslog-path.patch")

md5sums=(`grep ${pkgbase}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '4b0c3e0a7b7561185e20014fcbc1118e')

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	msg "fixing syslog path"
	patch -p1 -i ${srcdir}/syslog-path.patch

        # Set KUser default starting user to 1000
        sed -i "s|ID 500 CACHE|ID 1000 CACHE|" kuser/CMakeLists.txt
	
	cd ${srcdir}
        rm -rf build
	mkdir build && cd build
	
	msg "starting build ..."
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DBUILD_strigi-analyzer=OFF 
	make
}

package_kdeadmin-kcron() {
	pkgdesc="Crontab editor"
	depends=("kde-runtime>=${_kdever}")
	groups=("${pkgbase}" "kde" "kde-uninstall")
	conflicts=("kdeadmin-doc")
	categories=('system')

	splitdirs="kcron doc/kcron"
		for i in ${splitdirs} ; do
			cd ${srcdir}/build/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdeadmin-ksystemlog() {
	pkgdesc="Log viewer"
	screenshot="http://www.kde.org/images/screenshots/ksystemlog.png"
	depends=("kde-runtime>=${_kdever}")
	groups=("${pkgbase}" "kde" "kde-uninstall")
	conflicts=("kdeadmin-doc")
	categories=('system')

	splitdirs="ksystemlog doc/ksystemlog"
		for i in ${splitdirs} ; do
			cd ${srcdir}/build/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdeadmin-kuser() {
	pkgdesc="Manage users and groups"
	screenshot="http://www.kde.org/images/screenshots/kuser.png"
	depends=("kde-runtime>=${_kdever}")
	groups=("${pkgbase}" "kde" "kde-minimal" "kde-uninstall")
	conflicts=("kdeadmin-doc")
	categories=('system')

	splitdirs="kuser doc/kuser"
		for i in ${splitdirs} ; do
			cd ${srcdir}/build/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}
