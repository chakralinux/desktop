pkgname=kaudiocreator
pkgver=3.92.0+301+gfaf2072
pkgrel=1
pkgdesc="A program for ripping and encoding Audio-CDs, encoding files from disk"
arch=('x86_64')
url="https://www.kde.org/applications/multimedia/kaudiocreator/"
license=('GPL')
depends=('audiocd-kio' 'taglib' 'libdiscid' 'kcmutils' 'knotifyconfig' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'git' 'kdesignerplugin' 'kdoctools')
_commit=faf2072f18a1b699ddb6d3bc7d58f522f0914a38  # master
source=("git://git.kde.org/kaudiocreator.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/-/+/g' -e 's/v//'
}

prepare() {
  mkdir build
}

build() {
  cd build

  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
