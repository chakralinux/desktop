# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=xmonad-utils
pkgname=xmonad-utils
pkgver=0.1.3.2.1
pkgrel=1
pkgdesc="A small collection of X utilities"
url="http://hackage.haskell.org/package/xmonad-utils"
license=('custom:BSD3')
arch=('x86_64' 'i686')
makedepends=()
depends=('ghc=7.6.3-1' 'haskell-X11>=1.3' 'haskell-base<5' 'haskell-ghc>=6.8' 'haskell-random>=1.0' 'haskell-unix' 'gmp' 'glibc')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/xmonad-utils/0.1.3.2.1/xmonad-utils-0.1.3.2.1.tar.gz")
install="xmonad-utils.install"
sha512sums=('015feae34d4ca0610618e7a2999cce6a0b4df5263455db24fc209a362cd2faa60a500a0c00c7c3838339ac2666ef77e364a8aa12503010c9ebabf3db50c6893f')

build() {
    cd "${srcdir}/xmonad-utils-0.1.3.2.1"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix="/usr" \
        --docdir="/usr/share/doc/xmonad-utils" \
        --libsubdir='$compiler/site-local/$pkgid'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/xmonad-utils-0.1.3.2.1"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/xmonad-utils/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/xmonad-utils/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/xmonad-utils/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/xmonad-utils"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/xmonad-utils/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/xmonad-utils/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
