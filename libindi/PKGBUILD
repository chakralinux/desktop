pkgname=libindi
pkgver=1.2.0
pkgrel=1
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="http://www.indilib.org/index.php?title=Main_Page"
license=('GPL2')
arch=('x86_64')
depends=('libnova' 'cfitsio' 'boost-libs' 'libusb' 'libjpeg' 'gsl')
makedepends=('cmake' 'boost')
options=('staticlibs') # libindiclient builds as static only
source=("http://www.indilib.org/jdownloads/Source/${pkgname}_${pkgver}.tar.gz")
sha256sums=('a1cf17a4f0a0ca30afd4c131b82e181aeb42a68cda176d5b4c6945436ced2321')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}_${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
