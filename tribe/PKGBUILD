pkgname=tribe
pkgver=2014.05
_rls_notes=20140518
pkgrel=8
_codename=Descartes
pkgdesc="Tribe - Chakra LiveCD Installer"
url="http://git.chakra-project.org/chakra/"
license="GPL"
arch=('x86_64')
depends=('kdelibs' 'kdeedu-marble>=4.10.0' 'squashfs-tools' 'tribe-partitionmanager'
         'rsync' 'mkinitcpio-nfs-utils' 'ntfs-3g' 'libpwquality')
makedepends=('cmake' 'kde-workspace' 'automoc4')
options=('debug')

_gitname=tribe
_gitsha=26756af
source=("$pkgname"::"git://gitorious.org/chakra/${_gitname}.git#commit=${_gitsha}"
        "http://chakra.sourceforge.net/sources/tribe/RELEASE_NOTES_${_rls_notes}.html"
        "launch-tribe.sh")
md5sums=('SKIP'
         'e7d18506225a0b9d372ac4bc667941d0'
         'e31855da237a2c8029774831b3c04685')

groups=('kde-uninstall')
categories=('system')
install=tools-tribe.install

build() {        
	cd "$srcdir/$_gitname"
	
	# proper bootmnt folder
	sed -i -e "s/@ARCH@/${CARCH}/g" "src/config-tribe.h.cmake"

	# add pkgver to tribe
	sed -i -e "s~source build/from git~Build: ${pkgver} ($_codename)~g" "src/config-tribe.h.cmake"
	sed -i -e "s~code-name~$_codename~g" "scripts/postinstall-functions/01-job-initialize-target"
	sed -i -e "s~chakra-version~${pkgver}~g" "scripts/postinstall-functions/01-job-initialize-target"
	
	mkdir "$_gitname-build"
	cd "$_gitname-build"
	
	cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' 

	msg "Starting make..."
	make 
}

package() {
        cd "$srcdir/$_gitname/$_gitname-build"
	msg "Make successful, installing..."
	make DESTDIR=${pkgdir} install
        install -Dm755 ${srcdir}/launch-tribe.sh \
        ${pkgdir}/usr/bin/launch-tribe.sh
	cp -vf ${srcdir}/RELEASE_NOTES_${_rls_notes}.html $pkgdir/usr/share/tribe/config/RELEASE_NOTES.html

	# remove nasty files
	find ${pkgdir}/ -name ".git" -type d -exec rm -fr {} +
}
