# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=avogadrolibs
pkgver=0.9.0
pkgrel=1
pkgdesc="An advanced molecular editor"
arch=(i686 x86_64)
url="http://www.openchemistry.org/"
license=(custom)
depends=(openbabel qt5-base glew)
makedepends=(cmake eigen3 python2 boost)
source=("http://downloads.sourceforge.net/project/avogadro/avogadro2/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('af5c0a0c782cfa528d1c203840c69cf6')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DUSE_QT=ON \
    -DUSE_OPENGL=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
