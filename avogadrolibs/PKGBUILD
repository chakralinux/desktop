# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=avogadrolibs
pkgver=1.90.0
pkgrel=1
pkgdesc="An advanced molecular editor"
arch=(x86_64)
url="http://www.openchemistry.org/"
license=(custom)
depends=(molequeue openbabel qt5-base glew spglib hdf5)
makedepends=(cmake eigen3 python2 boost)
source=($pkgname-$pkgver.tar.gz::"https://github.com/OpenChemistry/avogadrolibs/archive/$pkgver.tar.gz")
md5sums=('f2b7014f48da5b2a72d49c01dafaf225')

prepare() {
  mkdir -p build

  # Find spglib
  sed -e 's|NAMES spglib|NAMES spglib symspg|' -i $pkgname-$pkgver/cmake/FindSpglib.cmake
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DUSE_HDF5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
