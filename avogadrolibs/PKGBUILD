# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/avogadrolibs/

pkgname=avogadrolibs
pkgver=1.91.0
pkgrel=1
pkgdesc="An advanced molecular editor"
arch=(x86_64)
url="http://www.openchemistry.org/"
license=(custom)
depends=(molequeue glew spglib libmsym libarchive hdf5)
makedepends=(cmake eigen3 python2 boost)
source=($pkgname-$pkgver.tar.gz::"https://github.com/OpenChemistry/avogadrolibs/archive/$pkgver.tar.gz")
md5sums=('79af9710f4927b8991f9b590bf11fbc1')


prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DOpenGL_GL_PREFERENCE=GLVND \
    -DUSE_HDF5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

  rm "$pkgdir"/usr/lib/libjsoncpp.a # provided by jsoncpp
}
