pkgname=lrzip
pkgver=0.631
pkgrel=1
pkgdesc='Multi-threaded compression with rzip/lzma, lzo, and zpaq'
url='https://github.com/ckolivas/lrzip'
license=('GPL')
arch=('x86_64')
depends=('lzo' 'zlib')
source=("http://ck.kolivas.org/apps/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('0d11e268d0d72310d6d73a8ce6bb3d85e26de3f34d8a713055f3f25a77226455')

build() {
  cd "$pkgname-$pkgver"

  CFLAGS="$CFLAGS -fomit-frame-pointer"
  CXXFLAGS="$CXXFLAGS -fomit-frame-pointer"
  
  ./autogen.sh --prefix=/usr "$flags"
  make
}

check() {
  make -C "$pkgname-$pkgver" -k check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install-strip
}
