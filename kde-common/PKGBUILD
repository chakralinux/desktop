#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

# original name (used for the source and pkgnames)
_origname=common

#
# package info
#
pkgname=kde-common
arch=(i686 x86_64)
pkgver=${_kdever}
pkgrel=1

pkgdesc="KDE SC - Common Configuration Files, Scripts and Artwork"
url="http://www.kde.org"
license=('GPL')
options=('!splitdbg' '!splithdr' 'strip')

provides=("kde-common=${_kdever}" "kde-agent=${_kdever}")

groups=("kde" "kde-minimal" "kde-uninstall")

# also add some obvious dependencies here, as there are users out there who dont update such stuff
depends=("${_qtpkg}>=${_qtver}" 'pinentry' 'openssh' 'bash>=4.2' 'glibc>=2.11')

install=kde-common.install

source=(http://www.kde-apps.org/CONTENT/content-files/48411-rootactions_servicemenu_2.4.10.tar.gz
	servicemenu-copy-path.desktop
	http://www.kde-apps.org/CONTENT/content-files/102825-compressattach.desktop.bz2
	servicemenu-rootactions.patch
	Chakra-Bugs.desktop
	Chakra-ChangeLog.desktop
	Chakra-Code.desktop
	Chakra-Documentation.desktop
	Chakra-Donate.desktop
	Chakra-Forum.desktop
	Chakra-Homepage.desktop
	Chakra.directory
	kdm.daemon
	chakra-config-updater.sh
	chakra-fix-configs.sh
	chakra-update-icons.sh
	chakra-update-mime.sh
	chakra-update-sycoca.sh
	chakra-update-user-sycoca.sh
	chakra-sysinfo.sh
	chakra-backup.sh
	Chakra-backup.desktop
	updates.conf
	chakra-icons.tar.gz
	agent-startup.sh
	agent-shutdown.sh)

md5sums=('2afd86671ad0db31718157aecd46d528'  # 48411-rootactions_servicemenu_2.4.10.tar.gz
         'a4108e264667380876ffa7e9842892f4'  # servicemenu-copy-path.desktop
         '630ffe7118eef8608a9e5e393a0b1e12'  # 102825-compressattach.desktop.bz2
         'e77e132d6da1a8d897db2ba630915b0f'  # servicemenu-rootactions.patch
         '3f775e8c5626c216ac9a3e2f5c336a6b'  # Chakra-Bugs.desktop
         '35f4bd7a1ae4f1741e056a6f5715d822'  # Chakra-ChangeLog.desktop
         '5cefa6e9baf388ae9d86f3b17eb12035'  # Chakra-Code.desktop
         '3a08ce7d26d6f3a1b06f5e0f3e05f0e3'  # Chakra-Documentation.desktop
         '490d34c6cc0b0e6e2c0a7ab98ab77f89'  # Chakra-Donate.desktop
         '1be911f58eeef76fe920b8ebe0941464'  # Chakra-Forum.desktop
         'c664cda009a7c8ec0e9fb36d1ed74e68'  # Chakra-Homepage.desktop
         'ad4cefe6e10e0985a89a0aea6aec530c'  # Chakra.directory
         '0688a20010ad27e35755e77c26c94d2c'  # kdm.daemon
         '535501ac9f3ad05ddd18f8cfee4a2e06'  # chakra-config-updater.sh
         '1cad70fdf0306e5590899865bbfb430b'  # chakra-fix-configs.sh
         '78a20a4ccaf943f5a2089f4d436fdf32'  # chakra-update-icons.sh
         'd3fcef1c7affa098683a09f26415d8ef'  # chakra-update-mime.sh
         'aaa1958b14518e9b0193afe497da830e'  # chakra-update-sycoca.sh
         '4e974016ca509f187b4549d95877bacc'  # chakra-update-user-sycoca.sh  
         '2b0aaf209d35e1e8c660db66e61d13c7'  # chakra-sysinfo.sh
         '660a7650478e5b7115fbb71811b5d9ee'  # chakra-backup.sh
         'f2ab6538277002028f1263fa7841e2e6'  # Chakra-backup.desktop
         '466d7fc7cab5738d674df45fdeee4eb0'  # updates.conf
         '1c85260174e58fc6efaa8d2cac947761'  # chakra-icons.tar.gz
         '7daec96c125e1e238a35f31ea4a476f0'  # agent-startup.sh
         '18e8d01124b19f7df2937c06e177bf7f') # agent-shutdown.sh

#
# build and install
#
build()
{
	# install scripts for gpg/ssh-agent
	install -D -m 755 -o root -g root $srcdir/agent-startup.sh $pkgdir/etc/kde/env/agent-startup.sh
	install -D -m 755 -o root -g root $srcdir/agent-shutdown.sh $pkgdir/etc/kde/shutdown/agent-shutdown.sh

	# install kdm rc script
	install -D -m755 -o root -g root ${srcdir}/kdm.daemon ${pkgdir}/etc/rc.d/kdm

	# install config updater scripts
	install -D -m755 -o root -g root ${srcdir}/chakra-config-updater.sh ${pkgdir}/usr/bin/chakra-config-updater
	install -D -m755 -o root -g root ${srcdir}/chakra-fix-configs.sh ${pkgdir}/usr/bin/chakra-fix-configs
	install -D -m755 -o root -g root ${srcdir}/chakra-update-icons.sh ${pkgdir}/usr/bin/chakra-update-icons
	install -D -m755 -o root -g root ${srcdir}/chakra-update-mime.sh ${pkgdir}/usr/bin/chakra-update-mime
	install -D -m755 -o root -g root ${srcdir}/chakra-update-sycoca.sh ${pkgdir}/usr/bin/chakra-update-sycoca
	install -D -m755 -o root -g root ${srcdir}/chakra-update-user-sycoca.sh ${pkgdir}/usr/bin/chakra-update-user-sycoca
	
	# install sysinfo script
	install -D -m755 -o root -g root ${srcdir}/chakra-sysinfo.sh ${pkgdir}/usr/bin/chakra-sysinfo

	# install backup script
	install -D -m755 -o root -g root ${srcdir}/chakra-backup.sh ${pkgdir}/usr/bin/chakra-backup
	install -D -m644 -o root -g root ${srcdir}/Chakra-backup.desktop ${pkgdir}/usr/share/applications/kde4/Chakra-backup.desktop

	# install config file for updates
	install -D -m755 -o root -g root ${srcdir}/updates.conf ${pkgdir}/etc/chakra/updates.conf

	# install root actions service menu
	cd ${srcdir}/rootactions_servicemenu_2.4.10/Root_Actions_2.4.10/dolphin-KDE4/
	mkdir -p ${pkgdir}/usr/share/kde4/services/ServiceMenus/
	install -m755 *.desktop ${pkgdir}/usr/share/kde4/services/ServiceMenus/
        cd ${srcdir}/rootactions_servicemenu_2.4.10/Root_Actions_2.4.10
        patch -p2 < ${srcdir}/servicemenu-rootactions.patch      
	install -Dm755 rootactions-servicemenu.pl ${pkgdir}/usr/bin/rootactions-servicemenu.pl

	# install all other service menus
	install -m755 ${srcdir}/servicemenu-copy-path.desktop ${pkgdir}/usr/share/kde4/services/ServiceMenus/copy-path.desktop
	install -m755 ${srcdir}/102825-compressattach.desktop ${pkgdir}/usr/share/kde4/services/ServiceMenus/compress-attach.desktop

	# install chakra icons
	cd ${srcdir}/chakra-icons/
	mkdir -p ${pkgdir}/usr/share/icons/hicolor/scalable/apps/
	for n in flat shiny smooth; do
		for i in 128 64 48 32 22 16; do
			mkdir -p ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/
			install -D -m755 chakra-${n}-${i}.png ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/chakra-${n}.png
		done
		install -D -m755 chakra-${n}.svgz ${pkgdir}/usr/share/icons/hicolor/scalable/apps/chakra-${n}.svgz
	done
	
	# install menu entries
	cd ${srcdir}
	install -m755 -d ${pkgdir}/usr/share/desktop-directories
	install -m755 -d ${pkgdir}/usr/share/applications/kde4
	install -m755 -d ${pkgdir}/usr/share/pixmaps
	install -m644 Chakra*.desktop ${pkgdir}/usr/share/applications/kde4
	install -m644 Chakra.directory ${pkgdir}/usr/share/desktop-directories/
}
