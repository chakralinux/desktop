# Maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#		H W Tovetj√§rn (totte) <totte@tott.es>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

## NOTE: when you update Haskell packages you'll have to update the entire stack to not break it ##

_hkgname=HTTP
pkgname=haskell-http
pkgver=4000.2.11
pkgrel=1
pkgdesc="A library for client-side HTTP"
url="http://hackage.haskell.org/package/HTTP"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc=7.6.3-1' 'haskell-mtl>=2.0' 'haskell-mtl<2.2' 'haskell-network<2.5' 'haskell-parsec')
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/HTTP/${pkgver}/HTTP-${pkgver}.tar.gz")
md5sums=('a293c8a1b6caebda06cf655f42d473d0')

build() {
    cd "${srcdir}/HTTP-${pkgver}"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix=/usr \
        --docdir=/usr/share/doc/haskell-http \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/HTTP-${pkgver}"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/${pkgname}/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/HTTP"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}

