# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=random
pkgname=haskell-random
pkgver=1.0.1.1
pkgrel=4
pkgdesc="Random number library"
url="http://hackage.haskell.org/package/random"
license=('custom:BSD3')
arch=('x86_64' 'i686')
makedepends=()
depends=('ghc=7.6.3-1' 'haskell-base>=3' 'haskell-base<5' 'haskell-time')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/random/1.0.1.1/random-1.0.1.1.tar.gz")
install="haskell-random.install"
sha512sums=('dc2f76e87a497d6f5196a8829027d5a9f0eaa2907995f432e26a1594c287c90884253a994fc4a325b0c05c97816e3c3b940a97ccb50d31917a87a99200f1ebff')

build() {
    cd "${srcdir}/random-1.0.1.1"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix="/usr" \
        --docdir="/usr/share/doc/haskell-random" \
        --libsubdir='$compiler/site-local/$pkgid'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/random-1.0.1.1"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/haskell-random/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/haskell-random/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/haskell-random/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/random"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/haskell-random/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/haskell-random/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
