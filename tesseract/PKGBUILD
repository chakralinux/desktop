# contributor (x86_64): Giuseppe Cal√†  <jiveaxe@gmail.com>

_langs=(afr ara aze bel ben bul cat ces chi_sim chi_tra chr dan deu ell eng enm
    epo equ est eus fin fra frk frm glg grc heb hin hrv hun ind isl ita ita_old
    jpn kan kor lav lit mal mkd mlt msa nld nor pol por ron rus slk slv spa
    spa_old sqi srp swa swe tam tel tgl tha tur ukr vie)

pkgname=tesseract
pkgver=4.0.0
pkgrel=1
pkgdesc="An OCR program"
arch=(x86_64)
url="https://github.com/tesseract-ocr/tesseract"
license=("APACHE")
depends=(libpng libtiff libjpeg-turbo zlib giflib gcc-libs leptonica)
makedepends=(icu cairo pango)
install=tesseract.install
optdepends=('icu' 'cairo' 'pango'
	    $(for l in ${_langs[@]}; do echo tesseract-data-${l}; done))
source=($pkgname-$pkgver.tar.gz::https://github.com/tesseract-ocr/tesseract/archive/$pkgver.tar.gz
	https://github.com/tesseract-ocr/tessdata/raw/master/osd.traineddata)
sha256sums=('a1f5422ca49a32e5f35c54dee5112b11b99928fc9f4ee6695cdc6768d69f61dd'
            'e19f2ae860792fdf372cf48d8ce70ae5da3c4052962fe22e9de1f680c374bb0e')

build() {
  cd "$srcdir"/$pkgname-${pkgver}
  [ -x configure ] || ./autogen.sh
  [ -f Makefile ] || ./configure --prefix=/usr
  make
  make training
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" training-install
  mkdir -p "$pkgdir"/usr/share/tessdata
  install -Dm0644 "$srcdir"/osd.traineddata "$pkgdir"/usr/share/tessdata/osd.traineddata
}
