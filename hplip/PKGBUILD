pkgname=hplip
pkgver=3.16.11
pkgrel=2
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet."
arch=('x86_64')
url="http://hplipopensource.com"
license=('GPL')
depends=('python3' 'ghostscript' 'net-snmp' 'wget' 'foomatic-db-engine'
         #dependencies added by default to run the GUI and other services
         'python3-pyqt5' 'python3-gobject' 'cups' 'libusb' 'python3-dbus')
makedepends=('sane' 'rpcbind' )
optdepends=('sane: for scanner support'
            'python3-pillow: for commandline scanning support'
            'python3-reportlab: for pdf output in hp-scan'
            'rpcbind: for network support')
install=hplip.install
source=(https://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz
        disable_upgrade.patch
        fix_tabs.patch
        0022-Add-include-cups-ppd.h-in-various-places-as-CUPS-2.2.patch)
md5sums=('7d187c3142ba323a3021767d333ad9f4'
         '4ebb9377a989b4fb776e37f5968bb7b0'
         '434fcb7583bb9dc21e64341672d219e2'
         'ceb7449b12e2ecf70f898bc50b161a3c')

prepare() {
 cd $pkgname-$pkgver
 
 export PYTHON=python3
 
 find . -type f -exec sed -i 's~^#.*env python~#!/usr/bin/env python3~' {} +
 sed -i '1s|#!/usr/bin/python|#!/usr/bin/python3|' doctor.py setup.py makeuri.py logcapture.py base/magic.py
 sed -i '1s|#!/usr/bin/python|#!/usr/bin/python3|' ui/upgradeform.py uninstall.py upgrade.py config_usb_printer.py
 sed -i 's|python ./print.py|python3 ./print.py|' scan.py
 sed -i 's|python ./testpage.py|python3 ./testpage.py|' setup.py
 sed -i 's|python ./setup.py|python3 ./setup.py|' ui4/devmgr5.py ui4/nodevicesdialog.py 
 sed -i 's|python %HOME%|python3 %HOME%|' base/utils.py
 sed -i 's|python ./plugin.py|python3 ./plugin.py|' base/pkit.py
 sed -i 's|/usr/bin/python|/usr/bin/python3|' data/rules/hplip-printer@.service
 
 # disable insecure update - https://bugs.archlinux.org/task/38083
 patch -Np0 -i ${srcdir}/disable_upgrade.patch
 
 # fix tabs breaking printing; FS#50588
 patch -Np0 -i ${srcdir}/fix_tabs.patch
 # add missing 'include <cups/ppd.h>' at various places
 patch -Np1 -i ${srcdir}/0022-Add-include-cups-ppd.h-in-various-places-as-CUPS-2.2.patch

 # Use system foomatic-rip for hpijs driver instead of foomatic-rip-hplip
 # The hpcups driver does not use foomatic-rip
 local i
 for i in ppd/hpijs/*.ppd.gz ; do
         rm -f ${i}.temp
         gunzip -c ${i} | sed 's/foomatic-rip-hplip/foomatic-rip/g' | \
                 gzip > ${i}.temp || return 1
         mv ${i}.temp ${i}
 done
   
 export AUTOMAKE='automake --foreign'
 autoreconf --force --install
}

build() {
 cd $pkgname-$pkgver
 ./configure --prefix=/usr \
	     --enable-qt5 \
             --disable-qt4 \
             --disable-foomatic-rip-hplip-install \
             --enable-foomatic-ppd-install \
             --enable-hpcups-install \
             --enable-new-hpcups \
             --enable-cups-ppd-install \
             --enable-cups-drv-install \
             --enable-hpijs-install \
             --enable-foomatic-drv-install \
             --enable-pp-build #--help

 make
}

package() {
  cd $pkgname-$pkgver
  make -j1 rulesdir=/usr/lib/udev/rules.d DESTDIR=$pkgdir install
  
  # remove config provided by sane and autostart of hp-daemon
  rm -rf $pkgdir/etc/{sane.d,xdg}
  
  # remove HAL .fdi file because HAL is no longer used
  rm -rf $pkgdir/usr/share/hal
 
  # remove rc script
  rm -rf $pkgdir/etc/init.d
}

