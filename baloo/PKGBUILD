# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="baloo"    
arch=('x86_64')
pkgver=${_kdever}
pkgrel=3
pkgdesc="The core baloo system"
url="http://www.kde.org"
license=('GPL'
         'LGPL'
         'FDL')
options=('docs'
         '!header'
         'debug'
         'log')
depends=("kdelibs>=${_kdever}"
         "kfilemetadata>=${pkgver}"
         'kdepimlibs'
         'xapian-core')
makedepends=('pkg-config'
             'cmake'
             'automoc4'
             'doxygen')
optdepends=("nepomuk-core: To migrate nepomuk metadata to baloo")
install="baloo.install"
source=($_mirror/${pkgname}-$_kdever.tar.xz
       'add_disable_kcm.diff')
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d" " -f1`
	    'e524850a76bc9e7499c8f2702e186c8fba978b670d9b6cb45902fe93cc133f2d')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # https://bugs.kde.org/show_bug.cgi?id=331932#c59
  patch -p1 -i "${srcdir}/add_disable_kcm.diff"
}

build() {
    cd ${pkgname}-${pkgver}
    rm -rf build 
    mkdir -p build && cd build
    cmake .. \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix}
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}/build
    make DESTDIR=${pkgdir} install
}
