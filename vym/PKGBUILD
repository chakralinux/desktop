pkgname=vym
pkgver=2.6.11
pkgrel=2
pkgdesc="A mindmapping tool"
arch=('x86_64')
url="http://www.insilmaril.de/vym/"
license=('GPL')
depends=('qt5-svg' 'desktop-file-utils' 'unzip' 'zip')
makedepends=('qt5-tools')
options=('!emptydirs')
screenshot="http://a.fsdn.com/con/app/proj/vym/screenshots/328321.jpg"
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2 \
        $pkgname.desktop)
sha256sums=('d48efee7da43c22fbb862c7d99cb6d855ece58808f2ea184310d2a79c8a2f6fb'
            'e299c69c213e7aac3f5b5d0ab088132b4ec7cb63a391f272e75ed64f049d541b')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver/lang

  lrelease-qt5 *.ts
}

build() {
  cd ${srcdir}/$pkgname-$pkgver

  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  # install files
  make INSTALL_ROOT="${pkgdir}" install
  
  install -d ${pkgdir}/usr/share/{doc/$pkgname,man/man1,$pkgname}

  install -D -m644 "${srcdir}"/$pkgname.desktop \
  "${pkgdir}"/usr/share/applications/$pkgname.desktop
  
  install -D -m644 icons/$pkgname-128x128.png \
  "${pkgdir}"/usr/share/pixmaps/$pkgname.png
  
  mv $pkgdir/usr/vym $pkgdir/usr/share
  
  # correct doc location
  mv doc/* "${pkgdir}"/usr/share/doc/$pkgname
  mv "${pkgdir}"/usr/share/doc/vym/vym.1.gz "${pkgdir}"/usr/share/man/man1/
}
