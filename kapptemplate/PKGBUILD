#Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kapptemplate

pkgname="kapptemplate"
arch=('x86_64')
pkgver=18.12.1
pkgrel=1
pkgdesc="KDE application template generator"
screenshot="http://www.kde.org/images/screenshots/kapptemplate.png"
depends=('kio')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
replaces=('kdesdk-kapptemplate')
conflicts=('kdesdk-kapptemplate')
provides=('kdesdk-kapptemplate')
categories=('programming')
license=('GPL' 'LGPL' 'FDL')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('aa4f3459f50241bf4c7e6eb24bae33ed3f88282d229af251373786ffc168a2e0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
