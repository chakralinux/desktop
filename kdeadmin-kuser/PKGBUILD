# Include global configuration
source ../kdeapps.conf

_pkgname="kuser"
pkgname="kdeadmin-kuser"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
url="http://www.kde.org/"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
makedepends=('pkg-config' 'cmake' 'automoc4' 'docbook-xsl' 'kdepimlibs4')
pkgdesc="Manage users and groups"
screenshot="http://www.kde.org/images/screenshots/kuser.png"
depends=("kde-runtime")
groups=("kdeadmin" "kde" "kde-minimal" "kde-uninstall")
conflicts=("kdeadmin-doc")
categories=('system')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz"
        kuser-cmake-3.4.patch::"https://quickgit.kde.org/?p=kuser.git&a=commitdiff&h=0bf52af&o=plain")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`
            '0b76f4b4f160a841309132a217c6ca22b89855bd6170e56c761e39f92f5afac5')

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    
    # Fix build with cmake 3.4
    patch -p1 -i ../kuser-cmake-3.4.patch
    
    # Set KUser default starting user to 1000
    sed -i "s|ID 500 CACHE|ID 1000 CACHE|" CMakeLists.txt
    cd ${srcdir}
    mkdir build
    cd build
    cmake ../${_pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' 
    make
}

package() {
    cd ${srcdir}/build
    make DESTDIR=${pkgdir} install
}
