# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# Contributor: Manuel Tortosa <manutortosa@chakra-project.org>

pkgname="system-config-printer"
pkgver=1.5.5
pkgrel=1
pkgdesc="A CUPS printer configuration tool and status applet"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('x86_64')
license=('GPL')
depends=('pycups' 'python3-dbus' 'pysmbc' 'libusb-compat' 'python3-pycurl' 'python3-requests')
makedepends=('intltool' 'python3' 'xmlto' 'docbook-xsl' 'desktop-file-utils' 'libcups' 'libxml2')
optdepends=('kdeadmin-print-manager: for the administration tool in KDE System Settings')
provides=('system-config-printer-common')
source=(http://cyberelk.net/tim/data/$pkgname/1.5/$pkgname-$pkgver.tar.xz{,.sig})
md5sums=('1aaee6a90449ee0c6b90da2eb5df03a9'
         'SKIP')


build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udevdir=/usr/lib/udev --with-udev-rules

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # Compile *.pyc
  find "${pkgdir}" -name '*.py' -exec python3 -mpy_compile {} +
  # Compile *.pyo
  find "${pkgdir}" -name '*.py' -exec python3 -O -mpy_compile {} +
  
  # new cups 2.0.0 service naming
  sed -i "s|cups.socket|org.cups.cupsd.socket|g" ${pkgdir}/usr/lib/systemd/system/configure-printer@.service
}
