#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

#
# package info
#
pkgbase="kdebase-runtime"
pkgname=('kdebase-runtime'
         'kdebase-runtime-doc')
        
arch=('i686' 'x86_64')
pkgver=${_kdever}
pkgrel=2

pkgdesc="split package"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')

options=('docs' '!splithdr' 'splitdbg' 'log')

makedepends=('pkgconfig' 'cmake' 'automoc4' "kdepimlibs>=${_kdever}" 'openslp'
             'xine-lib' 'libpulse' 'libssh' 'ntrack' 'libcanberra' 'libcanberra-pulse' 'samba')

groups=('kde' 'kde-complete')

noextract=(branding.svgz)

source=($_mirror/${pkgbase}-$_kdever.tar.bz2
	branding.svgz
	03_disable_logout_sound.patch
	04_disable_nepomuk_popups.patch
	05_add_chakra_to_drkonqui.patch)

md5sums=(`grep ${pkgbase}-$_kdever.tar.bz2 ../kde-sc.md5 | cut -d" " -f1`
        '68cd8eb57db22c2f2ed554b721253bc2'  #  branding.svgz
        'a434bf282ee391b2f857184be59a3b6f'  #  03_disable_logout_sound.patch
        'a3c844d7bbd9a31f39b1b877ae521340'  #  04_disable_nepomuk_popups.patch
        '63082e4f07e5840ae7ff3b42c015d72b') #  05_add_chakra_to_drkonqui.patch 


#
# build function
#
build()
{
	cd ${pkgbase}-${pkgver}

	msg "applying main patchset ..."
	patch -Np1 -i ${srcdir}/03_disable_logout_sound.patch || return 1
	patch -Np1 -i ${srcdir}/04_disable_nepomuk_popups.patch || return 1
	patch -Np1 -i ${srcdir}/05_add_chakra_to_drkonqui.patch || return 1

	# copy our branding stuff
	cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgbase}-${pkgver}/desktoptheme/oxygen/widgets/
	cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgbase}-${pkgver}/desktoptheme/air/widgets/

	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DWITH_PulseAudio=ON \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make || return 1
}



#
# split-install functions
#
package_kdebase-runtime()
{
	pkgdesc="KDE Base Runtime"
	depends=("kdelibs>=${_kdever}" 'oxygen-icons' 'xorg-xauth' 'hicolor-icon-theme'
		 'smbclient' 'rarian' 'libssh' 'libpulse' 'libssh' 'ntrack' 'libcanberra' 'libcanberra-pulse')
	optdepends=('htdig: to build the search index' 'gdb: drkonq crash handler')
	groups=("kde" "kde-minimal" "kde-uninstall")
	provides=('kde-runtime')
	install=${pkgbase}.install

	cd ${srcdir}/${pkgbase}-${pkgver}/
	make DESTDIR=${pkgdir} install || return 1

	# also in kdebase-runtime-doc
	rm -rf ${pkgdir}/usr/share/man
	rm -rf ${pkgdir}/usr/share/doc

	# put kdesu into the path
	cp ${pkgdir}/usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/kdesu

	# use transparent panel svg's everywhere
	for n in default oxygen; do
		cp -f ${pkgdir}/usr/share/apps/desktoptheme/$n/widgets/panel-background.svgz ${pkgdir}/usr/share/apps/desktoptheme/$n/opaque/widgets/panel-background.svgz
	done

	# include our patches into the package
	ls -1 ${startdir}/*.patch &>/dev/null 2>&1
	if [ "$?" = "0" ]; then
		warning "incuding patches into package"
		mkdir -p ${pkgdir}/usr/share/chakra/patches/${pkgbase} &>/dev/null
		for i in ${startdir}/*.patch; do
			msg "$i"
			cp $i ${pkgdir}/usr/share/chakra/patches/${pkgbase}/ &>/dev/null
		done
	else
		warning "no patches found, skipping to include them into the package..."
	fi
	
	# WORKAROUND, conflict with hicolor-icon-theme
	rm -rf ${pkgdir}/usr/share/icons/hicolor/index.theme
	rm -rf ${pkgdir}/usr/share/icons/default.kde4
}

package_kdebase-runtime-doc()
{
	pkgdesc="KDE Base Runtime - Documentation"
	groups=("kde" "kde-uninstall" "kde-doc")

	splitdirs="doc"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

