#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: Julien Humbert <julroy67 at gmail dot com>
# Contributor: Angel 'angvp' Velasquez <angvp at archlinux dot com.ve>

pkgbase=python-sqlalchemy
pkgname=('python3-sqlalchemy' 'python2-sqlalchemy')
pkgver=0.9.1
pkgrel=1
pkgdesc="Python SQL toolkit and Object Relational Mapper"
arch=('x86_64')
url="http://www.sqlalchemy.org/"
license=('custom: MIT')
makedepends=('python3' 'python2' 'python3-setuptools' 'python2-setuptools' 'python3-nose' 'python2-nose')
source=("https://pypi.python.org/packages/source/S/SQLAlchemy/SQLAlchemy-$pkgver.tar.gz")
md5sums=('4d59a03e56ee37fc7b53ff1ef5f4310d')

build() {
  cp -a SQLAlchemy-$pkgver SQLAlchemy2-$pkgver
  cd SQLAlchemy-$pkgver
  python3 setup.py build
  cd ../SQLAlchemy2-$pkgver
  python2 setup.py build
}

check() {
  cd SQLAlchemy-${pkgver}
  python3 setup.py check
  cd ../SQLAlchemy2-$pkgver
  python2 setup.py check

}

package_python3-sqlalchemy() {
  pkgdesc='Python SQL toolkit and Object Relational Mapper'
  depends=('python3')
  optdepends=('python3-psycopg2: connect to PostgreSQL database')

  cd SQLAlchemy-${pkgver}
  python3 setup.py install --root="${pkgdir}"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-sqlalchemy() {
  pkgdesc='Python 2 SQL toolkit and Object Relational Mapper'
  depends=('python2')
  optdepends=('python2-psycopg2: connect to PostgreSQL database')
  provides=('python-sqlalchemy')
  replaces=('python-sqlalchemy')
  conflicts=('python-sqlalchemy')

  cd SQLAlchemy2-$pkgver
  python2 setup.py install --root="$pkgdir"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}