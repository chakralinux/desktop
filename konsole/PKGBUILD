#Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/konsole
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=konsole
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE's terminal emulator"
arch=('x86_64')
url='http://kde.org/applications/system/konsole/'
license=('LGPL')
depends=('kdelibs4support' 'knotifyconfig' 'kpty')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde')
conflicts=('kde-baseapps-konsole')
provides=('kde-baseapps-konsole')
replaces=('kde-baseapps-konsole')
source=("$_mirror/${pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../konsole-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
