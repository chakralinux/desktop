#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neo Kolokotronis <tetris4@gmail.com>
# Contributor: (epsilom) Xavier Corredor <xavier.corredor.llano (a) gmail.com>
# Contributor: Gonzalo Seguel <gonzaloseguel@gmail.com>

pkgname=kfilebox-gtkfree
_pkgname=kfilebox
pkgver=0.4.8
pkgrel=1
pkgdesc="KDE4 Dropbox client (with own daemon)"
arch=('i686' 'x86_64')
url="http://kdropbox.deuteros.es"
license=('GPL')
depends=('kde-workspace' 'gettext')
provides=('dropbox')
replaces=('kdropbox')
categories=('network' 'backup' 'cloud')
source=(http://downloads.sourceforge.net/project/kdropbox/kfilebox-${pkgver}/Source/${_pkgname}-${pkgver}.tar.gz)
install=$_pkgname.install
md5sums=('348921712d37ec262eff44065c968d11')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    qmake
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make INSTALL_ROOT=${pkgdir} install
    #install locales
    for language in `ls ${srcdir}/${_pkgname}-${pkgver}/locale`
    do
      if [ -e ${srcdir}/${_pkgname}-${pkgver}/locale/$language/kfilebox.mo ]; then
	install -m 644 -p -D ${srcdir}/${_pkgname}-${pkgver}/locale/$language/kfilebox.mo ${pkgdir}/usr/share/locale/$language/LC_MESSAGES/kfilebox.mo
      fi
    done
}
