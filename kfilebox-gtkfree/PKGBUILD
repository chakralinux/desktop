#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neo Kolokotronis <tetris4@gmail.com>
# Contributor: (epsilom) Xavier Corredor <xavier.corredor.llano (a) gmail.com>
# Contributor: Gonzalo Seguel <gonzaloseguel@gmail.com>

pkgname=kfilebox-gtkfree
_pkgname=kfilebox
pkgver=0.4.9
pkgrel=1
pkgdesc="KDE4 Dropbox client (with own daemon)"
arch=('i686' 'x86_64')
url="http://kdropbox.deuteros.es"
license=('GPL')
depends=('kde-workspace' 'gettext')
provides=('dropbox')
replaces=('kdropbox')
categories=('network' 'backup' 'cloud')
# getting the source from gentoo since upstream was messed up
source=(http://dev.gentoo.org/~tampakrap/tarballs/kfilebox_0.4.9.tar.gz)
#source=(http://downloads.sourceforge.net/project/kdropbox/kfilebox-${pkgver}/Source/${_pkgname}_${pkgver}.tar.gz)
install=$_pkgname.install
md5sums=('fdec6cd4c37a4c9d06325f89d3ed2ee1')

build() {
    cd "${srcdir}/${_pkgname}_${pkgver}"
    qmake
    make
}

package() {
    cd "${srcdir}/${_pkgname}_${pkgver}"
    make INSTALL_ROOT=${pkgdir} install
    #install locales
    for language in `ls ${srcdir}/${_pkgname}_${pkgver}/locale`
    do
      if [ -e ${srcdir}/${_pkgname}_${pkgver}/locale/$language/kfilebox.mo ]; then
	install -m 644 -p -D ${srcdir}/${_pkgname}_${pkgver}/locale/$language/kfilebox.mo ${pkgdir}/usr/share/locale/$language/LC_MESSAGES/kfilebox.mo
      fi
    done
}
