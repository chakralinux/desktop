#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config

pkgname=cherokee
pkgver=1.2.103
_gitver=d021376
_pkgver=${pkgver}.${_gitver}
pkgrel=1
pkgdesc="A very fast, flexible and easy to configure Web Server"
arch=('x86_64')
url="http://www.cherokee-project.com/"
license=('GPL2')
depends=('openssl' 'pcre')
makedepends=('python2' 'gettext' 'libldap' 'pam' 'libmysqlclient'
             'ffmpeg' 'geoip')
optdepends=('python2: cherokee-admin (administrative web interface)'
            'libldap: ldap validator'
            'pam: pam validator'
            'libmysqlclient: mysql validator'
            'ffmpeg: Audio/Video streaming handler'
            'geoip: GeoIP rule module'
            'rrdtool: RRDtool based information collector')
categories=('network')
backup=('etc/cherokee/cherokee.conf'
        'etc/logrotate.d/cherokee')
source=("https://github.com/${pkgname}/webserver/archive/v${_pkgver}.zip"
        'cherokee.service'
        'cherokee.logrotate')
sha256sums=('5d8a5d99cb7382e2b851bbdc162bc46c192dc6b589ae34c34a6299ab419efe31'
            'a5ba46821799eaeea39c35f46ae51d58a8394f5e729889729f814562561d1edb'
            '20e26d633f8c1cd90eb21f41dd163b73a83846e405b1ce995e072c4efefc522e')

build() {
  cd "webserver-${_pkgver}"

  # Use subdirectory for logs
  sed -i -r 's|(%localstatedir%/log)|\1/cherokee|' cherokee.conf.sample.pre

  ./autogen.sh \
	      --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-static \
              --with-wwwroot=/srv/http \
              --with-wwwuser=http \
              --with-wwwgroup=http \
              --with-python=python2 \
              --enable-os-string="Chakra Linux"

  make
}

package() {
  cd "webserver-${_pkgver}"

  make DESTDIR="${pkgdir}" install

  # PAM configuration file for cherokee
  install -D -m644 pam.d_cherokee "${pkgdir}/etc/pam.d/${pkgname}"

  # Fix ownership of /var/lib/cherokee/graphs
  chown -R http:http "${pkgdir}/var/lib/${pkgname}/graphs"

  # Use Python 2
  sed -i 's/env python$/&2/' \
    "${pkgdir}/usr/share/cherokee/admin/"{server,upgrade_config}.py \
    "${pkgdir}/usr/bin/"{CTK-run,cherokee-{admin-launcher,tweak}}
  sed -i -r "s/['\"]python/&2/g" \
    "${pkgdir}/usr/share/cherokee/admin/wizards/django.py"

  # Compile Python scripts
  python2 -m compileall "${pkgdir}"
  python2 -O -m compileall "${pkgdir}"

  install -d -o http -g http "${pkgdir}/var/log/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgname}"
  
  # install service file
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

  # Cleanup
  rm -rf "${pkgdir}/srv"
}

# vim:set ts=2 sw=2 et:
