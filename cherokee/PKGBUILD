#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config

pkgname=cherokee
pkgver=1.2.101
pkgrel=4
pkgdesc="A very fast, flexible and easy to configure Web Server"
arch=('i686' 'x86_64')
url="http://www.cherokee-project.com/"
license=('GPL2')
depends=('openssl' 'pcre')
makedepends=('python2' 'gettext' 'libldap' 'pam' 'libmysqlclient'
             'ffmpeg' 'geoip')
optdepends=('python2: cherokee-admin (administrative web interface)'
            'libldap: ldap validator'
            'pam: pam validator'
            'libmysqlclient: mysql validator'
            'ffmpeg: Audio/Video streaming handler'
            'geoip: GeoIP rule module'
            'rrdtool: RRDtool based information collector')
categories=('network')
backup=('etc/cherokee/cherokee.conf'
        'etc/logrotate.d/cherokee')
options=('!libtool')
source=("http://www.cherokee-project.com/download/1.2/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'cherokee.rc'
        'cherokee.logrotate'
        'fix-ctk-path-handler-match.patch'
        'gentoo-cherokee-1.2.98-linux3.patch'
        'gentoo-cherokee-1.2.99-chakra-customized.patch')
md5sums=('ef47003355a2e368e4d9596cd070ef23'
         'aaf900097afba950f0a0a15d906a3648'
         '8d69341bd4002bffd69c6e82ff6c905f'
         '565326a51632bd803671b36b13d02f1d'
         'f8a102f6b4711a2af4ce17e7f59437ae'
         'e6800e28a2487698a04ad870412f42a9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix path matching bug in CTK apps (e.g. market)
  patch -Np1 -i "${srcdir}/fix-ctk-path-handler-match.patch"

  # Apply Gentoo patches
  patch -Np1 -i "${srcdir}/gentoo-cherokee-1.2.98-linux3.patch"
  patch -Np1 -i "${srcdir}/gentoo-cherokee-1.2.99-chakra-customized.patch"

  # Use subdirectory for logs
  sed -i -r 's|(%localstatedir%/log)|\1/cherokee|' cherokee.conf.sample.pre

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-wwwroot=/srv/http \
              --disable-static \
              --with-wwwuser=http \
              --with-wwwgroup=http \
              --enable-os-string="Chakra Linux"

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make -j1 DESTDIR="${pkgdir}" install

  # PAM configuration file for cherokee
  install -D -m644 pam.d_cherokee "${pkgdir}/etc/pam.d/${pkgname}"

  # Fix ownership of /var/lib/cherokee/graphs
  chown -R http:http "${pkgdir}/var/lib/${pkgname}/graphs"

  # Use Python 2
  sed -i 's/env python$/&2/' \
    "${pkgdir}/usr/share/cherokee/admin/"{server,upgrade_config}.py \
    "${pkgdir}/usr/bin/"{CTK-run,cherokee-{admin-launcher,tweak}}
  sed -i -r "s/['\"]python/&2/g" \
    "${pkgdir}/usr/share/cherokee/admin/wizards/django.py"

  # Compile Python scripts
  python2 -m compileall "${pkgdir}"
  python2 -O -m compileall "${pkgdir}"

  install -d -o http -g http "${pkgdir}/var/log/${pkgname}"
  install -D "${srcdir}/${pkgname}.rc" "${pkgdir}/etc/rc.d/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgname}"

  # Cleanup
  rm -rf "${pkgdir}/srv"
}

# vim:set ts=2 sw=2 et:
