# Arch Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hypothesis
pkgname=('python3-hypothesis' 'python2-hypothesis')
pkgver=3.48.0
pkgrel=1
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
license=('MPL')
url="https://hypothesis.readthedocs.org"
makedepends=('python3-setuptools' 'python2-setuptools'
             'python3-attrs'      'python2-attrs'
             'python3-coverage'   'python2-coverage'
             'python2-enum34')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/HypothesisWorks/hypothesis-python/archive/$pkgver.tar.gz")
sha512sums=('eb3377fae612840737f134c413a6835926580648f982cb6b8fb319571bd600348c621500bb0210c68c0aad4aa27e9cccf2e60af63cc08b2c15f04bf1c02152dc')

prepare() {
  cp -a hypothesis-python-$pkgver{,-py2}

  rm -r hypothesis-python-$pkgver/tests/py2
  rm -r hypothesis-python-$pkgver-py2/tests/py3

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/hypothesis-python-$pkgver
  python3 setup.py build

  cd "$srcdir"/hypothesis-python-$pkgver-py2
  python2 setup.py build
}

package_python3-hypothesis() {
  depends=('python3-attrs' 'python3-coverage')

  cd hypothesis-python-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-hypothesis() {
  depends=('python2-attrs' 'python2-coverage' 'python2-enum34')

  cd hypothesis-python-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
