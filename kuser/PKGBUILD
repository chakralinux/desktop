# Include global configuration
source ../kdeapps.conf

pkgname=kuser
pkgver=16.04.1
pkgrel=1
pkgdesc='Manage users and groups'
url='http://kde.org/applications/system/kuser/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeadmin')
depends=('kdelibs4support' 'kio' 'kldap')
makedepends=('cmake' 'extra-cmake-modules' 'kdoctools')
provides=(kdeadmin-kuser)
conflicts=(kdeadmin-kuser)
install=${pkgname}.install
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        'port_to_kf5.patch'
        'doc.patch')
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`
            'ca5eed9497dfef9b8a040b4aa115ab8ff8df7a6d1a283dc2b33bc6e0dd07d4af'
            '1608a9b7a8a075b7eedcb8ae4b3d12a7e1f11b8ba4c093ec671e348373bfb8d4')

prepare() {
  cd kuser-${pkgver}
  patch -p1 -i $srcdir/port_to_kf5.patch
  patch -Np1 -i $srcdir/doc.patch
  mkdir -p ../build
}

build() {
  cd build
  cmake_kf5 ../kuser-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}

