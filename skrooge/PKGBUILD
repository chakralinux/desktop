pkgname=skrooge
pkgver=2.4.0
pkgrel=1
pkgdesc="Allows you to manage your personal finances in KDE"
arch=('x86_64')
url="http://skrooge.org"
license=('GPL')
depends=('qt5-base' 'qt5-tools' 'qca-qt5' 'qt5-script' 'libofx>=0.9.10' 'grantlee-qt5' 
         'kross' 'krunner' 'knotifyconfig' 'sqlcipher' 'python3' 'kdelibs4support'
         'karchive' 'kconfig' 'kcoreaddons' 'ki18n' 'kitemviews' 'kwidgetsaddons'
         'kwindowsystem' 'kcompletion' 'kdoctools' 'kjobwidgets' 'kconfigwidgets'
         'kdesignerplugin' 'kiconthemes' 'kio' 'knewstuff' 'kparts' 'kwallet' 'kxmlgui')
makedepends=('cmake' 'extra-cmake-modules')
options=('!emptydirs')
categories=('office')
install=$pkgname.install
source=("http://download.kde.org/stable/skrooge/$pkgname-$pkgver.tar.xz")
sha256sums=('132d022337140f841f51420536c31dfe07c90fa3a38878279026825f5d2526fe')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=`kf5-config --prefix` \
          -DKDE_INSTALL_QTPLUGINDIR=`kf5-config --qt-plugins` \
          -DKDE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
