pkgname=skrooge
pkgver=2.13.0
pkgrel=2
pkgdesc="Allows you to manage your personal finances in KDE"
arch=('x86_64')
url="http://skrooge.org"
license=('GPL')
depends=('qt5-base' 'qca-qt5' 'qt5-script' 'libofx>=0.9.10' 'grantlee'
         'kross' 'krunner' 'knotifyconfig' 'sqlcipher'
         'karchive' 'kconfig' 'kcoreaddons' 'ki18n' 'kitemviews' 'kwidgetsaddons'
         'kwindowsystem' 'kcompletion' 'kdoctools' 'kjobwidgets' 'kconfigwidgets'
         'kdesignerplugin' 'kiconthemes' 'kio' 'knewstuff' 'kparts' 'kwallet' 'kxmlgui'
         'python3' 'hicolor-icon-theme')
makedepends=('cmake' 'extra-cmake-modules' 'qt5-tools' 'kdoctools')
options=('!emptydirs')
categories=('office')
install=$pkgname.install
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ed68bd9b4561c2049f3bbfc2abb6b835cbe05a46b1fbd1bd0c5547e8a60333b8'
            'SKIP')
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>

build() {
  cd $pkgname-$pkgver

  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDE_INSTALL_LIBDIR=/usr/lib \
          -DSKG_DESIGNER=OFF
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR=$pkgdir install
}
