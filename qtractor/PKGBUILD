pkgname=qtractor
pkgver=0.9.2
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="http://qtractor.sourceforge.net/"
screenshot=("http://kde-apps.org/CONTENT/content-pre1/56325-1.png")
license=('GPL')
depends=('qt5-base' 'qt5-x11extras' 'jack' 'libmad' 'libsamplerate' 'rubberband' 'liblo' 'suil'
	 'lilv' 'rubberband' 'shared-mime-info' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('ladspa' 'dssi' 'qt5-tools')
categories=('multimedia')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('1932e6d53e7083ee259bec1b9a89d72c809b6f36d6f94f24223c273edbbe79c8')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
	      --disable-lv2-ui-gtk2
  make
}

package () {
  cd $pkgname-$pkgver

  make DESTDIR=$pkgdir install
  install -d $pkgdir/usr/share/pixmaps
  cp $pkgdir/usr/share/icons/hicolor/scalable/apps/qtractor.svg \
  $pkgdir/usr/share/pixmaps/qtractor.svg

  # Install docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,README,README.VST,TODO}
}
