# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegames-granatier/
# Include global configuration

pkgname=granatier
pkgver=18.04.3
pkgrel=1
pkgdesc="Granatier is a clone of the classic Bomberman game, inspired by the Clanbomber clone"
arch=('x86_64')
url='http://kde.org/applications/games/granatier/'
license=('GPL' 'LGPL' 'FDL')
depends=('knotifyconfig' 'libkdegames')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
replaces=('kdegames-granatier')
conflicts=('kdegames-granatier')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegames')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('004ec05677cce6e2785870ec8e3e80d701bf616bf67766d3566c0f22d725ee44'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
