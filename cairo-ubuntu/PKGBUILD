#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=cairo-ubuntu
_pkgname=cairo
pkgver=1.10.2
_pkgver=$pkgver-2ubuntu2
pkgrel=1
pkgdesc="Cairo vector graphics library, with Ubuntu's LCD rendering patches."
arch=(i686 x86_64)
url="https://launchpad.net/ubuntu/natty/+source/cairo"
license=('LGPL' 'MPL')
depends=('fontconfig-ubuntu>=2.8.0' 'libpng>=1.4.0' 'libxft-ubuntu' 'libxrender' 'pixman>=0.16.6' 'xcb-util>=0.3.6')
provides=("$_pkgname=$pkgver")
conflicts=($_pkgname)
source=(http://cairographics.org/releases/$_pkgname-$pkgver.tar.gz
        http://archive.ubuntu.com/ubuntu/pool/main/c/$_pkgname/${_pkgname}_$_pkgver.debian.tar.gz
        cairo-respect-fontconfig.patch)
md5sums=('f101a9e88b783337b20b2e26dfd26d5f'
         'c28ce4e75bf13c2a10727b2af1385059'
         '79f7c141c49f3d65ab308cc706d50914')

build() {
  cd $srcdir/$_pkgname-$pkgver

  # Debian patches.
  for FILE in $(cat $srcdir/debian/patches/series) ; do    
    patch -Np1 -i $srcdir/debian/patches/$FILE
  done
  
  # Own patch.
  patch -Np1 -i $srcdir/cairo-respect-fontconfig.patch
  
  # Configure and build.
  ./configure \
    --disable-static \
    --enable-tee \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  make DESTDIR=$pkgdir install

  # Documentation.
  install -d $pkgdir/usr/share/doc/$_pkgname/
  install -m644 -t $pkgdir/usr/share/doc/$_pkgname/ NEWS README
}
