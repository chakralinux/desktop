pkgname=bogofilter
pkgver=1.2.4
pkgrel=2
pkgdesc="A fast Bayesian spam filtering tool"
arch=('x86_64')
license=(GPL3)
url="http://bogofilter.sourceforge.net"
depends=('db' 'perl' 'gsl')
backup=(etc/bogofilter/bogofilter.cf)
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}-current/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('d0a5eebb3274b23ceabe766a6443a1c5')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --sysconfdir=/etc/bogofilter  \
              --localstatedir=/var \
              --enable-transactions \
              --with-database=db

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  mv ${pkgdir}/etc/bogofilter/bogofilter.cf.example ${pkgdir}/etc/bogofilter/bogofilter.cf

  install -dm755 ${pkgdir}/usr/share/${pkgname}/contrib
  install -m644 contrib/* ${pkgdir}/usr/share/${pkgname}/contrib/
}
