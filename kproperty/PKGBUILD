# Contribution from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kproperty
pkgver=3.1.0
pkgrel=1
pkgdesc="A property editing framework with editor widget similar to what is known from Qt Designer"
arch=(x86_64)
url="http://www.kexi-project.org/"
license=(GPL2)
depends=(kwidgetsaddons kguiaddons kcoreaddons kconfig)
makedepends=(extra-cmake-modules python3 qt5-tools doxygen)
source=("https://download.kde.org/stable/$pkgname/src/$pkgname-$pkgver.tar.xz")
sha1sums=('ec21018a404161ad346218230eaf535649952478')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
