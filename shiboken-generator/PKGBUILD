#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=shiboken-generator
_pkgname=shiboken
_pyver=2.7
pkgver=1.0.2
pkgrel=1
pkgdesc="Support library for Python bindings."
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.pyside.org"
depends=("python>=$_pyver" 'qt>=4.7' 'openssl' 'generatorrunner>=0.6.8')
makedepends=('cmake' 'automoc4')
source=("http://www.pyside.org/files/$_pkgname-$pkgver.tar.bz2")
md5sums=('585aa365811575ec3b48d59ca007f6ae')

build(){
    cd $srcdir/shiboken-$pkgver
    mkdir -p build && cd build
    cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=RelWithDebInfo \
      ..
    make $_pkgname
}

package(){
    cd $srcdir/$_pkgname-$pkgver/build/generator
    make DESTDIR=$pkgdir install

    mkdir -m 755 -p $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -m 755 ../data/ShibokenConfig.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -m 755 ../data/ShibokenConfigVersion.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -m 755 ../data/ShibokenConfig-python$_pyver.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/

    mkdir -m 755 -p $pkgdir/usr/lib/pkgconfig/
    install -m 755 ../data/$_pkgname.pc $pkgdir/usr/lib/pkgconfig/
}
