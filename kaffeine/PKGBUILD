# Contributions from ArchLinux: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kaffeine

pkgname=kaffeine
pkgver=2.0.2
pkgrel=1
pkgdesc='Media player with support for digital television (DVB-C/S/S2/T and ATSC, decryption with CI/CAM)'
license=('GPL')
arch=('x86_64')
url="http://kaffeine.kde.org"
depends=('kio' 'hicolor-icon-theme' 'libxss' 'vlc')
makedepends=('extra-cmake-modules' 'kidletime')
categories=('multimedia')
install=kaffeine.install
source=("https://github.com/KDE/kaffeine/archive/$pkgver.tar.gz")
sha1sums=('284f64410d0ac39d1db39d4e282c299347b08d10')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
