pkgname=zint
pkgver=2.5.1
pkgrel=1
pkgdesc="A barcode encoding library supporting over 50 symbologies."
url="http://sourceforge.net/projects/zint/"
license=('GPLv3')
arch=('x86_64')
depends=('qt' 'zlib')
makedepends=('cmake')
install=${pkgname}.install
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.src.tar.gz"
        "zint.png")
sha512sums=('e58b4236e053ee1f304f7a8c0f647d59ecbed1c6b566685f3348bf09e172ceb3e4b43a445c7151915a2e0d70626be4961c45faa737228f22e505c90f591ec999'
            '6e9abd85465e41374a7ca35965a3a30447147c7f35cf0ffb7e310fad9c83564c79508612ac115fd467aab9bb9cf166c222a8c4c05f91fecfa42b6a3ced32e75a')
 
build() {
  cd "${pkgname}-${pkgver}"
 
  if [[ -e build ]]; then rm -rf build; fi
  mkdir build && cd build
 
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}
 
package() {
  cd "${pkgname}-${pkgver}/build"
 
  make DESTDIR="${pkgdir}" install
 
  # Install additional files
  cd ..
  install -Dm644 zint-qt.desktop "${pkgdir}/usr/share/applications/zint-qt.desktop"
  install -Dm644 zint.png "${pkgdir}/usr/share/pixmaps/zint.png"
}
