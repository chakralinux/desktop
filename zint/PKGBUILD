pkgname=zint
pkgver=2.6.3
pkgrel=1
pkgdesc="A barcode encoding library supporting over 50 symbologies."
url="http://sourceforge.net/projects/zint/"
license=('GPLv3')
arch=('x86_64')
depends=('qt5-base')
makedepends=('cmake' 'qt5-tools')
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.src.tar.gz"
        "zint.png")
sha512sums=('fb94204d7ef0b832f06cc5f4767b06fa4f026bee4a9ea67da2b69ec85b82412fe45ca9040a861a82083223cecc12337d37992f91d30d2dbc6de5ad2e6be2ff83'
            '6e9abd85465e41374a7ca35965a3a30447147c7f35cf0ffb7e310fad9c83564c79508612ac115fd467aab9bb9cf166c222a8c4c05f91fecfa42b6a3ced32e75a')
 
build() {
  cd ${pkgname}-${pkgver}.src
 
  if [[ -e build ]]; then rm -rf build; fi
  mkdir build && cd build
 
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}
 
package() {
  cd ${pkgname}-${pkgver}.src/build
 
  make DESTDIR="${pkgdir}" install
 
  # Install additional files
  cd ..
  install -Dm644 zint-qt.desktop "${pkgdir}/usr/share/applications/zint-qt.desktop"
  install -Dm644 zint.png "${pkgdir}/usr/share/pixmaps/zint.png"
}
