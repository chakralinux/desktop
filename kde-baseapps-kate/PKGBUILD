# Maintainer:   Manuel Tortosa <manutortosa@chakra-project@org>
# Contributors: H W Tovetjarn (totte) <totte@tott.es>
#               Andrea Scarpino <andrea@archlinux.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase='kate'
pkgname=('kde-baseapps-kate'
         'kde-baseapps-kwrite'
         'kde-baseapps-katepart')
pkgver=${_kdever}
pkgrel=2
arch=('x86_64')
license=('GPL'
         'LGPL'
         'FDL')
makedepends=("kdelibs>=${_kdever}"
             'cmake'
             'automoc4'
             'docbook-xsl'
             "kdebindings-python2>=${_kdever}"
             "kdebindings-python3>=${_kdever}")
options=('docs'
         '!header'
         'debug'
         'log')
source=("$_mirror/${pkgbase}-$_kdever.tar.xz"
        'pkgbuild-syntax-highlight.patch')
sha1sums=(`grep ${pkgbase}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`
          '80c324c9db81c07a02374e7a092d75e0bdd6b4ea')

prepare() {
    mkdir "${srcdir}/build"
    cd "${srcdir}/${pkgbase}-${pkgver}"
    patch -p1 -i "${srcdir}"/pkgbuild-syntax-highlight.patch
}

build() {
    cd "${srcdir}/build"
    cmake "../${pkgbase}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package_kde-baseapps-katepart() {
    pkgdesc='A fast and feature-rich text editor component'
    depends=('kdelibs')
    url='http://kate-editor.org/about-katepart/'
    install='kdebase-katepart.install'

    cd "${srcdir}/build/part"
    make DESTDIR="${pkgdir}" install
}

package_kde-baseapps-kwrite() {
    pkgdesc='KDE text editor'
    depends=('kde-runtime'
             'kde-baseapps-katepart')
    groups=('kde'
            'kde-baseapps'
            'kde-uninstall')
    url='http://www.kde.org/applications/utilities/kwrite/'
    install='kdebase-kwrite.install'

    cd "${srcdir}/build/kwrite"
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}/build/doc/kwrite"
    make DESTDIR="${pkgdir}" install
}

package_kde-baseapps-kate() {
    pkgdesc='KDE advanced text editor'
    depends=('kde-runtime'
             'kde-baseapps-katepart'
             'qjson')
    groups=('kde'
            'kde-baseapps'
            'kde-uninstall')
    url='http://www.kde.org/applications/utilities/kate/'
    install='kdesdk-kate.install'
    optdepends=('kdebase-konsole: Open a terminal in Kate'
                'kdebindings-python2: Python 2 bindings'
                'kdebindings-python3: Python 3 bindings')

    cd "${srcdir}/build/kate"
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}/build/doc/kate"
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}/build/addons"
    make DESTDIR="${pkgdir}" install
}
