pkgname=passenger
pkgver=5.0.15
pkgrel=1
pkgdesc="A fast and robust web server and application server for Ruby, Python and Node.js"
arch=('x86_64')
url="https://www.phusionpassenger.com/"
license=('GPL')
depends=('ruby' 'ruby-rack' 'curl')
makedepends=('apache')
install=passenger.install
options=('!emptydirs')
source=($pkgname-$pkgver.tar.gz::https://github.com/FooBarWidget/passenger/archive/release-$pkgver.tar.gz)
sha512sums=('77f718c651172c83bc038bac79f26e74099ef1c8484c421853f48bc7606feda0329f51aa084752c2fd19d7f9843c99f8794cf2a1388b6fb62f08f1ed16813a9a')

prepare() {
  cd $srcdir/passenger-release-$pkgver
  sed -i '1,1i#include <stdint.h>' \
    ext/common/Utils/MD5.cpp \
    ext/common/Logging.cpp \
    ext/common/Utils/StrIntUtils.cpp \
    ext/common/Utils.cpp \
    ext/common/ApplicationPool2/AppTypes.cpp \
    ext/boost/libs/thread/src/pthread/once.cpp \
    ext/boost/libs/thread/src/pthread/once_atomic.cpp \
    ext/boost/libs/thread/src/pthread/thread.cpp \
    ext/boost/atomic/atomic.hpp
}

build(){
  cd $srcdir/passenger-release-$pkgver

  ./bin/passenger-install-apache2-module -a
  rake nginx
}

package() {
  cd $srcdir/passenger-release-$pkgver

  install -dm755 "$pkgdir/usr/lib/passenger/"
  cp -R * $pkgdir/usr/lib/passenger/

  install -dm755 "$pkgdir/usr/lib/httpd/modules"
  ln -sf "/usr/lib/passenger/libout/apache2/mod_passenger.so" "$pkgdir/usr/lib/httpd/modules/mod_passenger.so"
}