pkgname=passenger
pkgver=5.0.11
pkgrel=1
pkgdesc="A fast and robust web server and application server for Ruby, Python and Node.js"
arch=('x86_64')
url="https://www.phusionpassenger.com/"
license=('GPL')
depends=('ruby' 'ruby-rack' 'curl')
makedepends=('apache')
install=passenger.install
options=('!emptydirs')
source=($pkgname-$pkgver.tar.gz::https://github.com/FooBarWidget/passenger/archive/release-$pkgver.tar.gz)
sha512sums=('4058a874ae4c60f109466c09791b9a6aa64cf1f89edbe4188fb39ddc51366a5d4c5c7144701f1410b9a39d941073e3ef5773d7febed1441b1e795516b8ee7948')

prepare() {
  cd $srcdir/passenger-release-$pkgver
  sed -i '1,1i#include <stdint.h>' \
    ext/common/Utils/MD5.cpp \
    ext/common/Logging.cpp \
    ext/common/Utils/StrIntUtils.cpp \
    ext/common/Utils.cpp \
    ext/common/ApplicationPool2/AppTypes.cpp \
    ext/boost/libs/thread/src/pthread/once.cpp \
    ext/boost/libs/thread/src/pthread/once_atomic.cpp \
    ext/boost/libs/thread/src/pthread/thread.cpp \
    ext/boost/atomic/atomic.hpp
}

build(){
  cd $srcdir/passenger-release-$pkgver

  ./bin/passenger-install-apache2-module -a
  rake nginx
}

package() {
  cd $srcdir/passenger-release-$pkgver

  install -dm755 "$pkgdir/usr/lib/passenger/"
  cp -R * $pkgdir/usr/lib/passenger/

  install -dm755 "$pkgdir/usr/lib/httpd/modules"
  ln -sf "/usr/lib/passenger/libout/apache2/mod_passenger.so" "$pkgdir/usr/lib/httpd/modules/mod_passenger.so"
}