#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <jmc@chakra-project.org>
#

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=blink
pkgver=20110511
pkgrel=1
pkgdesc="A state of the art, easy to use SIP client. You can use it with any SIP provider or its own fully-featured SIP service."
arch=('any')
url="http://icanblink.com/"
license=('GPL')
depends=('python26' 'python26-pyqt' 'python26-sipsimple')
makedepends=('darcs')

_darcstrunk="http://devel.ag-projects.com/repositories"
_darcsmod="blink-qt"

build() {
  msg "Starting Darcs pull..."
  cd "${srcdir}"
  if [ -d "${_darcsmod}/_darcs" ]; then
    (cd "${_darcsmod}" && darcs pull -a)
  else
    darcs get "${_darcstrunk}/${_darcsmod}" "${_darcsmod}"
  fi
  msg "Darcs pull done (or server timeout)."
}

package() {
  cd "${srcdir}/${_darcsmod}"
  python2.6 setup.py install --prefix=/usr --root="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
