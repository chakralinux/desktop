#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=redeclipse
pkgver=1.3
pkgrel=1
pkgdesc='A first-person shooter, built as a total conversion of Cube Engine 2'
arch=(i686 x86_64)
url=http://redeclipse.net
license=(CCPL-by-sa ZLIB)
depends=(enet libgl mesa sdl sdl_image sdl_mixer zlib hicolor-icon-theme)
makedepends=('mesa' 'gzip')
categories=('games')
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}_${pkgver}_nix_bsd.tar.bz2
        system-libs.diff)
md5sums=('ef11041eb9ef49c9a4b278ccfd544fe4'
         '06757915ad76d693b38852fb4f9d1a11')

build() {
  cd "$srcdir/$pkgname/src"

  # Patches.
  patch -uN Makefile $srcdir/system-libs.diff # System enet.

  # Compilation.
  make
}

package() {
  cd "$srcdir/$pkgname"
  make -C src/ DESTDIR="$pkgdir" prefix=/usr system-install

  # License.
  install -Dm644 doc/all-licenses.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
