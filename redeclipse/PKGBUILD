pkgname=redeclipse
pkgver=1.5.2
pkgrel=1
pkgdesc='A first-person shooter, built as a total conversion of Cube Engine 2'
arch=('x86_64')
url=http://redeclipse.net
license=("custom" "CC0" "CCPL:by" "CCPL:by-sa" "ZLIB" "MIT")
depends=('enet' 'glu' 'libgl' 'sdl' 'sdl_image' 'sdl_mixer' 'zlib' 'hicolor-icon-theme')
makedepends=('mesa' 'gzip' 'ed')
categories=('games')
source=("base-${pkgver}.tar.gz::https://github.com/red-eclipse/base/archive/v${pkgver}.tar.gz"
        "data-${pkgver}.tar.gz::https://github.com/red-eclipse/data/archive/v${pkgver}.tar.gz")
md5sums=('ebadab6ef5c828e2c728b283ba8f33af'
         '8e73af20821353e4d450a7113690226a')

prepare() {
    cd "${srcdir}/base-${pkgver}"
    rmdir data
    mv "${srcdir}/data-${pkgver}" data
}

build() {
    cd "${srcdir}/base-${pkgver}"
    make -C src/ client server
}

package() {
    cd "${srcdir}/base-${pkgver}"
    make -C src/ DESTDIR="${pkgdir}" prefix=/usr system-install
    install -Dm 0644 "${srcdir}/base-${pkgver}/doc/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    install -Dm 0644 "${srcdir}/base-${pkgver}/doc/all-licenses.txt" "${pkgdir}/usr/share/licenses/${pkgname}/all-licenses.txt"
}
