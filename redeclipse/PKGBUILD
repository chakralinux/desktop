#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=redeclipse
pkgver=1.2
pkgrel=2
pkgdesc='A first-person shooter, built as a total conversion of Cube Engine 2.'
arch=(i686 x86_64)
url=http://redeclipse.net
license=(CCPL-by-sa ZLIB)
depends=(enet libgl mesa sdl sdl_image sdl_mixer zlib hicolor-icon-theme)
makedepends=('mesa' 'gzip' 'imagemagick')
categories=('games')
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}_${pkgver}_linux_bsd.tar.bz2
	system-libs.diff)
md5sums=('e133361f79be01782bf0bbc4c5a01236'
         'f34900e6c8247754800d8fba0600dfb7')

build() {
  cd "$srcdir/$pkgname/src"

  # Patches.
  patch -uN Makefile $srcdir/system-libs.diff

  # Compilation.
  make
}

package() {
  cd "$srcdir/$pkgname"
  make -C src/ DESTDIR="$pkgdir" prefix=/usr system-install

  # License.
  install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 trademark.txt "$pkgdir/usr/share/licenses/$pkgname/TRADEMARK"
}
