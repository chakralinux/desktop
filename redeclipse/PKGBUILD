#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=redeclipse
pkgver=1.0
pkgrel=1
pkgdesc="A single-player and multi-player first-person ego-shooter, built as a total conversion of Cube Engine 2"
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('CCPL-by-sa' 'ZLIB')
depends=('sdl_image'  'libgl' 'sdl_mixer')
makedepends=('mesa')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver}_linux.tar.bz2")
md5sums=('8d8aa038c7c866bced43b110333b37d7')

build() {
  cd $srcdir/$pkgname/src
  make
}

package() {
  cd $srcdir/$pkgname
  
  install -d $pkgdir/usr/{share/$pkgname,}/bin
  sed -i "s|RE_DATA=.|RE_DATA=/usr/share/$pkgname|" $pkgname.sh
  install -m755 src/re{server,client} $pkgdir/usr/share/$pkgname/bin/
  cp -rf  data/ $pkgdir/usr/share/$pkgname

  cat <<EOF > $pkgdir/usr/bin/$pkgname
#!/bin/sh
cd /usr/share/$pkgname
exec bin/reclient -r "\$@"
EOF
  chmod 755 $pkgdir/usr/bin/$pkgname

  install -Dm644 src/redeclipse.ico $pkgdir/usr/share/pixmaps/$pkgname.png
  install -Dm644 license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
