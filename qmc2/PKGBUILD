pkgname=qmc2
pkgver=0.70
pkgrel=1
pkgdesc="Qt based UNIX MAME frontend supporting SDLMAME."
url="http://qmc2.batcom-it.net/"
license=("GPL")
arch=('x86_64')
depends=('qt5-webkit' 'qt5-multimedia' 'qt5-svg' 'sdl2' 'libxslt' 'libarchive')
makedepends=('rsync' 'mesa' 'qt5-tools')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        'qmamecat.desktop')
sha1sums=('811586a559e3fe1dbb62042d0cf4eb5e83e595c5'
          'b495666abfaca7f9d1241c14d010b6037f66f4d4')

build() {
  cd "$srcdir/$pkgname"

  make PREFIX=/usr DATADIR=/usr/share SYSCONFDIR=/etc QTDIR=/usr QMAKE=/usr/bin/qmake-qt5 LIBARCHIVE=1
}

package(){
  cd "$srcdir/$pkgname"
  
  make PREFIX=/usr DATADIR=/usr/share SYSCONFDIR=/etc QTDIR=/usr QMAKE=/usr/bin/qmake-qt5 LIBARCHIVE=1 DESTDIR=$pkgdir install

  install -d $pkgdir/usr/share/{applications,pixmaps}
  install -D -m644 "$srcdir/qmamecat.desktop" "$pkgdir/usr/share/applications"
  install -D -m644 "$srcdir/qmc2/data/img/mame.png" "$pkgdir/usr/share/pixmaps"
}
