# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/linphone

pkgname=linphone
pkgver=3.6.1
pkgrel=1
pkgdesc="A Voice-over-IP phone. CLI version only."
arch=('x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('alsa-lib' 'ffmpeg' 'libexosip2' 'speex' 'libv4l' 'v4l-utils' 'libpulse' 'libxv' 'mediastreamer' 'ortp' 
         'libupnp')
makedepends=('pkgconfig' 'perlxml' 'intltool')
optdepends=('pulseaudio')
options=('!libtool' '!emptydirs')
source=("http://download-mirror.savannah.gnu.org/releases/${pkgname}/${pkgver:0:3}.x/sources/${pkgname}-${pkgver}.tar.gz"
        'port-to-exosip-4.patch')
sha256sums=('05ba81223e9378c3bce8d33080213b9925af49bd9623cd9004eb3dd22ca9d2a0'
            '95e37746630d0c7564e17c89cceee2889a5a6a0e631aa451857936e3cff5bb00')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/port-to-exosip-4.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PKG_CONFIG=/usr/bin/pkg-config
  ./configure --prefix=/usr \
              --disable-static \
              --enable-ipv6 \
              --enable-video \
              --enable-gtk_ui=no \
              --enable-alsa \
              --enable-pulseaudio \
              --disable-artsc \
              --disable-strict \
              --libexecdir=/usr/lib/${pkgname} \
	      --enable-external-mediastreamer \
              --enable-external-ortp 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
