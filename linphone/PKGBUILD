## This is the linphone stack,
## normally along with linphone you should update the following packages:
## bcg729, belle-sip, bzrtp, mbedtls, mediastreamer, ortp, libantlr3c

pkgname=linphone
pkgver=3.11.1
pkgrel=2
pkgdesc="A Voice-over-IP phone - cli version"
arch=('x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('alsa-lib' 'ffmpeg' 'speex' 'libv4l' 'v4l-utils' 'libxml2'
	 'libpulse' 'libxv' 'libvpx' 'mediastreamer' 'ortp' 'bzrtp' 'bcg729'
	 'belle-sip' 'mesa-libgl' 'glew' 'gsm' 'sqlite3' 'readline' 'libupnp')
makedepends=('pkg-config' 'libtool' 'intltool' 'bcunit')
optdepends=('pulseaudio')
options=('!emptydirs')
source=("https://www.linphone.org/releases/sources/linphone/linphone-$pkgver.tar.gz")
sha256sums=('dc88ef3d311495b9db6041f2ed7b99f6b00a91339c1612e63b4071a4f0a0404e')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr --disable-static --disable-gtk-ui --enable-console-ui --enable-ipv6 --enable-video \
	--enable-alsa --enable-pulseaudio --disable-artsc --disable-strict \
	--libexecdir=/usr/lib/$pkgname \
	--enable-external-mediastreamer \
	--enable-external-ortp \
	--enable-zrtp
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  
  rm -rf ${pkgdir}/usr/share/{appdata,applications,doc,icons,pixmaps}
}

