## This is the linphone stack,
## normally along with linphone you should update the following packages:
## bcg729, belle-sip, bzrtp, mbedtls, mediastreamer, ortp, libantlr3c

pkgname=linphone
pkgver=3.10.2
pkgrel=1
pkgdesc="A Voice-over-IP phone - cli version"
arch=('x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('alsa-lib' 'ffmpeg' 'gtk2' 'speex' 'libv4l' 'libglade' 'v4l-utils'
	 'libpulse' 'libxv' 'mediastreamer' 'ortp' 'bzrtp' 'bcg729' 'libsoup'
	 'belle-sip' 'libnotify')
makedepends=('pkg-config' 'perl-xml-parser' 'intltool' 'sgmltools-lite' 'bcunit')
# 'perl-xml-parser' 'sgmltools-lite'
optdepends=('pulseaudio')
options=('!emptydirs')
source=("https://www.linphone.org/releases/sources/linphone/linphone-$pkgver.tar.gz")
sha256sums=('37cac1f4daa55fcd395b7b49925c01d1638b9d941c66d626ca36a9679332384c')

build() {
  cd $pkgname-$pkgver
  export PKG_CONFIG=/usr/bin/pkg-config
  unset SGML_CATALOG_FILES
  export CFLAGS="$CFLAGS -Dbctbx_list_remove_link=bctbx_list_unlink -Dbctbx_list_delete_link=bctbx_list_erase_link"
  ./configure --prefix=/usr --disable-static --disable-gtk-ui --enable-console-ui --enable-ipv6 --enable-video \
	--enable-alsa --enable-pulseaudio --disable-artsc --disable-strict \
	--libexecdir=/usr/lib/$pkgname \
	--enable-external-mediastreamer \
	--enable-external-ortp \
	--enable-zrtp
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
