# Include global configuration

pkgname=klickety
pkgver=18.08.0
pkgrel=1
pkgdesc="An adaptation of the Clickomania game"
url="http://kde.org/applications/games/klickety/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde kde-applications kdegames kde-uninstall)
depends=(libkdegames knotifyconfig hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegames-klickety)
replaces=(kdegames-klickety)

options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c023bbc9ce1fa55e66f3c0874b729df739112cf3fdf452444443836deb523dcb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
