#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neo Kolokotronis <tetris4@gmail.com>
# Contributor: csslayer <wengxt AT gmail com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=fcitx
pkgname=('fcitx' 'fcitx-qt' 'fcitx-gtk2' 'fcitx-gtk3')
pkgver=4.2.5
pkgrel=1
pkgdesc="A Flexible Input Method Framework"
arch=("i686" "x86_64")
license=('GPL')
url="http://code.google.com/p/fcitx"
makedepends=('intltool' 'cmake' 'pango' 'qt' 'icu' 'dbus-core' 'wget' 'gtk2' 'gtk3' 'opencc' 'lua' 'iso-codes' 'enchant')
source=("http://fcitx.googlecode.com/files/${pkgname}-${pkgver}.tar.xz")
md5sums=('4301375f3abfec374d3899ffdc9c7e28')

build() {
  cd $srcdir/

  msg "Creating make environment..."
  
  mkdir -p $srcdir/build

  msg "Starting make..."
  cd $srcdir/build
  cmake ../${pkgname}-${pkgver} -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
      -DENABLE_QT_IM_MODULE=On \
      -DENABLE_GTK2_IM_MODULE=On \
      -DENABLE_GTK3_IM_MODULE=On \
      -DENABLE_OPENCC=On \
      -DENABLE_LUA=On \
      -DENABLE_GIR=Off
  make
}

package_fcitx(){
    depends=('pango' 'dbus-core' 'opencc' 'lua' 'iso-codes' 'enchant' 'icu')
    conflicts=('fcitx-utf8-svn' 'fcitx-svn' 'fcitx-hg' 'fcitx-keyboard')
    replaces=('fcitx-keyboard')
    cd $srcdir/build
    make DESTDIR=$pkgdir install
    rm -rf $pkgdir/usr/lib/qt/
    rm -rf $pkgdir/usr/lib/gtk-2.0/
    rm -rf $pkgdir/usr/lib/gtk-3.0/
}

package_fcitx-qt()
{
    pkgdesc='QT IM Module for fcitx'
    depends=('qt' 'icu' 'fcitx')
    cd $srcdir/build/src/frontend/qt
    make DESTDIR=$pkgdir install
}

package_fcitx-gtk2()
{
    pkgdesc='GTK2 IM Module for fcitx'
    depends=('gtk2' 'fcitx')
    install=fcitx-gtk2.install
    cd $srcdir/build/src/frontend/gtk2
    make DESTDIR=$pkgdir install
}

package_fcitx-gtk3()
{
    pkgdesc='GTK3 IM Module for fcitx'
    depends=('gtk3' 'fcitx')
    install=fcitx-gtk3.install
    cd $srcdir/build/src/frontend/gtk3
    make DESTDIR=$pkgdir install
}
