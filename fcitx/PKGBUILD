pkgbase=fcitx
pkgname=('fcitx' 'fcitx-pinyin' 'fcitx-table')
pkgver=4.2.9.5
pkgrel=1
arch=('x86_64')
license=('GPL')
url="https://fcitx-im.org/wiki/Fcitx"
#giturl="https://github.com/fcitx/"
makedepends=('cmake' 'extra-cmake-modules' 'qt' 'wget' 'opencc' 'lua' 'iso-codes' 'enchant'
             'libxkbfile' 'libxkbcommon' 'cairo' 'pango' 'mesa' 'python2')
source=("https://download.fcitx-im.org/${pkgbase}/${pkgbase}-${pkgver}.tar.xz"{,.sig})
categories=('localization')
sha512sums=('154e0cf7c96e130bc0826217635bbda68c6b8f533b8db64d6e7c5615aab5f62bdba47128acaeb1eabfd7f7eca85468831ca74b7b9387c54f2c2acc23463f3a1f'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9')

build() {
  msg "Creating make environment..."
  
  mkdir -p build

  msg "Starting make..."
  cd build
  cmake ../${pkgbase}-${pkgver} -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
      -DSYSCONFDIR=/etc \
      -DENABLE_QT_IM_MODULE=On \
      -DENABLE_GTK2_IM_MODULE=Off \
      -DENABLE_GTK3_IM_MODULE=Off \
      -DENABLE_OPENCC=On \
      -DENABLE_LUA=On \
      -DENABLE_GIR=Off
  make
}

package_fcitx(){
    pkgdesc="A Flexible Input Method Framework"
    depends=('pango' 'dbus' 'libxinerama' 'opencc' 'lua' 'iso-codes' 'libxkbfile' 'libxkbcommon' 'shared-mime-info' 'gettext')
    optdepends=('qt: configure ui wrapper')
    install=fcitx.install
    cd build
    make DESTDIR=$pkgdir install

    # remove table
    rm ${pkgdir}/usr/lib/fcitx/fcitx-table.so
    rm -r ${pkgdir}/usr/share/fcitx/table/
    rm ${pkgdir}/usr/share/fcitx/*/fcitx-table.*
    rm ${pkgdir}/usr/share/fcitx/configdesc/table.desc

    # remove pinyin
    rm ${pkgdir}/usr/lib/fcitx/fcitx-pinyin.so
    rm -r ${pkgdir}/usr/share/fcitx/pinyin/
    rm ${pkgdir}/usr/share/fcitx/*/fcitx-pinyin.*
    rm -r ${pkgdir}/usr/include/fcitx/module/pinyin/
    rm ${pkgdir}/usr/share/fcitx/inputmethod/{pinyin,shuangpin}.conf

    # remove autostart
    rm -r ${pkgdir}/etc/xdg
}

package_fcitx-pinyin() {
    pkgdesc="Pinyin Support for Fcitx"
    depends=('fcitx')
    pushd .
    cd build/src/im/pinyin
    make DESTDIR=$pkgdir install
    popd
}

package_fcitx-table() {
    pkgdesc="Table Support for Fcitx"
    depends=('fcitx')
    optdepends=('fcitx-pinyin: for support of pinyin reverse loopup')

    pushd .
    cd build/src/im/table
    make DESTDIR=$pkgdir install
    popd
}
