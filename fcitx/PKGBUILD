#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neo Kolokotronis <tetris4@gmail.com>
# Contributor: csslayer <wengxt AT gmail com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=fcitx
pkgname=('fcitx' 'fcitx-pinyin' 'fcitx-table')
pkgver=4.2.8
pkgrel=1
arch=("i686" "x86_64")
license=('GPL')
url="http://code.google.com/p/fcitx"
replaces=('fcitx-qt' 'fcitx-keyboard')
conflicts=('fcitx-utf8-svn' 'fcitx-svn' 'fcitx-hg' 'fcitx-keyboard' 'fcitx-qt')
makedepends=('cmake' 'qt' 'icu' 'wget' 'opencc' 'lua' 'iso-codes' 'enchant')
source=("http://download.fcitx-im.org/${pkgname}/${pkgname}-${pkgver}.tar.xz")
categories=('localization')

build() {
  msg "Creating make environment..."
  
  mkdir -p build

  msg "Starting make..."
  cd build
  cmake ../${pkgname}-${pkgver} -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
      -DENABLE_QT_IM_MODULE=On \
      -DENABLE_GTK2_IM_MODULE=Off \
      -DENABLE_GTK3_IM_MODULE=Off \
      -DENABLE_OPENCC=On \
      -DENABLE_LUA=On \
      -DENABLE_GIR=Off
  make
}

package_fcitx(){
    pkgdesc="A Flexible Input Method Framework"
    depends=('pango' 'dbus-core' 'opencc' 'lua' 'iso-codes' 'icu' 'libxkbfile')
    install=fcitx.install
    cd build
    make DESTDIR=$pkgdir install

    # remove table
    rm ${pkgdir}/usr/lib/fcitx/fcitx-table.so
    rm -r ${pkgdir}/usr/share/fcitx/table/
    rm ${pkgdir}/usr/share/fcitx/*/fcitx-table.*
    rm ${pkgdir}/usr/share/fcitx/configdesc/table.desc

    # remove pinyin
    rm ${pkgdir}/usr/lib/fcitx/fcitx-pinyin.so
    rm -r ${pkgdir}/usr/share/fcitx/pinyin/
    rm ${pkgdir}/usr/share/fcitx/*/fcitx-pinyin.*
    rm -r ${pkgdir}/usr/include/fcitx/module/pinyin/ -r
    rm ${pkgdir}/usr/share/fcitx/inputmethod/{pinyin,shuangpin}.conf
}

package_fcitx-pinyin() {
    pkgdesc="Pinyin Support for Fcitx"
    depends=('fcitx')
    pushd .
    cd build/src/im/pinyin
    make DESTDIR=$pkgdir install
    popd
}

package_fcitx-table() {
    pkgdesc="Table Support for Fcitx"
    depends=('fcitx' 'fcitx-pinyin')

    pushd .
    cd build/src/im/table
    make DESTDIR=$pkgdir install
    popd
}

md5sums=('8b8649824d35309a56354f2427d79fd9')
