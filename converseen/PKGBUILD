#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
 
pkgname=converseen
pkgver=0.4.7
pkgrel=2
pkgdesc="The batch image converter and resizer. It supports more than 100 image formats."
arch=('i686' 'x86_64')
url="http://converseen.sourceforge.net/"
license=('GPL2')

# Converseen *needs* the SVG plugin from ImageMagick, so librsvg is a hard dependency
depends=('graphicsmagick' 'imagemagick' 'qt' 'openexr' 'librsvg')

source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('d21cb571b268ddf4bcdc395d01c0dc49')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Build
  qmake
  make
}

package() {
  basedir="${srcdir}/${pkgname}-${pkgver}"

  # Binary
  cd "${basedir}"
  install -Dm755 converseen "${pkgdir}/usr/bin/converseen"

  # Translations
  cd "${basedir}/loc"
  for file in $(find . -name '*.qm' -print); do
    install -Dm644 "${file}" "${pkgdir}/usr/share/converseen/${file}"
  done

  # Desktop icon
  cd "${basedir}/res"
  install -Dm644 converseen.png     "${pkgdir}/usr/share/pixmaps/converseen.png"
  install -Dm644 converseen.desktop "${pkgdir}/usr/share/applications/converseen.desktop"

  # KDE4 Servicemenus
  install -Dm644 converseen_import.desktop "${pkgdir}/usr/share/kde4/services/ServiceMenus/converseen_import.desktop"
}

# vim:set ts=2 sw=2 et:
