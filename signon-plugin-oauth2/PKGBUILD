# Contribution from Arch: https://www.archlinux.org/packages/extra/x86_64/signon-plugin-oauth2/

pkgname=signon-plugin-oauth2
_pkgname=signon-oauth2
pkgver=0.24
pkgrel=1
pkgdesc='OAuth 2 plugin for signon'
arch=('x86_64')
url="https://gitlab.com/accounts-sso/signon-plugin-oauth2"
license=('LGPL')
depends=('signon')
makedepends=('qt5-xmlpatterns')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/accounts-sso/signon-plugin-oauth2/repository/archive.tar.gz?ref=VERSION_$pkgver")
md5sums=('684564e23cb070c31cef7785ab142ad8')

prepare() {
  cd $pkgname-VERSION_$pkgver-*

  sed -i '/-Werror/d' common-project-config.pri

  # Do not install tests and example
  echo 'INSTALLS =' >>tests/tests.pro
  echo 'INSTALLS =' >>example/example.pro
}

build() {
  cd $pkgname-VERSION_$pkgver-*
  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd $pkgname-VERSION_$pkgver-*
  make INSTALL_ROOT="$pkgdir" install
}
