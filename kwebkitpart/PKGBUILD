# Maintainer: Jeff Huang <s8321414[at]gmail[dot]com>

pkgname=kwebkitpart
pkgver=1.3.4
pkgrel=1
pkgdesc="A WebKit browser component for KDE"
url="https://projects.kde.org/projects/extragear/base/kwebkitpart/"
arch=('x86_64')
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
conflicts=('kwebkitpart-svn')
install=${pkgname}.install
source=("ftp://ftp.archlinux.org/other/packages/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'sanitize-input.patch')
md5sums=('0a3e176a7c24f00e22c92a7b105585f8'
         'f9ae9520069ffac4c5da1f408fd4bbdf')

prepare() {
  cd ${srcdir}
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/sanitize-input.patch

  sed -i '/add_subdirectory(kdelauncher)/d' CMakeLists.txt
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}