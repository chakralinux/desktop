pkgname=libguess
pkgver=1.1
pkgrel=3
pkgdesc='High-speed character set detection library'
url='http://atheme.org/projects/libguess.html'
license=('custom')
depends=('libmowgli')
arch=('x86_64')
source=("http://distfiles.atheme.org/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e3c9ad3102c9447c591131674135795fb012e073d92e887a2b05c9bbbcbe913f')

prepare() {
  cd ${pkgname}-${pkgver}

  sed 's/libmowgli >= 0.9.50/libmowgli-2/g' -i configure
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -D COPYING "${pkgdir}"/usr/share/licenses/libguess/COPYING
}
