#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ptlib
pkgver=2.10.1
_pkgver="Stable 1"
_pkgver2="v3.10 Luyten"
pkgrel=1
pkgdesc="Portable Windows Library."
arch=('i686' 'x86_64')
url="http://www.opalvoip.org/"
license=('MPL')
depends=('alsa-lib' 'expat' 'gcc-libs' 'v4l-utils' 'openssl' 'sdl')
changelog=ChangeLog
source=("http://downloads.sourceforge.net/project/opalvoip/$_pkgver2/$_pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('35dfe5e21dcf96ad1d4b87099819ff1a')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure \
      --disable-avc \
      --disable-odbc \
      --enable-dc \
      --enable-expat \
      --enable-httpforms \
      --enable-ipv6 \
      --enable-opal \
      --enable-oss \
      --enable-plugins \
      --enable-sdl \
      --enable-v4l \
      --enable-v4l2 \
      --libdir=/usr/lib \
      --localstatedir=/var \
      --prefix=/usr \
      --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
