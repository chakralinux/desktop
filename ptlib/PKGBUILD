pkgname=ptlib
pkgver=2.14.4
_pkgver="Stable ${pkgver:5}"
_pkgver2="v3.14 Lacaille"
pkgrel=3
pkgdesc="Portable Windows Library."
arch=('x86_64')
url="http://www.opalvoip.org/"
license=('MPL')
depends=('alsa-lib' 'expat' 'gcc-libs' 'openssl' 'sdl' 'v4l-utils' 'libsasl')
source=("http://sourceforge.net/projects/opalvoip/files/$_pkgver2/$_pkgver/$pkgname-$pkgver.tar.bz2")
sha512sums=('a41850b5b55dfc23f5deac21449ba22f3badf4cb85b647db56673b065ffee0d68acf4fc7eb902a085d9ed2d78386359522b60f1a02ddbb958e4f5a84972499dc')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure \
      --disable-avc \
      --disable-odbc \
      --enable-dc \
      --enable-expat \
      --enable-httpforms \
      --enable-ipv6 \
      --enable-opal \
      --enable-oss \
      --enable-plugins \
      --enable-sdl \
      --enable-v4l \
      --enable-v4l2 \
      --libdir=/usr/lib \
      --localstatedir=/var \
      --prefix=/usr \
      --sysconfdir=/etc
  make -j1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
