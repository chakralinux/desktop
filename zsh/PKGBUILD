#
# Chakra Packages for Chakra, part of chakra-project.org
#
# Maintainer: Fabian Kosmale <0inkane@googlemail.com>
# Contributor: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=zsh
pkgver=5.0.8
pkgrel=1
pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
arch=('x86_64')
url='http://www.zsh.org/'
license=('custom')
depends=('pcre' 'libcap' 'gdbm')
categories=('system')
install=zsh.install
source=("http://www.zsh.org/pub/${pkgname}-${pkgver}.tar.gz"
        "zprofile")
md5sums=('3f2cc0d864d23a3292e7766759291572'
         '24a9335edf77252a7b5f52e079f7aef7')

prepare() {
  cd $pkgname-$pkgver

  # Set correct keymap path
  sed -i 's#/usr/share/keymaps#/usr/share/kbd/keymaps#g' Completion/Unix/Command/_loadkeys

  # Fix usb.ids path
  sed -i 's#/usr/share/misc/usb.ids#/usr/share/hwdata/usb.ids#g' Completion/Linux/Command/_lsusb

  # Remove unneeded and conflicting completion scripts
  for _fpath in AIX BSD Cygwin Darwin Debian Mandriva openSUSE Redhat Solaris; do
    rm -rf Completion/$_fpath
    sed "s#\s*Completion/$_fpath/\*/\*##g" -i Src/Zle/complete.mdd
  done
  rm -f  Completion/Linux/Command/_{pkgtool,rpmbuild,yast}
  rm -f  Completion/Unix/Command/_{osc,systemd}
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --enable-etcdir=/etc/zsh \
    --enable-zshenv=/etc/zsh/zshenv \
    --enable-zlogin=/etc/zsh/zlogin \
    --enable-zlogout=/etc/zsh/zlogout \
    --enable-zprofile=/etc/profile \
    --enable-zshrc=/etc/zsh/zshrc \
    --enable-maildir-support \
    --with-term-lib='ncursesw' \
    --enable-multibyte \
    --enable-function-subdirs \
    --enable-fndir=/usr/share/zsh/functions \
    --enable-scriptdir=/usr/share/zsh/scripts \
    --with-tcsetpgrp \
    --enable-pcre \
    --enable-cap \
    --enable-zsh-secure-free
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  HOME="$srcdir" make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m644 "${srcdir}/zprofile" "$pkgdir/etc/zsh/zprofile"
  install -D -m644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
