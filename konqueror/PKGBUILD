# Include global configuration

pkgname=konqueror
pkgver=18.07.90
pkgrel=1
pkgdesc="KDE File Manager & Web Browser"
arch=(x86_64)
url="https://konqueror.org/"
license=(LGPL)
depends=(dolphin keditbookmarks qt5-webengine lcms2)
makedepends=(extra-cmake-modules kdoctools python3 tidyhtml kdesignerplugin kdesu)
optdepends=('tidyhtml: tidy HTML plugin' 'kdesu: shell command plugin')
conflicts=(kde-baseapps-konqueror kde-baseapps-konq-plugins dolphinpart4)
replaces=(kde-baseapps-konqueror kde-baseapps-konq-plugins dolphinpart4)
provides=(kde-baseapps-konqueror kde-baseapps-konq-plugins dolphinpart4)
options=(docs debug)
groups=(kde kde-uninstall kde-applications kde-baseapps)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('daaf978bea8f2d832f92d8f10734e48ebf10a8939587d0873312333f9fe07fa7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
