# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=uim
pkgver=1.8.6
pkgrel=3
pkgdesc='Multilingual input method library'
url='http://code.google.com/p/uim/'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('libxft' 'libedit' 'anthy' 'm17n-lib')
makedepends=('intltool' 'gettext' 'qt' 'kdelibs')
optdepends=('qt: immodule and helper applications'
            'gtk2: immodule and helper applications'
            'gtk3: immodules and helper applications')
source=("https://uim.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
sha1sums=('409c30b31b9e58e3c18cb7862933487b0585cd1d')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/uim \
		--with-anthy-utf8 \
		--with-qt4-immodule \
		--with-qt4 \
	        --enable-kde4-applet \
                --enable-notify=knotify4 \
                --enable-dict \
                --with-sqlite3 \
                --with-ffi 

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
