# Arch contribution https://www.archlinux.org/packages/community/x86_64/mbedtls/

pkgname=mbedtls
pkgver=1.3.11
pkgrel=1
pkgdesc="Portable cryptographic and SSL/TLS library, aka polarssl"
arch=('x86_64')
url="https://tls.mbed.org"
license=('Apache')
depends=('glibc')
options=('staticlibs')
source=("https://tls.mbed.org/download/mbedtls-$pkgver-gpl.tgz"
        "programs.makefile.patch")
sha512sums=('242c486becc34d3b7ebba7624686aea6ed7713033aeabbfae7227284df322f191ddd5b9ded1228f100cd50bcfafb12396d93c30420afd3feb979b52a1860551e'
          'e77310e9bdd8b152838eaaf3bfc2e9fe42ab00945772d6102577062e69cd7de0992753eebcb3ae720eae2b6a2b63a02310db165a950151aaba64bbc52fd78dcf')

build() {
  cd "$pkgname-$pkgver"
  sed -i 's|//\(#define POLARSSL_THREADING_C\)|\1|' include/polarssl/config.h
  sed -i 's|//\(#define POLARSSL_THREADING_PTHREAD\)|\1|' include/polarssl/config.h
  # enable cert_write
  patch -p1 -d programs -i "$srcdir/programs.makefile.patch"
  LDFLAGS+=" -I../include " make SHARED=1 no_test
}

check() {
  cd "$pkgname-$pkgver"
  make SHARED=1 check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/usr" install
}