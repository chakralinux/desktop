# Arch contribution https://www.archlinux.org/packages/community/x86_64/mbedtls/

pkgname=mbedtls
pkgver=2.4.2
pkgrel=1
pkgdesc="Portable cryptographic and SSL/TLS library, aka polarssl"
arch=('x86_64')
url="https://tls.mbed.org"
license=('Apache')
depends=('glibc')
options=('staticlibs')
source=("https://tls.mbed.org/download/mbedtls-$pkgver-gpl.tgz")
sha256sums=('d01f2d5586a52055329d194d909103f445bd2d0b6b2b5f1c830fbf828ac6299f')

prepare() {
  cd "$pkgname-$pkgver"
  # enable flags for non-embedded systems
  sed -i 's|//\(#define MBEDTLS_THREADING_C\)|\1|' include/mbedtls/config.h
  sed -i 's|//\(#define MBEDTLS_THREADING_PTHREAD\)|\1|' include/mbedtls/config.h
}
          
build() {
  cd "$pkgname-$pkgver"
  LDFLAGS+=" -I../include " make SHARED=1 no_test
}

check() {
  cd "$pkgname-$pkgver"
  make SHARED=1 check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/usr" install
}
