# Include global configuration

pkgname=kross-interpreters
pkgver=18.08.1
pkgrel=1
pkgdesc='Language interpreters to enable in-process scripting with Kross'
arch=('x86_64')
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kross kdelibs4support)
makedepends=(extra-cmake-modules kdoctools python2)
optdepends=('python2: kross-python plugin')
options=('debug')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdebindings')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('022e4c47b0c24c08729d056cc1df489cff629e632ca3b1da5a568d5236896aee'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  # wrong version detected, force searching a Python2.x version
  # source (http://www.cmake.org/Bug/view.php?id=13794)
  # from
  #find_package(PythonLibs 2 EXACT REQUIRED)
  # to
  #find_package(PythonLibs 2.7 REQUIRED)
  
  sed -i 's/EXACT//g' ${srcdir}/${pkgname}-${pkgver}/CMakeLists.txt
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
