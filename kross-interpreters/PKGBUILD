# Include global configuration
source ../kdeapps.conf

pkgname=kross-interpreters
pkgver=${_kdever}
pkgrel=1
pkgdesc='Language interpreters to enable in-process scripting with Kross'
arch=('x86_64')
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kross kdelibs4support)
makedepends=(extra-cmake-modules kdoctools python2)
optdepends=('python2: kross-python plugin')
options=('debug')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdebindings')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"{,.sig})
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
                        'SKIP')
validpgpkeys=(${Avalidpgpkeys[@]})

prepare() {
  mkdir -p build
  
  # wrong version detected, force searching a Python2.x version
  # source (http://www.cmake.org/Bug/view.php?id=13794)
  # from
  #find_package(PythonLibs 2 EXACT REQUIRED)
  # to
  #find_package(PythonLibs 2.7 REQUIRED)
  
  sed -i 's/EXACT//g' ${srcdir}/${pkgname}-${pkgver}/CMakeLists.txt
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
