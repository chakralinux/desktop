# Include global configuration

pkgname=messagelib
pkgver=18.08.1
pkgrel=1
pkgdesc="All libs for messages"
arch=('x86_64')
url='https://github.com/KDE/messagelib'
license=('LGPL')
depends=(kdepim-apps-libs kmailtransport kmbox libgravatar akonadi-notes kidentitymanagement)
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools')
groups=(kde kde-applications kdepim kde-uninstall)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ba67915f8930fa607b016c3a76f8f2f2ca27fdd6d0aca1e97d9f43db8061e89f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
