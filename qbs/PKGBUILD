pkgname=qbs
pkgver=1.12.2
pkgrel=1
pkgdesc='Cross platform build tool'
arch=('x86_64')
url='https://wiki.qt.io/Qbs'
license=('LGPL')
depends=('qt5-script')
makedepends=('git')
# The source tarball may delay after qtcreator release, so we use git tag instead
# Check qtcreator's built-in qbs version before compile qtcreator:
# $ cd qtcreator
# $ makepkg -oddC
# Note the required qbs version down
# Update qbs pkgver ->  update qbs package ->  update dependent qbs version of qtcreator's PKGBUILD -> update qtcreator
# source=("https://download.qt.io/official_releases/qbs/${pkgver}/qbs-src-${pkgver}.tar.gz")
source=("git+https://code.qt.io/qbs/qbs.git#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
  cd $pkgname
  qmake-qt5 QBS_INSTALL_PREFIX=/usr \
    QBS_LIBEXEC_INSTALL_DIR=/usr/lib/qbs \
    QBS_LIBEXEC_DESTDIR=../../../lib/qbs \
    QBS_RELATIVE_LIBEXEC_PATH=../lib/qbs \
    CONFIG+=qbs_enable_project_file_updates \
    qbs.pro
  make
}

package() {
  cd $pkgname
  make INSTALL_ROOT="${pkgdir}" install
}
