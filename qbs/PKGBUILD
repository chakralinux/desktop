pkgname=qbs
pkgver=1.12.0
pkgrel=1
pkgdesc='Cross platform build tool'
arch=('x86_64')
url='https://wiki.qt.io/Qbs'
license=('LGPL')
depends=('qt5-script')
source=("https://download.qt.io/official_releases/qbs/${pkgver}/qbs-src-${pkgver}.tar.gz"
        qbs-header.patch)
sha256sums=('5efeb2492f8ccf0e7a5ea106f748e1c536f964674025aecb22c1ee948e3e35d1'
            'f80fb8dd5401713e1d70012f2bbc26a1c544963753c0de3b25584b27d257d367')

prepare() {
  cd $pkgname-src-$pkgver
  patch -p1 -i ../qbs-header.patch # Fix missing header
}

build() {
  cd $pkgname-src-$pkgver
  qmake-qt5 QBS_INSTALL_PREFIX=/usr \
    QBS_LIBEXEC_INSTALL_DIR=/usr/lib/qbs \
    QBS_LIBEXEC_DESTDIR=../../../lib/qbs \
    QBS_RELATIVE_LIBEXEC_PATH=../lib/qbs \
    CONFIG+=qbs_enable_project_file_updates \
    qbs.pro
  make
}

package() {
  cd $pkgname-src-$pkgver
  make INSTALL_ROOT="${pkgdir}" install
}
