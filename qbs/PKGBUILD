pkgname=qbs
pkgver=1.9.0
pkgrel=1
pkgdesc='Cross platform build tool'
arch=('x86_64')
url='https://www.qt.io/'
license=('LGPL')
depends=('qt5-base' 'qt5-script')
makedepends=('gcc')
source=($pkgname-$pkgver.tar.gz::"https://github.com/qbs/qbs/archive/v$pkgver.tar.gz")
md5sums=('783d2b75d68cf499641b1829e9221825')

build() {
  cd qbs-$pkgver
  qmake-qt5 QBS_INSTALL_PREFIX=/usr \
    QBS_LIBEXEC_INSTALL_DIR=/usr/lib/qbs \
    QBS_RELATIVE_LIBEXEC_PATH=../lib/qbs \
    CONFIG+=qbs_enable_project_file_updates \
    qbs.pro
  make
}

package() {
  cd qbs-$pkgver
  make INSTALL_ROOT="${pkgdir}" install
}
