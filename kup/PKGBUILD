pkgname=kup
_pkgname=Kup
pkgver=0.7.3
pkgrel=1
pkgdesc="A KDE-based frontend for the bup backup software"
arch=('x86_64')
url="https://github.com/spersson/Kup"
license=('GPL')
depends=('qt5-base' 'plasma-framework' 'kcoreaddons' 'kdbusaddons' 'ki18n' 'kio' 'solid' 'kidletime' 
         'knotifications' 'kiconthemes' 'kconfig' 'kinit' 'bup' 'rsync' 'python2-pyxattr' 'git')
makedepends=('cmake' 'extra-cmake-modules')
source=("https://github.com/spersson/${_pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('47b97314dd11579a6ecc723de687c9ebe7cafb300d2ce39f2510d36c17de2f3b')
 
build() {
	cd ${_pkgname}-${pkgname}-${pkgver}
	[ -d "build" ] && rm -rf build
	mkdir build
	cd build
 
	cmake ../ \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}
 
package() {
	cd ${_pkgname}-${pkgname}-${pkgver}
	cd build
	make DESTDIR=${pkgdir} install
}
