# Maintainer: Jeff Huang <s8321414[at]gmail[dot]com>
# Contributer: Francesco Marinucci <framari [at] opmbx [dot] org>
 
pkgname=kup
_pkgname=Kup
pkgver=0.5.1
pkgrel=1
pkgdesc="A KDE-based frontend for the bup backup software"
arch=('x86_64')
url="http://kde-apps.org/content/show.php/Kup+Backup+System?content=147465"
license=('GPL')
depends=('kde-runtime' 'kdelibs' 'bup' 'rsync' 'python2-pyxattr')
makedepends=('cmake' 'automoc4')
source=("https://github.com/spersson/${_pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c5b9c8e0500967a2e0bc4e2853fab25c41aaab4e40c06db92d64d5bd0b3bcdb7')
 
build() {
	cd ${_pkgname}-${pkgname}-${pkgver}
	[ -d "build" ] && rm -rf build
	mkdir build
	cd build
 
	cmake ../ \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}
 
package() {
	cd ${_pkgname}-${pkgname}-${pkgver}
	cd build
	make DESTDIR=${pkgdir} install
}
