# Contributor : Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kdoctools
pkgver=${KFVersion}
pkgrel=1
pkgdesc='Various tools to work with documentation'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdoctools'
license=('LGPL')
depends=('karchive' 'docbook-xsl')
makedepends=('extra-cmake-modules')
checkdepends=('cmake')
groups=('kf5')
options=('staticlibs' 'debug')
source=("${KFServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('cdf7e52809ebf579b6e8c247d35f9313')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

