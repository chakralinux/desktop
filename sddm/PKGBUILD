#Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/sddm

pkgname=sddm
pkgver=0.12.0
pkgrel=6
pkgdesc='QML based X11 and Wayland display manager'
arch=('x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'python3-docutils' 'qt5-tools')
conflicts=('sddm-qt5-git' 'sddm-git')
provides=('sddm-qt5-git' 'sddm-git')
replaces=('sddm-qt5-git' 'sddm-git')
install="${pkgname}.install"
backup=('usr/share/sddm/scripts/Xsetup'
        'etc/sddm.conf'
        'etc/pam.d/sddm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        'sddm.conf'
        fix-segfault.patch::"https://github.com/davispuh/sddm/commit/671c11d0.patch"
        CVE-2015-0856.patch::"https://github.com/sddm/sddm/commit/4cfed6b0.patch")
md5sums=('e3261ac93a50c71c973cc79b85387765'
         '542926387bb8ac496c90684a6a3febc6'
         '208f3b1b49792984989172dd809a03bf'
         '80eb7eddd8d3d259a6c28e83b9394575')

prepare() {
  cd ${srcdir}

  # Fix segfault https://github.com/sddm/sddm/issues/489
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../fix-segfault.patch
  # Fix CVE-2015-0856
  patch -p1 -i ../CVE-2015-0856.patch
}
         
build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DDBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
        -DBUILD_MAN_PAGES=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  # set the default configuration, with heritage as theme
  install -m 644 -p ${srcdir}/sddm.conf ${pkgdir}/etc/
}
