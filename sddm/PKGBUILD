#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/sddm

pkgname=sddm
pkgver=0.11.0
pkgrel=4
pkgdesc='QML based X11 display manager'
arch=('x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'python3-docutils' 'qt5-tools')
conflicts=('sddm-qt5-git' 'sddm-git')
provides=('sddm-qt5-git' 'sddm-git')
replaces=('sddm-qt5-git' 'sddm-git')
install="${pkgname}.install"
backup=('usr/share/sddm/scripts/Xsetup'
        'etc/sddm.conf')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        'sddm.conf')
md5sums=('e110a7683867400dc9484d4744fd41dd'
         '7e4de1ad45e6a0e930b96fd051e48da4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DBUILD_MAN_PAGES=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  # set the default configuration, with breeze as theme
  install -m 644 -p ${srcdir}/sddm.conf ${pkgdir}/etc/
} 
