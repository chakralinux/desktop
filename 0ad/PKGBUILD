#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrian Chaves Fernandez (Gallaecio) <adriyetichaves@gmail.com>

pkgname=0ad
pkgver=0.0.11
_pkgver=$pkgver-alpha
pkgrel=1
pkgdesc="Cross-platform, 3D and historically-based real-time strategy game"
screenshot=http://www.wildfiregames.com/0ad/images/news_images/dynamic-borders.jpg
arch=('i686' 'x86_64')
url="http://wildfiregames.com/0ad"
license=('GPL2' 'custom:MIT') # source licenses.
depends=('boost-libs' 'curl' 'enet' 'gamin' 'libogg' 'libpng' 'libvorbis' 'libxcursor' 'libxml2' 'mesa' 'nvidia-texture-tools' 'openal' 'sdl' 'zip' 'zlib' 0ad-data=$pkgver)
makedepends=('boost' 'python2')
replaces=(0ad-debug) # If someone wants a debug version, they should rebuild 0ad with config=debug.
categories=('games')
changelog=ChangeLog
source=(http://downloads.sourceforge.net/project/zero-ad/releases/$pkgname-$_pkgver-unix-build.tar.xz)
md5sums=('0feedd076ea441ca821073b4b7014946')

build() {
  cd $srcdir/$pkgname-$_pkgver/build/workspaces

  # NOTE: These sed commands are *not* likely to be changed between releases, like most patches.
  sed \
    -e 's/unix_names = { "boost_filesystem-mt", "boost_system-mt" },/unix_names = { "boost_filesystem", "boost_system" },/g' \
    -e 's/unix_names = { "boost_signals-mt" },/unix_names = { "boost_signals" },/g' \
    -i "${srcdir}/${pkgbase}-${_pkgver}/build/premake/extern_libs4.lua"
  sed -i 's/export HOSTTYPE="$HOSTTYPE"/export HOSTTYPE="$CARCH"/g' ./update-workspaces.sh

  # Atlas disabled because it requires GTK.
  ./update-workspaces.sh \
    --disable-atlas \
    --with-system-enet \
    --with-system-nvtt \
    --bindir=/usr/bin \
    --libdir=/usr/lib \
    --datadir=/usr/share/${pkgname}/data


  cd gcc
  make config=release
}

package() {

  cd $srcdir/$pkgname-$_pkgver

  # Manual installation.
  install -d ${pkgdir}/usr/{bin,lib}
  install -Dm755 ${srcdir}/${pkgbase}-${_pkgver}/binaries/system/pyrogenesis ${pkgdir}/usr/bin
  install -Dm755 ${srcdir}/${pkgbase}-${_pkgver}/binaries/system/*.so{,.1.0} ${pkgdir}/usr/lib

  # Execution script.
  install -d $pkgdir/usr/bin/
  local scriptname="${pkgbase}"
  echo "#!/bin/sh"		>  $pkgdir/usr/bin/$scriptname
  echo "pyrogenesis $*"		>> $pkgdir/usr/bin/$scriptname
  chmod +x $pkgdir/usr/bin/$scriptname

  # Desktop Integration.
  install -D ./build/resources/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -D ./build/resources/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png

  # Legal.
  install -Dm644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
  
}
