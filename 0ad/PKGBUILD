#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=0ad
pkgver=alpha_6
_pkgver=r09786-alpha
pkgrel=1
pkgdesc="Cross-platform, 3D and historically-based real-time strategy game."
arch=('i686' 'x86_64')
url="http://wildfiregames.com/0ad"
license=('GPL2' 'CCPL')
depends=('binutils' 'boost' 'crypto++' 'curl' 'devil' 'enet' 'fam' 'libogg' 'libpng' 'libvorbis' 'libxml2' 'mesa' 'nasm' 'openal' 'python2' 'sdl' 'zip' 'zlib')
makedepends=('cmake')
changelog=ChangeLog
source=(http://downloads.sourceforge.net/project/zero-ad/releases/$pkgname-$_pkgver-unix-build.tar.xz
	http://downloads.sourceforge.net/project/zero-ad/releases/$pkgname-$_pkgver-unix-data.tar.xz)
md5sums=('cb4be5f4a9b74a535f39815a6b0c3e85'
         '085da67a4a681a34418aba49f04cbd28')

build()
{
  cd $srcdir/$pkgname-$_pkgver/build/workspaces

  sed -i \
  's/unix_names = { "boost_signals-mt", "boost_filesystem-mt", "boost_system-mt" },/unix_names = { "boost_signals", "boost_filesystem", "boost_system" },/g' \
  $srcdir/$pkgname-$_pkgver/build/premake/extern_libs.lua

  # Atlas disabled because it requires GTK.
  ./update-workspaces.sh \
    --disable-atlas \
    --with-system-enet

  cd $srcdir/$pkgname-$_pkgver/build/workspaces/gcc

#   CONFIG=Debug make
  CONFIG=Release make
}

package()
{  
  install -d $pkgdir/usr/share/$pkgname
  cp -r $srcdir/$pkgname-$_pkgver/binaries/* $pkgdir/usr/share/$pkgname

  # Execution script:
  install -d $pkgdir/usr/bin/
  echo "#!/bin/sh"			>  $pkgdir/usr/bin/$pkgname
  echo "cd /usr/share/0ad/system"	>> $pkgdir/usr/bin/$pkgname
  echo "./pyrogenesis $*"		>> $pkgdir/usr/bin/$pkgname
  chmod +x $pkgdir/usr/bin/$pkgname
}
