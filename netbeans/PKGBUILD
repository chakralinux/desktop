#Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/netbeans

pkgname=netbeans
_pkgdate=201411102027
pkgver=8.0.2
pkgrel=1
pkgdesc='Netbeans IDE development platform for Java, HTML5, PHP, Groovy, C and C++.'
arch=('x86_64')
url='http://www.netbeans.org'
license=('CDDL')
depends=('java-environment' 'libxtst' 'giflib' 'atk')
makedepends=('gendesk' 'setconf')
optdepends=('php: for developing programs in php'
            'ruby: for developing programs in ruby')
categories=('programming')
options=('!strip')
backup=('usr/share/netbeans/etc/netbeans.conf')
install="${pkgname}.install"
source=("http://dlc.sun.com.edgesuite.net/${pkgname}/${pkgver}/final/zip/${pkgname}-${pkgver}-${_pkgdate}.zip")
sha256sums=('e6e8e1cf033453ce60ee512b63122ca74af27f3f08a8eaa2ba5e1eda4aceb9f2')

prepare() {
  gendesk -f -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}"
  setconf "${pkgname}/bin/${pkgname}" basedir "/usr/share/${pkgname}"
}

package() {
  mkdir -p "${pkgdir}/usr/"{bin,share/applications}
  cp -r "${pkgname}" "${pkgdir}/usr/share/"
  
  # Desktop shortcut and icon
  install -Dm644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  cd "${pkgname}"
  install -Dm644 "nb/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Cleanup (contains the file that is now placed in /usr/bin + .exe-files)
  rm -r "${pkgdir}/usr/share/${pkgname}/bin"
}
