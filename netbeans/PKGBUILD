#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# contributor Fabio Lima <fheday at dcc [dot] fc [dot] up [dot] pt>

pkgname=netbeans
pkgver=7.1
pkgrel=1
pkgdesc='Netbeans IDE development platform.'
arch=('any')
url='http://www.netbeans.org'
license=('CDDL')
depends=('java-environment>=6')
optdepends=('php: for developing programs in php'
            'ruby: for developing programs in ruby'
            'python: for developing programs in python')
install="netbeans.install"
source=('http://dlc.sun.com.edgesuite.net/netbeans/7.1/final/zip/netbeans-7.1-201112071828-ml.zip'
        'netbeans.desktop')
md5sums=('ddcb3dfaf315becbda1e9d5e20ab4e3d'
         '88c631d0d263218e01ea886fde2bc913')

package() {
  rm "${pkgname}/bin/netbeans.exe"

  install -d -m755 "${pkgdir}/usr/share/${pkgname}"
  cp -r "${pkgname}"/* "${pkgdir}/usr/share/${pkgname}"

  install -D -m644 netbeans.desktop "${pkgdir}/usr/share/applications/netbeans.desktop"

  install -d -m755 "${pkgdir}/usr/bin"
  ln -s /usr/share/netbeans/bin/netbeans "${pkgdir}/usr/bin/netbeans"

  # Globally enable antialiasing in NetBeans
  local __aaoptions="-J-Dswing.aatext=true -J-Dawt.useSystemAAFontSettings=on"
  sed -n -e "s:netbeans_default_options=\"\(.*\)\":netbeans_default_options=\"\1 ${__aaoptions}\":" \
    "${pkgdir}/usr/share/netbeans/etc/netbeans.conf"
}

# vim:set ts=2 sw=2 et:
