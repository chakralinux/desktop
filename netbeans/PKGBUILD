# NOTE you need to update pkgdate together with pkgver
# check out pkgdate here https://netbeans.org/downloads/zip.html

pkgname=netbeans
pkgver=8.2
_pkgdate=201610071157
pkgrel=2
pkgdesc='Netbeans IDE development platform for Java, HTML5, PHP, Groovy, C and C++.'
arch=('x86_64')
url='http://www.netbeans.org'
license=('CDDL')
depends=('java-environment' 'ttf-font' 'libxtst' 'giflib' 'atk')
makedepends=('gendesk' 'setconf')
optdepends=('php: for developing programs in php'
            'ruby: for developing programs in ruby'
            'groovy: for developing programs in groovy')
categories=('programming')
options=('!strip')
backup=('usr/share/netbeans/etc/netbeans.conf')
source=("http://bits.netbeans.org/netbeans/$pkgver/community/zip/netbeans-$pkgver-$_pkgdate.zip")
sha256sums=('0a3a8fc9c8acfbbb8a48284328ecee80139fc7ed89d8a94f509fbf8295b47346')

prepare() {
  gendesk -f -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}"
  setconf "${pkgname}/bin/${pkgname}" basedir "/usr/share/${pkgname}"
}

package() {
  mkdir -p "${pkgdir}/usr/"{bin,share/applications}
  cp -r "${pkgname}" "${pkgdir}/usr/share/"
  
  # Desktop shortcut and icon
  install -Dm644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  cd "${pkgname}"
  install -Dm644 "nb/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Cleanup (contains the file that is now placed in /usr/bin + .exe-files)
  rm -r "${pkgdir}/usr/share/${pkgname}/bin"
}
