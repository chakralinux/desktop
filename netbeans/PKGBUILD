#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# contributor Fabio Lima <fheday at dcc [dot] fc [dot] up [dot] pt>

pkgname=netbeans
_pkgdate=201207171143
pkgver=7.2
pkgrel=1
pkgdesc='Netbeans IDE development platform.'
arch=('any')
url='http://www.netbeans.org'
license=('CDDL')
depends=('java-environment>=6')
optdepends=('php: for developing programs in php'
            'ruby: for developing programs in ruby'
            'python: for developing programs in python')
categories=('programming')
install="${pkgname}.install"
source=("http://download.netbeans.org/${pkgname}/${pkgver}/final/zip/${pkgname}-${pkgver}-${_pkgdate}-ml.zip"
        'netbeans.desktop')
md5sums=('2870255966b864f63df3f55efdf4bf98'
         '385f0c0f506880813f7b3afc239adb0c')

package() {
  cd "${srcdir}/${pkgname}"

  rm bin/*.exe

  install -d -m755 "${pkgdir}/usr/share/${pkgname}"
  cp -r * "${pkgdir}/usr/share/${pkgname}"

  install -d -m755 "${pkgdir}/usr/bin"
  ln -s /usr/share/netbeans/bin/netbeans \
        "${pkgdir}/usr/bin/netbeans"

  install -D -m644 "${srcdir}/netbeans.desktop" \
                   "${pkgdir}/usr/share/applications/netbeans.desktop"

  # Globally enable antialiasing in NetBeans
  local __aaoptions="-J-Dswing.aatext=true -J-Dawt.useSystemAAFontSettings=on"
  sed -e "s:netbeans_default_options=\"\(.*\)\":netbeans_default_options=\"\1 ${__aaoptions}\":" \
      -i "${pkgdir}/usr/share/netbeans/etc/netbeans.conf"
}

# vim:set ts=2 sw=2 et:
