pkgname=smplayer
pkgver=17.4.2
pkgrel=1
pkgdesc="A complete front-end for MPlayer"
arch=('x86_64')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('qt5-script' 'mplayer' 'hicolor-icon-theme' 'libxkbcommon-x11')
makedepends=('qt5-tools')
optdepends=('smplayer-themes: icon themes collection'
            'smplayer-skins: skin themes collection'
            'smtube: browse and play youtube videos'
            'mpv: for using mpv instead of mplayer')
categories=('multimedia')
install=smplayer.install
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('05577880df3620ab238a9a28055816c8e97d294ad4b2b39eb7de9be281bc85d1')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make PREFIX=/usr QMAKE=qmake-qt5 LRELEASE=lrelease-qt5 \
    DOC_PATH="\\\"/usr/share/doc/smplayer\\\"" \
    QMAKE_OPTS=DEFINES+=NO_DEBUG_ON_CONSOLE
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DOC_PATH=/usr/share/doc/smplayer \
    DESTDIR="$pkgdir" PREFIX="/usr" install
    
  # Icon
  install -dm755 ${pkgdir}/usr/share/pixmaps
  ln -s /usr/share/icons/hicolor/64x64/apps/smplayer.png \
        ${pkgdir}/usr/share/pixmaps/

  # Menu entries.
  sed -i 's|Exec=smplayer %U|Exec=smplayer %F|' ${pkgdir}/usr/share/applications/smplayer.desktop || return 1
  sed -i 's|Exec=smplayer -add-to-playlist %U|Exec=smplayer -add-to-playlist %F|' \
  	${pkgdir}/usr/share/applications/smplayer_enqueue.desktop 
}
