#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=smplayer
_pkgname=SMPlayer
pkgver=0.8.3
pkgrel=1
pkgdesc="A complete front-end for MPlayer"
arch=('x86_64')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('qt>=4.8.4' 'mplayer>=30526' 'mpg123')
categories=('multimedia')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$_pkgname/${pkgver:0:5}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('48755bffdff3878df32d8c047d7a3d52')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make PREFIX=/usr 
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make PREFIX=${pkgdir}/usr install 

  # Icon
  install -dm755 ${pkgdir}/usr/share/pixmaps
  ln -s /usr/share/icons/hicolor/64x64/apps/smplayer.png \
        ${pkgdir}/usr/share/pixmaps/

  # Menu entries.
  sed -i 's|Exec=smplayer %U|Exec=smplayer %F|' ${pkgdir}/usr/share/applications/smplayer.desktop || return 1
  sed -i 's|Exec=smplayer -add-to-playlist %U|Exec=smplayer -add-to-playlist %F|' \
  	${pkgdir}/usr/share/applications/smplayer_enqueue.desktop 
}
