pkgname=polkit
pkgver=0.113
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=('glib2' 'pam' 'expat' 'systemd' 'js17' 'python2')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'docbook-xsl' 'git')
options=('!libtool')
install=polkit.install
source=("http://www.freedesktop.org/software/polkit/releases/$pkgname-$pkgver.tar.gz")
md5sums=('4b77776c9e4f897dcfe03b2c34198edf')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/polkit-1 \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-libsystemd-login=yes \
    --disable-static \
    --enable-gtk-doc \
    --with-os-type=chakra 

  make 
}

check() {
  cd $pkgname-$pkgver
  make -k check || :
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  chown 102 "$pkgdir"/etc/polkit-1/rules.d
  chown 102 "$pkgdir"/usr/share/polkit-1/rules.d
}
