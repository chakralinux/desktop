#
# Desktop Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=polkit
pkgver=0.107
pkgrel=3
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=('glib2' 'pam' 'expat>=2.0.1' 'systemd' 'js')
makedepends=('intltool>=0.41.1' 'gobject-introspection>=0.9.5' 'docbook-xsl')
replaces=('consolekit')
provides=('consolekit')
conflicts=('consolekit')
options=('!libtool')
install=polkit.install
source=("http://www.freedesktop.org/software/polkit/releases/$pkgname-$pkgver.tar.gz"
        'polkit.pam'
        'polkit-0107-fall-back-to-uid0.patch')
md5sums=('0e4f9c53f43fd1b25ac3f0d2e09b2ae1'
         '6564f95878297b954f0572bc1610dd15'
	 'ff70b87eff8c97e436ce1754ed9891eb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix https://bugzilla.redhat.com/show_bug.cgi?id=834494
  patch -p1 -i "${srcdir}/polkit-0107-fall-back-to-uid0.patch"
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-1 \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --disable-static --with-os-type=chakra 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  chown 102 "$pkgdir/etc/polkit-1/rules.d"
  chown 102 "$pkgdir/usr/share/polkit-1/rules.d"
  
  install -m644 "${srcdir}/polkit.pam" "${pkgdir}/etc/pam.d/polkit-1" 
}
