# Include global configuration

pkgname=akonadi
pkgver=18.08.1
pkgrel=1
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('x86_64')
url='http://community.kde.org/KDE_PIM/Akonadi'
license=('LGPL')
depends=('shared-mime-info' 'qt5-base' 'libxslt' 'mariadb' 'kitemviews' 'kio'
        'kdesignerplugin' 'kitemmodels' 'boost')
makedepends=(extra-cmake-modules postgresql qt5-tools boost python kdesignerplugin)
checkdepends=('xorg-server-xvfb')
optdepends=('postgresql: PostgreSQL backend'
            'mariadb: MariaDB backend')
conflicts=('akonadi-client')
provides=('akonadi-client' 'akonadi-qt4')
replaces=('akonadi-client' 'akonadi-qt4')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('71296a35b0cf6cb1b603685da6cd59ad45ebd0b8a3ba7e933b13bf65fbf8373a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

check() {
  # 17 akonadi-dbconfigtest failed as we changed the default backend
  # 24~30 failed due to qt cannot find the sqldriver which is not installed yet
  cd build
  xvfb-run -a dbus-launch --exit-with-session make test || warning "Tests failed"
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
