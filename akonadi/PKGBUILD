# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

pkgname=akonadi
pkgver=1.12.91
pkgrel=1
pkgdesc="PIM layer, which provides an asynchronous API to access all kinds of PIM data"
arch=('x86_64')
url='http://www.akonadi-project.org'
license=('LGPL')
depends=('shared-mime-info'
         'boost-libs'
         'akonadi-backend'
         'soprano>=2.7.56')
makedepends=('pkg-config'
             'cmake'
             'automoc4'
             'boost'
             'postgresql')
options=('debug')
install=$pkgname.install
source=("http://download.kde.org/stable/akonadi/src/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('4246c7535a56c613775b0b908e589ece77366d5500a19eb332ac04f636ea130b')

prepare() {
    mkdir "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DINSTALL_QSQLITE_IN_QT_PREFIX=TRUE
    make 
}

package() {
    cd ${srcdir}/build
    make DESTDIR=${pkgdir} install
}
