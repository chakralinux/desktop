# Include global configuration
source ../kdeapps.conf

pkgname=akonadi
pkgver=${_kdever}
pkgrel=2
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('x86_64')
url='http://community.kde.org/KDE_PIM/Akonadi'
license=('LGPL')
depends=('shared-mime-info' 'qt5-base' 'libxslt' 'mariadb' 'kitemviews' 'kio'
        'kdesignerplugin' 'kitemmodels' 'boost')
makedepends=('extra-cmake-modules' 'postgresql')
checkdepends=('xorg-server-xvfb')
optdepends=('postgresql: PostgreSQL backend'
            'mariadb: MariaDB backend')
install="${pkgname}.install"
conflicts=('akonadi-client')
provides=('akonadi-client' 'akonadi-qt4')
replaces=('akonadi-client' 'akonadi-qt4')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"{,.sig})
sha256sums=('85509bfb47f29e05b7985d72be633b7dab38dbaf49a3aab1eee6b9b88762a9b1'
            'SKIP')
validpgpkeys=(${Avalidpgpkeys[@]})

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

check() {
  # 17 akonadi-dbconfigtest failed as we changed the default backend
  # 24~30 failed due to qt cannot find the sqldriver which is not installed yet
  cd build
  xvfb-run -a dbus-launch --exit-with-session make test || warning "Tests failed"
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
