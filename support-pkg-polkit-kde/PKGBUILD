#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=polkit-kde
pkgver=0.95.1
pkgrel=2
pkgdesc='KDE PolicyKit Authentication Agent'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
groups=("kde-uninstall" "kde-support")
source=("ftp://ftp.kde.org/pub/kde/stable/apps/KDE4.x/admin/${pkgname}-1-${pkgver}.tar.bz2"
        'xdg-path.patch'
        'gcc45.patch')
md5sums=('68e40d2a43335cb5876a1c8bc51c461b'
         '84d4873e60c1b628bcbaaf38c7ab2f44'
	 'f00a359d35bfbedb47662341d5fd160e')

build() {
	cd ${srcdir}/${pkgname}-1-${pkgver}
	patch -p0 -i ${srcdir}/xdg-path.patch || return 1
	patch -p0 -i ${srcdir}/gcc45.patch || return 1
	cd ..
	mkdir build
	cd build
	export XDG_CONFIG_DIRS=/etc/xdg
	cmake ../${pkgname}-1-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_RPATH=ON
	make || return 1
}

package() {
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install
}
