#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=scummvm
pkgver=1.5.0
pkgrel=1
pkgdesc="A virtual machine for several classic graphical point-and-click adventure games."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.scummvm.org/"
depends=('alsa-lib' 'flac' 'fluidsynth' 'libmad' 'libvorbis' 'sdl' 'zlib')
categories=('games')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
	      "${pkgname}.desktop")
md5sums=('d71d1df5c8262556fedbe885972fbf6e'
         'dd8104df19d60c5c0afdef194395c7f8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --with-flac-prefix=/usr \
              --backend=sdl \
              --disable-debug

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # Manual installation.
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "dists/${pkgname}.6" "${pkgdir}/usr/share/man/man6/${pkgname}.6"

  # Desktop integration.
  install -Dm644 "dists/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "icons/${pkgname}.xpm" "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"

  # Themes.
  install -Dm644 gui/themes/scummmodern.zip "${pkgdir}/usr/share/scummvm/scummmodern.zip"
}
