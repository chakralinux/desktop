pkgname=scummvm
pkgver=1.8.1
pkgrel=1
pkgdesc="A virtual machine for several classic graphical point-and-click adventure games."
arch=('x86_64')
license=('GPL3')
url="http://www.scummvm.org/"
depends=('alsa-lib' 'flac' 'fluidsynth' 'glu' 'libmad' 'libvorbis' 'sdl' 'zlib')
categories=('games')
source=("https://github.com/scummvm/scummvm/archive/v${pkgver}.tar.gz"
	      "${pkgname}.desktop")
sha256sums=('2b107719d6d8e242ea5c3103eb483c0193669e973158a0f6ff93d24ad3a08a27'
            '1165dfe59a4ba25d9605ca0e1be650706bbd784b0c7f66d7a9700538d0031090')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --with-flac-prefix=/usr \
              --backend=sdl \
              --disable-debug

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # Manual installation.
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "dists/${pkgname}.6" "${pkgdir}/usr/share/man/man6/${pkgname}.6"

  # Desktop integration.
  install -Dm644 "dists/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "icons/${pkgname}.xpm" "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"

  # Themes.
  install -Dm644 gui/themes/scummmodern.zip "${pkgdir}/usr/share/scummvm/scummmodern.zip"
}
