# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Source global configuration
source ../kdeapps.conf 

pkgname=zanshin
pkgver=0.5.0
pkgrel=3
pkgdesc="To-do management application for KDE based on Akonadi"
arch=(x86_64)
url="https://zanshin.kde.org"
license=(GPL)
install=$pkgname.install
depends=(kdepim-runtime kontactinterface)
makedepends=(extra-cmake-modules kdoctools boost python3 krunner kdesignerplugin)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0781c088741186f3d9ff3027dc961103d33a59b0e3aeb6bc4c66ff24eb3adb4f'
            'SKIP')
validpgpkeys=(17166A5275C5FA6F1A48FD78074BBBCB8DECC9E2) # Kevin Ottens <ervin+bluesystems@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
