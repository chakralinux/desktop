# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=transformers
pkgname=haskell-transformers
pkgver=0.3.0.0
pkgrel=4
pkgdesc="Concrete functor and monad transformers"
url="http://hackage.haskell.org/package/transformers"
license=('custom:BSD3')
arch=('x86_64' 'i686')
makedepends=()
depends=('ghc=7.6.3-1' 'haskell-base>=2')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/transformers/0.3.0.0/transformers-0.3.0.0.tar.gz")
install="haskell-transformers.install"
sha512sums=('aa444d01ea2296f9a94908872f167f383b0299768b8599b7f488c1d7499b027a90ffd1ef56e250b474d7c547e0acd94ae91b1cc80cdbd4943b5dca2770e2ab93')

build() {
    cd "${srcdir}/transformers-0.3.0.0"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix="/usr" \
        --docdir="/usr/share/doc/haskell-transformers" \
        --libsubdir='$compiler/site-local/$pkgid'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/transformers-0.3.0.0"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/haskell-transformers/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/haskell-transformers/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/haskell-transformers/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/transformers"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/haskell-transformers/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/haskell-transformers/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
