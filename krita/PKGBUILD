# Contribution from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=3.0
pkgrel=2
pkgdesc="Edit and paint images"
arch=('x86_64')
url="http://krita.org"
license=('LGPL')
depends=('kio' 'kitemmodels' 'gsl' 'libraw' 'opencolorio' 'exiv2' 'openexr' 'openjpeg' 'fftw' 'curl' 'boost-libs' 
         'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'boost' 'eigen3' 'vc' 'poppler-qt5')
optdepends=('poppler-qt5: PDF filter')
conflicts=('calligra-krita')
provides=('calligra-krita')
replaces=('calligra-krita')
source=("http://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tgz"
         krita-fix-build.patch::"https://quickgit.kde.org/?p=krita.git&a=commitdiff&h=100a148e&o=plain")
install=$pkgname.install
sha1sums=('6e0f7763e2ed5e266d916e7f76fadbaaf2c84eb5'
          '5f21c5f6c00240a1c2b6fe731086ff1169a773d5')

prepare() {

# Fix build
  cd $pkgname-$pkgver
  patch -p1 -i ../krita-fix-build.patch
}

build() {
  cd $pkgname-$pkgver
  mkdir -p build/
  cd build/ 
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
}
