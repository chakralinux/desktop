# Contribution from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=3.3.3
pkgrel=2
pkgdesc="Edit and paint images"
arch=('x86_64')
url="https://krita.org"
license=('LGPL')
depends=('kio' 'kitemmodels' 'gsl' 'libraw' 'opencolorio' 'exiv2' 'openexr' 'openjpeg' 'fftw' 'curl' 'boost-libs' 
         'hicolor-icon-theme' 'ffmpeg' 'qt5-multimedia')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'boost' 'eigen3' 'vc' 'poppler-qt5' 'opencolorio')
optdepends=('poppler-qt5: PDF filter'
            'opencolorio: for the LUT docker')
conflicts=('calligra-krita')
provides=('calligra-krita')
replaces=('calligra-krita')
source=("https://download.kde.org/stable/krita/$pkgver/$pkgname-$pkgver.tar.gz")
install=$pkgname.install
sha1sums=('66c868ffd00d5bb86e55f8e38ceda81d8003e133')

build() {
  cd $pkgname-$pkgver
  mkdir -p build/
  cd build/ 
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
}
