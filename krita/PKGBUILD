# Contribution from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=4.0.1
pkgrel=1
pkgdesc="Edit and paint images"
arch=('x86_64')
url="https://krita.org"
license=('LGPL')
depends=('kio' 'kitemmodels' 'gsl' 'libraw' 'opencolorio' 'exiv2' 'openexr' 'openjpeg' 'fftw' 'curl' 'boost-libs' 
         'hicolor-icon-theme' 'ffmpeg' 'qt5-multimedia' 'giflib')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'boost' 'eigen3' 'vc' 'poppler-qt5' 'opencolorio' 'python3-pyqt5')
optdepends=('poppler-qt5: PDF filter'
            'opencolorio: for the LUT docker'
            'python-pyqt5: for the Python plugins')
conflicts=('calligra-krita')
provides=('calligra-krita')
replaces=('calligra-krita')
source=("https://download.kde.org/stable/krita/$pkgver/$pkgname-$pkgver.tar.gz")
install=$pkgname.install
sha1sums=('5f5d500092c0e6eab9672adc835832626d285161')

build() {
  cd $pkgname-$pkgver
  mkdir -p build/
  cd build/ 
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
}
