# Contribution from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=4.1.7
pkgrel=1
pkgdesc="Edit and paint images"
arch=('x86_64')
url="https://krita.org"
license=('GPL3')
depends=('kitemviews' 'kitemmodels' 'ki18n' 'kcompletion' 'karchive' 'kguiaddons' 'kcrash' 'qt5-svg' 'qt5-multimedia'
         'gsl' 'libraw' 'exiv2' 'openexr' 'fftw' 'curl' 'boost-libs' 'giflib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'eigen3' 'vc' 'poppler-qt5' 'opencolorio'
             'python3-pyqt5' 'python3-sip' 'libheif')
optdepends=('poppler-qt5: PDF filter'
            'opencolorio: for the LUT docker'
            'python3-pyqt5: for the Python plugins'
            "krita-plugin-gmic: G'MIC plugin"
            'libheif: HEIF filter')
conflicts=('calligra-krita')
provides=('calligra-krita')
replaces=('calligra-krita')
source=("https://download.kde.org/stable/krita/$pkgver/$pkgname-$pkgver.tar.gz")
sha1sums=('2b127151468fad8eceb1b498d8fee52fe288b374')

build() {
  cd $pkgname-$pkgver
  mkdir -p build/
  cd build/ 
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
}
