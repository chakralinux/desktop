# Contribution from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=4.1.6
pkgrel=1
pkgdesc="Edit and paint images"
arch=('x86_64')
url="https://krita.org"
license=('LGPL')
depends=('kio' 'kitemmodels' 'gsl' 'libraw' 'opencolorio' 'exiv2' 'openexr' 'openjpeg' 'fftw' 'curl' 'boost-libs' 
         'hicolor-icon-theme' 'ffmpeg' 'qt5-multimedia' 'giflib')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'boost' 'eigen3' 'vc' 'poppler-qt5' 'opencolorio'
             'python3-pyqt5' 'python3-sip' 'libheif')
optdepends=('poppler-qt5: PDF filter'
            'opencolorio: for the LUT docker'
            'python3-pyqt5: for the Python plugins'
            "krita-plugin-gmic: G'MIC plugin"
            'libheif: HEIF filter')
conflicts=('calligra-krita')
provides=('calligra-krita')
replaces=('calligra-krita')
source=("https://download.kde.org/stable/krita/$pkgver/$pkgname-$pkgver.tar.gz")
sha1sums=('637985854761e77d2bead1a190d9018581f51a27')

build() {
  cd $pkgname-$pkgver
  mkdir -p build/
  cd build/ 
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
}
