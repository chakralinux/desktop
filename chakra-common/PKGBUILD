# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=chakra-common
arch=('x86_64')
pkgver=${_kdever}
pkgrel=2
pkgdesc="Common configuration files, scripts and artwork"
url="http://www.chakra-project.org"
license=('GPL'
         'BSD')
options=('!debug'
         '!header'
         'strip')
groups=("kde"
        "kde-minimal"
        "kde-uninstall")
depends=("${_qtpkg}>=${_qtver}"
         'pinentry'
         'openssh'
         'bash>=4.2'
         'glibc>=2.15'
         'python3'
         'python3-requests'
         'python3-appdirs')
provides=('kde-common'
          'tcp_wrappers'
          'chakra-paste')
replaces=('kde-common'
          'chakra-paste')
conflicts=('kde-common'
           'chakra-paste')
install=chakra-common.install
source=('http://www.kde-apps.org/CONTENT/content-files/48411-rootactions_servicemenu_2.4.10.tar.gz'
        'servicemenu-copy-path.desktop'
        'http://www.kde-apps.org/CONTENT/content-files/102825-compressattach.desktop.bz2'
        'servicemenu-rootactions.patch'
        'Chakra-Bugs.desktop'
        'Chakra-ChangeLog.desktop'
        'Chakra-Code.desktop'
        'Chakra-Documentation.desktop'
        'Chakra-Donate.desktop'
        'Chakra-Forum.desktop'
        'Chakra-Homepage.desktop'
        'Chakra.directory'
        'chakra-config-updater.sh'
        'chakra-update-icons.sh'
        'chakra-update-mime.sh'
        'chakra-update-sycoca.sh'
        'chakra-update-user-sycoca.sh'
        'chakra-sysinfo.sh'
        'chakra-backup.sh'
        'Chakra-backup.desktop'
        'updates.conf'
        'chakra-icons.tar.gz'
        'agent-startup.sh'
        'agent-shutdown.sh'
        'chakra-helper.sh'
        'https://gitorious.org/chakra-paste/chakra-paste/archive/05f0a884a4de1460309ee95d678eb034c4c964b5.tar.gz')
sha256sums=('92978c921647a2fa00d82c0bde59ebd15f7c805e87e11e1f9c4789bde3fbd8b5'
            '2bf00472d9b50bc9befcf9cdca92a1a6d31690287de330e6390ca65c17aba02e'
            'ca7d66aec559282581dd577746dff86b9b4127ae871ee2788b74ab66f99edbfa'
            'c20b837934d708653de775ed19bd013e4b8509ba5dffbbd5ac4c905e3102f15c'
            '8ed24dbb68a94f07a326ab848560a70437261e44b7f22586042fbbf7e088e259'
            'cff1d0489f5f4b7eb92a0cee9a7cf100bd8c401c9395a78d95068c5437330a28'
            'cff4c2898324cc1680234876e1bf956a3eafbd77b3a5ae59bea23a8c2d5c073b'
            '9904b71f56678f833b80e549dbe57ed2db3e61e86275c312c6c8193544599f7f'
            '64988707a45a6eb5be34d4af6b89c79925de773c87e21d805437255c22fb5714'
            '9c437a426b693cb86ec58051892389cfe84157f08f5cca9929a80a305ae73ff6'
            'cdc9cae45c8772884b42449760ea362aafe0548c38eb7470a15e58733970e24c'
            'cf6be6c5dd059021b140f97f82be4ed33e887aa8b588b5d4adb0b035d05a90a6'
            '9ef92be376fde231946ab995afd15ac457bd67aac25779e81b1980e8b337cfd4'
            '7e947d88d6c5464b5e11770d597af11bcfb01283bc9bed2bea15fcb3dbb90e33'
            'd155420e3db5ec0d43ec4e53f568b55cd718c42fef7792f1e8bdc75b960ff1e3'
            '8b3a4020008c8506b5b7110b4d070746b134d751c769035ee2ffbb7b9fd7c31b'
            '83fe20e57cc05cd869cb468cf90d554ff45f9c993e6a3c48829fb9230ebe5c4c'
            '3ac5808d9da1b37e0787ee11f4154306847840697e5590c1856b5bd13727c119'
            'fdbf09b51795af2603569805e00c43e05f531e901e81c7b9189b4897a89a50ee'
            '2ea8bc1cbe178d66f3560d166e4158d1ce953a349d8c7658891f5600736e67b7'
            'da64dc6bc248703da7eb8ad38af73b87c9836b3b35cf0529e988a69ee5922e8b'
            'b20160225a990513ae58027b7e70bb958253da881be02f296e54e9ccca09b348'
            'e4a44b4ed3130d1fa309a6b5c829461ec09ec595c31cd4a8715d79942fe8f0ef'
            '530ecdbe32c8f33991cd6c2ccf29212f28bb4462d1844f5acba335b5984a9347'
            '4dd54e7c635d00a6e0374f73d93dc826379a21aa53de1361e1631763a6da8273'
            '780c9c40d0884b2c145b95b8a394bf0b90c6696fcef250c1d01a66985237e43a')
  
package() {
    # Install scripts for gpg/ssh-agent
    install -D -m 755 -o root -g root $srcdir/agent-startup.sh $pkgdir/etc/kde/env/agent-startup.sh
    install -D -m 755 -o root -g root $srcdir/agent-shutdown.sh $pkgdir/etc/kde/shutdown/agent-shutdown.sh

    # Install KDM RC script
    #install -D -m755 -o root -g root ${srcdir}/kdm.daemon ${pkgdir}/etc/rc.d/kdm

    # Install configuration updater scripts
    install -D -m755 -o root -g root ${srcdir}/chakra-config-updater.sh ${pkgdir}/usr/bin/chakra-config-updater
    install -D -m755 -o root -g root ${srcdir}/chakra-update-icons.sh ${pkgdir}/usr/bin/chakra-update-icons
    install -D -m755 -o root -g root ${srcdir}/chakra-update-mime.sh ${pkgdir}/usr/bin/chakra-update-mime
    install -D -m755 -o root -g root ${srcdir}/chakra-update-sycoca.sh ${pkgdir}/usr/bin/chakra-update-sycoca
    install -D -m755 -o root -g root ${srcdir}/chakra-update-user-sycoca.sh ${pkgdir}/usr/bin/chakra-update-user-sycoca

    # Install sysinfo script
    install -D -m755 -o root -g root ${srcdir}/chakra-sysinfo.sh ${pkgdir}/usr/bin/chakra-sysinfo

    # Install backup script
    install -D -m755 -o root -g root ${srcdir}/chakra-backup.sh ${pkgdir}/usr/bin/chakra-backup
    install -D -m644 -o root -g root ${srcdir}/Chakra-backup.desktop ${pkgdir}/usr/share/applications/kde4/Chakra-backup.desktop

    # Install config file for updates
    install -D -m755 -o root -g root ${srcdir}/updates.conf ${pkgdir}/etc/chakra/updates.conf

    # Install root actions service menu
    cd ${srcdir}/rootactions_servicemenu_2.4.10/Root_Actions_2.4.10/dolphin-KDE4/
    mkdir -p ${pkgdir}/usr/share/kde4/services/ServiceMenus/
    install -m755 *.desktop ${pkgdir}/usr/share/kde4/services/ServiceMenus/
        cd ${srcdir}/rootactions_servicemenu_2.4.10/Root_Actions_2.4.10
        patch -p2 < ${srcdir}/servicemenu-rootactions.patch      
    install -Dm755 rootactions-servicemenu.pl ${pkgdir}/usr/bin/rootactions-servicemenu.pl
        sed "s~Icon=terminal~Icon=utilities-terminal~g" -i ${pkgdir}/usr/share/kde4/services/ServiceMenus/10-rootactionsfolders.desktop
        sed "s~Icon=terminal~Icon=utilities-terminal~g" -i ${pkgdir}/usr/share/kde4/services/ServiceMenus/11-rootactionsfiles.desktop

    # Install all other service menus
    install -m755 ${srcdir}/servicemenu-copy-path.desktop ${pkgdir}/usr/share/kde4/services/ServiceMenus/copy-path.desktop

    install -m755 ${srcdir}/102825-compressattach.desktop ${pkgdir}/usr/share/kde4/services/ServiceMenus/compress-attach.desktop
        sed "s~Icon=attach~Icon=mail-attachment~g" -i ${pkgdir}/usr/share/kde4/services/ServiceMenus/compress-attach.desktop

    # Install Chakra icons
    cd ${srcdir}/chakra-icons/
    mkdir -p ${pkgdir}/usr/share/icons/hicolor/scalable/apps/
    for n in flat shiny smooth shield; do
        for i in 128 64 48 32 22 16; do
            mkdir -p ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/
            install -D -m755 chakra-${n}-${i}.png ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/chakra-${n}.png
        done
        install -D -m755 chakra-${n}.svgz ${pkgdir}/usr/share/icons/hicolor/scalable/apps/chakra-${n}.svgz
    done

    # Install menu entries
    cd ${srcdir}
    install -m755 -d ${pkgdir}/usr/share/desktop-directories
    install -m755 -d ${pkgdir}/usr/share/applications/kde4
    install -m755 -d ${pkgdir}/usr/share/pixmaps
    install -m644 Chakra*.desktop ${pkgdir}/usr/share/applications/kde4
    install -m644 Chakra.directory ${pkgdir}/usr/share/desktop-directories/
  
    # chakra-paste
    install -D -m777 chakra-paste-chakra-paste/chakra-paste "${pkgdir}/usr/bin/chakra-paste"
  
    # chakra-helper
    install -D -m777 chakra-helper.sh "${pkgdir}/usr/bin/chakra-helper"
}
