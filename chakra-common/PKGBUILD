# Include global configuration
source ../kdeapps.conf

pkgname=chakra-common
arch=('x86_64')
pkgver=${_kdever}
pkgrel=3
pkgdesc="Common configuration files, scripts and artwork"
url="http://www.chakralinux.org"
license=('GPL' 'BSD')
options=('!debug' 'strip')
groups=("kde" "kde-minimal" "kde-uninstall")
depends=("qt5-base"
         'pinentry'
         'openssh'
         'bash>=4.2'
         'glibc>=2.15'
         'python3'  'python3-jinja' 'python3-pyelftools' 'python3-tqdm')
install=chakra-common.install
source=('https://dl.opendesktop.org/api/files/download/id/1485341541/rootactions_servicemenu_2.9.1.tar.gz'
        'servicemenu-copy-path.desktop'
        #'http://www.kde-apps.org/CONTENT/content-files/102825-compressattach.desktop.bz2'
        'servicemenu-rootactions.patch'
        'Chakra-Bugs.desktop'
        'Chakra-ChangeLog.desktop'
        'Chakra-Code.desktop'
        'Chakra-Documentation.desktop'
        'Chakra-Donate.desktop'
        'Chakra-Forum.desktop'
        'Chakra-Homepage.desktop'
        'Chakra.directory'
        'chakra-config-updater.sh'
        'chakra-update-icons.sh'
        'chakra-update-mime.sh'
        'chakra-update-sycoca.sh'
        'chakra-update-user-sycoca.sh'
        'chakra-sysinfo.sh'
        #'chakra-backup.sh'
        #'Chakra-backup.desktop'
        'updates.conf'
        'chakra-icons.tar.gz'
        'chakra-helper.sh'
        'kde4.sh'
        'chakra-check-links.py::https://raw.githubusercontent.com/Inkane/listMissingLibs/master/list_missing_libs.py')
        # skipping chakra-paste because gitorious has issues with wget
#        'https://gitorious.org/chakra-paste/chakra-paste/archive/05f0a884a4de1460309ee95d678eb034c4c964b5.tar.gz'
sha256sums=('8c02e552d6d18246235e3141eebd02b125fad15717660dfa231b629a14837f65'
            '92c69474167e6f8e7bac8e95c90702ac20aa95add759a55e7bb8453d52f46e40'
            'ba4b1f429bd6900f6989df6e50b69fea38110ccb4a304a903de6e291d0af8b74'
            'd87b802cb60705439b0e43517837a4e71dc882a4ce9863f762fa9bf6112433cb'
            '8dc16af56320b0e95c105453bd625319edbdf066ab1c8435ed8eaea606d601d0'
            '15d1e26bc76535efcc9abd536f5c1b75aaacf5d92b13e035b17a0ebb96aef5ab'
            'c1574b4e9b3649db9063ccc8ed71d88c89603b4e2594934d43d726b02b477ef8'
            '23e22cbbd1d7a6630717a1e06698b0735e780189af780292de9d7d5bfa0a0faa'
            '75c4fe63a05b1c4820072e1ac591970d0649018489e870ab9157fb29b24d1c2f'
            '1ba63dbaf15e0becdc2ce745f0253f8422f1873467b83681356cf18d102818f0'
            'cf6be6c5dd059021b140f97f82be4ed33e887aa8b588b5d4adb0b035d05a90a6'
            '18c53e91e5ff32754e0a71d208bff62f5f6880e68a86699e734b28d5e2387aab'
            '7e947d88d6c5464b5e11770d597af11bcfb01283bc9bed2bea15fcb3dbb90e33'
            'd155420e3db5ec0d43ec4e53f568b55cd718c42fef7792f1e8bdc75b960ff1e3'
            'a99cbf6a29700b71564baffeb8b8d7f645a4e67a9868b68609c31937beef3d15'
            '79bd1a0dbce88e1ab12a5db3aa87b1a7bbd464df7e64facc6c33abd507706bbf'
            '3ac5808d9da1b37e0787ee11f4154306847840697e5590c1856b5bd13727c119'
            'da64dc6bc248703da7eb8ad38af73b87c9836b3b35cf0529e988a69ee5922e8b'
            'b20160225a990513ae58027b7e70bb958253da881be02f296e54e9ccca09b348'
            '9e0c9408c9bc12aeca2fd66a4723b57a6878307ba956df7cca43f8112dd71429'
            '7e4ae4587973d6cc253b20ff7573ac25801f148f794a9000901c5137cd4780b3'
            '9d3c09db7e5f6ae0c8cdb47ba686b22260266126a214c87af0fae2bb355eb03e')

package() {
    # Install scripts for kf5 
    install -D -m755 -o root -g root $srcdir/kde4.sh $pkgdir/etc/xdg/plasma-workspace/env/kde4.sh

    # Install configuration updater scripts
    install -D -m755 -o root -g root ${srcdir}/chakra-config-updater.sh ${pkgdir}/usr/bin/chakra-config-updater
    install -D -m755 -o root -g root ${srcdir}/chakra-update-icons.sh ${pkgdir}/usr/bin/chakra-update-icons
    install -D -m755 -o root -g root ${srcdir}/chakra-update-mime.sh ${pkgdir}/usr/bin/chakra-update-mime
    install -D -m755 -o root -g root ${srcdir}/chakra-update-sycoca.sh ${pkgdir}/usr/bin/chakra-update-sycoca
    install -D -m755 -o root -g root ${srcdir}/chakra-update-user-sycoca.sh ${pkgdir}/usr/bin/chakra-update-user-sycoca

    # Install sysinfo script
    install -D -m755 -o root -g root ${srcdir}/chakra-sysinfo.sh ${pkgdir}/usr/bin/chakra-sysinfo

    # Install backup script
    #install -D -m755 -o root -g root ${srcdir}/chakra-backup.sh ${pkgdir}/usr/bin/chakra-backup
    #install -D -m644 -o root -g root ${srcdir}/Chakra-backup.desktop ${pkgdir}/usr/share/applications/Chakra-backup.desktop

    # Install config file for updates
    install -D -m755 -o root -g root ${srcdir}/updates.conf ${pkgdir}/etc/chakra/updates.conf

    # Install root actions
    cd ${srcdir}/rootactions_servicemenu_2.9.1/Root_Actions_2.9.1/
    install -dm755 ${pkgdir}/usr/share/kservices5/ServiceMenus/
    install -Dm755 dolphin-KDE4/*.desktop ${pkgdir}/usr/share/kservices5/ServiceMenus/
    install -Dm644 krusader-KDE4/krusader_rootactions.xml "$pkgdir"/usr/share/apps/krusader/krusader_rootactions.xml
    install -Dm755 rootactions-servicemenu.pl "$pkgdir"/usr/bin/rootactions-servicemenu.pl

    # Install Chakra icons
    cd ${srcdir}/chakra-icons/
    mkdir -p ${pkgdir}/usr/share/icons/hicolor/scalable/apps/
    for n in flat shiny smooth shield; do
        for i in 128 64 48 32 22 16; do
            mkdir -p ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/
            install -D -m755 chakra-${n}-${i}.png ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/chakra-${n}.png
        done
        install -D -m755 chakra-${n}.svgz ${pkgdir}/usr/share/icons/hicolor/scalable/apps/chakra-${n}.svgz
    done

    # Install menu entries
    cd ${srcdir}
    install -m755 -d ${pkgdir}/usr/share/desktop-directories
    install -m755 -d ${pkgdir}/usr/share/applications
    install -m755 -d ${pkgdir}/usr/share/pixmaps
    install -m644 Chakra*.desktop ${pkgdir}/usr/share/applications
    install -m644 Chakra.directory ${pkgdir}/usr/share/desktop-directories/
  
    # chakra-paste
    #install -Dm755 chakra-paste-chakra-paste/chakra-paste "${pkgdir}/usr/bin/chakra-paste"
  
    # chakra-helper
    install -Dm755 chakra-helper.sh "${pkgdir}/usr/bin/chakra-helper"

    # chakra-check-links
    install -Dm755 chakra-check-links.py "${pkgdir}/usr/bin/chakra-check-links"
}
