# Include global configuration
source ../kdeapps.conf

pkgname=chakra-common
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="Common configuration files, scripts and artwork"
url="http://www.chakraos.org"
license=('GPL'
         'BSD')
options=('!debug'
         'strip')
groups=("kde"
        "kde-minimal"
        "kde-uninstall")
depends=("qt5-base"
         'pinentry'
         'openssh'
         'bash>=4.2'
         'glibc>=2.15')
provides=('kde-common'
          'tcp_wrappers'
          'chakra-paste')
replaces=('kde-common'
          'chakra-paste')
conflicts=('kde-common'
           'chakra-paste')
install=chakra-common.install
source=('http://kde-apps.org/CONTENT/content-files/48411-rootactions_servicemenu_2.8.6.tar.gz'
        'servicemenu-copy-path.desktop'
        'http://www.kde-apps.org/CONTENT/content-files/102825-compressattach.desktop.bz2'
        'servicemenu-rootactions.patch'
        'Chakra-Bugs.desktop'
        'Chakra-ChangeLog.desktop'
        'Chakra-Code.desktop'
        'Chakra-Documentation.desktop'
        'Chakra-Donate.desktop'
        'Chakra-Forum.desktop'
        'Chakra-Homepage.desktop'
        'Chakra.directory'
        'chakra-config-updater.sh'
        'chakra-update-icons.sh'
        'chakra-update-mime.sh'
        'chakra-update-sycoca.sh'
        'chakra-update-user-sycoca.sh'
        'chakra-sysinfo.sh'
        'chakra-backup.sh'
        'Chakra-backup.desktop'
        'updates.conf'
        'chakra-icons.tar.gz'
        'agent-startup.sh'
        'agent-shutdown.sh'
        'chakra-helper.sh'
        'kde4.sh')
        # skipping chakra-paste because gitorious has issues with wget
#        'https://gitorious.org/chakra-paste/chakra-paste/archive/05f0a884a4de1460309ee95d678eb034c4c964b5.tar.gz'
sha256sums=('9b2cdc947f1da2c56d0f009ff470f7aa9021f214a2470c33ee4628281b7c1163'
            '2bf00472d9b50bc9befcf9cdca92a1a6d31690287de330e6390ca65c17aba02e'
            'ca7d66aec559282581dd577746dff86b9b4127ae871ee2788b74ab66f99edbfa'
            'ba4b1f429bd6900f6989df6e50b69fea38110ccb4a304a903de6e291d0af8b74'
            '6e8e719bff3827844e87dbfa4676ef9a00ec626e2d2d508508badb284f4d7bae'
            '9dc9f3afb6b201e5dc409827d78f5738f0523025b960759026d01fc9b53c64cf'
            'f079ce83c680773c37d5f6f795729b39a558364516997d605a0954f1a75582db'
            '6eceee616f5f06ca715ee44db93c03b9a8011fddb58cf522c01d22ec368f21f9'
            'b41bb79ca735429f00f0a0bc332dce0a86e8d16785e3fc38c59b076c1e969778'
            '824dbb7f1808b81c8738b0dcde08332be9adbd113776c4f7facd9a5d476c9613'
            'e68f4eb4efd53edc3d4707a94d131ef7a8bc9eed1c503c58b3a03ea4ba620829'
            'cf6be6c5dd059021b140f97f82be4ed33e887aa8b588b5d4adb0b035d05a90a6'
            '18c53e91e5ff32754e0a71d208bff62f5f6880e68a86699e734b28d5e2387aab'
            '7e947d88d6c5464b5e11770d597af11bcfb01283bc9bed2bea15fcb3dbb90e33'
            'd155420e3db5ec0d43ec4e53f568b55cd718c42fef7792f1e8bdc75b960ff1e3'
            'a99cbf6a29700b71564baffeb8b8d7f645a4e67a9868b68609c31937beef3d15'
            '79bd1a0dbce88e1ab12a5db3aa87b1a7bbd464df7e64facc6c33abd507706bbf'
            '3ac5808d9da1b37e0787ee11f4154306847840697e5590c1856b5bd13727c119'
            'fdbf09b51795af2603569805e00c43e05f531e901e81c7b9189b4897a89a50ee'
            '2ea8bc1cbe178d66f3560d166e4158d1ce953a349d8c7658891f5600736e67b7'
            'da64dc6bc248703da7eb8ad38af73b87c9836b3b35cf0529e988a69ee5922e8b'
            'b20160225a990513ae58027b7e70bb958253da881be02f296e54e9ccca09b348'
            '15dbeea94793a9d6922f7c5fed107f63d5b2b5715c12874f0884e4dc5905d5c6'
            '80da5b02a11ff9471775b2173a8c251d649d292a0c822469be43a5227b403444'
            '9e0c9408c9bc12aeca2fd66a4723b57a6878307ba956df7cca43f8112dd71429'
            '7e4ae4587973d6cc253b20ff7573ac25801f148f794a9000901c5137cd4780b3')


package() {
    # Install scripts for gpg/ssh-agent
    install -D -m 755 -o root -g root $srcdir/agent-startup.sh $pkgdir/etc/kde/env/agent-startup.sh
    install -D -m 755 -o root -g root $srcdir/agent-shutdown.sh $pkgdir/etc/kde/shutdown/agent-shutdown.sh
 
    # Install scripts for kf5 
    install -D -m 755 -o root -g root $srcdir/agent-startup.sh $pkgdir/etc/xdg/plasma-workspace/env/agent-startup.sh
    install -D -m 755 -o root -g root $srcdir/agent-shutdown.sh $pkgdir/etc/xdg/plasma-workspace/shutdown/agent-shutdown.sh
    install -D -m 755 -o root -g root $srcdir/kde4.sh $pkgdir/etc/xdg/plasma-workspace/env/kde4.sh

    # Install KDM RC script
    #install -D -m755 -o root -g root ${srcdir}/kdm.daemon ${pkgdir}/etc/rc.d/kdm

    # Install configuration updater scripts
    install -D -m755 -o root -g root ${srcdir}/chakra-config-updater.sh ${pkgdir}/usr/bin/chakra-config-updater
    install -D -m755 -o root -g root ${srcdir}/chakra-update-icons.sh ${pkgdir}/usr/bin/chakra-update-icons
    install -D -m755 -o root -g root ${srcdir}/chakra-update-mime.sh ${pkgdir}/usr/bin/chakra-update-mime
    install -D -m755 -o root -g root ${srcdir}/chakra-update-sycoca.sh ${pkgdir}/usr/bin/chakra-update-sycoca
    install -D -m755 -o root -g root ${srcdir}/chakra-update-user-sycoca.sh ${pkgdir}/usr/bin/chakra-update-user-sycoca

    # Install sysinfo script
    install -D -m755 -o root -g root ${srcdir}/chakra-sysinfo.sh ${pkgdir}/usr/bin/chakra-sysinfo

    # Install backup script
    install -D -m755 -o root -g root ${srcdir}/chakra-backup.sh ${pkgdir}/usr/bin/chakra-backup
    install -D -m644 -o root -g root ${srcdir}/Chakra-backup.desktop ${pkgdir}/usr/share/applications/kde4/Chakra-backup.desktop

    # Install config file for updates
    install -D -m755 -o root -g root ${srcdir}/updates.conf ${pkgdir}/etc/chakra/updates.conf

    # Install root actions
    cd ${srcdir}/rootactions_servicemenu_2.8.6/Root_Actions_2.8.6/
    install -dm755 ${pkgdir}/usr/share/kservices5/ServiceMenus/
    install -Dm755 dolphin-KDE4/*.desktop ${pkgdir}/usr/share/kservices5/ServiceMenus/
    install -Dm644 krusader-KDE4/krusader_rootactions.xml "$pkgdir"/usr/share/apps/krusader/krusader_rootactions.xml
    install -Dm755 rootactions-servicemenu.pl "$pkgdir"/usr/bin/rootactions-servicemenu.pl

    # Install Chakra icons
    cd ${srcdir}/chakra-icons/
    mkdir -p ${pkgdir}/usr/share/icons/hicolor/scalable/apps/
    for n in flat shiny smooth shield; do
        for i in 128 64 48 32 22 16; do
            mkdir -p ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/
            install -D -m755 chakra-${n}-${i}.png ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/chakra-${n}.png
        done
        install -D -m755 chakra-${n}.svgz ${pkgdir}/usr/share/icons/hicolor/scalable/apps/chakra-${n}.svgz
    done

    # Install menu entries
    cd ${srcdir}
    install -m755 -d ${pkgdir}/usr/share/desktop-directories
    install -m755 -d ${pkgdir}/usr/share/applications
    install -m755 -d ${pkgdir}/usr/share/pixmaps
    install -m644 Chakra*.desktop ${pkgdir}/usr/share/applications
    install -m644 Chakra.directory ${pkgdir}/usr/share/desktop-directories/
  
    # chakra-paste
  #  install -D -m777 chakra-paste-chakra-paste/chakra-paste "${pkgdir}/usr/bin/chakra-paste"
  
    # chakra-helper
    install -D -m777 chakra-helper.sh "${pkgdir}/usr/bin/chakra-helper"
}
