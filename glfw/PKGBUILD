pkgname=glfw
pkgver=3.1.2
pkgrel=1
pkgdesc="A free, open source, portable framework for OpenGL application development"
arch=('x86_64')
url="http://www.glfw.org/"
license=('custom:ZLIB')
depends=('libgl' 'libxrandr' 'glu' 'xorg-xinput' 'libxxf86vm' 'libxcursor')
makedepends=('mesa' 'cmake' 'doxygen')
conflicts=('glfw2')
replaces=('glfw2')
source=("https://github.com/glfw/glfw/archive/${pkgver}.tar.gz")
sha512sums=('07e8dcc2623fa87558f44e8a01e0f12c0fbbf8b67798337a500c8208ebebedfb545156feb11cba16fa6a4ae1ef0ae21c7ebf934791e4f62a953b727c48f9d150')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=ON
  make docs
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  cd build
  make DESTDIR=$pkgdir install

  cd ../
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  
  #docs
  install -d "${pkgdir}/usr/share/doc/glfw/"
  cp -r "${srcdir}/glfw-${pkgver}/build/docs/html" "${pkgdir}/usr/share/doc/glfw"
}