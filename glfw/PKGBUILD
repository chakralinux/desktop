pkgname=glfw
pkgver=3.2.1
pkgrel=1
pkgdesc="A free, open source, portable framework for OpenGL application development"
arch=('x86_64')
url="http://www.glfw.org/"
license=('custom:ZLIB')
depends=('libgl' 'libxrandr' 'glu' 'xorg-xinput' 'libxxf86vm' 'libxcursor')
makedepends=('mesa' 'cmake' 'doxygen')
source=("https://github.com/glfw/glfw/archive/${pkgver}.tar.gz")
sha512sums=('c7921f993b9a99b3b9421fefadb039cd475c42d85f5b5a35d7c5401c70491349bb885a02fd31e527de06a8b40d9d49a1fdb92c964e13c04ae092c6b98eb491dc')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=ON
  make docs
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  cd build
  make DESTDIR=$pkgdir install

  cd ../
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  
  #docs
  install -d "${pkgdir}/usr/share/doc/glfw/"
  cp -r "${srcdir}/glfw-${pkgver}/build/docs/html" "${pkgdir}/usr/share/doc/glfw"
}
