pkgname=opencl-headers
pkgver=2.2.20170516
pkgrel=1
pkgdesc='OpenCL (Open Computing Language) header files'
arch=('any')
url='https://www.khronos.org/registry/cl/'
license=('custom')
source=('git+https://github.com/KhronosGroup/OpenCL-Headers.git#commit=ec2566b'
        'git+https://github.com/KhronosGroup/OpenCL-CLHPP.git#commit=e250e2e'
        'LICENSE.txt')
sha1sums=('SKIP'
          'SKIP'
          '98abb35b2eca82d0fc19db6d28fcc7bd20be0655')

package() {
  cd "${srcdir}"/OpenCL-Headers/opencl22/CL

  install -dm755 "${pkgdir}"/usr/include/CL

  for h in *.h; do
    install -m 644 ${h} "${pkgdir}"/usr/include/CL/
  done

  # remove useless headers
  rm "${pkgdir}"/usr/include/CL/{cl_d3d,cl_dx9}*.h

  cd "${srcdir}"/OpenCL-CLHPP

  python gen_cl_hpp.py -i input_cl.hpp -o cl.hpp
  install -m 644 cl.hpp "${pkgdir}"/usr/include/CL/
  install -m 644 input_cl2.hpp "${pkgdir}"/usr/include/CL/cl2.hpp

  install -D -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

