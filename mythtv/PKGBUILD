# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: <kleptophobiac@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=mythtv
pkgver=0.27.4
pkgrel=1
pkgdesc="A Homebrew PVR project"
arch=('x86_64')
url="http://www.mythtv.org/"
license=('GPL')
depends=('avahi' 'fftw' 'lame' 'libass' 'libavc1394' 'libcdio' 'libiec61883'
         'libpulse' 'libva' 'libvpx' 'libxinerama' 'lirc-utils'
         'mariadb-clients' 'mysql-python' 'perl-dbd-mysql'
         'perl-io-socket-inet6' 'perl-libwww' 'perl-net-upnp' 'python2-lxml'
         'qtwebkit' 'taglib' 'urlgrabber' 'x264')
makedepends=('glew' 'libcec' 'libxml2' 'mesa' 'libgl' 'openssl' 'yasm')
optdepends=('glew: for GPU commercial flagging'
            'libcec: for consumer electronics control capabilities'
            'libxml2: to read blu-ray metadata'
            'openssl: for AirTunes (RAOP) support'
            'udisks: detect changes to removable media'
            'xmltv: to download tv listings')
install='mythtv.install'
source=(
	"$pkgname-$pkgver.tar.gz::https://github.com/MythTV/$pkgname/archive/v$pkgver.tar.gz"
	'mythbackend.service'
	'libcec-2-support.2.patch')
sha512sums=('6f858f32467756e96db6f79ccb2c42edc0bf22d217cb7b9dc9188ada994bbb9aafe9f72dd71aca10cd26bad1bd5d344af64487ec5fc925872aba81cf7aa257c8'
            'c0814f064635448b56ccf28fd59ed93402343fa076fde58df6bdbe0a47266d89f780a84075d07b4e08f8fcd603580bf2da8cc7092debf6b6f4efa53bd2b78c1b'
            '5d4db25c767e5417148fc7b9dd6a8276cb7737203a2247e7a5c5e24084a3fa9246ebef177f4c65491d84eb40f91774f9821c5051cb3a0258c625b0315b4d3d87')

prepare() {
  cd $srcdir/$pkgname-$pkgver/$pkgname

  find 'bindings/python' 'contrib' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  patch -Np2 -i $srcdir/libcec-2-support.2.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver/$pkgname

  ./configure --prefix=/usr \
              --cpu=x86-64 \
              --disable-altivec \
              --disable-audio-jack \
              --disable-ccache \
              --disable-distcc \
              --enable-libfftw3 \
              --enable-libmp3lame \
              --enable-libvpx \
              --enable-libx264 \
              --enable-vaapi \
              --python=python2
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/$pkgname
  make INSTALL_ROOT=$pkgdir install

  install -Dm644 $srcdir/mythbackend.service $pkgdir/usr/lib/systemd/system/mythbackend.service
  install -Dm644 'database/mc.sql' $pkgdir/usr/share/mythtv/mc.sql

  install -d $pkgdir/{usr/share/mythtv,var/log/mythtv}
  cp -R 'contrib' $pkgdir/usr/share/mythtv
}
