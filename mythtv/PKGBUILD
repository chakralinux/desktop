# Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/mythtv

pkgname=mythtv
pkgver=0.28.1
pkgrel=1
pkgdesc="A Homebrew PVR project"
arch=('x86_64')
url="http://www.mythtv.org/"
license=('GPL')
depends=('avahi' 'fftw' 'lame' 'libass' 'libavc1394' 'libcdio' 'libiec61883'
         'libgl' 'libpulse' 'libva' 'libvpx' 'libxinerama' 'lirc' 'libxrandr'
         'mariadb-clients' 'mysql-python' 'perl-dbd-mysql' 'exiv2'
         'perl-io-socket-inet6' 'perl-libwww' 'perl-net-upnp' 'python2-lxml'
         'qt5-webkit' 'qt5-script' 'taglib' 'urlgrabber' 'libx264' 'libvdpau')
makedepends=('glew' 'libcec' 'libxml2' 'mesa' 'openssl' 'x264' 'yasm' 'gdb')
optdepends=('glew: for GPU commercial flagging'
            'libcec: for consumer electronics control capabilities'
            'libxml2: to read blu-ray metadata'
            'openssl: for AirTunes (RAOP) support')
install='mythtv.install'
source=("$pkgname-$pkgver.tar.gz::https://github.com/MythTV/$pkgname/archive/v$pkgver.tar.gz"
	'mythbackend.service'
        '99-mythbackend.rules')
sha256sums=('598270a1cc89abd72ef4a86c1aa8cd6644ec189c87b7df0547a835f9d0020cac'
            '30a301d31e2396b08eee97188619b900a834081d9792a36ae274fb0dfa65a9dc'
            'ecfd02bbbef5de9773f4de2c52e9b2b382ce8137735f249d7900270d304fd333')

prepare() {
  cd $srcdir/$pkgname-$pkgver/$pkgname
  
  find 'bindings/python' 'contrib' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
}

build() {
  cd $srcdir/$pkgname-$pkgver/$pkgname

  ./configure --prefix=/usr \
              --cpu=x86-64 \
              --disable-altivec \
              --disable-audio-jack \
              --disable-ccache \
              --disable-distcc \
              --enable-libfftw3 \
              --enable-libmp3lame \
              --enable-libvpx \
              --enable-libx264 \
              --enable-vaapi \
              --python=python2 \
              --perl-config-opts=INSTALLDIRS=vendor
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/$pkgname
  make INSTALL_ROOT=$pkgdir install

  install -Dm644 $srcdir/mythbackend.service $pkgdir/usr/lib/systemd/system/mythbackend.service
  install -Dm644 'database/mc.sql' $pkgdir/usr/share/mythtv/mc.sql

  install -d $pkgdir/{usr/share/mythtv,var/log/mythtv}
  cp -R 'contrib' $pkgdir/usr/share/mythtv
  
  # Install udev rules
  # https://www.mythtv.org/wiki/Systemd_mythbackend_Configuration#Delay_starting_the_backend_until_tuners_have_initialized
  install -Dm644 "$srcdir"/99-mythbackend.rules "$pkgdir"/usr/lib/udev/rules.d/99-mythbackend.rules
}
