#
# Chakra Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=graphviz
pkgver=2.28.0
pkgrel=3
pkgdesc="Creates acircular graph images"
arch=('i686' 'x86_64')
license=('CPL')
depends=('gd' 'librsvg' 'libxaw' 'ghostscript' 'pango')
makedepends=('swig' 'guile' 'lua' 'ocaml' 'perl' 'php' 'python2' 'r' 'ruby' 'tk' 'qt')
optdepends=('guile: guile bindings'
            'lua: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'php: php bindings'
            'python2: python bindings'
            'r: r bindings'
            'ruby: ruby bindings'
            'tcl: tcl bindings'
            'qt: gvedit')
url="http://www.graphviz.org/"
options=('!libtool')
categories=('graphics')
source=(${url}/pub/${pkgname}/stable/SOURCES/${pkgname}-${pkgver}.tar.gz
       'LICENSE')
install=graphviz.install
sha1sums=('4725d88a13e071ee22e632de551d4a55ca08ee7d'
          '5a83febc0974e17d6b2356a4b1be9a834b715378')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i "/RUBY_INCLUDES=\"/s|\"$| `eval echo -I/usr/include/ruby-1.9.1/{$CARCH-linux,ruby/backward,}`\"|" configure
	sed -i '/LIBPOSTFIX="64"/d' configure
	export PYTHON=python2

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# cf. http://www.graphviz.org/License.php
	install -Dm644 ../LICENSE "${pkgdir}"/usr/share/licenses/graphviz/LICENSE
}
