#
# Chakra Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=graphviz
pkgver=2.30.1
pkgrel=1
pkgdesc="Creates acircular graph images"
url="http://www.graphviz.org/"
categories=('graphics')
arch=('x86_64')
license=('CPL')
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango')
makedepends=('swig' 'mono' 'guile' 'lua' 'ocaml' 'perl' 'php' 'python2' 'r' 'ruby' 'tk' 'qt')
optdepends=('mono: sharp bindings'
            'guile: guile bindings'
            'lua: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'php: php bindings'
            'python2: python bindings'
            'r: r bindings'
            'ruby: ruby bindings'
            'tcl: tcl bindings'
            'qt: gvedit')
options=('!libtool')
source=(${url}/pub/${pkgname}/stable/SOURCES/${pkgname}-${pkgver}.tar.gz
        'LICENSE')
install=graphviz.install
sha1sums=('96739220c4bbcf1bd3bd52e7111f4e60497185c6'
          '5a83febc0974e17d6b2356a4b1be9a834b715378'
          '8aab7295d1c59e4076c8ca7bcc6cd230edaa547c')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed \
		-e 's: qw(\([^)]*\)): (qw(\1)):g' \
		-i config/config_perl.pl
	sed \
		-e '/"archdir"/s:$:+" '"$(echo -I/usr/include/ruby-1.9.1/{$CARCH-linux,ruby/backward,})"'":' \
		-e '/CONFIG/s:site:vendor:' \
		-i config/config_ruby.rb
	sed \
		-e '/LIBPOSTFIX="64"/d' \
		-i configure
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export PYTHON=python2

	# install the graph and cgraph api alongside
	sed 's/@WITH_CGRAPH_FALSE@//g' -i lib/graph/Makefile.in

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# cf. http://www.graphviz.org/License.php
	install -Dm644 ../LICENSE "${pkgdir}"/usr/share/licenses/graphviz/LICENSE

	# everything has been built against cgraph, but use graph as default api
	sed '/#define WITH_CGRAPH 1/d' -i "${pkgdir}"/usr/include/graphviz/types.h
}
