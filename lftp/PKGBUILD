pkgname=lftp
pkgver=4.7.5
pkgrel=2
pkgdesc="Sophisticated command line based FTP client"
arch=('x86_64')
url="https://lftp.tech/"
license=('GPL3')
depends=('gcc-libs' 'readline' "gnutls>=2.6.8" "expat>=2.0.1" 'sh')
optdepends=('perl: needed for convert-netscape-cookies and verify-file')
categories=('network')
backup=('etc/lftp.conf')
source=(http://lftp.yar.ru/ftp/${pkgname}-${pkgver}.tar.xz{,.asc})
md5sums=('88c1505f8264429353ea7f249597eeb7'
         'SKIP')
validpgpkeys=('C027FA3148652A5513036413A824BB69F2A99A18') # "Alexander V. Lukyanov <lav@yars.free.net>"

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-gnutls \
    --without-openssl \
    --without-included-regex \
    --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/lib
}
