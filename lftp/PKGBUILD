pkgname=lftp
pkgver=4.7.0
pkgrel=1
pkgdesc="Sophisticated command line based FTP client"
arch=('x86_64')
url="http://lftp.yar.ru/"
license=('GPL3')
depends=('gcc-libs' 'readline' "gnutls>=2.6.8" "expat>=2.0.1" 'sh')
optdepends=('perl: needed for convert-netscape-cookies and verify-file')
categories=('network')
backup=('etc/lftp.conf')
source=(http://lftp.yar.ru/ftp/${pkgname}-${pkgver}.tar.xz{,.asc})
md5sums=('090595bc34b88f32e7914afe3ac923f7'
         'SKIP')
validpgpkeys=('C027FA3148652A5513036413A824BB69F2A99A18') # "Alexander V. Lukyanov <lav@yars.free.net>"

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-gnutls \
    --without-openssl \
    --without-included-regex \
    --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/lib
}
