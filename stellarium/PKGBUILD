#
# Apps Packages for Chakra, part of chakraos.org
#
# Maintainer: Martín González <koteccr at gmail dot com>

pkgname=stellarium
pkgver=0.13.2
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=('x86_64')
url="http://www.stellarium.org/"
screenshot=('http://www.stellarium.org/img/screenshots/0.10-from-mars.jpg')
license=('GPL2')
depends=('libpng' 'libgl' 'qt5-quick1' 'glu' 'openssl' 'zlib' 'gettext' 
         'xdg-utils' 'freetype2' 'phonon' 'qt5-multimedia' 'desktop-file-utils')
makedepends=('cmake' 'boost' 'mesa' 'mesa-libgl' 'qt5-tools')
categories=('education')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e5718e28d484b6f16e13f334fe91c3611557b46b7989f4bf68fdcb6d76c059ab')

build() {
  cd ${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RELEASE -DENABLE_SOUND=1
  make
}

package() {
  cd ${pkgname}-${pkgver}
 
  make DESTDIR="${pkgdir}" install

  # install desktop file
  install -Dm644 data/stellarium.desktop \
	"${pkgdir}/usr/share/applications/stellarium.desktop"

  # install desktop icons
  for i in 16 32 48 64 128 256 512; do
    install -Dm644 data/icons/${i}x${i}/stellarium.png \
      "${pkgdir}/usr/share/icons/hicolor/apps/${i}x${i}/stellarium.png"
  done    
}
