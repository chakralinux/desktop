#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Martín González <koteccr at gmail dot com>

pkgname=stellarium
pkgver=0.12.0
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=('x86_64')
url="http://www.stellarium.org/"
screenshot=('http://www.stellarium.org/img/screenshots/0.10-from-mars.jpg')
license=('GPL2')
depends=('mesa' 'libgl' 'qt' 'openssl' 'zlib' 'gettext')
makedepends=('cmake' 'boost')
categories=('education')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('1bcff9abc5eb8acd56acd13211fb1a0b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RELEASE
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
 
  make DESTDIR=${pkgdir} install

  # install desktop file
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/data/stellarium.desktop \
	${pkgdir}/usr/share/applications/stellarium.desktop

  # install desktop icons
  for i in 16 32 48 64 128 256 512; do
    install -Dm644 ${srcdir}/${pkgname}-${pkgver}/data/icons/${i}x${i}/stellarium.png \
      ${pkgdir}/usr/share/icons/hicolor/apps/${i}x${i}/stellarium.png
  done    
}
