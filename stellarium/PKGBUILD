#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Martín González <koteccr at gmail dot com>

pkgname=stellarium
pkgver=0.13.1
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=('x86_64')
url="http://www.stellarium.org/"
screenshot=('http://www.stellarium.org/img/screenshots/0.10-from-mars.jpg')
license=('GPL2')
depends=('libpng' 'libgl' 'qt5-quick1' 'glu' 'openssl' 'zlib' 'gettext' 
         'xdg-utils' 'freetype2' 'phonon' 'qt5-multimedia')
makedepends=('cmake' 'boost' 'mesa' 'mesa-libgl' 'qt5-tools')
categories=('education')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "stellarium-desktop.patch")
sha256sums=('ceb69e058545236e46406adbb8e2cbb0a26078eae471a945df09131f61cc3278'
            '50485fc974ce965039f825ca33f4bb4cf5a4dea76b4fce1a635c3027c287c7eb')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ../stellarium-desktop.patch
}

build() {
  cd ${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RELEASE -DENABLE_SOUND=1
  make
}

package() {
  cd ${pkgname}-${pkgver}
 
  make DESTDIR="${pkgdir}" install

  # install desktop file
  install -Dm644 data/stellarium.desktop \
	"${pkgdir}/usr/share/applications/stellarium.desktop"

  # install desktop icons
  for i in 16 32 48 64 128 256 512; do
    install -Dm644 data/icons/${i}x${i}/stellarium.png \
      "${pkgdir}/usr/share/icons/hicolor/apps/${i}x${i}/stellarium.png"
  done    
}
