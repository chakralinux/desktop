# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=pcsclite
_pkgname=pcsc-lite
pkgver=1.8.11
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('udev')
options=('!libtool' '!docs')
_srcver=3991
source=("https://alioth.debian.org/frs/download.php/file/${_srcver}/${_pkgname}-${pkgver}.tar.bz2")
sha256sums=('945041c94c53959ae5a767616a4ec5099fe67f549bfd344e8bd0cfe7a3c71ac6')

build() {

  cd "${srcdir}/${_pkgname}-${pkgver}"
  sed -i -e "s:python:python2:g" src/spy/pcsc-spy

  ./configure --enable-ipcdir=/run/pcscd \
              --enable-libudev \
              --enable-usbdropdir=/usr/lib/pcsc/drivers \
              --prefix=/usr \
              --sysconfdir=/etc \
              --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D "${srcdir}/${_pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -d "${pkgdir}/usr/lib/pcsc/drivers"
}
