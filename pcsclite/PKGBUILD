#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=pcsclite
_pkgname=pcsc-lite
pkgver=1.8.6
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('udev')
options=('!libtool' '!docs')
source=("https://alioth.debian.org/frs/download.php/3757/${_pkgname}-${pkgver}.tar.bz2")
sha256sums=('1e44049168d1ce55fd56c175f61206955254df0f385455f2a20ec7e8a0e6a77a')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  sed -i -e "s:python:python2:g" src/spy/pcsc-spy

  ./configure --enable-ipcdir=/run/pcscd \
              --enable-libudev \
              --enable-usbdropdir=/usr/lib/pcsc/drivers \
              --prefix=/usr \
              --sysconfdir=/etc \
              --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D "${srcdir}/${_pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -d "${pkgdir}/usr/lib/pcsc/drivers"
}
