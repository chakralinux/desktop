# Include global configuration

pkgname=dragon
pkgver=18.08.0
pkgrel=1
pkgdesc="A simple multimedia player"
url='http://kde.org/applications/multimedia/dragonplayer/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('hicolor-icon-theme' 'kparts')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdemultimedia')
conflicts=('kdemultimedia-dragonplayer')
replaces=('kdemultimedia-dragonplayer')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a6f7ae038b3f7f753bec5e99e9b257225f48000cc4b1d5d0ce3bb3d3beb51b08'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
