# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../plasma.conf

pkgname=breeze
pkgver=${PSubVersion}
pkgrel=1
pkgdesc='A plasma theme'
arch=('x86_64')
url='projects.kde.org/'
license=('LGPL')
depends=('frameworkintegration' 'kdecoration')
makedepends=('extra-cmake-modules')
checkdepends=("cmake")
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "maximize-window-button.patch")
sha256sums=( $(getSum ${pkgname})
            '694444a32cbb7cf368f9933f39083508eedb3c39028f12caeba9d5e839c0d39e')

prepare() {
  mkdir -p build

  cd ${pkgname}-${PSubVersion}
  patch -Np1 -i ../maximize-window-button.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${PSubVersion}
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # make QtCurve preset and color scheme available for KDE4 too
  install -d "$pkgdir"/usr/share/apps/{QtCurve,color-schemes}
  ln -s /usr/share/QtCurve/Breeze.qtcurve "$pkgdir"/usr/share/apps/QtCurve/
  ln -s /usr/share/color-schemes/Breeze{,Dark}.colors "$pkgdir"/usr/share/apps/color-schemes/
}
