# Maintainer: Michael Haesel <michael[dot]haesel[at]gmail[dot]com>
# Contributor: Carlos Garc√≠a carlos-lbs  <carlos.garcia(at)logicalbricks(dot)com>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds
 
_name=ocsync
pkgname=csync-owncloud
pkgver=0.91.4
pkgrel=1
pkgdesc="A file synchronizer especially designed for you, the normal user."
arch=("x86_64")
url="http://www.csync.org"
license=('GPL2')
depends=('sqlite3' 'iniparser' 'neon' 'libssh' 'smbclient')
makedepends=('cmake')
provides=('csync')
conflicts=('csync')
source=("http://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2")
sha256sums=('9ce5d04ff0475be4c3d3bfff43a554980d88277e95de4128a3f18611529b2cb6')

prepare() {
  if [[ -e ${_name}-${pkgver}-build ]]; then rm -rf ${_name}-${pkgver}-build; fi
  mkdir ${_name}-${pkgver}-build
}

build() {
  cd ${_name}-${pkgver}-build
  cmake -DCMAKE_BUILD_TYPE="Release" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSCONF_INSTALL_DIR=/etc \
        ../${_name}-${pkgver}
  make
}
 
package() {
  cd ${_name}-${pkgver}-build
   make DESTDIR="${pkgdir}" install
}

