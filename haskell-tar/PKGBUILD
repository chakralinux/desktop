# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tar
pkgname=haskell-tar
pkgver=0.4.0.1
pkgrel=3
pkgdesc="Reading, writing and manipulating tar archive files"
url="http://hackage.haskell.org/package/tar"
license=('custom:BSD3')
arch=('x86_64' 'i686')
makedepends=()
depends=('ghc=7.6.3-1' 'haskell-base>=3' 'haskell-base<5' 'haskell-bytestring' 'haskell-directory' 'haskell-filepath' 'haskell-old-time' 'haskell-time')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/tar/0.4.0.1/tar-0.4.0.1.tar.gz")
install="haskell-tar.install"
sha512sums=('7cb042f77dc70b856b4dfd5d0a06620f669f9260a84800ad27e1803ec24a50932bb3775b35a6cc91cefb30ea4cd2b453dd6566007d586327acea0edccc0dee47')

build() {
    cd "${srcdir}/tar-0.4.0.1"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix="/usr" \
        --docdir="/usr/share/doc/haskell-tar" \
        --libsubdir='$compiler/site-local/$pkgid'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/tar-0.4.0.1"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/haskell-tar/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/haskell-tar/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/haskell-tar/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/tar"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/haskell-tar/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/haskell-tar/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
