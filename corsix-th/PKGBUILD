#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=corsix-th
_pkgname=CorsixTH
pkgver=0.11
_pkgver=$pkgver
pkgrel=1
pkgdesc='Reimplementation of the game engine of Theme Hospital.'
arch=('i686' 'x86_64')
url='http://corsix-th.googlecode.com'
license=('MIT')
depends=('lua' 'sdl_mixer' 'timidity++')
categories=('games')
makedepends=('cmake')
install=$pkgname.install
source=("$url/files/$_pkgname-$_pkgver-Source.tar.gz"
        "$pkgname.sh")
sha1sums=('55a7cb58511830f8ca80a0074e27c941f175f530'
          '7fd6ae8db366b7f9c4671708e8ea7beb48f1bea3')

build() {
  cd $srcdir/

  mkdir -p build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/share \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    ..

  make
}

package() {
  cd $srcdir/build

  make DESTDIR=$pkgdir install

  # “Link” to de actual executable:
  install -Dm755 $srcdir/$pkgname.sh $pkgdir/usr/bin/$pkgname

  # License:
  install -Dm644 $srcdir/CorsixTH/LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/License.txt
}
