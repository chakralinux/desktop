# Contributions from Arch
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qtav
pkgver=1.12.0
pkgrel=1
pkgdesc="A cross-platform multimedia playback framework based on Qt and FFmpeg"
arch=('x86_64')
url="http://www.qtav.org/"
license=('LGPL')
depends=('qt5-declarative' 'qt5-svg' 'qt5-quickcontrols' 'ffmpeg' 'libxv' 'openal' 'uchardet' 'desktop-file-utils')
source=(https://github.com/wang-bin/QtAV/archive/v${pkgver}.tar.gz)
sha256sums=('65ab859985d73521c2d17e72117baae0f3f6242b69f59994acada76fa5364193')

prepare() {
  cd QtAV-${pkgver}

  # fix mkspecs install dir
  sed -e 's|\$\$\[QT_INSTALL_BINS\]\/\.\.\/mkspecs|\$\$\[QT_INSTALL_ARCHDATA\]\/mkspecs|g' -i tools/install_sdk/install_sdk.pro
}

build() {
  cd QtAV-${pkgver}

  qmake-qt5 "CONFIG+=no_rpath recheck"
  make
}

package() {
  cd QtAV-${pkgver}

  make INSTALL_ROOT="${pkgdir}" install
}
