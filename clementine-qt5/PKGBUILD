pkgname=clementine-qt5
_pkgname=Clementine
pkgver=1.3.1.r608.gfb0083546
pkgrel=1
pkgdesc="Experimental Qt5 version of Clementine, a modern music player and library organiser."
arch=('x86_64')
license=('GPL')
depends=('gstreamer' 'taglib' 'glew' 'desktop-file-utils' 'hicolor-icon-theme'
         'qt5-base' 'libechonest' 'protobuf' 'crypto++' 'chromaprint'
         'libmygpo-qt5' 'qt5-x11extras' 'libgpod' 'liblastfm' 'sparsehash' 'libcdio' 'libmtp' 'fftw')
makedepends=('git' 'boost' 'cmake' 'mesa' 'qt5-tools' 'gst-libav')
optdepends=('gst-plugins-base: for more open formats'
            'gst-plugins-good: for use with "Good" plugin libraries'
            'gst-plugins-bad: for use with "Bad" plugin libraries'
            'gst-plugins-ugly: for use with "Ugly" plugin libraries'
            'libspotify: add support for Spotify, due to license restriction, please build it 
                         with ccr yourself'
            'dropbox: add support for Dropbox')
url="http://www.clementine-player.org/"
source=('git+https://github.com/clementine-player/Clementine.git#branch=qt5'
        'spotify_install.patch' 'spotify_blob.patch')
sha256sums=('SKIP'
            '05a438fb445790200258c536d396a6bda9df38a9b775d5259421376ec1878786'
            '440b7e0b9b70d3e379ec9404eb09c8eca584188d81269904fd57375671a74a8e')
conflicts=('clementine')

pkgver() {
  cd ${_pkgname}
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${_pkgname}
  mkdir -p build
  sed 's,<lastfm5,<lastfm,g' -i src/internet/lastfm/* -i src/core/*.cpp
  patch -Np1 -i ../spotify_blob.patch
  patch -Np0 -i ../spotify_install.patch
}
build() {
  cd ${_pkgname}/build
  cmake ".." -DLASTFM5_INCLUDE_DIRS=/usr/include/lastfm \
             -DLASTFM51_INCLUDE_DIRS=/usr/include/lastfm \
             -DCMAKE_INSTALL_PREFIX=/usr \
             -DBUILD_WERROR=OFF
  make 
}

package(){
  cd ${_pkgname}/build
  make DESTDIR=${pkgdir} install
}
