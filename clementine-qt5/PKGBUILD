pkgname=clementine-qt5
_pkgname=Clementine
pkgver=1.3.1.r359.g987aa20b9
_pkgver=987aa20
pkgrel=2
pkgdesc="Experimental Qt5 version of Clementine, a modern music player and library organiser."
arch=('x86_64')
license=('GPL')
depends=('gstreamer' 'taglib' 'glew' 'desktop-file-utils' 'hicolor-icon-theme'
         'qt5-base' 'libechonest' 'protobuf' 'crypto++' 'chromaprint'
         'libmygpo-qt5' 'qt5-x11extras' 'libgpod' 'liblastfm' 'sparsehash' 'libcdio' 'libmtp' 'fftw')
makedepends=('git' 'boost' 'cmake' 'mesa' 'qt5-tools' 'gst-libav')
install=clementine.install
optdepends=('gst-plugins-base: for more open formats'
            'gst-plugins-good: for use with "Good" plugin libraries'
            'gst-plugins-bad: for use with "Bad" plugin libraries'
            'gst-plugins-ugly: for use with "Ugly" plugin libraries'
            'libspotify: add support for Spotify, due to license restriction, please build it 
                         with ccr yourself'
            'dropbox: add support for Dropbox')
url="http://www.clementine-player.org/"
source=("git+https://github.com/clementine-player/Clementine.git#commit=${_pkgver}")
sha256sums=('SKIP')
conflicts=('clementine')

pkgver() {
  cd ${srcdir}/${_pkgname}
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${srcdir}/${_pkgname}
  mkdir -p build
  sed 's,<lastfm5,<lastfm,g' -i src/internet/lastfm/* -i src/core/*.cpp
}
build() {
  cd ${srcdir}/${_pkgname}/build
  cmake ".." -DLASTFM5_INCLUDE_DIRS=/usr/include/lastfm \
             -DLASTFM51_INCLUDE_DIRS=/usr/include/lastfm \
             -DCMAKE_INSTALL_PREFIX=/usr \
             -DCMAKE_BUILD_TYPE=Release \
             -DBUILD_WERROR=OFF
  make 
}

package(){
  cd ${srcdir}/${_pkgname}/build
  make DESTDIR=${pkgdir} install
}
