pkgname=nmap
pkgver=7.30
pkgrel=1
pkgdesc="A network exploration tool and security/port scanner"
arch=('x86_64')
url="http://nmap.org"
license=('custom:GPL')
depends=('pcre' 'openssl' 'libpcap>=1.0.0' 'lua')
# pygtk is needed for zenmap
makedepends=('python2')
optdepends=('pygtk: gui for nmap - zenmap')
options=('!makeflags')
source=(http://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('ba38a042ec67e315d903d28a4976b74999da94c646667c0c63f31e587d6d8d0f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PYTHON=python2
  ./configure \
	  --prefix=/usr \
	  --libexecdir=/usr/lib \
	  --mandir=/usr/share/man

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # remove zenmap uninstall script
  rm "${pkgdir}/usr/bin/uninstall_zenmap"

  # install custom GPL2 license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
