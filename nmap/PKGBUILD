pkgname=nmap
pkgver=7.50
pkgrel=1
pkgdesc="A network exploration tool and security/port scanner"
arch=('x86_64')
url="http://nmap.org"
license=('custom:GPL')
depends=('pcre' 'openssl' 'libpcap>=1.0.0' 'lua')
# pygtk is needed for zenmap
makedepends=('python2')
optdepends=('pygtk: gui for nmap - zenmap')
options=('!makeflags')
source=(http://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e9a96a8e02bfc9e80c617932acc61112c23089521ee7d6b1502ecf8e3b1674b2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PYTHON=python2
  ./configure \
	  --prefix=/usr \
	  --libexecdir=/usr/lib \
	  --mandir=/usr/share/man

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # remove zenmap uninstall script
  rm "${pkgdir}/usr/bin/uninstall_zenmap"

  # install custom GPL2 license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
