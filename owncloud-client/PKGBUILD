# Maintainer: Manuel 'UtG' <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributor:  eyome <baikalink@hotmail.fr>
# Contributor: Carlos García carlos-lbs  <carlos.garcia(at)logicalbricks(dot)com>
# Contributor: Giuseppe Calà <jiveaxe@gmail.com>

_name=mirall
pkgname=owncloud-client
pkgver=1.7.0
pkgrel=1
pkgdesc="ownCloud client based on mirall"
arch=('x86_64')
url="http://owncloud.org/"
screenshot="http://owncloud.org/wp-content/uploads/2012/03/linux3.png"
license=('GPL2')
depends=('qtwebkit' 'neon' 'sqlite3' 'qtkeychain')
makedepends=('cmake' 'libssh' 'smbclient' 'desktop-file-utils')
groups=('network')
conflicts=('csync-owncloud')
backup=('etc/owncloud-client/sync-exclude.lst')
install=${pkgname}.install
source=("http://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2")
sha256sums=('b1cb0612e5022de263dc4c6309eba8207d694a40a80dae6762b4a56fa8d4d944')

prepare() {
  cd ${_name}-${pkgver}/
  if [[ -d build ]]; then rm -rf build; fi
  mkdir build
}

build() {
  cd ${_name}-${pkgver}/build

  cmake -DBUILD_WITH_QT4=on \
        -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        -WITH_DOC=TRUE \
        -DMEM_NULL_TESTS=On -DUNIT_TESTING=On \
        -DCMAKE_BUILD_TYPE="Release" \
        ..
  make
}

check() {
  cd ${_name}-${pkgver}/build
  make test
}
 
package() {
  cd ${_name}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
  
  # Fix desktop file
  desktop-file-edit --set-key="Keywords" --set-value="ownCloud;syncing;file;sharing;" \
    "${pkgdir}/usr/share/applications/owncloud.desktop"
}
