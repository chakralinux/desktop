# Maintainer: Manuel 'UtG' <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributor:  eyome <baikalink@hotmail.fr>
# Contributor: Carlos García carlos-lbs  <carlos.garcia(at)logicalbricks(dot)com>
# Contributor: Giuseppe Calà <jiveaxe@gmail.com>

_name=mirall
pkgname=owncloud-client
pkgver=1.6.0
pkgrel=5
pkgdesc="ownCloud client based on mirall"
arch=('x86_64')
url="http://owncloud.org/"
screenshot="http://owncloud.org/wp-content/uploads/2012/03/linux3.png"
license=('GPL2')
depends=('qtwebkit' 'neon' 'sqlite3' 'qtkeychain')
makedepends=('cmake' 'libssh' 'smbclient' 'desktop-file-utils')
groups=('network')
conflicts=('csync-owncloud')
backup=('etc/owncloud-client/sync-exclude.lst')
install=${pkgname}.install
source=("http://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2"
        'man.patch')
sha256sums=('12b5da4b5598db3ef5d4d26e464de3534f3a6b8a864719949d82c77cfc353ab5'
            '1cec1ec7761d087e197f0ccdf5e1e8236db0bd29a1babe49130c00f81e82e011')

prepare() {
  cd ${srcdir}/${_name}-${pkgver}/
  if [[ -d build ]]; then rm -rf build; fi
  mkdir build
  msg 'patch the man directory'
  patch -p0 -i "${srcdir}/man.patch"
}

build() {
  cd ${srcdir}/${_name}-${pkgver}/build

  cmake -DBUILD_WITH_QT4=on \
        -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        -WITH_DOC=TRUE \
        -DMEM_NULL_TESTS=On -DUNIT_TESTING=On \
        -DCMAKE_BUILD_TYPE="Release" \
        ..
  make
}

check() {
  cd ${srcdir}/${_name}-${pkgver}/build
  make test
}
 
package() {
  cd ${srcdir}/${_name}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
  
  # Fix desktop file
  desktop-file-edit --set-key="Keywords" --set-value="ownCloud;syncing;file;sharing;" \
    "${pkgdir}/usr/share/applications/owncloud.desktop"
}
