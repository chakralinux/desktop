_name=owncloudclient
pkgname=owncloud-client
pkgver=2.0.1
pkgrel=2
pkgdesc="ownCloud client based on mirall"
arch=('x86_64')
url="http://owncloud.org/"
screenshot="http://owncloud.org/wp-content/uploads/2012/03/linux3.png"
license=('GPL2')
depends=('qtkeychain-qt5' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('cmake' 'python3-sphinx' 'qt5-tools' 'doxygen')
groups=('network')
conflicts=('csync-owncloud')
backup=('etc/owncloud-client/sync-exclude.lst')
install=${pkgname}.install
source=("https://download.owncloud.com/desktop/stable/$_name-$pkgver.tar.xz")
sha512sums=('a09044c5c74c15761d0a531908ac1882f77f02e7199af4f8370253ae5f8d0966609d706a771f074d3c686d33f1db260e61be94a1f8fca239aa6d79d30093ef41')

build() {
  mkdir -p build && cd build

  cmake ../${_name}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
