# Maintainer: Michael Haesel <michael[dot]haesel[at]gmail[dot]com>
# Contributor:  eyome <baikalink@hotmail.fr>
# Contributor: Carlos García carlos-lbs  <carlos.garcia(at)logicalbricks(dot)com>
# Contributor: Giuseppe Calà <jiveaxe@gmail.com>
# http://gitorious.org/chakra-packages/apps/trees/master/owncloud-client

_buildtype="Release"
_name=mirall
pkgname=owncloud-client
pkgver=1.4.0
pkgrel=1
pkgdesc="ownCloud client based on mirall"
arch=('x86_64')
url="http://owncloud.org/"
screenshot="http://en.opensuse.org/images/b/bb/New_oc_connect.png"
license=('GPL2')
depends=('qtwebkit' 'csync-owncloud>=0.90.0' 'neon' 'sqlite3')
makedepends=('cmake' 'libssh' 'smbclient' 'desktop-file-utils')
provides=('mirall' 'owncloud-client')
conflicts=('mirall-git' 'mirall')
categories=('network')
options=()
backup=('etc/owncloud-client/sync-exclude.lst')
install=${pkgname}.install
source=("http://download.owncloud.com/download/${_name}-${pkgver}.tar.bz2")
sha256sums=('8efd1d779c7c21cd50860aa74bffefad536df5637da4a59648ea39ce6b0a3737')

if [[ ! ${_buildtype} == "Release" ]] && [[ ! ${_buildtype} == "release" ]]; then
  options=(!strip)
fi

prepare() {
  if [[ -e "${srcdir}/${_name}-${pkgver}-build" ]]; then rm -rf "${srcdir}/${_name}-${pkgver}-build"; fi
  mkdir "${srcdir}/${_name}-${pkgver}-build"
}

build() {
  cd "${srcdir}/${_name}-${pkgver}-build"
 
  cmake -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCSYNC_INCLUDE_PATH=/usr/include/ocsync \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        -WITH_DOC=TRUE \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        ../${_name}-${pkgver}
  make
}
 
package() {
  cd "${srcdir}/${_name}-${pkgver}-build"
 
  make DESTDIR="${pkgdir}" install
  
  # Fix desktop file
  desktop-file-edit --set-key="Keywords" --set-value="ownCloud;syncing;file;sharing;" \
    "${pkgdir}/usr/share/applications/owncloud.desktop"
}
