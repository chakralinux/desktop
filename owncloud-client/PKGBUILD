# Maintainer: Michael Haesel <michael[dot]haesel[at]gmail[dot]com>
# Contributor:  eyome <baikalink@hotmail.fr>
# Contributor: Carlos García carlos-lbs  <carlos.garcia(at)logicalbricks(dot)com>
# Contributor: Giuseppe Calà <jiveaxe@gmail.com>

_name=mirall
pkgname=owncloud-client
pkgver=1.5.3
pkgrel=1
pkgdesc="ownCloud client based on mirall"
arch=('x86_64')
url="http://owncloud.org/"
screenshot="http://owncloud.org/wp-content/uploads/2012/03/linux3.png"
license=('GPL2')
depends=('qtwebkit' 'neon' 'sqlite3' 'qtkeychain')
makedepends=('cmake' 'libssh' 'smbclient' 'desktop-file-utils')
categories=('network')
conflicts=('csync-owncloud')
backup=('etc/owncloud-client/sync-exclude.lst')
install=${pkgname}.install
source=("http://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2")
sha256sums=('a50f235eba7a24915e583c3c5bb68ccd86171296151a9520f5a375842b3ced8e')

prepare() {
  if [[ -e ${_name}-${pkgver}-build ]]; then rm -rf ${_name}-${pkgver}-build; fi
  mkdir ${_name}-${pkgver}-build
}

build() {
  cd ${_name}-${pkgver}-build
 
  cmake -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCSYNC_INCLUDE_PATH=/usr/include/ocsync \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        -WITH_DOC=TRUE \
        -DCMAKE_BUILD_TYPE="Release" \
        ../${_name}-${pkgver}
  make
}
 
package() {
  cd ${_name}-${pkgver}-build
 
  make DESTDIR="${pkgdir}" install
  
  # Fix desktop file
  desktop-file-edit --set-key="Keywords" --set-value="ownCloud;syncing;file;sharing;" \
    "${pkgdir}/usr/share/applications/owncloud.desktop"
}
