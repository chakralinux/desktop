_name=owncloudclient
pkgname=owncloud-client
pkgver=1.8.1
pkgrel=1
pkgdesc="ownCloud client based on mirall"
arch=('x86_64')
url="http://owncloud.org/"
screenshot="http://owncloud.org/wp-content/uploads/2012/03/linux3.png"
license=('GPL2')
depends=('qtwebkit' 'neon' 'sqlite3' 'qtkeychain')
makedepends=('cmake' 'libssh' 'smbclient' 'python3-sphinx')
groups=('network')
conflicts=('csync-owncloud')
backup=('etc/owncloud-client/sync-exclude.lst')
install=${pkgname}.install
source=("http://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.xz")
sha256sums=('8b9bb4fea2a4b80938a3477813839ac325ac1ed39a85a8f1e0cd3ebb0e430ea3')

prepare() {
  cd ${_name}-${pkgver}/
  if [[ -d build ]]; then rm -rf build; fi
  mkdir build
}

build() {
  cd ${_name}-${pkgver}/build

  cmake -DBUILD_WITH_QT4=on \
        -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        -WITH_DOC=TRUE \
        -DMEM_NULL_TESTS=On -DUNIT_TESTING=On \
        -DCMAKE_BUILD_TYPE="Release" \
        ..
  make
  make doc-man
}
 
package() {
  cd ${_name}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}
