# Contributions from Arch: 

_name=client
pkgname=owncloud-client
pkgver=2.2.2
pkgrel=1
pkgdesc="ownCloud client based on mirall"
arch=('x86_64')
url="http://owncloud.org/"
screenshot="http://owncloud.org/wp-content/uploads/2012/03/linux3.png"
license=('GPL2')
depends=('qtkeychain-qt5' 'qt5-webkit' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('extra-cmake-modules' 'python3-sphinx' 'qt5-tools' 'doxygen' 'kio')
groups=('network')
conflicts=('csync-owncloud')
backup=('etc/owncloud-client/sync-exclude.lst')
install=${pkgname}.install
source=("https://github.com/owncloud/client/archive/v${pkgver}.tar.gz")
sha256sums=('7f789af156b30defec9f2608e682367a823684dd13917187db4a59e9b2a3e8c2')

build() {
  mkdir -p build && cd build

  cmake ../${_name}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 

  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
