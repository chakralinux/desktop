#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=spring
_pkgname=springrts
pkgver=92.0
pkgrel=1
pkgdesc='A 3D real-time-strategy game engine.'
arch=('i686' 'x86_64')
url="http://springrts.com/"
license=('GPL2')
depends=('boost' 'devil' 'freetype2' 'glew' 'icu' 'libvorbis' 'openal')
makedepends=('cmake' 'java-environment' 'lzma-utils' 'p7zip' 'python' 'zip')
optdepends=('java-runtime: java-based bots.')
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/${pkgname}_${pkgver}_src.tar.lzma")
md5sums=('6dcf665cfe7d6eaf0e287086685c6709')

build() {
  bsdtar -xf ${pkgname}_${pkgver}_src.tar.lzma
  
  cd ${pkgname}_$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    .

  make
}

package() {
  cd ${pkgname}_$pkgver
  make DESTDIR=$pkgdir install

  install -d $pkgdir/etc/spring
  echo '$HOME/.spring' > $pkgdir/etc/spring/datadir
}
