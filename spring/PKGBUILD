pkgname=spring
_pkgname=springrts
pkgver=104.0
pkgrel=1
pkgdesc='A 3D real-time-strategy game engine.'
arch=('x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl2' 'libunwind'
         'libxcursor' 'curl' 'shared-mime-info' 'desktop-file-utils' 'libx11')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python2' 'java-environment' 'mesa')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
source=("https://downloads.sourceforge.net/sourceforge/$_pkgname/${pkgname}_${pkgver}_src.tar.lzma"
        fix-reinterpret_cast-in-constexpr.patch)
sha256sums=('ceb0bf59f8aac14f2d844f920e898724913326cdf1adf3f10d88ecc317a3ec16'
            '982b8a4da8e6ccb8f4f977d7f75de0206ad4ccf488d0843ad37bab34f7e35bca')

prepare() {
  cd spring_$pkgver

  # https://springrts.com/mantis/view.php?id=5781
  # https://github.com/spring/spring/commit/6104061fe3
  patch -Np1 -i ../fix-reinterpret_cast-in-constexpr.patch
}

build() {
  bsdtar -xf ${pkgname}_${pkgver}_src.tar.lzma
  
  cd ${pkgname}_$pkgver

  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd spring_${pkgver}

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/etc/spring"
  echo '$HOME/.local/share/spring' > "$pkgdir/etc/spring/datadir"
}
