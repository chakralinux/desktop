#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=spring
_pkgname=springrts
pkgver=0.82.7.1
pkgrel=6
pkgdesc='A 3D real-time-strategy game engine.'
arch=('i686' 'x86_64')
url="http://springrts.com/"
license=('GPL2')
depends=('boost' 'devil' 'freetype2' 'glew' 'icu' 'libvorbis' 'openal')
makedepends=('cmake' 'java-environment' 'lzma-utils' 'p7zip' 'python' 'zip')
optdepends=('java-runtime: java-based bots.')
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/${pkgname}_${pkgver}_src.tar.lzma")
md5sums=('378cf0b18a5dd5b840964e5945778503')

build() {
  bsdtar -xf ${pkgname}_${pkgver}_src.tar.lzma
  
  cd ${pkgname}_$pkgver
  
  sed -i '1i\
	  #include <list>' rts/lib/lobby/Connection.h

  cmake \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=RelWithDebInfo \
  -DDATADIR=share/spring \
  .

  make
}

package() {
  cd ${pkgname}_$pkgver
  make DESTDIR=$pkgdir install

  install -d $pkgdir/etc/spring
  echo '$HOME/.spring' > $pkgdir/etc/spring/datadir
}
