# Include global configuration
source ../kdeapps.conf

pkgname=('kdeaccessibility-kaccessible')
_pkgname="kaccessible"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc='Provides accessibility services like focus tracking and a screenreader'
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
depends=("kde-runtime"
         'speech-dispatcher')
conflicts=("kdeaccessibility-doc" "kdeaccessibility-colorschemes" "kdeaccessibility-iconthemes")
install='kdeaccessibility.install'
provides=('kdeaccessibility-common' 'kaccessible')
replaces=('kdeaccessibility-common')
groups=("kdeaccessibility" "kde" "kde-uninstall" "kde-doc")
options=('docs' 'debug')
makedepends=('pkgconfig' 'cmake' 'automoc4' "kdelibs>=${_libsver}" 'docbook-xsl')
categories=('accessibility')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz"{,.sig})
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
                        'SKIP')
validpgpkeys=(${Avalidpgpkeys})

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
