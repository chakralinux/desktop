# Maintainer: Manuel 'UtG' <utg[dot]chakra.linux[at]gmail[dot]com>

pkgname=python2-sipsimple
pkgver=1.3.0
pkgrel=1
pkgdesc="SIP SIMPLE Client SDK is a Software Development Kit for easy development of SIP end-points that support rich media like Audio, Instant Messaging, File Transfers, Desktop Sharing and Presence"
arch=(x86_64)
#url="http://download.ag-projects.com/SipClient"
url="http://sipsimpleclient.com/"
license=('custom:MIT' 'LGPL')
depends=('python2>=2.7'
         'python2-gnutls>=1.1.9'
         'python2-application>=1.2.8'
         'python2-backports>=1.0.0'
         'python2-cjson>=1.0.5'
         'python2-dateutil>=1.4'
         'python2-eventlet>=0.8.11'
         'python2-greenlet>=0.4.0'
         'python2-lxml>=2.1.2'
         'python2-msrplib'
         'python2-xcaplib'
         'python2-cython>=0.13.0'
         'python2-dns>=1.6.0'
         'python2-twisted>=8.1.0'
         'python2-zope-interface>=3.3.1'
         'python2-imaging>=1.1.6'
         'openssl'
         'gnutls>=2.4.1'
         'libxml2>=2.6.32'
         'libxslt>=1.1.24'
         'util-linux'
         'alsa-lib')
makedepends=('python2-setuptools'
             'subversion'
             'darcs')
source=("http://download.ag-projects.com/SipClient/python-sipsimple-$pkgver.tar.gz")
md5sums=('222f6c0e845508f775063fe8b9cc8fb4')

build() {
  cd "$srcdir/python-sipsimple-$pkgver"
  python2 setup.py build_ext --pjsip-clean-compile
}

package() {
  cd "$srcdir/python-sipsimple-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  # license
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

