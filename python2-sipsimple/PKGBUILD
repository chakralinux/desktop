#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <jmc@chakra-project.org>
#

pkgname=python2-sipsimple
pkgver=20120826
pkgrel=1
pkgdesc="SIP SIMPLE Client SDK is a Software Development Kit for easy development of SIP end-points that support rich media like Audio, Instant Messaging, File Transfers, Desktop Sharing and Presence."
arch=('i686' 'x86_64')
license=('LGPL')
depends=('python2>=2.7'
         'python2-gnutls>=1.1.9'
         'python2-application>=1.2.8'
         'python2-backports>=1.0.0'
         'python2-cjson>=1.0.5'
         'python-dateutil>=1.4'
         'python2-eventlet>=0.8.11'
         'python2-greenlet>=0.4.0'
         'python-lxml>=2.1.2'
         'python2-msrplib'
         'python2-xcaplib'
         'python-cython>=0.13.0'
         'python-dnspython>=1.6.0'
         'twisted>=8.1.0'
         'zope-interface>=3.3.1'
         'python-imaging>=1.1.6'
         'openssl'
         'gnutls>=2.4.1'
         'libxml2>=2.6.32'
         'libxslt>=1.1.24')
makedepends=('setuptools>=0.6c9'
             'subversion'
             'darcs')
optdepends=('python2-sipclients: command line programs to test the SIP SIMPLE Client library')
url="http://sipsimpleclient.com/"

_darcstrunk="http://devel.ag-projects.com/repositories"
_darcsmod="python-sipsimple"

build() {
  msg "Starting Darcs pull..."

  cd "${srcdir}"

  if [ -d "${_darcsmod}/_darcs" ]; then
    (cd "${_darcsmod}" && darcs pull -a)
  else
    darcs get "${_darcstrunk}/${_darcsmod}" "${_darcsmod}"
  fi

  msg "Darcs pull done (or server timeout)."

  cd "${srcdir}/${_darcsmod}"
  MAKEFLAGS=-j1 python2 setup.py build_ext --pjsip-clean-compile --pjsip-disable-assertions
}

package() {
  cd "${srcdir}/${_darcsmod}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
