#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=openttd
pkgver=1.2.2
pkgrel=1
pkgdesc="An engine for running Transport Tycoon Deluxe or alternative data files"
arch=('i686' 'x86_64')
url='http://www.openttd.org'
license=('GPL2')
depends=('desktop-file-utils' 'fontconfig' 'hicolor-icon-theme' 'icu' 'libpng' 'sdl')
optdepends=('openttd-opengfx: graphic set.' 'openttd-openmsx: music set.' 'openttd-opensfx: sound set.')
conflicts=('openttd-dev' 'openttd-svn')
changelog=ChangeLog
categories=('games')
source=("http://binaries.openttd.org/releases/$pkgver/$pkgname-$pkgver-source.tar.xz")
md5sums=('67aef6cab3aff9ca7d8e16f7db344191')

build() {
  cd $srcdir/$pkgname-$pkgver 

 ./configure \
    --prefix-dir=/usr \
    --binary-name=$pkgname \
    --binary-dir=bin \
    --data-dir=share/$pkgname \
    --install-dir=$pkgdir \
    --doc-dir=share/doc/$pkgname \
    --menu-name="OpenTTD" \
    --personal-dir=.$pkgname    

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver 
  make install
}
