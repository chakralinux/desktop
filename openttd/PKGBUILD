#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=openttd
pkgver=1.1.0
pkgrel=1
pkgdesc="An engine for running Transport Tycoon Deluxe or alternative data files."
arch=('i686' 'x86_64')
url='http://www.openttd.org'
license=('GPL2')
depends=('desktop-file-utils' 'fontconfig' 'hicolor-icon-theme' 'icu' 'libpng' 'sdl')
conflicts=('openttd-dev' 'openttd-svn')
install=$pkgname.install
changelog=ChangeLog
source=("http://binaries.openttd.org/releases/$pkgver/$pkgname-$pkgver-source.tar.xz")
md5sums=('b5b1f2ecc7327505e360047236b09d06')

build() {
  cd $srcdir/$pkgname-$pkgver 

 ./configure \
    --prefix-dir=/usr \
    --binary-name=$pkgname \
    --binary-dir=bin \
    --data-dir=share/$pkgname \
    --install-dir=$pkgdir \
    --doc-dir=share/doc/$pkgname \
    --menu-name="OpenTTD" \
    --personal-dir=.$pkgname    

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver 
  make install
}
