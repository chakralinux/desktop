# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=openttd
pkgver=1.5.3
pkgrel=1
pkgdesc="An engine for running Transport Tycoon Deluxe or alternative data files"
arch=('x86_64')
url='http://www.openttd.org'
license=('GPL2')
depends=('desktop-file-utils' 'fontconfig' 'hicolor-icon-theme' 'icu' 'libpng' 'sdl' 'xz')
optdepends=('openttd-opengfx: graphic set.'
            'openttd-openmsx: music set.'
            'openttd-opensfx: sound set.')
conflicts=('openttd-dev' 'openttd-svn')
changelog=ChangeLog
categories=('games')
source=("http://binaries.openttd.org/releases/$pkgver/$pkgname-$pkgver-source.tar.xz")
md5sums=('d7a07405756ed81d40b8d702e164f61c')

build() {
  cd $pkgname-$pkgver 

 ./configure \
    --prefix-dir=/usr \
    --binary-name=$pkgname \
    --binary-dir=bin \
    --data-dir=share/$pkgname \
    --install-dir=$pkgdir \
    --doc-dir=share/doc/$pkgname \
    --menu-name="OpenTTD" \
    --personal-dir=.$pkgname    

  make
}

package() {
  cd $pkgname-$pkgver 
  make install
}
