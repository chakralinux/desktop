#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=flare
_pkgname=clintbellanger-$pkgname
pkgver=0.14.1
_pkgver=05b5a9c
_pkgver2=v$pkgver-0-g$_pkgver
pkgrel=1
pkgdesc="Action roleplaying game from an isometric perspective."
arch=('i686' 'x86_64')
url="http://clintbellanger.net/rpg/"
license=('GPL2' 'CCPL-by-sa')
depends=('sdl' 'sdl_image' 'sdl_mixer')
replaces=('osare')
changelog=ChangeLog
source=(https://download.github.com/clintbellanger-$pkgname-$_pkgver2.tar.gz)
md5sums=('f53b8060e897ea5161f7bc34450005fe')

build() {
  cd $srcdir/$_pkgname-$_pkgver/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    .
  make
}

package() {
  cd $srcdir/$_pkgname-$_pkgver

  msg "Installing basic directory structure..."
  install -m755 -d $pkgdir/usr/{bin,share/$pkgname}

  msg "Installing executable..."
  install -m755 $pkgname $pkgdir/usr/share/$pkgname/$pkgname

  msg "Installing media..."
  for folder in animations art enemies engine fonts images itemdb items maps music npcs powers quests soundfx tiled tilesetdefs
  do
    cp -R $folder $pkgdir/usr/share/$pkgname/
  done

  msg "Installing execution script..."
  cat > $pkgdir/usr/bin/$pkgname << EOF
#!/bin/sh
cd /usr/share/$pkgname/
./$pkgname
EOF
  chmod +x $pkgdir/usr/bin/$pkgname
}
