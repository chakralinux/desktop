#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=flare
pkgver=0.13
_pkgver=013
pkgrel=1
pkgdesc="Action roleplaying game from an isometric perspective."
arch=('i686' 'x86_64')
url="http://clintbellanger.net/rpg/"
license=('GPL2' 'CCPL-by-sa')
depends=('sdl' 'sdl_image' 'sdl_mixer')
replaces=('osare')
changelog=ChangeLog
source=("http://$pkgname-engine.googlecode.com/files/${pkgname}_src_v$_pkgver.zip")
md5sums=('a674696506cb316eb7ef98b12c98d06f')

build() {
  cd $srcdir/${pkgname}_src_v$_pkgver/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    .
  make
}

package() {
  cd $srcdir/${pkgname}_src_v$_pkgver/

  msg "Installing basic directory structure..."
  install -m755 -d $pkgdir/usr/{bin,share/$pkgname}

  msg "Installing executable..."
  install -m755 $pkgname $pkgdir/usr/share/$pkgname/$pkgname

  msg "Installing media..."
  for folder in enemies fonts images items maps music npcs powers saves soundfx tilesetdefs
  do
    cp -R $folder $pkgdir/usr/share/$pkgname/
  done

  msg "Installing execution script..."
  cat > $pkgdir/usr/bin/$pkgname << EOF
#!/bin/sh
cd /usr/share/$pkgname/
./$pkgname
EOF

  msg "Setting the right permissions..."
  chmod -R +x $pkgdir/usr/bin/$pkgname
  chmod -R 775 $pkgdir/usr/share/$pkgname/saves
  chown -R :games $pkgdir/usr/share/$pkgname
}
