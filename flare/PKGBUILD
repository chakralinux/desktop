#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=flare
_pkgname=flare-game
pkgver=0.18
pkgrel=1
pkgdesc="Action roleplaying game from an isometric perspective"
arch=('i686' 'x86_64')
url="http://clintbellanger.net/rpg/"
license=('GPL2' 'CCPL-by-sa')
depends=('sdl' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
replaces=('osare')
categories=('games')
changelog=ChangeLog
source=(http://downloads.sourceforge.net/project/flare-game/$pkgname-$pkgver.tar.gz)
md5sums=('4775a845694798f7df8b60b1303b8c37')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Some CMake changes.
  sed -i -e "s#share/games#share#" -e "s#/games#/bin#" CMakeLists.txt
 
  # Executable compilation.
  mkdir build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBINDIR=bin \
    -DDATADIR=share/$pkgname \
    ..
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
 
  # Documentation.
  install -Dm644 ../README $pkgdir/usr/share/doc/$pkgname/readme.txt
}

