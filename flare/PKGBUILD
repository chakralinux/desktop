#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=flare
_pkgname=flare-game
pkgver=0.17
_developer=clintbellanger
_code=b3344302fd
pkgrel=1
pkgdesc="Action roleplaying game from an isometric perspective"
arch=('i686' 'x86_64')
url="http://clintbellanger.net/rpg/"
license=('GPL2' 'CCPL-by-sa')
depends=('sdl' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
replaces=('osare')
categories=('games')
changelog=ChangeLog
source=($pkgname-$pkgver.tar.gz::https://github.com/$_developer/$_pkgname/tarball/v$pkgver)
md5sums=('dd513533f78d522b24d10488b37e8683')

build() {
  cd $srcdir/$_developer-$_pkgname-*

  # Some CMake changes.
  sed -i -e "s#share/games#share#" -e "s#/games#/bin#" CMakeLists.txt
 
  # Executable compilation.
  mkdir build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBINDIR=bin \
    -DDATADIR=share/$pkgname \
    ..
  make
}

package() {
  cd $srcdir/$_developer-$_pkgname-*
  cd build
  make DESTDIR=$pkgdir install
 
  # Documentation.
  install -Dm644 ../README $pkgdir/usr/share/doc/$pkgname/readme.txt
}
