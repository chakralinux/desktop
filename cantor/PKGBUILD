# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdeedu-cantor/
# Include global configuration
source ../kdeapps.conf

pkgname=cantor
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE frontend to mathematical software"
url="http://kde.org/applications/education/cantor/"
screenshot="https://www.kde.org/images/screenshots/cantor.png"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('analitza' 'kdelibs4support' 'knewstuff' 'kpty' 'ktexteditor' 'libqalculate' 'libspectre')
optdepends=('luajit: LUA backend'
            'maxima: Maxima backend'
	    'octave: Octave backend'
            'python3: Python 3 backend'
            'python2: Python 2 backend'
            'r: R backend'
            'sage-mathematics: Sage backend')
makedepends=('extra-cmake-modules' 'kdoctools' 'luajit' 'python3' 'python2' 'r')
replaces=('kdeedu-cantor')
conflicts=('kdeedu-cantor')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeedu')
options=('docs' 'debug')
install=${pkgname}.install
source=("$_mirror/${pkgname}-$_kdever.tar.xz"{,.sig})
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
                        'SKIP')
validpgpkeys=(${Avalidpgpkeys})

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}

