# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdeedu-cantor/
# Include global configuration
source ../kdeapps.conf

pkgname=cantor
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE frontend to mathematical software"
url="http://kde.org/applications/education/cantor/"
screenshot="https://www.kde.org/images/screenshots/cantor.png"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('analitza' 'libspectre' 'kpty' 'ktexteditor' 'knewstuff' 'libqalculate' 'hicolor-icon-theme')
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend'
            'luajit: LUA backend'
            'python3: Python 3 backend'
	    'python2: Python 2 backend'
            'sagemath: SageMath backend')
            #'julia: Julia backend')
makedepends=('extra-cmake-modules' 'python3' 'python2' 'kdoctools' 'luajit' 'r') # 'julia')
replaces=('kdeedu-cantor')
conflicts=('kdeedu-cantor')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeedu')
options=('docs' 'debug')
install=${pkgname}.install
source=("$_mirror/${pkgname}-$_kdever.tar.xz"{,.sig}
fno-operator-names_cantor.patch::https://cgit.kde.org/cantor.git/patch/?id=4b8ef6bed62daced90c7826985650c2a813d2996)
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
                        'SKIP'
                        '775ab4f099bdab34bff88d2f2bb6de9477ef68d1f5e062659b68d6a0718d7135')
validpgpkeys=(${Avalidpgpkeys})

prepare() {
  mkdir -p build

  cd ${pkgname}-${pkgver}
  # https://cgit.kde.org/cantor.git/patch/?id=4b8ef6bed62daced90c7826985650c2a813d2996
  patch -Np1 -i "${srcdir}/fno-operator-names_cantor.patch"
  
  # wait patch for julia, enable later when the operato names are fixed
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver} \
    -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}

