pkgname=irssi
pkgver=0.8.19
pkgrel=1
pkgdesc="Modular text mode IRC client with Perl scripting"
arch=('x86_64')
url="http://irssi.org/"
license=('GPL')
depends=('glib2' 'openssl' 'perl')
makedepends=('elinks' 'git')
categories=('communication')
optdepends=('perl-libwww: for the scriptassist script')
backup=(etc/irssi.conf)
screenshot=('http://irssi.org/themefiles/dma147.png')
source=(git+https://github.com/irssi/irssi.git#tag=${pkgver})
options=('!libtool')
sha1sums=('SKIP')

build() {
  cd "$srcdir/$pkgname"

  ./autogen.sh --prefix=/usr \
              --enable-ipv6 \
	      --with-proxy \
	      --with-bot \
	      --sysconfdir=/etc \
	      --with-perl-lib=vendor
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
}
