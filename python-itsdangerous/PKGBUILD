pkgbase=python-itsdangerous
pkgname=('python3-itsdangerous' 'python2-itsdangerous')
pkgver=0.24
pkgrel=2
pkgdesc='Various helpers to pass trusted data to untrusted environments'
arch=('any')
url="http://pypi.python.org/pypi/itsdangerous"
license=('BSD')
makedepends=('python3' 'python2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mitsuhiko/itsdangerous/archive/${pkgver}.tar.gz")
sha256sums=('b035b2da1d493b00a5ee7a5b750f96929de87b610643939fbe382902774626fc')

prepare() {
  cp -r "itsdangerous-$pkgver" "python3-itsdangerous-$pkgver"
  cp -r "itsdangerous-$pkgver" "python2-itsdangerous-$pkgver"
}

package_python3-itsdangerous() {
  cd "$pkgname-$pkgver"
  depends+=('python3')

  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-itsdangerous() {
  cd "$pkgname-$pkgver"
  depends+=('python2')

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
