#
# Chakra Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=tor
pkgver=0.2.2.34
pkgrel=1
pkgdesc="Anonymizing overlay network"
arch=('i686' 'x86_64')
url="http://www.torproject.org/"
license=('BSD')
depends=('openssl' 'libevent' 'tsocks' 'bash')
backup=('etc/tor/torrc' 'etc/tor/torrc-dist' 'etc/tor/tor-tsocks.conf')
install=tor.install
source=(http://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz
	'tor.conf'
	'tor.rc')
md5sums=('0f1bbb8e086ea2aba41ff7f898fcf3bd'
         'c97998c760c4351f12ad41d514071a9b'
         '23348b8e3b38c6ed2a45b4be42a7de8e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var 
  make 
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install 

  install -d ${pkgdir}/var/lib/tor 
  
  mv "$pkgdir/etc/tor/torrc.sample" "$pkgdir/etc/tor/torrc-dist" 
  install -D -m644 "$srcdir/tor.conf" "$pkgdir/etc/tor/torrc" 
  install -D -m755 "$srcdir/tor.rc" "$pkgdir/etc/rc.d/tor" 
  
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE" 
}
