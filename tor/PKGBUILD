pkgname=tor
pkgver=0.2.8.9
pkgrel=1
pkgdesc="Anonymizing overlay network"
arch=('x86_64')
url="http://www.torproject.org/"
license=('BSD')
depends=('openssl' 'libevent' 'torsocks' 'bash' 'libseccomp')
makedepends=('ca-certificates')
backup=('etc/tor/torrc' 'etc/tor/torrc-dist')
conflicts=('vidalia') # to enforce removal of unsupported software, not really conflicts
categories=('network')
install=${pkgname}.install
option=('!docs' '!emptydirs')
source=("http://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'torrc'
        'tor.conf.d')
md5sums=('e1f8bc2b2e2b365fc916388304e5e40d'
         'SKIP'
         '173621ad48f0052b95eb9786a1c0302c'
         'f656f3e93f0c2ed46464d9b784876dbc')
validpgpkeys=('B35BF85BF19489D04E28C33C21194EBB165733EA'
              '2133BC600AB133E1D826D173FE43009C4607B1FB')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --enable-systemd
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install 

  install -dm0700 "${pkgdir}/var/lib/tor"

  # Install config files
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"
  install -Dm0644 "${srcdir}/tor.conf.d" "${pkgdir}/etc/conf.d/tor"
  
  # Install systemd service file
  install -Dm0644 "contrib/dist/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"

  # Install license
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" 
}
