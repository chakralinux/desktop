# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdemultimedia-juk/
# Include global configuration
source ../kdeapps.conf

pkgname=kdemultimedia-juk
_pkgname=juk
pkgver=${_kdever}
pkgrel=1
pkgdesc='A jukebox, tagger and music collection manager'
url='http://kde.org/applications/multimedia/juk/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=("kde-runtime" 'taglib')
makedepends=('cmake' 'automoc4')
groups=("kde" "kdemultimedia" "kde-uninstall")
provides=('juk')
options=('docs' 'debug')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DWITH_TunePimp=OFF \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
