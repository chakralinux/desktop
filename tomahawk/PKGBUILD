#
# Apps SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# contributor kote martini438[at]gmail.com

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
# Default is RelWithDebInfo to help with debugging.

_buildtype="RelWithDebInfo"

##############################################################

pkgname=tomahawk
pkgver=0.3.3
pkgrel=1
pkgdesc="The social media player. A Music Player App written in C++/Qt"
arch=('i686' 'x86_64')
url="http://tomahawk-player.org/"
screenshot="https://img.skitch.com/20110813-n6w28ugdugyeptxgaj83y4ptp5.medium.jpg"
license=('GPL3')
depends=('phonon' 'taglib' 'boost' 'clucene' 'libechonest' 'jreen' 'qtweetlib' 'attica' 'quazip')
makedepends=('cmake')
provides=('tomahawk')
conflicts=('tomahawk-git')
categories=('multimedia')
options=(!strip)
source=(http://download.tomahawk-player.org/${pkgname}-${pkgver}.tar.bz2)
md5sums=('0ee6ff7cd7fcf2dd01db6772902efbb4')
install=tomahawk.install

# Clean options array to strip pkg if release buildtype is chosen
if [[ ${_buildtype} == "Release" ]] || [[ ${_buildtype} == "release" ]]; then
  options=()
fi

build() {
 
  cd ${srcdir}/${pkgname}-${pkgver}

  msg "Starting build..."

  rm -rf ${srcdir}/${pkgname}-${pkgver}-build
  cp -r ${srcdir}/${pkgname}-${pkgver} ${srcdir}/${pkgname}-${pkgver}-build
  cd ${srcdir}/${pkgname}-${pkgver}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=${_buildtype}
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}-build
  make DESTDIR=${pkgdir} install
}
