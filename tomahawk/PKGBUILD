pkgname=tomahawk
pkgver=0.8.4
pkgrel=9
pkgdesc="The social media player. A Music Player App written in C++/Qt"
arch=('x86_64')
url="http://tomahawk-player.org/"
screenshot="https://img.skitch.com/20110813-n6w28ugdugyeptxgaj83y4ptp5.medium.jpg"
license=('GPL3')
depends=('attica-qt5' 'gnutls' 'libechonest' 'liblastfm' 'lucene++'
         'phonon-qt5' 'qca-qt5' 'qt5-svg' 'qt5-webkit' 'qtkeychain-qt5' 'quazip'
         'taglib>=1.10' 'boost')
makedepends=('cmake' 'sparsehash' 'qt5-tools' 'telepathy-qt5')
optdepends=('telepathy-qt5: integration with Telepathy')
conflicts=('tomahawk-git')
categories=('multimedia')
options=(!strip)
source=("http://download.tomahawk-player.org/${pkgname}-${pkgver}.tar.bz2"
        'chakra-build.patch'
	'quazip5.patch')
md5sums=('04832abe1786edcc55805875b5882445'
         '2f46cf4c280e9130f5b9a0ab0270bf37'
         'd95c02d871ee6895df0612aba44f98f8')

prepare() {
  rm -rf "build"
  mkdir "build"

  # patch to enable libechonest and liblastfm
  patch -p0 -i "chakra-build.patch"

  # patch to use quazip 5
  patch -p0 -i "quazip5.patch"

}

build() {
  cd "build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR="lib/${pkgname}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_WITH_QT4=OFF \
        -DBUILD_HATCHET=OFF \
        -DTAGLIB_MIN_VERSION=1.10 \
        -DQuaZip_INCLUDE_DIR=/usr/include/quazip5 \
        -DQuaZip_LIBRARY=/usr/lib/libquazip5.so.1 \
        "../${pkgname}-${pkgver}"
  make
}

package() {
  cd "build"
  make DESTDIR=${pkgdir} install
}
