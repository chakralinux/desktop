# Contributions from Arch:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>

pkgbase=python-fuse
pkgname=(python3-fuse python2-fuse)
pkgver=0.3.1
pkgrel=1
arch=('x86_64')
pkgdesc="Python bindings for FUSE"
url="https://github.com/libfuse/python-fuse"
license=('LGPL')
makedepends=('python3-setuptools' 'python2-setuptools' 'fuse2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/libfuse/python-fuse/archive/v$pkgver.tar.gz")
sha256sums=('243cfab15ee357dbcdac98c2ee5929f6e8c65d2ec9d77597e3732f79ce649eec')

build() {
  cd $pkgbase-$pkgver
  python3 setup.py build
  python2 setup.py build
}

package_python3-fuse() {
  depends=('fuse2' 'python3')

  cd $pkgbase-$pkgver
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python2-fuse() {
  depends=('fuse2' 'python2')

  cd $pkgbase-$pkgver
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
