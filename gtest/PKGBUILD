# Maintainer: Weng Xuetian <wengxt@gmail.com>
# Contributor: Alexandre Bique <bique.alexandre@gmail.com>
# Contributor: Louis R. Marascio <lrm@fitnr.com>
# Contributor: Cody Maloney <cmaloney@theoreticalchaos.com>
 
pkgname=gtest
pkgver=1.7.0
pkgrel=1
pkgdesc="Google Test - C++ testing utility based on the xUnit framework (like JUnit)"
arch=('x86_64')
url="http://code.google.com/p/googletest/"
license=('BSD')
options=('!libtool')
depends=('gcc-libs' 'sh')
makedepends=('python2' 'cmake')
source=("http://googletest.googlecode.com/files/${pkgname}-${pkgver}.zip")
sha256sums=('247ca18dd83f53deb1328be17e4b1be31514cedfc1e3424f672bf11fd7e0d60d')

build() {
  rm -rf ${pkgname}-${pkgver}/build
  mkdir ${pkgname}-${pkgver}/build && cd ${pkgname}-${pkgver}/build
  cmake -DBUILD_SHARED_LIBS=ON -DCMAKE_SKIP_RPATH=ON ..
  make
}
 
package() {
  cd ${pkgname}-${pkgver}
  install -dm 0755 "${pkgdir}"/usr/{lib,include/gtest/internal,share/licenses/$pkgname,src/gtest/src,src/gtest/cmake}
  install -m 0644 build/libgtest{,_main}.so "${pkgdir}/usr/lib/"
  install -m 0644 include/gtest/*.h "${pkgdir}/usr/include/gtest/"
  install -m 0644 include/gtest/internal/*.h "${pkgdir}/usr/include/gtest/internal/"
  install -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -m 0644 fused-src/gtest/* "${pkgdir}/usr/src/gtest/src/"
  install -m 0644 CMakeLists.txt "${pkgdir}/usr/src/gtest/"
  install -m 0644 cmake/* "${pkgdir}/usr/src/gtest/cmake/"
}

