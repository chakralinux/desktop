pkgbase=python-pyzmq
pkgname=('python2-pyzmq' 'python3-pyzmq')
pkgver=16.0.2
pkgrel=1
pkgdesc="Python bindings for zeromq, written in Cython"
arch=('x86_64')
url="http://www.zeromq.org/bindings:python"
license=('LGPL')
depends=('zeromq' 'python3')
makedepends=('python3' 'python2' 'zeromq')
source=("https://files.pythonhosted.org/packages/source/p/pyzmq/pyzmq-$pkgver.tar.gz")
sha256sums=('0322543fff5ab6f87d11a8a099c4c07dd8a1719040084b6ce9162bcdf5c45c9d')

prepare() {
  cd "$srcdir"
  cp -a pyzmq-${pkgver} py2zmq-${pkgver}
}

build() {
  cd "$srcdir/pyzmq-$pkgver"
  # py3 errors added in 2.2.0.1 
  sed -i 's|except socket.error, e:|except socket.error as e:|' zmq/eventloop/ioloop.py
  sed -i 's|except gevent.Timeout, t:|except gevent.Timeout as t:|' zmq/green/core.py
  cd "$srcdir/py2zmq-$pkgver"
  sed -i 's|^#!/usr/bin/env python$|&2|' $(find ./ -name '*.py')
}

package_python2-pyzmq() {
  pkgdesc="Python2 bindings for zeromq, written in Cython"
  depends=(zeromq python2)
  cd "$srcdir/py2zmq-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=0
}

package_python3-pyzmq() {
  pkgdesc="Python3 bindings for zeromq, written in Cython"
  depends=(zeromq python)
  cd "$srcdir/pyzmq-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=0
}

# missing python2-singledispatch
#check()
#{
#  cd "$srcdir/pyzmq-$pkgver"
#  python3 setup.py build_ext --inplace
#  python3 setup.py test
#  cd "$srcdir/py2zmq-$pkgver"
#  python2 setup.py build_ext --inplace
#  python2 setup.py test
#}
