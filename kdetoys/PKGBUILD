#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=('kdetoys')
pkgname=('kdetoys-amor'
         'kdetoys-kteatime'
         'kdetoys-ktux')

arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="split package"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')

options=('docs' '!splithdr' 'splitdbg' 'log')

makedepends=('pkgconfig' 'cmake' 'automoc4' "kde-workspace>=${_kdever}")

source=($_mirror/${pkgbase}-$_kdever.tar.xz)
md5sums=(`grep ${pkgbase}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}

        # build it
	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make 
}

package_kdetoys-amor() {
	pkgdesc="Amusing Misuse Of Resources puts comic figures above your windows"
	conflicts=("kdetoys-common" "kdetoys-doc")
	groups=("kde" "kdetoys" "kde-uninstall")
	categories=('accessories')
	install=${pkgbase}.install

	cd ${srcdir}/${pkgbase}-${pkgver}/amor
	make DESTDIR=${pkgdir} install 
}

package_kdetoys-kteatime() {
	pkgdesc="System tray applet that makes sure your tea doesn't get too strong"
	conflicts=("kdetoys-common" "kdetoys-doc")
	groups=("kde" "kdetoys" "kde-uninstall")
	categories=('accessories')
	install=${pkgbase}.install

	cd ${srcdir}/${pkgbase}-${pkgver}/kteatime
	make DESTDIR=${pkgdir} install 
}

package_kdetoys-ktux() {
	pkgdesc="Tux-in-a-Spaceship screen saver"
	conflicts=("kdetoys-common")
	groups=("kde" "kdetoys" "kde-uninstall")
	categories=('accessories')
	install=${pkgbase}.install

	cd ${srcdir}/${pkgbase}-${pkgver}/ktux
	make DESTDIR=${pkgdir} install 
}

