pkgbase=python-path
pkgname=('python3-path' 'python2-path')
pkgver=8.1.2
pkgrel=2
pkgdesc="Aka path.py, implements path objects as first-class entities"
arch=('any')
url="http://ipython.org"
license=('MIT')
depends=('python3')
makedepends=('python3-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/p/path.py/path.py-$pkgver.tar.gz")
sha256sums=('ada95d117c4559abe64080961daf5badda68561afdd34c278f8ca20f2fa466d2')


prepare() {
  cd "$srcdir"
  cp -r path.py-$pkgver python2-path.py-$pkgver
}

package_python3-path() {
  cd "$srcdir/path.py-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -d "$pkgdir/usr/share/licenses/$pkgname/"
  head -n 21 path.py > "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

package_python2-path() {
  depends=('python2')
  cd "$srcdir/python2-path.py-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -d "$pkgdir/usr/share/licenses/$pkgname/"
  head -n 21 path.py > "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
