pkgname=vokoscreen
pkgver=2.5.0
pkgrel=1
pkgdesc='An easy to use screencast creator to record educational videos, live recordings of browser, installation, videoconferences, etc'
arch=('x86_64')
url='http://linuxecke.volkoh.de/vokoscreen/vokoscreen.html'
license=('GPL2')
depends=('qt5-x11extras' 'ffmpeg' 'lame' 'desktop-file-utils' 'xdg-utils' 'lsof')
optdepends=('pulseaudio-alsa: for PulseAudio support')
makedepends=('qt5-tools')
install=${pkgname}.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/vkohaupt/vokoscreen/archive/${pkgver}.tar.gz"
        'fix_lrelease.patch')
sha1sums=('d435c5687144dfea1025e23ce6e50f822da6ca61'
          'fa76417491ba68119395b82381d290b9c1155f9a')

prepare() {
	cd "${srcdir}"/${pkgname}-${pkgver}

	# Fix lrelease path
	patch -Np1 < ../fix_lrelease.patch

	# Create build directory
	mkdir -p "${srcdir}"/build
	
	#fix .desktop file
	sed -i s!"Comment=screencast"!"Comment=An easy to use screencast creator"! \
	applications/vokoscreen.desktop
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)

	if [[ ${njobs} -ge 8 ]]; then
		njobs=$(( $njobs - 2 ))
	fi

	# Building package
	cd "${srcdir}"/build
	qmake-qt5 ../${pkgname}-${pkgver} \
		CONFIG+=release \
		CONFIG+=c++14

	make -j${njobs}
}

package() {
	# Installing package
	cd "${srcdir}"/build
	make INSTALL_ROOT="${pkgdir}" install
}
