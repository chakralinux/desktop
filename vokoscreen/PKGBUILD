
pkgname=vokoscreen
pkgver=2.5.8
_pkgver=2.5.8-beta
pkgrel=3
pkgdesc="An easy to use screencast creator to record educational videos, live recordings of browser, installation, videoconferences, etc."
arch=('x86_64')
url="http://linuxecke.volkoh.de/vokoscreen/vokoscreen.html"
screenshot="http://linuxecke.volkoh.de/vokoscreen/vokoscreen-picture-screen.png"
license=('GPL2')
categories=('multimedia')
depends=('qt5-x11extras' 'ffmpeg' 'lame' 'mkvtoolnix' 'opencv' 'qt5-multimedia')
makedepends=('qt5-tools' 'libxrandr')
optdepends=('pulseaudio' 'alsa-utils')
source=("https://github.com/vkohaupt/$pkgname/archive/$_pkgver.tar.gz"
        'fix_lrelease.patch')
sha512sums=('dd9e25ec9ab7eeddb99aeeac3865f4306d741c78cb81b6a62fcc9d366270446683eadc483bc65637e30b639764ea978ae290baabfcb9ae298ad63fb040a805f6'
            'efcb0853f383769210e985a45c96179512f7f080f65f3c532a597acd3fd08bd58f3ef86571abfb9af5a270c8ae87a677fe8c9b3064229940027125ff24af18c9')

prepare() {
	cd $pkgname-$_pkgver

	# lrelease fix
	patch -Np1 < ../fix_lrelease.patch	
    
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)

	if [[ ${njobs} -ge 8 ]]; then
		njobs=$(( $njobs - 2 ))
	fi

	# Building package
	mkdir -p build/
	cd build/
	qmake-qt5 ../$pkgname-$_pkgver \
		CONFIG+=release \
		CONFIG+=c++14 \
		-spec linux-g++

	make -j${njobs}
}




package() {
	# Installing package
	cd build/
	make INSTALL_ROOT=$pkgdir install
}
