pkgname=('python3-scipy' 'python2-scipy')
pkgver=0.19.0
pkgrel=1
pkgdesc="SciPy is open-source software for mathematics, science, and engineering."
arch=('x86_64')
url="http://www.scipy.org/"
license=('BSD')
makedepends=('gcc-fortran' 'python3-numpy' 'python2-numpy' 'python3-setuptools' 'python2-setuptools')
checkdepends=('python3-nose' 'python2-nose')
source=("https://github.com/scipy/scipy/releases/download/v${pkgver}/scipy-${pkgver}.tar.xz")
sha256sums=('ed52232afb2b321a4978e39040d94bf81af90176ba64f58c4499dc305a024437')

build() {
  export LDFLAGS="-Wall -shared"

  # 2 builds
  cp -r scipy-${pkgver} scipy-${pkgver}-py2

  # build for python3
  cd scipy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 build

  # build for python2
  cd ../scipy-${pkgver}-py2

  for file in $(find . -name '*.py' -print); do
       sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
       sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py config_fc --fcompiler=gnu95 build
}

check() {
  # we need to do a temp install so we can import scipy
  # also, the tests must not be run from the scipy source directory
  export LDFLAGS="-Wall -shared"

  cd ${srcdir}/scipy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${srcdir}/test --optimize=1
  export PYTHONPATH=${srcdir}/test/usr/lib/python3.5/site-packages
  cd ${srcdir}
  python3 -c "from scipy import test; test('full')"

  cd ${srcdir}/scipy-${pkgver}-py2
  python2 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${srcdir}/test --optimize=1
  export PYTHONPATH=${srcdir}/test/usr/lib/python2.7/site-packages
  cd ${srcdir}
  python2 -c "from scipy import test; test('full')"
}

package_python3-scipy() {
  depends=('python3-numpy')
  provides=('python3-scipy' 'scipy')
  optdepends=('python3-pillow: for image saving module')

  cd scipy-${pkgver}
  export LDFLAGS="-Wall -shared"

  python3 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python3-scipy/LICENSE"
}

package_python2-scipy() {
  depends=('python2-numpy')
  optdepends=('python2-pillow: for image saving module')
  conflicts=('python3-scipy<0.9.0')

  cd scipy-${pkgver}-py2
  export LDFLAGS="-Wall -shared"

  python2 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python2-scipy/LICENSE"
}

# vim:set ts=2 sw=2 et:
