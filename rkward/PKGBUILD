# Contributions from AUR: http://aur.archlinux.org/packages.php?ID=4277

pkgname=rkward
pkgver=0.6.4
pkgrel=1
pkgdesc="Easy to use transparent frontend to R for KDE4"
arch=('x86_64')
url="http://rkward.sourceforge.net/"
license=('GPL')
depends=('kde-baseapps-katepart' 'kde-runtime' 'r' 'docbook-xsl' 'docbook-xml')
categories=('programming')
screenshot="http://sourceforge.net/apps/mediawiki/rkward/nfs/project/r/rk/rkward/0/0a/RKWard_060_450.png"
makedepends=('cmake' 'automoc4' 'icu')
source=(http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a0fb7d084519e710ea3c7dc60bb92d24126bcb002bd1d1e00231f480e373f6c5')
	 
build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	mkdir -p build && cd build
	cmake -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` \
	      -DCMAKE_BUILD_TYPE=Release \
	      -DR_LIBDIR="/usr/lib/R/library" \
	      ..
	make 
}

package(){
	cd "${srcdir}/${pkgname}-${pkgver}/build"
	make DESTDIR="$pkgdir" install
	
	# Some cleanup
	rm -vf "${pkgdir}/usr/lib/R/library/R.css"
	rm -vf "${pkgdir}/usr/share/apps/katepart/syntax/r.xml"
	rm -vf "${pkgdir}/usr/share/doc/kde/html/en/rkward/common"	
}
