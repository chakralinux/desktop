# Contributions from AUR: http://aur.archlinux.org/packages.php?ID=4277

pkgname=rkward
pkgver=0.7.0b
pkgrel=1
pkgdesc="An easy to use and easily extensible IDE/GUI for R"
arch=('x86_64')
url="https://rkward.kde.org/"
license=('GPL')
depends=('kdewebkit' 'ktexteditor' 'r')
makedepends=('kdoctools' 'extra-cmake-modules')
categories=('programming')
source=("https://download.kde.org/stable/${pkgname}/${pkgver//b/}/src/${pkgname}-${pkgver}.tar.gz")
sha256sums=('556e7bc8d3b8383e60c20c630f899075a6d39a04d29e56f4278191bb6ff853f4')
	 
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DR_LIBDIR=/usr/lib/R/library
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  rm $pkgdir/usr/share/org.kde.syntax-highlighting/syntax/r.xml
}
