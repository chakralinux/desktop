pkgname=gammu
pkgver=1.37.4
pkgrel=1
pkgdesc="GNU All Mobile Management Utilities"
arch=('x86_64')
url="http://wammu.eu/"
license=('GPL')
depends=('bluez' 'curl' 'libmysqlclient'
         'postgresql-libs' 'python2' 'libusb' 'gnutls')
makedepends=('cmake' 'doxygen' 'mysql')
optdepends=('dialog: support for the gammu-config script')
categories=('utils')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('b0fd316f09e862a732aa9ad6fd2f49f09ad69307fd05f50e58f40d33c15deea0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # bash completion dir change
  sed -i 's,DESTINATION "/etc/bash_completion.d",DESTINATION "/usr/share/bash-completion/completions",' contrib/CMakeLists.txt
  cmake . -DCMAKE_INSTALL_PREFIX="/usr"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mv $pkgdir/usr/lib64/* $pkgdir/usr/lib
  rm -d $pkgdir/usr/lib64
}
