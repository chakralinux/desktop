# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kwin
source ../plasma.conf

pkgname=kwin
pkgver=5.2.0.1
#pkgver=${PSubVersion}
pkgrel=1
pkgdesc='KDE Window manager'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kwin'
license=('LGPL')
depends=('qt5-multimedia' 'plasma-framework' 'knewstuff' 'libxcursor' 'kinit'
         'hicolor-icon-theme' 'libepoxy' 'kwayland' 'kdecoration' 'libinput')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools')
groups=('plasma')
install=${pkgname}.install
conflicts=('kde-workspace')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz"
       'kdebug-341971.patch')
sha256sums=( $(getSum ${pkgname}) 
       '88a08cb1bf34910a5d5aad161dddbc0327f55d4064b6d240753c8e7a8d059017')

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/kdebug-341971.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
