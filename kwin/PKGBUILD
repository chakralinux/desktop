# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../plasma.conf

pkgname=kwin
pkgver=${PVersion}
pkgrel=1
pkgdesc='KDE Window manager'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kwin'
license=('LGPL')
depends=('qt5-multimedia' 'plasma-framework' 'knewstuff' 'libxcursor' 'kinit'
         'hicolor-icon-theme' 'libepoxy' 'kwayland' 'kdecoration' 'kactivities-frameworks'
         'libinput' 'xcb-util-wm')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools')
checkdepends=("cmake" "xorg-server-xvfb")
groups=('plasma')
install=${pkgname}.install
conflicts=('kde-workspace')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PSubVersion}.tar.xz")
sha256sums=( $(getSum ${pkgname}) )

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

check() {
  cd build
  # some test need kwin to be installed (service type)
  xvfb-run -s "-screen 0 640x480x24" make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
