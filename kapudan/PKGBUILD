# Maintainer: george <george@chakra-project.org>

pkgname=kapudan
_pkgname=kapudan  
pkgver=2014.09
pkgrel=1
pkgdesc="Chakra's desktop greeter, a fork of Pardus's Kaptan."
arch=('x86_64')
url='http://gitorious.org/chakra/kapudan/'
screenshot='http://i.imgur.com/71aU5.png'
license=('GPLv2')
conflicts=('kapudan-git')
depends=('python2' 'kde-baseapps-konsole' 'kde-runtime'
         'kdebindings-python2' 'python2-pyqt4' 'python2-xlib'
         'python2-imaging' 'xdg-user-dirs' 'ksuperkey')
makedepends=('python-distribute' 'git' 'intltool')
optdepends=('spun: update notifications'
            'clamav: for the security page')
_gitname=kapudan
_gitsha=f3c5610
source=("$pkgname"::"git://gitorious.org/chakra/${_gitname}.git#commit=${_gitsha}")
md5sums=('SKIP')

package() {
    cd "${srcdir}/${pkgname}"
    python2 setup.py install --root="${pkgdir}"
    install -Dm755 kapudan-rootactions "${pkgdir}/usr/bin/kapudan-rootactions"
    install -Dm755 kapudan.desktop "${pkgdir}/usr/share/applications/kapudan.desktop"
    install -Dm644 data/kapudan.svgz \
        "${pkgdir}/usr/share/icons/hicolor/scalable/apps/kapudan.svgz"
    install -dm755 \
        "${pkgdir}/usr/share/kde4/apps/kapudan/kapudan/kde-themes/"
    install -Dm644 data/kde-themes/* \
        "${pkgdir}/usr/share/kde4/apps/kapudan/kapudan/kde-themes/"
    install -Dm755 kapudan-autostart.desktop \
        "${pkgdir}/usr/share/kde4/apps/kapudan/kapudan/kapudan-autostart.desktop"
    install -Dm755 data/ksuperkey.desktop \
        "${pkgdir}/usr/share/kde4/apps/kapudan/kapudan/ksuperkey.desktop"
}
