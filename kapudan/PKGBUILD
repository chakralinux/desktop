#
# Desktop Packages for Chakra, part of chakra-project.org
# 
# Maintainer: george <george[at]chakra-project.org>

pkgname=kapudan
_pkgname=chakra-kapudan
pkgver=0.4
pkgrel=1
pkgdesc="Chakra's desktop greeter, a fork of Pardus's Kaptan."
arch=('x86_64')
url='http://gitorious.org/chakra/kapudan/'
screenshot='http://i.imgur.com/71aU5.png'
license=('GPLv2')
conflicts=('kapudan-git')
depends=('python2' 'kde-baseapps-konsole' 'kde-runtime'
         'kdebindings-pykde4' 'pyqt' 'python2-xlib'
         'python2-v4l2capture' 'python2-imaging')
makedepends=('python-distribute' 'git')
optdepends=('spun: update notifications'
            'clamav: for the security page')
source=("${pkgname}-${pkgver}.tar.gz"::"http://gitorious.org/chakra/kapudan/archive-tarball/${pkgver}")
md5sums=('6cdf111f80e57ed8eddfd42fffcf8548')

# create tarball: source PKGBUILD && mksource

mksource() {
    git clone git://gitorious.org/chakra/${pkgname}.git ${pkgname}
    pushd ${pkgname}
    popd
    rm ${pkgname}/PKGBUILD
    tar -cvJf ${pkgname}-${pkgver}.tar.xz ${pkgname}
    md5sum ${pkgname}-${pkgver}.tar.xz
}

package() {
    cd "${srcdir}/${_pkgname}"
    python2 setup.py install --root="${pkgdir}"
    install -Dm755 kapudan-rootactions "${pkgdir}/usr/bin/kapudan-rootactions"
    install -Dm755 kapudan.desktop "${pkgdir}/usr/share/applications/kapudan.desktop"
    install -Dm644 data/kapudan.svgz \
        "${pkgdir}/usr/share/icons/hicolor/scalable/apps/kapudan.svgz"
    install -dm755 \
        "${pkgdir}/usr/share/kde4/apps/kapudan/kapudan/kde-themes/"
    install -Dm644 data/kde-themes/* \
        "${pkgdir}/usr/share/kde4/apps/kapudan/kapudan/kde-themes/"
    install -Dm755 kapudan-autostart.desktop \
        "${pkgdir}/usr/share/kde4/apps/kapudan/kapudan/kapudan-autostart.desktop"
}
