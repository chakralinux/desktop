#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=freecol
_pkgname=FreeCol
pkgver=0.10.5
pkgrel=1
pkgdesc="A turn-based strategy game based on Colonization"
arch=('any')
url="http://www.freecol.org/"
license=('GPL')
depends=('java-runtime')
makedepends=('apache-ant')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-src.zip
        main.desktop
        main.sh)
md5sums=('96948b3e2d764dfd78ed7c3e456d972b'
         '68922dfb7c1db20c13c4eb6930f9bd93'
         '058e8125acc3b475e90cb2be7cd8f55a')

build() {
  cd $srcdir/$pkgname
  ant
}

package() {
  cd $srcdir/$pkgname

  # Binary.
  install -Dm644 $_pkgname.jar		$pkgdir/usr/share/java/$pkgname/$pkgname.jar

  # Documentation.
  install -Dm644 doc/$_pkgname.pdf	$pkgdir/usr/share/doc/$pkgname/$pkgname.pdf

  # Desktop integration.
  install -Dm644 $pkgname.xpm		$pkgdir/usr/share/pixmaps/$pkgname.xpm
  install -Dm644 $srcdir/main.desktop	$pkgdir/usr/share/applications/$pkgname.desktop

  # Execution script.
  install -Dm755 $srcdir/main.sh	$pkgdir/usr/bin/$pkgname

  # Data.
  cp -r data jars $pkgdir/usr/share/java/$pkgname/
  find $pkgdir/usr/share/java/$pkgname -type d -exec chmod 755 '{}' \;
  find $pkgdir/usr/share/java/$pkgname -type f -exec chmod 644 '{}' \;
}
