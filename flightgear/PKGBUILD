pkgbase=flightgear
pkgname=('flightgear' 'flightgear-data')
pkgver=2016.4.4
pkgrel=1
_pkgver=${pkgver%.*}
pkgdesc="An open-source, multi-platform flight simulator."
url="http://www.flightgear.org/"
arch=('x86_64')
depends=('fltk' 'libxmu' 'libxi' 'openal' 'openscenegraph' 'zlib' 'subversion' 'libxrandr' 'glu')
makedepends=('boost' 'cmake' 'mesa' 'sharutils' 'qt5-base' 'plib' 'freeglut' 'freealut')
license=('GPL')
screenshot="http://www.flightgear.org/wp-content/gallery/gallery-v2-6/737-300_egkk_hom.jpg"
categories=('games')
options=('!makeflags' '!strip')
source=( #flightgear
        "http://downloads.sourceforge.net/project/flightgear/release-$_pkgver/flightgear-$pkgver.tar.bz2"
        # flightgear-data
        "http://downloads.sourceforge.net/project/flightgear/release-$_pkgver/FlightGear-$pkgver-data.tar.bz2"
        # addons - global object and sharedmodels (from latest version)
        "http://downloads.sourceforge.net/project/flightgear/scenery/GlobalObjects.tgz"
        "http://downloads.sourceforge.net/project/flightgear/scenery/SharedModels.tgz"
        #simgear
        "http://downloads.sourceforge.net/project/flightgear/release-$_pkgver/simgear-$pkgver.tar.bz2")
sha1sums=('187f4b2549030766abf64130c01f08f2993fc803'
          '8daec4ca2b46cc9a4b3179c25bc79a0a86745faa'
          '786aa46da9eae2fe1d6937716a3982d711e07aad'
          'f298354a23e54b0b49d69218828b866ad961f6ca'
          '45cf751bbe27b4905a461d309c3ae1fdb651c1a1')

prepare() {
  mkdir -p $srcdir/fake_install
}

build() {
  #build simgear
  cd $srcdir/simgear-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib .
  make
  make DESTDIR=$srcdir/fake_install install

  # build flightgear
  cd $srcdir/$pkgbase-$pkgver
  INCLUDEPATH+=$srcdir/fake_install/usr/include/simgear
  LD_PRELOAD=$srcdir/fake_install/usr/lib/{libSimGearCore.a,libSimGearScene.a}
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DFG_DATA_DIR:STRING="/usr/share/flightgear/data" \
	-DSimGear_DIR=$srcdir/fake_install/usr/lib/cmake/SimGear
  uudecode -o package/flightgear.png package/flightgear.png.uue
  make
  sed -i 's|Exec=.*|Exec=fgfs --fg-root=/usr/share/flightgear/data|' package/flightgear.desktop  
}

package_flightgear() {
  depends+=('flightgear-data' 'qt5-base')

  cd $pkgbase-$pkgver
  
  make DESTDIR=$pkgdir install

  # Desktop Integration
  install -Dm0644 package/flightgear.desktop $pkgdir/usr/share/applications/flightgear.desktop
  install -Dm0644 package/flightgear.ico $pkgdir/usr/share/icons/flightgear.ico
  install -Dm0644 package/flightgear.png $pkgdir/usr/share/icons/flightgear.png
  install -Dm0644 scripts/completion/fg-completion.bash $pkgdir/usr/share/bash-completion/completions/fgfs
  ln -sf flightgear $pkgdir/usr/share/FlightGear
}

package_flightgear-data(){
  pkgdesc="Base-Data for FlightGear - contains aircraft and world map addons"
  arch=('any')

  install -d $pkgdir/usr/share/flightgear
  mv fgdata/ $pkgdir/usr/share/flightgear/data
  # use aircraft addon
  cp -r Models/* $pkgdir/usr/share/flightgear/data/Models
  # use world map addon
  cp -r Objects/* $pkgdir/usr/share/flightgear/data/Scenery/Objects
  chown root:root $pkgdir/usr/share/flightgear/data
}
