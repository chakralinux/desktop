pkgbase=flightgear
pkgname=('flightgear' 'flightgear-data')
pkgver=2016.4.3
_pkgver=2016.4
pkgrel=1
pkgdesc="An open-source, multi-platform flight simulator."
arch=('x86_64')
depends=('fltk' 'libxmu' 'libxi' 'openal' 'openscenegraph' 'zlib' "simgear=$pkgver" 'subversion' 'libxrandr' 'glu')
makedepends=('boost' 'cmake' 'mesa' 'sharutils' "simgear=$pkgver" 'qt5-base')
license=('GPL')
url="http://www.flightgear.org/"
screenshot="http://www.flightgear.org/wp-content/gallery/gallery-v2-6/737-300_egkk_hom.jpg"
categories=('games')
options=('!makeflags' '!strip')
source=(# flightgear
        "http://downloads.sourceforge.net/project/flightgear/release-$_pkgver/flightgear-$pkgver.tar.bz2"
	"sec-fix.patch"
        # flightgear-data
        "http://downloads.sourceforge.net/project/flightgear/release-$_pkgver/FlightGear-$pkgver-data.tar.bz2"
        # addons - global object and sharedmodels (from latest version)
        "http://downloads.sourceforge.net/project/flightgear/scenery/GlobalObjects.tgz"
        "http://downloads.sourceforge.net/project/flightgear/scenery/SharedModels.tgz")
sha1sums=('4928e3a614bdaaa3539eb70c9b83067939ff62a2'
          'a5ad2cb30f58da853a62a6a7474af57df1593f75'
          '6eb39b4290ac7d483042157bc4039650671037c7'
          '5e7b0ecd30ffffbe47bedc98b090a32f3edc712b'
          'f503cbe93b7d6bbd9b7172075bc3d06b39ee569c')

prepare() {
  cd $srcdir/flightgear-$pkgver
  patch -p1 -i $srcdir/sec-fix.patch
}

build() {

  # build flightgear
  cd $pkgbase-$pkgver
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DFG_DATA_DIR:STRING="/usr/share/flightgear/data" .
  uudecode -o package/flightgear.png package/flightgear.png.uue
  make
  sed -i 's|Exec=.*|Exec=fgfs --fg-root=/usr/share/flightgear/data|' package/flightgear.desktop  
}

package_flightgear() {
  depends=('flightgear-data')
  optdepends=('qt5-base: fgfs --launcher')

  cd $pkgbase-$pkgver
  
  make DESTDIR=$pkgdir install

  # Desktop Integration
  install -Dm0644 package/flightgear.desktop $pkgdir/usr/share/applications/flightgear.desktop
  install -Dm0644 package/flightgear.ico $pkgdir/usr/share/icons/flightgear.ico
  install -Dm0644 package/flightgear.png $pkgdir/usr/share/icons/flightgear.png
  install -Dm0644 scripts/completion/fg-completion.bash $pkgdir/usr/share/bash-completion/completions/fgfs
  ln -sf flightgear $pkgdir/usr/share/FlightGear
}

package_flightgear-data(){
  pkgdesc="Base-Data for FlightGear - contains aircraft and world map addons"
  arch=('any')

  install -d $pkgdir/usr/share/flightgear
  mv fgdata/ $pkgdir/usr/share/flightgear/data
  # use aircraft addon
  cp -r Models/* $pkgdir/usr/share/flightgear/data/Models
  # use world map addon
  cp -r Objects/* $pkgdir/usr/share/flightgear/data/Scenery/Objects
  chown root:root $pkgdir/usr/share/flightgear/data
}
