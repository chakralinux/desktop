#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=flightgear
_pkgname=FlightGear
pkgver=2.8.0
pkgrel=2
pkgdesc="An open-source, multi-platform flight simulator."
arch=(i686 x86_64)
depends=('libxmu' 'libxi' 'openal' 'openscenegraph' 'zlib' 'simgear' 'subversion')
makedepends=('boost' 'cmake')
optdepends=('flightgear-aircrafts: Additional aircrafts.')
license=(GPL3)
url="http://www.flightgear.org"
screenshot="http://www.flightgear.org/wp-content/gallery/gallery-v2-6/737-300_egkk_hom.jpg"
categories=('games')
source=(http://ftp.icm.edu.pl/packages/$pkgname/Source/$pkgname-$pkgver.tar.bz2)
sha256sums=('bfda4f269d90128f4e4cb828de692a49dc0b93db41fe20a2b5272f3f271f3a01')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  mkdir -p build
  cd build
  
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    ..
    
  make
}

package() {
  depends=( "${depends[@]}" "flightgear-data" )

  cd $srcdir/$pkgname-$pkgver/build
  
  make DESTDIR=$pkgdir install

  # Execution script.
  echo "#!/bin/bash"	 > $pkgdir/usr/bin/$pkgname
  echo "fgfs --fg-root=/usr/share/$pkgname/data \$@"	>> $pkgdir/usr/bin/$pkgname
  chmod +x $pkgdir/usr/bin/$pkgname
  
  # Desktop Integration.
  cd .. # From /build
  install -Dm0644 package/$pkgname.desktop    $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm0644 package/$pkgname.ico        $pkgdir/usr/share/icons/$pkgname.ico
}
