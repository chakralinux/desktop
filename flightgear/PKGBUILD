pkgbase=flightgear
pkgname=('flightgear' 'flightgear-data')
pkgver=2016.2.1
_pkgver=2016.2
pkgrel=1
pkgdesc="An open-source, multi-platform flight simulator."
arch=('x86_64')
depends=('fltk' 'libxmu' 'libxi' 'openal' 'openscenegraph' 'zlib' "simgear=$pkgver" 'subversion' 'libxrandr' 'glu')
makedepends=('boost' 'cmake' 'mesa' 'sharutils' "simgear=$pkgver")
license=('GPL')
url="http://flightgear.sourceforge.net/"
screenshot="http://www.flightgear.org/wp-content/gallery/gallery-v2-6/737-300_egkk_hom.jpg"
categories=('games')
options=('!makeflags' '!strip')
source=(# flightgear
        "http://downloads.sourceforge.net/project/flightgear/release-$_pkgver/flightgear-$pkgver.tar.bz2"
	"build-fix.patch"
        # flightgear-data
        "http://downloads.sourceforge.net/project/flightgear/release-$_pkgver/FlightGear-$pkgver-data.tar.bz2"
        # addons - global object and sharedmodels (from latest version)
        "http://downloads.sourceforge.net/project/flightgear/scenery/GlobalObjects.tgz"
        "http://downloads.sourceforge.net/project/flightgear/scenery/SharedModels.tgz")
sha1sums=('6564eb63a466b38b43214c7647734c5dedad9a3c'
          '3261cab4abe211c86602cdf3965a612407f77f9d'
          '85834559a66e394077087ab8710e7c0312776160'
          '07ac0123038d2b5dd091cd02341c6f69da14f32e'
          '9b72a729e36f4dee674858dd419e59b6aa480d56')

prepare() {
  cd $srcdir/flightgear-$pkgver
  patch -p1 -i $srcdir/build-fix.patch
}

build() {

  # build flightgear
  cd $pkgbase-$pkgver
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DFG_DATA_DIR:STRING="/usr/share/flightgear/data" .
  uudecode -o package/flightgear.png package/flightgear.png.uue
  make
  sed -i 's|Exec=.*|Exec=fgfs --fg-root=/usr/share/flightgear/data|' package/flightgear.desktop  
}

package_flightgear() {
  depends=('flightgear-data')

  cd $pkgbase-$pkgver
  
  make DESTDIR=$pkgdir install

  # Desktop Integration
  install -Dm0644 package/flightgear.desktop $pkgdir/usr/share/applications/flightgear.desktop
  install -Dm0644 package/flightgear.ico $pkgdir/usr/share/icons/flightgear.ico
  install -Dm0644 package/flightgear.png $pkgdir/usr/share/icons/flightgear.png
  install -Dm0644 scripts/completion/fg-completion.bash $pkgdir/usr/share/bash-completion/completions/fgfs
  ln -sf flightgear $pkgdir/usr/share/FlightGear
}

package_flightgear-data(){
  pkgdesc="Base-Data for FlightGear - contains aircraft and world map addons"
  arch=('any')

  install -d $pkgdir/usr/share/flightgear
  mv fgdata/ $pkgdir/usr/share/flightgear/data
  # use aircraft addon
  cp -r Models/* $pkgdir/usr/share/flightgear/data/Models
  # use world map addon
  cp -r Objects/* $pkgdir/usr/share/flightgear/data/Scenery/Objects
  chown root:root $pkgdir/usr/share/flightgear/data
}
