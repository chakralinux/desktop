#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf


#
# package info
#
pkgbase=('kdesdk')
pkgname=('kdesdk-cervisia'
	 'kdesdk-doc'
	 'kdesdk-dolphin-plugins'
         'kdesdk-kapptemplate'
         'kdesdk-kcachegrind'
	 'kdesdk-kdeaccounts-plugin'
         'kdesdk-kdepalettes'  
         'kdesdk-kioslave'
         'kdesdk-kmtrace'
         'kdesdk-kompare'
         'kdesdk-kpartloader'
         'kdesdk-kprofilemethod'
         'kdesdk-kstartperf'
         'kdesdk-kuiviewer'
         'kdesdk-lokalize'
         'kdesdk-okteta'
         'kdesdk-poxml'
         'kdesdk-scripts'
         'kdesdk-strigi-analyzer'
         'kdesdk-umbrello')

# FIXME:  The following modules are not ported yet:
#        'kdesdk-kspy'
#        'kdesdk-scheck'
#        'kdesdk-kunittest'

arch=('i686' 'x86_64')
pkgver=${_kdever}
pkgrel=1

pkgdesc="split package"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')

options=('docs' '!splithdr' 'splitdbg' 'log')

makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' "kde-runtime>=${_kdever}" 'antlr2'
             "kdepimlibs>=${_kdever}" 'subversion' "kdebindings-pykde4>=${_kdever}" "kde-baseapps-konqueror>=${_kdever}" 'java-runtime')

source=("$_mirror/${pkgbase}-$_kdever.tar.bz2")
md5sums=(`grep ${pkgbase}-$_kdever.tar.bz2 ../kde-sc.md5 | cut -d" " -f1`)

#
# build function
#
build()
{
	cd ${srcdir}/${pkgbase}-${pkgver}

	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
	make || return 1
}


#
# split-install functions
#
package_kdesdk-doc()
{
	pkgdesc="KDE Software Development Kit - Documentation"
	groups=("kde" "kde-devel" "kde-doc" "kde-uninstall" "kdesdk")

	splitdirs="doc"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-dolphin-plugins()
{
	pkgdesc='Extra Dolphin plugins'
	depends=("kde-baseapps-dolphin>=${_kdever}" 'subversion' 'git' "kdesdk-kompare>=${_kdever}")
	install=${pkgbase}.install

	splitdirs="git svn"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/dolphin-plugins/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-cervisia()
{
	pkgdesc="A CVS GUI frontend for KDE"
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="cervisia"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kapptemplate()
{
	pkgdesc="KDE application template generator"
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="kapptemplate"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kcachegrind()
{
	pkgdesc="KDE profiler visualization"
	depends=("kde-runtime>=${_kdever}" "python2")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="kcachegrind"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kdeaccounts-plugin()
{
	pkgdesc="Addressbook plugin that puts names/email addresses of all KDE SVN accounts into an addressbook"
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="kdeaccounts-plugin"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kdepalettes()
{
	pkgdesc='Palettes for the Gimp that match the KDE standard color palette'
	optdepends=('gimp')

	install -D -m644 $srcdir/${pkgbase}-${pkgver}/kdepalettes/KDE_Gimp \
		$pkgdir/usr/share/gimp/2.0/palettes/KDE.gpl
}

package_kdesdk-kioslave()
{
	pkgdesc='KDED Subversion Module'
	depends=("kde-runtime>=${_kdever}" 'subversion')
        conflicts=('kdesdk-common')
	replaces=("kdesdk-common")

	splitdirs="kioslave"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kmtrace()
{
	pkgdesc="A KDE memory leak tracer"
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="kmtrace"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kompare()
{
	pkgdesc="A KDE visual diff viewer"
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="kompare"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kpartloader()
{
	pkgdesc="KPartloader is a very simple application to test loading of a KPart."
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="kpartloader"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kprofilemethod()
{
	pkgdesc="Macros for profiling using QTime"
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="kprofilemethod"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kstartperf()
{
	pkgdesc="Measures startup time for KDE applications"
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="kstartperf"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-kuiviewer()
{
	pkgdesc="KUIViewer is a utility to display and test .ui-files"
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="kuiviewer"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-lokalize()
{
	pkgdesc="Computer aided translation system"
	depends=("kde-runtime>=${_kdever}" "kdebindings-pykde4>=${_kdever}")
	optdepends=('translate-toolkit: enable extra python script')
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="lokalize"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-okteta()
{
	pkgdesc="Okteta is a simple editor for the raw data of files, also known as a hex editor."
	depends=("kde-runtime>=${_kdever}" "kde-runtime>=${_kdever}"
		 "kde-baseapps>=${_kdever}")
	replaces=('kdeutils-okteta')
	groups=("kde" "kde-devel" "kdesdk" "kde-uninstall")
	install=${pkgbase}.install

	splitdirs="okteta"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-poxml()
{
	pkgdesc="Tools for using .po-files to translate DocBook XML files"
	depends=("kde-runtime>=${_kdever}" 'qt' 'antlr2')
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="poxml"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-scripts()
{
	pkgdesc="Extra scripts for the KDE SDK"
	depends=("kde-runtime>=${_kdever}" 'python')
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="scripts"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}


package_kdesdk-strigi-analyzer() {
	pkgdesc='Strigi-Analyzer for KDE SDK'
	depends=("kdelibs>=${_kdever}")
	conflicts=("kdesdk-common")

	splitdirs="strigi-analyzer"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdesdk-umbrello()
{
	pkgdesc="An UML modeling tool for KDE"
	depends=("kde-runtime>=${_kdever}")
	groups=("kde" "kde-devel" "kde-uninstall" "kdesdk")
	install=${pkgbase}.install

	splitdirs="umbrello"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}
