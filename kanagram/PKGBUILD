# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kanagram

pkgname=kanagram
pkgver=18.04.2
pkgrel=1
pkgdesc="Letter order game"
url="http://kde.org/applications/education/kanagram/"
screenshot="https://www.kde.org/images/screenshots/kanagram.png"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkeduvocdocument' 'kdeclarative' 'knewstuff' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
provides=('kdeedu-kanagram')
conflicts=('kdeedu-kanagram')
replaces=('kdeedu-kanagram')
groups=("kde" "kdeedu" "kde-uninstall")
categories=('education')
install=${pkgname}.install
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7d687bda6187a27af4d651128fad20bf14d2e73b7ccc4368840ba603ba8d8926'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
