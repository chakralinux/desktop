# Maintainer: Daniele Cocca <jmc@chakra-project.org>

pkgbase=appdirs
pkgname=("python2-${pkgbase}" "python3-${pkgbase}")
pkgver=1.3.0
pkgrel=1
pkgdesc='A small Python module for determining appropriate platform-specific dirs, e.g. a "user data dir".'
arch=('any')
url="http://github.com/ActiveState/appdirs"
license=('MIT')
makedepends=('python2' 'python2-distribute' 'python3' 'python3-distribute')
source=("https://pypi.python.org/packages/source/a/${pkgbase}/${pkgbase}-${pkgver}.tar.gz")
md5sums=('19d02a6e64227c529dac715400832f6e')

build() {
  msg2 "Starting the Python 2.x build"
  cd "${srcdir}"
  rm -rf "python2-${pkgbase}"
  cp -r "${pkgbase}-${pkgver}" "python2-${pkgbase}"

  cd "python2-${pkgbase}"
  python2 setup.py build

  msg2 "Starting the Python 3.x build"
  cd "${srcdir}"
  rm -rf "python3-${pkgbase}"
  cp -r "${pkgbase}-${pkgver}" "python3-${pkgbase}"

  cd "python3-${pkgbase}"
  python3 setup.py build
}

package_python2-appdirs() {
depends=('python2')

  cd "${srcdir}/python2-${pkgbase}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE.txt \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python3-appdirs() {
depends=('python3')

  cd "${srcdir}/python3-${pkgbase}"
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE.txt \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
