# Maintainer abveritas[at]chakraos[dot]org>

pkgname=amarok
replaces=('amarok-base' 'amarok-engine-xine' 'amarok-base-mysqlfree')
pkgver=2.8.0
pkgrel=9
pkgdesc="A media player for KDE"
arch=('x86_64')
url="http://amarok.kde.org"
screenshot="http://amarok.kde.org/files/Amarok-2.6-InDulciJubilo-screenie4.png"
license=('GPL2' 'LGPL2.1' 'FDL')
depends=('kde-runtime' 'mariadb' 'qtscriptgenerator' 'taglib-extras' 'liblastfm-qt4' 'ffmpeg' 'libofa'
         'qjson' 'libbluray' 'libmygpo-qt')
makedepends=('pkgconfig' 'automoc4' 'cmake' 'libgpod' 'libmtp' 'docbook-xsl' 'mesa' 'clamz' 'loudmouth')
optdepends=("libgpod: support Apple iPod audio devices"
            "libmtp: support for portable media devices"
            "ifuse: support for Apple iPod Touch and iPhone"
            "clamz: allow to download songs from Amazon.com"
            "loudmouth: backend needed by mp3tunes for syncing")
categories=('multimedia')
install="${pkgname}.install"
source=("http://mirrors.mit.edu/kde/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
	taglib_version_comparison.patch)
md5sums=('53cfcb4819668b10e13b061478c7b32a'
         'ef7849e457357a1bdac7970a3633da45')
prepare(){
   cd "${srcdir}/${pkgname}-${pkgver}"
   patch -Np1 -i ../taglib_version_comparison.patch
   if [[ -e build ]]; then rm -rf build; fi
   mkdir build
}
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE4_BUILD_TESTS=OFF \
    -DWITH_NepomukCore=OFF \
    -DWITH_Soprano=OFF
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}

