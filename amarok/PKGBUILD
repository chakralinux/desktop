#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Martín González <koteccr[at]gmail[dot]com>

pkgname=amarok
replaces=('amarok-base' 'amarok-engine-xine' 'amarok-base-mysqlfree')
pkgver=2.6.0
pkgrel=5
pkgdesc="A media player for KDE"
arch=('x86_64')
url="http://amarok.kde.org"
screenshot="http://amarok.kde.org/files/Amarok-2.6-InDulciJubilo-screenie4.png"
license=('GPL2' 'LGPL2' 'FDL')
depends=('kde-runtime' 'mysql' 'qtscriptgenerator' 'taglib-extras' 'liblastfm' 'ffmpeg' 'libofa'
         'qjson' 'mesa' 'loudmouth')
makedepends=('pkgconfig' 'automoc4' 'cmake' 'libgpod' 'libmtp' 'docbook-xsl' 'libmygpo-qt' )
optdepends=("libgpod: support Apple iPod audio devices"
            "libmtp: support for portable media devices"
            "libmygpo-qt: support for gpodder.net"
	    "ifuse: support for Apple iPod Touch and iPhone")
categories=('multimedia')
install="${pkgname}.install"
source=("http://mirrors.mit.edu/kde/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('116f53c1109c7acd1ac7d78ec75c01c4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}
