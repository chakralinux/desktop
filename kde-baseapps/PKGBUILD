# Include global configuration
source ../kdeapps.conf

pkgbase=kde-baseapps
pkgname=('kde-baseapps-kdepasswd'
         'kde-baseapps-kdialog'
         'kde-baseapps-keditbookmarks'
         'kde-baseapps-kfind'
         'kde-baseapps-konq-plugins'
         'kde-baseapps-konqueror'
         'kde-baseapps-lib'
         'kde-baseapps-plasma')
pkgver=${_kdever}
pkgrel=4
arch=('x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdebase')
makedepends=('kdelibs' 'cmake' 'automoc4' 'tidyhtml' 'docbook-xsl')
options=('docs' 'debug')
source=("$_mirror/${pkgbase}-$_kdever.tar.xz")
sha256sums=(`grep ${pkgbase}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver} \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package_kde-baseapps-kdepasswd() {
  pkgdesc='Change Password'
  depends=('kdebase-runtime' 'kde-baseapps-lib')

  cd "$srcdir"/build/kdepasswd
  make DESTDIR="$pkgdir" install
  cd "$srcdir"/build/doc/kdepasswd
  make DESTDIR="$pkgdir" install
}

package_kde-baseapps-kdialog() {
  pkgdesc='A utility for displaying dialog boxes from shell scripts'
  depends=('kdebase-runtime')

  cd "$srcdir"/build/kdialog
  make DESTDIR="$pkgdir" install
}

package_kde-baseapps-keditbookmarks() {
  pkgdesc='Bookmark Organizer and Editor'
  depends=('kdebase-runtime')

  cd "$srcdir"/build/keditbookmarks
  make DESTDIR="$pkgdir" install
}

package_kde-baseapps-kfind() {
  pkgdesc='Find Files/Folders'
  depends=('kdebase-runtime' 'kde-baseapps-lib')
  url="http://kde.org/applications/utilities/kfind/"
  install='kde-baseapps.install'

  cd "$srcdir"/build/kfind
  make DESTDIR="$pkgdir" install
  cd "$srcdir"/build/doc/kfind
  make DESTDIR="$pkgdir" install
}

package_kde-baseapps-konq-plugins() {
  pkgdesc='Extra plugins for Konqueror'
  depends=('kde-baseapps-konqueror' 'tidyhtml')
  replaces=('konq-plugins')
  install='kde-baseapps.install'

  cd "$srcdir"/build/konq-plugins
  make DESTDIR="$pkgdir" install
}

package_kde-baseapps-konqueror() {
  pkgdesc='KDE File Manager & Web Browser'
  depends=('dolphinpart4' 'kde-baseapps-keditbookmarks')
  optdepends=('kwebkitpart: to enable webkit engine')
  url="http://kde.org/applications/internet/konqueror/"
  install='konqueror.install'
  conflicts=('kdebase-nsplugins')
  replaces=('kdebase-nsplugins')

  for i in konqueror doc/konqueror nsplugins; do
    cd "$srcdir"/build/${i}
    make DESTDIR="$pkgdir" install
  done
}

package_kde-baseapps-lib() {
  pkgdesc='KDE libraries for the basic desktop applications'
  groups=()
  depends=('kdelibs')
  conflicts=('kde-baseapps')
  replaces=('kdebase-lib') #'kdebase-lib' can be removed
  provides=('kde-baseapps' 'kdebase-lib')

  cd "$srcdir"/build/lib
  make DESTDIR="$pkgdir" install
}

package_kde-baseapps-plasma() {
  pkgdesc='Display the contents of folders (UserÂ´s home folder as default)'
  depends=('kdebase-workspace' 'kde-baseapps-lib')
  groups=()

  cd "$srcdir"/build/plasma
  make DESTDIR="$pkgdir" install
}