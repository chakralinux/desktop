# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase="kde-baseapps"
pkgname=('kde-baseapps'
        'kde-baseapps-dolphin'
        'kde-baseapps-konqueror'
        'kde-baseapps-konqueror-plugins')
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' '!header' 'debug' 'log')
makedepends=("kde-workspace>=${_workspace_version}" 'pkg-config' 'cmake' 'automoc4' 'libraw1394' 'ruby'
             'tidyhtml' 'docbook-xsl' 'baloo-widgets')
source=("$_mirror/${pkgbase}-$_kdever.tar.xz"
        "http://www.kde-apps.org/CONTENT/content-files/99752-kde_cdemu-0.5.0.tar.bz2")
sha256sums=(`grep ${pkgbase}-$_kdever.tar.xz  ../checksums.txt | cut -d" " -f1`
            'd93744048cf7ae6b86861d26e2c38c27fafd4a7d6e5703e03ed09d776273303a')

build() {
    cd ${pkgbase}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package_kde-baseapps() {
    pkgdesc="KDE Base Applications"
    depends=("kde-runtime>=${_kdever}" 'libraw1394' 'nepomuk-widgets')
    optdepends=("cdemu-client: Support transparent CD/DVD image mounting in KDE")
    conflicts=("kdebase" 'kde-baseapps-doc')
    replaces=('kdebase' 'kde-baseapps-doc')
    provides=("kdebase" "baseapps" 'kde-baseapps-doc')
    groups=("${pkgbase}" "kde" "kde-minimal" "kde-uninstall")
    install=kde-baseapps.install

    splitdirs="kdepasswd kdialog keditbookmarks kfind lib nsplugins plasma konqueror doc/kdepasswd doc/kfind"
    for i in ${splitdirs} ; do
        cd ${srcdir}/${pkgbase}-${pkgver}/${i}
        make DESTDIR=${pkgdir} install
    done
        
    # install cdemu frontend
    cd ${srcdir}/kde_cdemu
    cmake . \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
    make DESTDIR=${pkgdir} install
    
    msg "Splitting Konqueror ..."
    rm -vR ${pkgdir}/usr/bin/kfmclient
    rm -vR ${pkgdir}/usr/share/applications/kde4/kfmclient*
    rm -vR ${pkgdir}/usr/include/konqsidebarplugin.h 
    rm -vR ${pkgdir}/usr/lib/libkonqsidebarplugin.so*
    rm -vR ${pkgdir}/usr/lib/libkdeinit4_konqueror.so*
    rm -vR ${pkgdir}/usr/lib/libkonquerorprivate.so*
    rm -vR ${pkgdir}/usr/lib/kde4/konq_aboutpage.so*
    rm -vR ${pkgdir}/usr/lib/kde4/konqsidebar_web.so*
    rm -vR ${pkgdir}/usr/lib/kde4/konq_sidebartree_bookmarks.so*
    rm -vR ${pkgdir}/usr/lib/kde4/konq_sidebartree_dirtree.so*
    rm -vR ${pkgdir}/usr/lib/kde4/konq_sidebar.so*
    rm -vR ${pkgdir}/usr/lib/kde4/konqsidebar_history.so*
    rm -vR ${pkgdir}/usr/lib/kde4/kded_konqy_preloader.so*
    rm -vR ${pkgdir}/usr/lib/kde4/konqsidebar_places.so*
    rm -vR ${pkgdir}/usr/lib/kde4/konqsidebar_tree.so*
    rm -vR ${pkgdir}/usr/share/config/konqsidebartngrc
    rm -vR ${pkgdir}/usr/share/config.kcfg/konqueror.kcfg
    rm -vR ${pkgdir}/usr/share/autostart/konqy_preload.desktop
    rm -vR ${pkgdir}/usr/share/applications/kde4/konquerorsu.desktop
    rm -vR ${pkgdir}/usr/share/applications/kde4/konqbrowser.desktop
    rm -vR ${pkgdir}/usr/share/apps/konqsidebartng
    rm -vR ${pkgdir}/usr/share/apps/konqueror

    # Don't ship the Konqueror desktop file
    mv -v ${pkgdir}/usr/share/kde4/services/konqueror.desktop ${srcdir}/konqueror.desktop

    rm -vR ${pkgdir}/usr/share/icons/
    rm -vR ${pkgdir}/usr/share/dbus-1/interfaces/org.kde.Konq*
    rm -vR ${pkgdir}/usr/share/dbus-1/interfaces/org.kde.konq*
    rm -vR ${pkgdir}/usr/bin/konqueror
    
    # we don't want the cdemu manager app. the service menu + devicenotifier will suffice
    rm -rfv ${pkgdir}/usr/share/applications/kde4/kde_cdemu.desktop

    # Fix missing KFind icon, fixes FS#325
    sed -i -e "s~Icon=kfind~Icon=edit-find~g" ${pkgdir}/usr/share/applications/kde4/kfind.desktop
}

package_kde-baseapps-dolphin() {
    pkgdesc="The default KDE file manager"
    depends=("${pkgbase}>=${_kdever}")
    groups=("${pkgbase}" "kde" "kde-complete" "kde-uninstall")
    conflicts=("kdebase-dolphin")
    provides=("kdebase-dolphin")
    replaces=("kdebase-dolphin")
    categories=('system')
    install=dolphin.install

    splitdirs="dolphin doc/dolphin"
    for i in ${splitdirs} ; do
        cd ${srcdir}/${pkgbase}-${pkgver}/${i}
        make DESTDIR=${pkgdir} install
    done
}

package_kde-baseapps-konqueror() {
    pkgdesc="The default KDE web browser"
    depends=("${pkgbase}>=${_kdever}" "kde-baseapps-dolphin>=${_kdever}")
    groups=("${pkgbase}" "kde" "kde-minimal" "kde-uninstall")
    conflicts=("kdebase-konqueror")
    provides=("kdebase-konqueror")
    replaces=("kdebase-konqueror")
    categories=('network')
    install=konqueror.install

    splitdirs="konqueror doc/konqueror"
    for i in ${splitdirs} ; do
        cd ${srcdir}/${pkgbase}-${pkgver}/${i}
        make DESTDIR=${pkgdir} install
    done

    msg "Remove conflicting files ..."
    rm -vR ${pkgdir}/usr/lib/kde4/kcm_history.so*
    rm -vR ${pkgdir}/usr/lib/kde4/kcm_kio.so*
    rm -vR ${pkgdir}/usr/lib/kde4/kcm_konq.so*
    rm -vR ${pkgdir}/usr/lib/kde4/kcm_konqhtml.so*
    rm -vR ${pkgdir}/usr/lib/kde4/kcm_kurifilt.so*
    rm -vR ${pkgdir}/usr/lib/kde4/kcm_performance.so*
    rm -vR ${pkgdir}/usr/lib/libkdeinit4_kfmclient.so*
    rm -vR ${pkgdir}/usr/share/applications/kde4/Home.desktop
    rm -vR ${pkgdir}/usr/share/apps/kcmcss
    #rm -vR ${pkgdir}/usr/share/apps/kconf_update
    rm -vR ${pkgdir}/usr/share/apps/kcontrol
    rm -vR ${pkgdir}/usr/share/kde4/service*
  
    # Move the Konqueror desktop file back
    mkdir -pv ${pkgdir}/usr/share/kde4/services
    cp -v ${srcdir}/konqueror.desktop ${pkgdir}/usr/share/kde4/services/konqueror.desktop
}

package_kde-baseapps-konqueror-plugins() {
    pkgdesc="KDE Base Applications - Extra plugins for Konqueror"
    depends=("kde-baseapps-konqueror>=${_kdever}" 'tidyhtml')
    groups=("${pkgbase}" "kde" "kde-minimal" "kde-uninstall")
    conflicts=("konq-plugins" "kde-baseapps-konq-plugins")
    provides=("konq-plugins" "kde-baseapps-konq-plugins")
    replaces=("konq-plugins" "kde-baseapps-konq-plugins")
    categories=('network')
    install=konqueror.install

    splitdirs="konq-plugins"
    for i in ${splitdirs} ; do
        cd ${srcdir}/${pkgbase}-${pkgver}/${i}
        make DESTDIR=${pkgdir} install
    done
}
