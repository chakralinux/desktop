pkgname=rsyslog
pkgver=8.33.0
pkgrel=1
pkgdesc="An enhanced multi-threaded syslogd with a focus on security and reliability"
url="http://www.rsyslog.com/"
arch=('x86_64')
license=('GPL3')
depends=('zlib' 'libestr' 'libee' 'json-c' 'systemd' 'liblogging' 'librelp>=1.2.12' 'libfastjson>=0.99.8')
makedepends=('postgresql-libs>=8.4.1' 'libmariadbclient' 'net-snmp' 'gnutls'
	     'python3-docutils')
optdepends=('postgresql-libs: PostgreSQL Database Support'
	    'libmariadbclient: MySQL Database Support'
	    'net-snmp'
	    'gnutls')
backup=('etc/rsyslog.conf'
	'etc/logrotate.d/rsyslog')
source=("https://github.com/rsyslog/rsyslog/archive/v$pkgver.tar.gz"
        'rsyslog.logrotate'
	'rsyslog.conf')
sha256sums=('c52694efebbc86af80e61d660735291bdc68a4605b47a639ded79915e52cfac6'
            '0f5bea3fd4dff2c9f097bf95768b2e1f6e9cfd9a08eab98bc3b3b4d2ed44119a'
            '5fd51665ab9a81fbb24773068cb261b8dec073d74082c121633f49b9381d9a3f')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i rsyslog.service.in \
    -e 's|rsyslogd -n|rsyslogd -n -i /run/rsyslogd.pid|' \
    -e '/ExecStart=.*$/iPIDFile=/run/rsyslogd.pid'
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  autoreconf -fvi
  ./configure --prefix=/usr \
              --enable-mysql \
              --enable-pgsql \
              --enable-mail \
              --enable-imfile \
              --enable-snmp \
              --enable-gnutls \
              --enable-inet \
              --enable-imjournal \
              --enable-omjournal \
              --enable-relp \
              --enable-impstats \
              --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}
  install -Dm644 $srcdir/${pkgname}.conf ${pkgdir}/etc/${pkgname}.conf
  install -Dm644 $srcdir/${pkgname}.logrotate ${pkgdir}/etc/logrotate.d/${pkgname}
  install -Dm644 sample.conf $pkgdir/usr/share/rsyslog/sample.conf
}
