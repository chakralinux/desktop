#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=chocolate-doom
pkgver=1.5.0
pkgrel=1
pkgdesc="A Doom port reproducing the behavior of the original DOS version."
arch=('i686' 'x86_64')
url="http://www.chocolate-doom.org/"
license=('GPL2')
depends=('sdl_mixer' 'sdl_net')
makedepends=('autoconf' 'automake')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz \
	chocolate-doom.png \
	chocolate-setup.png \
        chocolate-doom.desktop \
        chocolate-setup.desktop)
md5sums=('e3c78de5965da92b1592e2d20af87e05'
         '7202cf0d89b6e339996028eaa08087b5'
         'dbd12210652965e96a8127e22fe4a4cb'
         '3ba5256126c0c5a0492c53d4c8beb45d'
         'da656a93e2f855761c7cce2874d6e72c')

build() {
  cd $srcdir/$pkgname-$pkgver

  sed 's|/games|/bin|g' -i {src,setup}/Makefile.am
  sed 's|share/games|share|g' -i src/d_iwad.c
  sed	-e '/samplerate/d' \
	-e 's| CFLAGS=|: # CFLAGS=|g' \
	-i configure.in

  autoreconf -i

  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  install -m644 -D	../chocolate-doom.png	$pkgdir/usr/share/pixmaps/$pkgname.png
  install -m644		../chocolate-setup.png	$pkgdir/usr/share/pixmaps/chocolate-setup.png
  mkdir -p					$pkgdir/usr/share/{applications,doom}
  install -m644 	../chocolate-{doom,setup}.desktop \
						$pkgdir/usr/share/applications/
  install -m644 -D	src/doom-screensaver.desktop \
						$pkgdir/usr/share/applications/chocolate-doom-screensaver.desktop
}
