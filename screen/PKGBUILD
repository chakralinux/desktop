pkgname=screen
pkgver=4.4.0
pkgrel=1
_ptygroup=5 #the UID of our PTY/TTY group
pkgdesc="Full-screen window manager that multiplexes a physical terminal"
arch=('x86_64')
url="http://www.gnu.org/software/screen/"
license=('GPL')
depends=('ncurses' 'pam')
backup=('etc/screenrc' 'etc/pam.d/screen')
options=('!makeflags')
groups=('system')
install=screen.install
source=("http://ftp.gnu.org/gnu/screen/screen-${pkgver}.tar.gz"
	screen.pam)
md5sums=('d26e11a3648c2b11aaefa215a55dfd39'
         '74f7919d7b65436cc2ea5341fa8ca374')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure \
		  --prefix=/usr \
		  --enable-pam \
		  --enable-colors256 \
		  --enable-rxvt_osc \
		  --enable-telnet \
		  --with-pty-group=$_ptygroup \
		  --mandir=/usr/share/man \
		  --infodir=/usr/share/info \
		  --with-socket-dir=/run/screens \
		  --with-sys-screenrc=/etc/screenrc \
		  
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
  
  install -Dm644 ${srcdir}/screen.pam ${pkgdir}/etc/pam.d/screen

  install -Dm644 etc/completer.zsh ${pkgdir}/usr/share/zsh/site-functions/screen
  install -Dm644 etc/etcscreenrc ${pkgdir}/etc/screenrc
  install -Dm644 etc/screenrc ${pkgdir}/etc/skel/.screenrc
}
