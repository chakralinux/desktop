# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=sfml
pkgver=2.1
_pkgver=2.1
pkgrel=1
pkgdesc="A simple, fast, cross-platform and object-oriented multimedia API."
arch=('x86_64')
url="http://www.sfml-dev.org"
license=('ZLIB')
depends=('freetype2' 'glew' 'libjpeg-turbo' 'libsndfile' 'libxrandr' 'openal' 'soil')
makedepends=('mesa' 'doxygen' 'cmake')
source=("http://www.sfml-dev.org/download/sfml/${pkgver}/SFML-${pkgver}-sources.zip")
md5sums=('2de81448733f3f46964f23f41cd42e92')

build() {
  cd "$srcdir"/SFML-${pkgver}

  mkdir -p build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr .. \
        -DSFML_BUILD_EXAMPLES=true \
        -DSFML_BUILD_DOC=true
  make
}

package() {
  cd "$srcdir"/SFML-${pkgver}/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$pkgdir/usr/share/SFML/cmake/Modules/FindSFML.cmake" "$pkgdir/usr/share/cmake-2.8/Modules/FindSFML.cmake"

  install -Dm644 ../license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
