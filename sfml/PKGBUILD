# Contribution from Arch https://www.archlinux.org/packages/community/x86_64/sfml/

pkgname=sfml
pkgver=2.5.1
pkgrel=1
pkgdesc='A simple, fast, cross-platform, and object-oriented multimedia API'
arch=('x86_64')
url='https://www.sfml-dev.org/'
license=('zlib')
depends=('libsndfile' 'libxrandr' 'libjpeg' 'openal' 'glew' 'freetype2' 'xcb-util-image' 'libx11')
makedepends=('mesa' 'cmake' 'doxygen' 'systemd')
source=("https://github.com/SFML/SFML/archive/${pkgver}.tar.gz")
sha256sums=('438c91a917cc8aa19e82c6f59f8714da353c488584a007d401efac8368e1c785')

build() {
  cd "$srcdir"/SFML-${pkgver}

  mkdir -p build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -SFML_USE_SYSTEM_DEPS=ON \
      -DSFML_BUILD_EXAMPLES=1 \
      -DSFML_BUILD_DOC=1 \
      -DSFML_INSTALL_PKGCONFIG_FILES=1
  make
  make doc
}

package() {
  cd "$srcdir"/SFML-${pkgver}/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 ../license.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

