# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributions from: https://www.archlinux.org/packages/community/x86_64/sfml/

pkgname=sfml
_pkgname=SFML
pkgver=2.1
pkgrel=1
pkgdesc="A simple, fast, cross-platform and object-oriented multimedia API."
arch=('x86_64')
url="http://www.sfml-dev.org"
license=('ZLIB')
depends=('freetype2' 'glew' 'libjpeg-turbo' 'libsndfile' 'libxrandr' 'openal' 'soil')
makedepends=('mesa' 'doxygen' 'cmake')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}::https://github.com/LaurentGomila/SFML/archive/${pkgver}.tar.gz")
md5sums=('6457a5903b156390012773e9ef33f65f')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver/src/${_pkgname}/Window/Linux"
  sed -i '26a #define GLXContextID XID' GlxContext.hpp
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSFML_BUILD_EXAMPLES=true \
    -DSFML_BUILD_DOC=true
  make
  make doc
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$pkgdir/usr/share/SFML/cmake/Modules/FindSFML.cmake" "$pkgdir/usr/share/cmake-2.8/Modules/FindSFML.cmake"
  install -Dm644 ../license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
