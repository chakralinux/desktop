# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=sfml
_pkgname=SFML
pkgver=2.0
pkgrel=1
pkgdesc="A simple, fast, cross-platform and object-oriented multimedia API."
arch=('x86_64')
url="http://www.sfml-dev.org"
license=('ZLIB')
depends=('freetype2' 'glew' 'libjpeg-turbo' 'libsndfile' 'libxrandr' 'openal' 'soil')
makedepends=('mesa' 'doxygen' 'cmake')
source=("http://www.sfml-dev.org/download/sfml/$pkgver/SFML-$pkgver-sources.zip")
md5sums=('fd40dad518fdda52244475a2007dd584')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  mkdir build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSFML_BUILD_EXAMPLES=ON \
    -DSFML_BUILD_DOC=ON \
    ..
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$pkgdir/usr/share/SFML/cmake/Modules/FindSFML.cmake" "$pkgdir/usr/share/cmake-2.8/Modules/FindSFML.cmake"
  install -Dm644 ../license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
