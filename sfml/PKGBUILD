# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=sfml
_pkgname=SFML
pkgver=2.0rc1
_pkgver=2.0-rc
pkgrel=1
pkgdesc="A simple, fast, cross-platform and object-oriented multimedia API"
arch=('i686' 'x86_64')
url="http://www.sfml-dev.org"
license=('ZLIB')
depends=('freetype2' 'glew' 'libjpeg' 'libpng' 'libsndfile' 'libxrandr' 'openal')
makedepends=(mesa cmake doxygen)
changelog=ChangeLog
source=("$pkgname-$pkgver.tar.gz::https://github.com/LaurentGomila/$_pkgname/tarball/$_pkgver")
md5sums=('03c8e6817f4525092b94a3d44e8eada8')

build() {
  cd $srcdir/Laurent*
  mkdir -p build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOC=true \
    -DBUILD_EXAMPLES=true \
    ..
  make
  make doc
}

package() {
  cd "$srcdir"/Laurent*/build
  make DESTDIR="$pkgdir/" install

  # CMake Module.
  install -Dm644 "$pkgdir/usr/share/$_pkgname/cmake/Modules/FindSFML.cmake" "$pkgdir/usr/share/cmake-2.8/Modules/FindSFML.cmake"

  # License.
  install -Dm644 ../license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
