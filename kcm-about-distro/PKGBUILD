# Maintainer (Chakra): msx <msx[at]archlinux[dot]us>

pkgname=kcm-about-distro
_pkgname=about-distro
pkgver=2.0.1
pkgrel=3
pkgdesc="KCM displaying distribution and system information"
arch=('x86_64')
url='https://projects.kde.org/about-distro'
screenshot="http://i.imgur.com/ZidApNr.png"
license=('GPL3')
depends=('kconfigwidgets' 'solid' 'python3')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/stable/$_pkgname/$pkgver/src/$_pkgname-$pkgver.tar.xz"
        'kcm-about-distrorc'
        'chakra-label.png')
        #'Module_Chakra.patch'
md5sums=('bfde9eea480fb59af8c1817ed39fd8c2'
         '8ad607452f80603c36f5ba28e30f51d6'
         'becd80aad126da45464128e825acd116')
         #'f0c4012841d4dc68fe54f3b25982abaf'
install="kcm-about-distro.install"

prepare(){
  cd ${srcdir}/about-distro-*
  
  # Why we need this patch?
  #patch -Np1 -i ${srcdir}/Module_Chakra.patch
  
  # Let's remove Chakra's release date to make space for the whole name's project
  sed -i 's/%1\ %2/%1/g' src/Module.cpp
  
}


build() {
  cd "${srcdir}"
  
  mkdir -p build
  cd build
  cmake ../$_pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/kcm-about-distrorc" "$pkgdir/etc/xdg/kcm-about-distrorc"
  install -Dm644 "$srcdir/chakra-label.png" "$pkgdir/usr/share/$_pkgname/chakra-label.png"
}
