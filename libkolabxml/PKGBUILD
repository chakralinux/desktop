
pkgname=libkolabxml
pkgver=1.1.1
pkgrel=1
pkgdesc="Kolab XML format schema definitions library"
url='http://git.kolab.org/libkolabxml/'
arch=('x86_64')
license=('GPL')
depends=('xerces-c'
         'boost-libs')
makedepends=('cmake'
             'boost'
             'xsd'
             'qt'
             'swig')
source=("http://mirror.kolabsys.com/pub/releases/${pkgname}-${pkgver}.tar.gz"{,.gpg})
sha256sums=('98b6a9fb9e792fd0a1e4e249f22e5f85670a9a4432c5b58b1b625b2985042f3b'
            '0557db50cfa1a3d6e3841af898b21d29f1c17182b9091fb22f264b4d78ad4981')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT5_BUILD=ON \
    -DBUILD_TESTS=OFF  
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
