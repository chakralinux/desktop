# Maintainer:
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Andrea Scarpino <andrea@archlinux.org>

pkgname=libkolabxml
pkgver=1.0.1
pkgrel=3
pkgdesc="Kolab XML Format Schema Definitions Library"
url='http://git.kolab.org/libkolabxml/'
arch=('x86_64')
license=('GPL')
depends=('xerces-c'
         'boost-libs')
makedepends=('cmake'
             'boost'
             'xsd'
             'qt'
             'swig')
source=("http://mirror.kolabsys.com/pub/releases/${pkgname}-${pkgver}.tar.gz"{,.gpg})
sha512sums=('cfdd4e952cf1d59703659a053c732eadf88a32dcaee778de7410062886ff177648724f187d657c7168383a18af340b2fb96599024c90f54321f5116b252e0259'
            'bd06d920f5ecec51b7342a32626fe27e20d1089e91b411428f04dd96a8eaf5aec9bac9f4042c0f11a3ea0bef4952ac26e83722f4daf87c5f45185c80577c0575')

prepare() {
    mkdir "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake "../${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DQT_QMAKE_EXECUTABLE=qmake
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}
