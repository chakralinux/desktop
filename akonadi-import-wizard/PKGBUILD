# Include global configuration

pkgname=akonadi-import-wizard
pkgver=18.12.1
pkgrel=1
pkgdesc="Import data from other mail clients to KMail"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(hicolor-icon-theme mailcommon)
makedepends=(extra-cmake-modules boost kdoctools python3)
conflicts=('kmail<16.11')
options=(docs debug)
groups=(kde kde-uninstall kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b2dc7882d394610ce42a17290d41f0ef4dae4de4bdd82e6f0c11d1b0afb62527'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
