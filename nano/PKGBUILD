pkgname=nano
pkgver=2.8.6
_mainver=2.8
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="https://www.nano-editor.org"
groups=('base')
depends=('glibc' 'ncurses' 'sh')
source=(http://www.nano-editor.org/dist/v${_mainver}/nano-${pkgver}.tar.gz)
backup=(etc/nanorc)
install=nano.install
md5sums=('e05d83c0f1687b733322f806c080d256')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-color \
              --enable-nanorc \
	      --enable-multibuffer \
	      --disable-wrapping-as-root
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}
