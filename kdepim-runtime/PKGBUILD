# Include global configuration

pkgname=kdepim-runtime
pkgver=18.12.1
pkgrel=1
pkgdesc='Extends the functionality of kdepim'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=(hicolor-icon-theme libkgapi libkolab akonadi-socialutils akonadi-calendar
         knotifyconfig kross kalarmcal kmbox kimap qt5-speech syndication kdav pimcommon)
makedepends=(extra-cmake-modules kdoctools boost)
options=('debug')
provides=('kio-pim') # the libkdepim pkg was splitted during the 16.08 release
replaces=('kio-pim') # kio is now part of multiple packages (kmailtransport, kldap, kdepim-runtime)
conflicts=('kio-pim') # can be removed when 16.12 is released

source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6c5963d47becb0ba39b2ab8a6ec35b41c7a3e7205c8937d8a48773e93fce69ed'
            'SKIP')

validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
