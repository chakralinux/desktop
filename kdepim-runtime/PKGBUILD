# Include global configuration
source ../kdeapps.conf

pkgname=kdepim-runtime
pkgver=${_kdever}
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=(hicolor-icon-theme libkgapi libkolab akonadi-socialutils akonadi-calendar
         knotifyconfig kross kalarmcal kmbox kimap qt5-speech syndication kdav)
makedepends=(extra-cmake-modules kdoctools boost)
install=$pkgname.install
options=('debug')
provides=('kio-pim') # the libkdepim pkg was splitted during the 16.08 release
replaces=('kio-pim') # kio is now part of multiple packages (kmailtransport, kldap, kdepim-runtime)
conflicts=('kio-pim') # can be removed when 16.12 is released

source=("$_mirror/${pkgname}-$_kdever.tar.xz"{,.sig})
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
            'SKIP')

# KDE patch
source+=(kdebug-379733.patch::'https://cgit.kde.org/kdepim-runtime.git/patch/?id=e8b131fe179f474af9de12f1289ea70cd99e90e4')
sha256sums+=('1e4b78e14922a1df7d29dfc7efe7e4c8def5eee41062ca7f401c0a867ffe3901')

validpgpkeys=(${Avalidpgpkeys})

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  # fix crash on account configuration
  patch -p1 -i ../kdebug-379733.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
