# Include global configuration
source ../kdeapps.conf

pkgname=kdepim-runtime
pkgver=${_kdever}
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=(hicolor-icon-theme libkgapi libkolab akonadi-socialutils akonadi-calendar
         knotifyconfig kross kalarmcal kmbox kimap qt5-speech syndication)
makedepends=(extra-cmake-modules kdoctools boost)
install=$pkgname.install
options=('debug')
provides=('kio-pim') # the libkdepim pkg was splitted during the 16.08 release
replaces=('kio-pim') # kio is now part of multiple packages (kmailtransport, kldap, kdepim-runtime)
conflicts=('kio-pim') # can be removed when 16.12 is released

source=("$_mirror/${pkgname}-$_kdever.tar.xz"{,.sig})
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
                        'SKIP')

source+=("https://github.com/KDE/kdepim-runtime/commit/8ae09b6f4afa8ceb2770da67b6dc79c78b86af47.diff")
sha256sums+=('eaa67581a14a0c84d74ca7d3dac7a585fa40355940b33351587f698b46008053')

validpgpkeys=(${Avalidpgpkeys})

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}

  #Fix DATA LOSS bug in ical resource which failed to create std.ics if it didn't exist
  patch -p1 -i ${srcdir}/8ae09b6f4afa8ceb2770da67b6dc79c78b86af47.diff
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
