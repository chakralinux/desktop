# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=oxygen-icons
pkgname=('oxygen-icons' 'oxygen-icons-svg')
arch=('x86_64')
pkgver=${_kdever}
pkgrel=3
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
install=oxygen-icons.install
options=('log')
makedepends=('pkg-config' 'cmake' 'automoc4')
source=($_mirror/$pkgbase-$_kdever.tar.xz)
sha256sums=(`grep $pkgbase-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

prepare() {
    mkdir -p "${srcdir}/svg"
    cd "${srcdir}/$pkgbase-$pkgver"
    patch -Np0 -i "${srcdir}/install_svg_icons_too.patch"
}

build() {
    cd "${srcdir}/$pkgbase-$pkgver"
    cmake . \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=${_installprefix}
    make
}

package_oxygen-icons() {
    pkgdesc="Oxygen icons"
    groups=("kde" "kde-minimal" "kde-uninstall")

    cd "${srcdir}/$pkgbase-$pkgver"
    make DESTDIR=${pkgdir} install
    
    # Move SVG files
    mv "${pkgdir}/usr/share/icons/oxygen/scalable" "${srcdir}/svg"
}

package_oxygen-icons-svg() {
    pkgdesc="Oxygen icons (SVG sources)"
    groups=("kde-uninstall")
    depends=("oxygen-icons")

    mkdir -p "${pkgdir}/usr/share/icons/oxygen"
    mv "${srcdir}/svg/scalable" "${pkgdir}/usr/share/icons/oxygen"
    rm -rf "$pkgdir/usr/share/icons/oxygen/scalable/apps/clock.svgz"
}
