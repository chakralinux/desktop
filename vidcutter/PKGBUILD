pkgname=vidcutter
pkgver=3.5.0
pkgrel=1
pkgdesc="FFmpeg based video cutter & joiner with a modern PyQt5 GUI"
arch=('x86_64')
license=('GPL3')
url="http://vidcutter.ozmartians.com"
depends=('python3-pyqt5' 'python3-sip' 'ffmpeg' 'mpv' 'mediainfo')
makedepends=('git' 'python3-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ozmartian/${pkgname}/archive/${pkgver}.tar.gz)
sha256sums=('6c07aa8d70c66e11ce2bcc37cea6083a84fe8b4602342da40a78e651dcd04b93')

prepare() {
    cd $pkgname-$pkgver
    sed -i "s/pypi/arch/" "vidcutter/__init__.py"
}

build() {
    cd $pkgname-$pkgver
    python3 setup.py build
}

package() {
    cd $pkgname-$pkgver
    python3 setup.py install --root=$pkgdir --optimize=1 --skip-build
}
