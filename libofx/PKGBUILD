pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.9.11
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('x86_64')
url="http://libofx.sourceforge.net"
license=('GPL')
depends=('opensp' 'curl' 'libxml++')
checkdepends=('gnupg')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha1sums=('ec716e0ab85d64c3211a9e0a397d4e9d552ab61c')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr
  make CXXFLAGS+=-std=c++11
}

check() {
  cd "$srcdir/$pkgbase-$pkgver"
  make check
}

package_libofx() {
  options=('!docs')

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install-docDATA
  make -C doc DESTDIR="$pkgdir" install
}
