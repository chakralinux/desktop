pkgname=pdf2svg
pkgver=0.2.3
pkgrel=1
pkgdesc="A PDF to SVG converter."
arch=('x86_64')
url="http://www.cityinthesky.co.uk/pdf2svg.html"
license=('GPL3')
depends=('lcms2' 'poppler-glib' 'glib2')
source=("https://github.com/dawbarton/pdf2svg/archive/v${pkgver}.tar.gz")
sha256sums=('4fb186070b3e7d33a51821e3307dce57300a062570d028feccd4e628d50dea8a')

prepare() {
  cd ${pkgname}-${pkgver}
  # fixes to remove the gtk2 dependency (unneeded, because glib contains everything)
  sed -i '22a #include <stdlib.h>' ./pdf2svg.c
  sed -i "23,24d" ./configure.ac
  sed -i -e 's/$(GTK2_[^)]*) //g' ./Makefile.am  # reomve notion of gtk2
}

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
