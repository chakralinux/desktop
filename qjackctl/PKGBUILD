pkgname=qjackctl
pkgver=0.5.4
pkgrel=1
pkgdesc="A Qt front-end for the JACK low-latency audio server"
url="http://qjackctl.sourceforge.net/"
arch=('x86_64')
license=('GPL')
depends=('jack' 'qt5-base' 'qt5-x11extras')
makedepends=('qt5-tools')
options=('!makeflags')
categories=('multimedia')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha1sums=('37b41499c249bd76d7c0b8e6c7ef5475dd944dcf')

build() {
  cd $pkgname-$pkgver
 ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
