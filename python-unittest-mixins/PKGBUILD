# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-unittest-mixins
pkgname=('python3-unittest-mixins' 'python2-unittest-mixins')
pkgver=1.4
pkgrel=1
pkgdesc='Helpful mixins for unittest classes'
arch=('any')
license=('Apache')
url='https://github.com/nedbat/unittest-mixins'
makedepends=('python3-setuptools' 'python2-setuptools')
checkdepends=('python3-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/nedbat/unittest-mixins/archive/v$pkgver.tar.gz")
sha512sums=('c6f23c8013f1f19de46c22f73e52d7b88c8646ac4a5b28423bb9c4835b87706cd7f5cef53627a7118cdaa6f58689c5511d7ae8710eb8f4d2a3ceef3f911c1521')

prepare() {
  cp -a unittest-mixins-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/unittest-mixins-$pkgver
  python3 setup.py build

  cd "$srcdir"/unittest-mixins-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/unittest-mixins-$pkgver
  python3 setup.py pytest

  cd "$srcdir"/unittest-mixins-$pkgver-py2
  python2 setup.py pytest
}

package_python3-unittest-mixins() {
  depends=('python3-six')

  cd unittest-mixins-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-unittest-mixins() {
  depends=('python2-six')

  cd unittest-mixins-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
