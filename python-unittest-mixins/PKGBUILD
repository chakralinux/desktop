# Contributions from Arch:
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-unittest-mixins
pkgname=('python3-unittest-mixins' 'python2-unittest-mixins')
pkgver=1.6
pkgrel=1
pkgdesc='Helpful mixins for unittest classes'
arch=('any')
license=('Apache')
url='https://github.com/nedbat/unittest-mixins'
makedepends=('python3-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/nedbat/unittest-mixins/archive/v$pkgver.tar.gz")
sha256sums=('387ebaaf7095fd1e293fa9b8d29148920493fdc37f8be59f070077b06f358bb8')

prepare() {
  cp -a unittest-mixins-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/unittest-mixins-$pkgver
  python3 setup.py build

  cd "$srcdir"/unittest-mixins-$pkgver-py2
  python2 setup.py build
}

package_python3-unittest-mixins() {
  depends=('python3-six')

  cd unittest-mixins-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-unittest-mixins() {
  depends=('python2-six')

  cd unittest-mixins-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
