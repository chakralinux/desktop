pkgname=ed
pkgver=1.14.1
pkgrel=1
pkgdesc="A POSIX-compliant line-oriented text editor"
arch=('x86_64')
license=('GPL')
url="http://www.gnu.org/software/ed/ed.html"
depends=('glibc' 'sh')
groups=('base-devel')
options=('!emptydirs')
source=("ftp://ftp.gnu.org/gnu/ed/$pkgname-$pkgver.tar.lz")
install='ed.install'
sha256sums=('ffb97eb8f2a2b5a71a9b97e3872adce953aa1b8958e04c5b7bf11d556f32552a')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/ \
              --bindir=/usr/bin \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              "CFLAGS=$CFLAGS" \
              "LDFLAGS=$LDFLAGS"

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" "DESTDIR=$pkgdir" install
}
