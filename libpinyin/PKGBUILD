# Maintainer: UtG  <utg[dot]chakra.linux[at]gmail[dot]com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=libpinyin
pkgver=1.0.0
pkgrel=1
_modelver=7
pkgdesc="Library to deal with pinyin."
arch=('x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('db' 'glib2')
makedepends=('git' 'libtool' 'autoconf' 'automake')
source=("git+https://github.com/$pkgname/${pkgname}.git#tag=$pkgver"
        "http://downloads.sourceforge.net/libpinyin/model${_modelver}.text.tar.gz")
noextract=("model${_modelver}.text.tar.gz")
sha512sums=('SKIP'
            '74a56a23cdca24124037f688ed85a921089155a1b9a0b61e646f805b2bb698c56682c5d6a47ff86657c8b131a831dbee9d5cc5165d4c1f235b77fcd5b2e480ad')

build() {
  cd ${pkgname}

  cp ../model${_modelver}.text.tar.gz data/
  sed -i "/wget.*model${_modelver}\.text\.tar\.gz/ d" data/Makefile.am

  aclocal && libtoolize --force && autoheader && automake -a && autoconf
  ./configure --prefix=/usr && make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}
