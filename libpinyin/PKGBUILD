pkgname=libpinyin
pkgver=1.5.92
pkgrel=1
_modelver=11
pkgdesc="Library to deal with pinyin."
arch=('x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('db' 'glib2')
makedepends=('git' 'libtool' 'autoconf' 'automake')
source=("https://github.com/libpinyin/libpinyin/archive/${pkgver}.tar.gz"
        "http://downloads.sourceforge.net/libpinyin/model${_modelver}.text.tar.gz")
noextract=("model${_modelver}.text.tar.gz")
sha256sums=('af57a8d02be16e9f9491c33c57b362dd2d9bbc6c46d7993c6d6553fe525526ec'
            'd0428e87a8c6b214036641a3317834f8352e77cbba0f040c7bd3495d13ef3f71')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  cp ../model${_modelver}.text.tar.gz data/
  sed -i "/wget.*model${_modelver}\.text\.tar\.gz/ d" data/Makefile.am

  aclocal && libtoolize --force && autoheader && automake -a && autoconf
  ./configure --prefix=/usr && make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
