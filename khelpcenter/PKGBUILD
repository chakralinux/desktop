#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/khelpcenter

pkgname=khelpcenter
pkgver=18.04.3
pkgrel=1
pkgdesc="Application to show KDE Applications' documentation"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/khelpcenter'
license=('LGPL')
depends=('khtml' 'kinit' 'kdoctools' 'xapian-core' 'grantlee')
makedepends=('extra-cmake-modules' 'python3')
groups=('kde' 'kde-minimal' 'kde-uninstall')
options=("debug")
conflicts=('khelpcenter4' 'kde-runtime-doc')
replaces=('khelpcenter4' 'kde-runtime-doc')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('867372d2cee8ccbcb383edf869b6311465e5c8cb032c25d2b130da1435943e1d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
