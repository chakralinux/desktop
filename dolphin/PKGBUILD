# Include global configuration

pkgname=dolphin
pkgver=18.08.0
pkgrel=1
pkgdesc="File Manager"
arch=('x86_64')
url="http://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=('baloo-widgets' 'knewstuff' 'kio-extras' 'ktexteditor' 'kactivities-frameworks' 'kio>=5.16.0' 'kcmutils')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
conflicts=('kde-baseapps-dolphin<15.04.3-1')
replaces=(kde-baseapps-dolphin)
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdebase')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5efb4c5dc304a6d179915b6553ad0f5232dc4e6b38a9d4dbd80a72a5351e6fe4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
