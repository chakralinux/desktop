# Include global configuration
source ../kdeapps.conf

pkgname=dolphin
pkgver=${_kdever}
pkgrel=3
pkgdesc="File Manager"
arch=('x86_64')
url="http://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=('baloo-widgets' 'knewstuff' 'kio-extras' 'ktexteditor' 'kactivities-frameworks')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
conflicts=('kde-baseapps-dolphin<15.04.3-1')
replaces=(kde-baseapps-dolphin)
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdebase')
install=$pkgname.install
options=('docs' 'debug')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        'konqpopupmenuplugin.desktop')
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`
            '992d383b854427fcd5fc6b7ea6ccd050d2d6c5d24859b6a5a3fec62a9e261540')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install

  # workaround for service menu to work, the devs are using mix DE so they didn't notice this
  install -Dm644 ${srcdir}/konqpopupmenuplugin.desktop \
                 ${pkgdir}/usr/share/kservicetypes5/konqpopupmenuplugin.desktop
}
