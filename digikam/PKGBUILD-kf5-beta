pkgbase=digikam
pkgname=('digikam' 'kipi-plugins')
_pkgver=5.0.0-beta2
pkgver=${_pkgver//-/_}
pkgrel=1
url="http://digikam.sourceforge.net"
arch=('x86_64')
license=('GPL')
options=("debug")
makedepends=('extra-cmake-modules' 'libkipi' 'libksane' 'liblqr' 'opencv' 'boost'
             'libgpod' 'hugin' 'opencv' 'qt5-webkit' 'doxygen' 'lensfun' 'imagemagick' 'eigen3' 'libpgf'
             'libusb' 'kio' 'kdoctools' 'marble' 'kfilemetadata' 'akonadi-contact' 'qt5-multimedia'
             'threadweaver' 'kcalcore' 'knotifyconfig' 'kdelibs4support' 'libkvkontakte' 'libmediawiki')
source=("http://download.kde.org/unstable/${pkgname}/${pkgname}-${_pkgver}.tar.bz2")
sha1sums=('db2f6d7e84e67a338bee9373bdba2dc116f5e1f0')


prepare() {
  mkdir -p build

  sed -e 's|${KF5_MIN_VERSION}||' -i $pkgname-$_pkgver/core/CMakeLists.txt -i $pkgname-$_pkgver/extra/kipi-plugins/CMakeLists.txt
}

build() {

  cd build

  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DDIGIKAMSC_CHECKOUT_PO=ON 
  make
}

package_digikam() {
  pkgdesc="Digital photo management application for KDE"
  depends=('liblqr' 'libkipi' 'lensfun' 'libpgf' 'opencv' 'akonadi-contact'
	'knotifyconfig' 'libksane' 'kfilemetadata' 'qt5-multimedia' 'marble')
  optdepends=('kipi-plugins: more tools and plugins')
  install=digikam.install

  pushd build/core
  make DESTDIR="${pkgdir}" install
  popd
  
  pushd build/doc/digikam
  make DESTDIR="${pkgdir}" install
  popd
}

package_kipi-plugins() {
  pkgdesc="A collection of plugins extending the KDE graphics and image applications as digiKam"
  depends=('kio' 'libkipi')
  optdepends=('kcalcore: Calendar plugin'
      #       'libmediawiki: MediaWiki Export plugin'
      #       'libkvkontakte: VKontakte.ru Exporter plugin'
              'libgpod: iPodExport plugin'
              'opencv: Remove Red Eyes plugin'
              'imagemagick: Video SlideShow plugin'
              'qt5-gstreamer: Video SlideShow plugin'
              'threadweaver: panorama plugin'
	      'hugin: panorama plugin')
  install=kipi-plugins.install

  pushd build/extra/kipi-plugins
  make DESTDIR="${pkgdir}" install
  popd

  pushd build/doc/kipi-plugins
  make DESTDIR="${pkgdir}" install
  popd
}
