#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgbase=digikam
pkgname=('digikam' 'kipi-plugins' 'libkvkontakte')
pkgver=2.4.1
pkgrel=1
url="http://digikam.sourceforge.net"
arch=('i686' 'x86_64')
license=('GPL')
categories=('graphics')
makedepends=('kde-runtime' 'kdegraphics-kgamma' 'kdepimlibs' 'lensfun' 'kdeedu-marble' 'liblqr' 'opencv' 'lapack' 'libgpod'
	     'pkgconfig' 'cmake' 'automoc4' 'doxygen' 'docbook-xsl' 'qjson')
source=(http://sourceforge.net/projects/digikam/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('4e36b8cc33f5ac589e4b45b22c902fdd')

build() {
   cd ${srcdir}

   mkdir -p build
   cd build
   cmake ../${pkgname}-${pkgver} \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DCMAKE_SKIP_RPATH=ON
   make 
}

package_digikam() {
  pkgdesc="Digital photo management application for kde"
  depends=('kde-runtime' 'kdegraphics-kgamma' 'kdepimlibs' 'lensfun' 'kdeedu-marble' 'liblqr' 'opencv' 'kipi-plugins' 'lapack' 'libgpod')
  install=digikam.install
  replaces=('digikamimageplugins')

  splitdirs="core doc po"
  for i in ${splitdirs} ; do
      cd ${srcdir}/build/${i}
      make DESTDIR="${pkgdir}" install
  done
  rm -rf "${pkgdir}/usr/share/icons/oxygen"
}

package_libkvkontakte() {
  pkgdesc="KDE C++ library for asynchronous interaction with vkontakte.rusocial network"
  depends=('kdelibs')

  cd "${srcdir}"/build/extra/libkvkontakte
  make DESTDIR="${pkgdir}" install
}

package_kipi-plugins() {
  pkgdesc="Kipi plugins and additional libs for digikam and kde apps"
  depends=('kdegraphics-kgamma' 'kdepimlibs' 'opencv' 'libgpod' 'sane' 'hugin-tools' 'libgphoto2-nolimit' 'libkvkontakte')
  optdepends=('imagemagick: for picture manipulation'
	      'qca: for shwup webservice support')
  install=kipi-plugins.install
  provides=('libkface libkgeomap libmediawiki')
  replaces=('libkface libkgeomap libmediawiki')
  conflicts=('libkface libkgeomap libmediawiki')

  splitdirs="libkface libkgeomap libmediawiki kipi-plugins"
  for i in ${splitdirs} ; do
      cd ${srcdir}/build/extra/${i}
      make DESTDIR="${pkgdir}" install
  done
}
