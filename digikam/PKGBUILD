pkgbase=digikam
pkgname=('digikam' 'kipi-plugins')
pkgver=5.0.0-beta6
pkgrel=1
url="http://digikam.sourceforge.net"
arch=('x86_64')
license=('GPL')
options=("debug")
makedepends=('extra-cmake-modules' 'libkipi' 'libksane' 'liblqr' 'boost'
             'libgpod' 'hugin' 'opencv' 'qt5-webkit' 'doxygen' 'lensfun' 'imagemagick' 'eigen3' 'libpgf'
             'libusb' 'kio' 'kdoctools' 'marble' 'kfilemetadata' 'akonadi-contact' 'qt5-multimedia'
             'threadweaver' 'kcalcore' 'knotifyconfig' 'kdelibs4support' 'libkvkontakte' 'libmediawiki' 'mariadb')
source=("http://download.kde.org/unstable/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('b3fb866e364df19bae4cf44676cd6d4161b71d70')


prepare() {
  mkdir -p build

  sed -e 's|${KF5_MIN_VERSION}||' -i $pkgname-$pkgver/core/CMakeLists.txt -i $pkgname-$pkgver/extra/kipi-plugins/CMakeLists.txt
}

build() {

  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_OPENCV3=ON \
    -DDIGIKAMSC_CHECKOUT_PO=ON  \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_INTERNALMYSQL=ON
    
  make
}

package_digikam() {
  pkgdesc="Digital photo management application for KDE"
  depends=('liblqr' 'libkipi' 'lensfun' 'libpgf' 'opencv' 'akonadi-contact'
	'knotifyconfig' 'libksane' 'kfilemetadata' 'qt5-multimedia' 'marble' 'mariadb')
  optdepends=('kipi-plugins: more tools and plugins')
  install=digikam.install

  pushd build/core
  make DESTDIR="${pkgdir}" install
  popd
  
  pushd build/doc/digikam
  make DESTDIR="${pkgdir}" install
  popd
}

package_kipi-plugins() {
  pkgdesc="A collection of plugins extending the KDE graphics and image applications as digiKam"
  depends=('kio' 'libkipi')
  optdepends=('kcalcore: Calendar plugin'
      #       'libmediawiki: MediaWiki Export plugin'
      #       'libkvkontakte: VKontakte.ru Exporter plugin'
              'libgpod: iPodExport plugin'
              'opencv: Remove Red Eyes plugin'
              'imagemagick: Video SlideShow plugin'
              'qt5-gstreamer: Video SlideShow plugin'
              'threadweaver: panorama plugin'
	      'hugin: panorama plugin')
  install=kipi-plugins.install

  pushd build/extra/kipi-plugins
  make DESTDIR="${pkgdir}" install
  popd

  pushd build/doc/kipi-plugins
  make DESTDIR="${pkgdir}" install
  popd
}
