 pkgbase=digikam
pkgname=('digikam' 'kipi-plugins')
pkgver=4.14.0
pkgrel=2
url="http://digikam.sourceforge.net"
arch=('x86_64')
license=('GPL')
screenshot=('http://i.imgur.com/6YnUM.png')
makedepends=('kde-runtime' 'kdepimlibs4' 'lensfun' 'libmarble4'
             'liblqr' 'opencv' 'lapack' 'libgpod' 'libkvkontakte' 'pkgconfig' 'cmake' 'automoc4'
             'doxygen' 'docbook-xsl' 'qjson' 'libkexiv2_4' 'libkdcraw4'
             'libksane4' 'mariadb' 'qt4-gstreamer' 'boost' 'hugin-tools' 'eigen3' 
             'libbluray' 'libpgf' 'libkface' 'libkgeomap' 'kfilemetadata4')
source=("http://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
options=("debug")
md5sums=('8fd3cb0a822e8c2d4102e8adc00c082f')

build() {
   cd ${srcdir}
 
   mkdir -p build && cd build

   cmake ../${pkgname}-${pkgver} \
     -DCMAKE_BUILD_TYPE=RelWithDebInfo \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DCMAKE_INSTALL_LIBDIR=/usr/lib \
     -DCMAKE_SKIP_RPATH=ON \
     -DBUILD_tests=OFF \
     -DDIGIKAMSC_COMPILE_LIBMEDIAWIKI=ON \
     -DENABLE_KDEPIMLIBSSUPPORT=ON \
     -DENABLE_MYSQLSUPPORT=ON

   make 
}

package_digikam() {
  pkgdesc="Digital photo management application for kde"
  depends=('kde-runtime' 'kdepimlibs4' 'lensfun' 'libmarble4' 'liblqr'
           'mariadb' 'opencv' 'kipi-plugins' 'lapack' 'libgpod' 'libpgf' 'libkface' 'libkgeomap')
  install=digikam.install
  replaces=('digikamimageplugins')
  categories=('graphics')
  screenshot=('http://farm4.staticflickr.com/3197/2606712653_e9b9dc6ca1_o.png')

  splitdirs="core doc po"
  for i in ${splitdirs} ; do
      cd ${srcdir}/build/${i}
      make DESTDIR="${pkgdir}" install
  done
  rm -rf "${pkgdir}/usr/share/icons/oxygen"
  rm "${pkgdir}"/usr/share/locale/kde4/*/LC_MESSAGES/libkipi.mo
}

package_kipi-plugins() {
  pkgdesc="Kipi plugins and additional libs for digikam and kde apps"
  depends=('kdepimlibs4' 'opencv' 'libgpod' 'sane' 'hugin-tools'
           'libgphoto2-nolimit' 'libkvkontakte' 'libkexiv2_4' 'libkdcraw4'
           'libksane4' 'qt4-gstreamer' 'kfilemetadata4')
  optdepends=('imagemagick: for picture manipulation'
	      'qca: for shwup webservice support'
	      'libkgeomap: Geolocalize tool')
  install=kipi-plugins.install
  categories=('graphics')

  cd ${srcdir}/build/extra/kipi-plugins

  make DESTDIR="${pkgdir}" install
}