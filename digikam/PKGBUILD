pkgbase=digikam
pkgname=('digikam' 'kipi-plugins')
_pkgver=5.3.0
pkgver=${_pkgver//-/_}
pkgrel=2
epoch=1
url="https://www.digikam.org/"
arch=('x86_64')
license=('GPL')
options=("debug")
makedepends=('extra-cmake-modules' 'libkipi' 'libksane' 'liblqr' 'boost'
             'libgpod' 'hugin' 'opencv' 'qt5-webkit' 'doxygen' 'lensfun' 'imagemagick' 'eigen3' 'libpgf'
             'libusb' 'kio' 'kdoctools' 'marble' 'kfilemetadata' 'akonadi-contact' 'qt5-multimedia'
             'threadweaver' 'kcalcore' 'knotifyconfig' 'libkvkontakte' 'libmediawiki' 'mariadb')
source=("http://download.kde.org/stable/${pkgname}/${pkgname}-${_pkgver}.tar.xz")
sha1sums=('3a62f6efdfafc37341626cbf0225cd7adc9d0633')

prepare() {
  mkdir -p build

  sed -e '/add_subdirectory(showfoto)/d' -i $pkgname-${_pkgver}/doc-translated/CMakeLists.txt
}

build() {

  cd build

  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_TESTING=OFF \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_OPENCV3=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_INTERNALMYSQL=ON \
    -DENABLE_APPSTYLES=ON
    
  make
}

package_digikam() {
  pkgdesc="Digital photo management application for KDE"
  depends=('liblqr' 'libkipi' 'lensfun' 'libpgf' 'opencv' 'akonadi-contact'
	   'knotifyconfig' 'libksane' 'kfilemetadata' 'qt5-multimedia' 'marble' 'mariadb' 'kcalcore')
  optdepends=('kipi-plugins: more tools and plugins'
	      'qt5-imageformats: support for additional image formats (WEBP, TIFF)'
	      'hugin: panorama tool')
  install=digikam.install

  cd build/core
  make DESTDIR="$pkgdir" install
  cd $srcdir/build/doc/digikam
  make DESTDIR="$pkgdir" install
  cd $srcdir/build/doc-translated
  make DESTDIR="$pkgdir" install
  cd $srcdir/build/po
  make DESTDIR="$pkgdir" install

  # Provided by kipi-plugins
  rm "$pkgdir"/usr/share/locale/*/LC_MESSAGES/{kipiplugin*,libkvkontakte}.mo
}

package_kipi-plugins() {
  pkgdesc="A collection of plugins extending the KDE graphics and image applications as digiKam"
  depends=('kio' 'libkipi')
  optdepends=('kcalcore: Calendar plugin'
              'libmediawiki: MediaWiki Export plugin'
              'libkvkontakte: VKontakte.ru Exporter plugin'
              'libgpod: iPodExport plugin'
              'opencv: Remove Red Eyes plugin'
              'imagemagick: Video SlideShow plugin'
              'qt5-gstreamer: Video SlideShow plugin'
              'threadweaver: panorama plugin'
	      'hugin: panorama plugin')
  install=kipi-plugins.install

  cd build/extra
  make DESTDIR="$pkgdir" install
  cd $srcdir/build/po
  make DESTDIR="$pkgdir" install

  # Provided by digikam
  rm "${pkgdir}"/usr/share/locale/*/LC_MESSAGES/digikam.mo
}
