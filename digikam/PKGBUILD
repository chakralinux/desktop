pkgbase=digikam
pkgname=('digikam' 'kipi-plugins' 'libkvkontakte')
pkgver=4.9.0
pkgrel=2
url="http://digikam.sourceforge.net"
arch=('x86_64')
license=('GPL')
screenshot=('http://i.imgur.com/6YnUM.png')
makedepends=('kde-runtime' 'kdegraphics-kgamma' 'kdepimlibs' 'lensfun' 'kdeedu-marble'
             'liblqr' 'opencv' 'lapack' 'libgpod' 'pkgconfig' 'cmake' 'automoc4'
             'doxygen' 'docbook-xsl' 'qjson' 'kdegraphics-libkexiv2' 'kdegraphics-libkdcraw'
             'kdegraphics-libksane' 'mariadb' 'qt-gstreamer' 'boost' 'hugin-tools' 'eigen3' 'libbluray' 'baloo' 'libpgf' 'libkface' 'libkgeomap')
source=("http://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
options=("debug")
md5sums=('9f0b00b6421fb310921399bae3089b5a')

build() {
   cd ${srcdir}
 
   mkdir -p build
   cd build
   cmake ../${pkgname}-${pkgver} \
     -DCMAKE_BUILD_TYPE=RelWithDebInfo \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DCMAKE_SKIP_RPATH=ON \
     -DBUILD_tests=OFF \
     -DDIGIKAMSC_COMPILE_LIBMEDIAWIKI=ON \
     -DDIGIKAMSC_COMPILE_LIBKVKONTAKTE=ON \
     -DENABLE_KDEPIMLIBSSUPPORT=ON \
     -DENABLE_MYSQLSUPPORT=ON

   make 
}

package_digikam() {
  pkgdesc="Digital photo management application for kde"
  depends=('kde-runtime' 'kdegraphics-kgamma' 'kdepimlibs' 'lensfun' 'kdeedu-marble' 'liblqr'
           'mariadb' 'opencv' 'kipi-plugins' 'lapack' 'libgpod' 'baloo' 'libpgf' 'libkface' 'libkgeomap')
  install=digikam.install
  replaces=('digikamimageplugins')
  categories=('graphics')
  screenshot=('http://farm4.staticflickr.com/3197/2606712653_e9b9dc6ca1_o.png')

  splitdirs="core doc po"
  for i in ${splitdirs} ; do
      cd ${srcdir}/build/${i}
      make DESTDIR="${pkgdir}" install
  done
  rm -rf "${pkgdir}/usr/share/icons/oxygen"
  rm "${pkgdir}"/usr/share/locale/kde4/*/LC_MESSAGES/libkipi.mo
}

package_libkvkontakte() {
  pkgdesc="KDE C++ library for asynchronous interaction with vkontakte.rusocial network"
  depends=('kdelibs')

  cd "${srcdir}"/build/extra/libkvkontakte
  make DESTDIR="${pkgdir}" install
}

package_kipi-plugins() {
  pkgdesc="Kipi plugins and additional libs for digikam and kde apps"
  depends=('kdegraphics-kgamma' 'kdepimlibs' 'opencv' 'libgpod' 'sane' 'hugin-tools'
           'libgphoto2-nolimit' 'libkvkontakte' 'kdegraphics-libkexiv2' 'kdegraphics-libkdcraw'
           'kdegraphics-libksane' 'qt-gstreamer')
  optdepends=('imagemagick: for picture manipulation'
	      'qca: for shwup webservice support'
	      'libkgeomap: Geolocalize tool')
  install=kipi-plugins.install
  provides=('libmediawiki')
  replaces=('libmediawiki')
  conflicts=('libmediawiki')
  categories=('graphics')

  splitdirs="libmediawiki kipi-plugins"
  for i in ${splitdirs} ; do
      cd ${srcdir}/build/extra/${i}

      make DESTDIR="${pkgdir}" install
  done
}
