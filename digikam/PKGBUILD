pkgname=digikam
_pkgver=6.0.0
pkgver=${_pkgver//-/_}
pkgrel=1
epoch=1
pkgdesc="Digital photo management application for KDE"
url="https://www.digikam.org/"
arch=('x86_64')
license=('GPL')
options=("debug")
depends=('liblqr' 'libkipi' 'lensfun' 'libpgf' 'opencv' 'akonadi-contact'
	   'knotifyconfig' 'libksane' 'kfilemetadata' 'qtav' 'marble' 'mariadb' 'kcalcore' 'exiv2')
makedepends=('extra-cmake-modules' 'libksane' 'liblqr' 'boost'
             'libgpod' 'opencv' 'qt5-webkit' 'doxygen' 'lensfun' 'imagemagick' 'eigen3' 'libpgf'
             'libusb' 'kio' 'kdoctools' 'marble' 'kfilemetadata' 'akonadi-contact' 'qtav'
             'threadweaver' 'kcalcore' 'knotifyconfig' 'libkvkontakte' 'libmediawiki' 'mariadb' 'prison'
             'libavc1394' 'libiec61883' 'jack2' 'exiv2' 'glu')
optdepends=('qt5-imageformats: support for additional image formats (WEBP, TIFF)'
	      'hugin: panorama tool')
source=("https://download.kde.org/stable/${pkgname}/${_pkgver}/${pkgname}-${_pkgver}.tar.xz"{,.sig})
sha1sums=('ccc8f5a793b3ebeb6b0a9756de9dbd2e5c267334'
          'SKIP')
validpgpkeys=(D1CF2444A7858C5F2FB095B74A77747BC2386E50) # digiKam.org (digiKam project) <digikamdeveloper@gmail.com>

prepare() {
  mkdir -p build
}

build() {

  cd build

  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_TESTING=OFF \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_INTERNALMYSQL=ON \
    -DENABLE_APPSTYLES=ON
  make
}

package() {
  


  cd build/core
  make DESTDIR="$pkgdir" install
  cd $srcdir/build/po
  make DESTDIR="$pkgdir" install
}
