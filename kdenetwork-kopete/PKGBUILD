# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_pkgname="kopete"
pkgname="kdenetwork-kopete"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="Kopete, the instant messenger"
url="http://kde.org/applications/internet/kopete/"
depends=("kde-runtime>=${_kdever}"
         "kdepimlibs>=${_kdever}"
         'qca-ossl'
         'libotr>=4.0.0'
         'libmsn'
         'libidn'
         'qimageblitz'
         'libxss'
         'libv4l'
         'msilbc'
         'libgadu')
groups=("kde"
        "kdenetwork"
        "kde-uninstall")
categories=('communication')
conflicts=("kdenetwork-common"
           "kdenetwork-doc")
install=kdenetwork.install
license=('GPL'
         'LGPL'
         'FDL')
options=('docs'
         '!header'
         'debug'
         'log')
makedepends=('pkg-config'
             'cmake'
             'automoc4'
             'docbook-xsl')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DWITH_Xmms=OFF \
        -DWITH_qq=OFF \
        -DWITH_meanwhile=OFF \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install 
}
