#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: Gene Kodadek <gkodadek@gmail.com>

pkgname=tuxpaint
pkgver=0.9.22
pkgrel=1
pkgdesc="Tux Paint is a free drawing program designed for young children."
arch=('x86_64')
url="http://www.tuxpaint.org"
license=('GPL3')
depends=('fribidi' 'gperf' 'libpaper' 'librsvg' 'libpng' 'python2' 'sdl_image' 'sdl_mixer' 'sdl_pango' 'sdl_ttf')
backup=('etc/tuxpaint/tuxpaint.conf')
categories=('education')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('bb9d7e4460ed8084be23b5dfcbf49ef3')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Python 2 fix.
  for file in docs/zh_tw/mkTuxpaintIM.py fonts/locale/zh_tw_docs/maketuxfont.py; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
  
  sed -i 's:$(ARCH_LINKS):$(ARCH_LINKS) -lpng:' Makefile
  
  # Add categories to .desktop file.
  echo "Categories=Game;KidsGame;Graphics;RasterGraphics;" >> src/tuxpaint.desktop

  # Build.
  make PREFIX=/usr
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make \
    PREFIX=/usr \
    DESTDIR=$pkgdir \
    CONFDIR=$pkgdir/etc \
    MAN_PREFIX=$pkgdir/usr/share/man \
    X11_ICON_PREFIX=$pkgdir/usr/share/pixmaps \
    KDE_PREFIX="" \
    KDE_ICON_PREFIX="" \
    install

  # Desktop integration.
  install -D -m 644 $srcdir/$pkgname-$pkgver/src/tuxpaint.desktop \
		    $pkgdir/usr/share/applications/tuxpaint.desktop

  # Permissions adjustments.
  chmod -R 644 $pkgdir/usr/share/doc/tuxpaint-dev/*
  chmod 755 $pkgdir/usr/share/doc/tuxpaint-dev/html

  # Configuration stuff.
  mkdir -p $pkgdir/etc/tuxpaint
  mv $pkgdir/etc/tuxpaint.conf $pkgdir/etc/tuxpaint/
}
