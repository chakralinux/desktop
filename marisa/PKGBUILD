# Maintainer: Weng Xuetian <wengxt AT gmail com>

pkgname=marisa
pkgver=0.2.4
pkgrel=2
pkgdesc="Matching Algorithm with Recursively Implemented StorAge"
arch=('i686' 'x86_64')
url="http://code.google.com/p/marisa-trie"
license=('LGPL')
depends=('python2' 'swig')
makedepends=()
source=("http://marisa-trie.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")

build(){
  cd "$srcdir"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --disable-static
  make

  # build Python bindings
  pushd bindings/python
  python2 setup.py build_ext --include-dirs="$srcdir/${pkgname}-${pkgver}/lib" --library-dirs="$srcdir/${pkgname}-${pkgver}/lib/.libs"
  python2 setup.py build
  popd

}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install Python bindings
  pushd bindings/python
  python2 setup.py install --root="${pkgdir}"
  popd

}
md5sums=('55192e66f6f9e3d04924a6ebb8eec02a')
