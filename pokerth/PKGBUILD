#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <daniele.cocca@gmail.com>

pkgname=pokerth
_realname=PokerTH
pkgver=0.9.3
pkgrel=1
pkgdesc="PokerTH is a Texas Holdem poker game written in C++/Qt."
arch=('i686' 'x86_64')
license=('AGPL3' 'custom:Variety of FOSS licenses for data files')
depends=('boost-libs' 'curl' 'gnutls' 'libgsasl' 'libircclient' 'qt' 'sdl_mixer' 'tinyxml')
makedepends=('boost')
url="http://www.pokerth.net/"
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${_realname}-${pkgver}-src.tar.bz2")
md5sums=('09d173512f723441f288b2844e3c68cb')

build() {
  cd "${srcdir}/${_realname}-${pkgver}-src"
  
  sed -i 's/QMAKE_CXXFLAGS += -std=gnu++0x/QMAKE_CXXFLAGS += -std=gnu++0x -DBOOST_FILESYSTEM_VERSION=2/' *.pro

  sed -i '23 i #include <libircclient/libirc_rfcnumeric.h>' src/net/common/ircthread.cpp

  # fix g++: error: unrecognized option '-no_dead_strip_inits_and_terms'
  sed \
    -e 's/QMAKE_LFLAGS += -no_dead_strip_inits_and_terms//' \
    -i zlib_compress.pro pokerth_game.pro pokerth_server.pro

  # Chakra look & feel tweaks
  sed -i -e "s~a.setStyle(new QPlastiqueStyle);~//~g" src/pokerth.cpp
  sed -i -e "s~a.setStyleSheet(font1String +~a.setStyleSheet(~g" src/pokerth.cpp

  qmake "${pkgname}.pro"
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}-src"

  make INSTALL_ROOT="${pkgdir}" install

  install -D "${pkgname}" "$pkgdir/usr/bin/${pkgname}"
  install -D -m644 docs/pokerth.1 "${pkgdir}/usr/share/man/man1/pokerth.1"

  install -D -m644 data/data-copyright.txt "${pkgdir}/usr/share/licenses/pokerth/data-copyright.txt"
  rm -f "${pkgdir}/usr/share/pokerth/data/data-copyright.txt"
}
