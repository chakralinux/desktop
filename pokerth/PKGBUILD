#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <daniele.cocca@gmail.com>

pkgname=pokerth
_realname=PokerTH
pkgver=0.8.3
pkgrel=5
pkgdesc="PokerTH is a Texas Holdem poker game written in C++/Qt."
arch=('i686' 'x86_64')
license=('GPL' 'custom')
depends=('qt' 'zlib' 'curl' 'gnutls' 'libgsasl' 'sdl' 'sdl_mixer' 'icu' 'boost' 'boost-libs' 'krb5')
makedepends=('boost' 'boost-libs')
url="http://www.pokerth.net/"
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${_realname}-${pkgver}-src.tar.bz2")
md5sums=('adbe56ad5f547eb255dad91de564bf1c')

build() {
  cd "${srcdir}/${_realname}-${pkgver}-src"
  
  sed -i 's/QMAKE_CXXFLAGS += -std=gnu++0x/QMAKE_CXXFLAGS += -std=gnu++0x -DBOOST_FILESYSTEM_VERSION=2/' *.pro

  # Chakra look & feel tweaks
  sed -i -e "s~a.setStyle(new QPlastiqueStyle);~//~g" src/pokerth.cpp
  sed -i -e "s~a.setStyleSheet(font1String +~a.setStyleSheet(~g" src/pokerth.cpp

  qmake "${pkgname}.pro"
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}-src"

  make INSTALL_ROOT="${pkgdir}" install

  install -D "${pkgname}" "$pkgdir/usr/bin/${pkgname}"
  install -D -m644 docs/pokerth.1 "${pkgdir}/usr/share/man/man1/pokerth.1"

  install -D -m644 data/data-copyright.txt "${pkgdir}/usr/share/licenses/pokerth/data-copyright.txt"
  rm -f "${pkgdir}/usr/share/pokerth/data/data-copyright.txt"
}

# vim:set ts=2 sw=2 et:
