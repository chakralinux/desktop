#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <daniele.cocca@gmail.com>

pkgname=pokerth
_realname=PokerTH
pkgver=0.9.5
pkgrel=1
pkgdesc="PokerTH is a Texas Holdem poker game written in C++/Qt."
arch=('i686' 'x86_64')
license=('AGPL3' 'custom:Variety of FOSS licenses for data files')
depends=('boost-libs' 'curl' 'gnutls' 'libgsasl' 'libircclient' 'qt' 'sdl_mixer' 'tinyxml')
makedepends=('boost')
categories=('games')
url="http://www.pokerth.net/"
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${_realname}-${pkgver}-src.tar.bz2")
md5sums=('4bdea98bfc142a825c64b77feb4667d0')

build() {
  cd "${srcdir}/${_realname}-${pkgver}-src"

  # Fix missing include
  sed -e '23 i #include <libircclient/libirc_rfcnumeric.h>' \
      -i src/net/common/ircthread.cpp

  qmake "${pkgname}.pro"
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}-src"

  make INSTALL_ROOT="${pkgdir}" install

  install -D "${pkgname}" "$pkgdir/usr/bin/${pkgname}"
  install -D -m644 docs/pokerth.1 "${pkgdir}/usr/share/man/man1/pokerth.1"

  install -D -m644 data/data-copyright.txt "${pkgdir}/usr/share/licenses/pokerth/data-copyright.txt"
  rm -f "${pkgdir}/usr/share/pokerth/data/data-copyright.txt"
}

# vim:set ts=2 sw=2 et:
