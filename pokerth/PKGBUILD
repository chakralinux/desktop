#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <daniele.cocca@gmail.com>

pkgname=pokerth
_realname=PokerTH
pkgver=1.0.1
pkgrel=2
pkgdesc="PokerTH is a Texas Holdem poker game written in C++/Qt."
arch=('x86_64')
license=('AGPL3' 'custom:Variety of FOSS licenses for data files')
depends=('boost-libs' 'curl' 'gnutls' 'libgsasl' 'libircclient' 'qt' 'sdl_mixer' 'tinyxml' 'protobuf')
makedepends=('boost')
url="http://www.pokerth.net/"
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${_realname}-${pkgver}-src.tar.bz2"
        "pokerth-1.0.1-boost.patch::https://github.com/pokerth/pokerth/commit/8adbe8edb982c56ad126264dab4b98fe45ed520f.diff#diff-29b89cfa10024074b0fab826fc532936")
md5sums=('c6dcf0ed68ab50af91371348ffad5d00'
         '6b126138564121c1f3de29ed3c328cb0')

build() {
  cd "${srcdir}/${_realname}-${pkgver}-src"

  patch -p1 < "$srcdir/pokerth-1.0.1-boost.patch"

  qmake "${pkgname}.pro"
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}-src"

  make INSTALL_ROOT="${pkgdir}" install

  install -D "${pkgname}" "$pkgdir/usr/bin/${pkgname}"
  install -D -m644 docs/pokerth.1 "${pkgdir}/usr/share/man/man1/pokerth.1"

  install -D -m644 data/data-copyright.txt "${pkgdir}/usr/share/licenses/pokerth/data-copyright.txt"
  rm -f "${pkgdir}/usr/share/pokerth/data/data-copyright.txt"
}
