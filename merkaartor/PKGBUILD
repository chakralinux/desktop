# Contributions from AUR: https://aur.archlinux.org/packages/merkaartor/

pkgname=merkaartor
pkgver=0.18.3
pkgrel=3
pkgdesc="An editor for the OpenStreetMap project"
arch=('x86_64')
url="https://github.com/openstreetmap/merkaartor"
license=('GPL2')
depends=('desktop-file-utils' 'exiv2' 'gdal' 'libxml2' 'libxslt' 'proj'
	 'qt5-webkit' 'qt5-svg' 'qt5-tools' 'libspatialite' 'unixodbc')
optdepends=('gpsd')
categories=('network' 'education')
screenshot=(http://merkaartor.be/attachments/download/195)
#source=("http://merkaartor.be/attachments/download/301/merkaartor-0.18.1.tar.bz2")
source=("https://github.com/openstreetmap/merkaartor/archive/${pkgver}.tar.gz")
sha512sums=('b34087e0317c196b59dd1de57778b9293d23e4530bd5734661dc2ce53eef85317f512ee446056e045aa8a06146bd029d6274fa5a6f68128db27795d44f0ef915')

prepare() {
  cd $srcdir/$pkgname-$pkgver/src
  sed -i s!'Education;Geography;Network;Qt;'!'Education;Geography;'! \
  $pkgname.desktop
}

build() {
  cd $srcdir/$pkgname-$pkgver
  echo 'QMAKE_MOC = $$QMAKE_MOC -DBOOST_TT_HAS_OPERATOR_HPP_INCLUDED' >> src/src.pro
  lrelease-qt5 src/src.pro
  qmake-qt5 \
    NODEBUG=1 \
    PREFIX="/usr" \
    RELEASE=1 \
    Merkaartor.pro
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install
}
