#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: Michael Kanis <mkanis gmx de>
# Contributor: Paulo Freire <paulofreire gmail com>
# Contributor: Brice Maron <brice bmaron net>

pkgname=merkaartor
pkgver=0.18.1
_pkgver=main
pkgrel=1
pkgdesc="An editor for the OpenStreetMap project"
arch=('i686' 'x86_64')
url="http://www.merkaartor.org/"
license=('GPL2')
depends=('exiv2' 'gdal' 'libxml2' 'libxslt' 'proj' 'qt')
optdepends=('gpsd')
makedepends=('boost')
conflicts=('merkaartor-svn')
categories=('network' 'education')
source=("$pkgname-$pkgver.tar.gz::https://gitorious.org/$pkgname/main/archive-tarball/$pkgver")
md5sums=('bf4fc33d43f169d7fa3d99ada8c0f1b6')

build() {
  cd $srcdir/$pkgname-$_pkgver

  lrelease ./src/src.pro
  qmake \
    GEOIMAGE=1 \
    NODEBUG=1 \
    PREFIX="/usr" \
    RELEASE=1 \
    Merkaartor.pro
  make
}

package() {
  cd $srcdir/$pkgname-$_pkgver
  make INSTALL_ROOT=$pkgdir install
}
