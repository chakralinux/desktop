# Contributions from AUR: https://aur.archlinux.org/packages/merkaartor/

pkgname=merkaartor
pkgver=0.18.2
pkgrel=4
pkgdesc="An editor for the OpenStreetMap project"
arch=('x86_64')
url="https://github.com/openstreetmap/merkaartor"
license=('GPL2')
depends=('desktop-file-utils' 'exiv2' 'gdal' 'libxml2' 'libxslt' 'proj'
	 'qt5-webkit' 'qt5-svg' 'qt5-tools' 'libspatialite' 'unixodbc')
optdepends=('gpsd')
makedepends=('gcc')
conflicts=('merkaartor-svn')
categories=('network' 'education')
screenshot=(http://merkaartor.be/attachments/download/195)
#source=("http://merkaartor.be/attachments/download/301/merkaartor-0.18.1.tar.bz2")
source=("https://github.com/openstreetmap/merkaartor/archive/0.18.2.tar.gz")
sha512sums=('577dd88e522eb3f9c21d3588358137dc9419a4d3961299a2162f7edb07e2b95a46d87022b57f0ea190bf9d76a2b1d2aada61c03e952e29c432e250e4686d594d')

prepare() {
  cd $srcdir/$pkgname-$pkgver/src
  sed -i s!'Education;Geography;Network;Qt;'!'Education;Geography;'! \
  $pkgname.desktop

}

build() {
  cd $srcdir/$pkgname-$pkgver
  echo 'QMAKE_MOC = $$QMAKE_MOC -DBOOST_TT_HAS_OPERATOR_HPP_INCLUDED' >> src/src.pro
  lrelease-qt5 src/src.pro
  qmake-qt5 \
    NODEBUG=1 \
    PREFIX="/usr" \
    RELEASE=1 \
    Merkaartor.pro
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install
}
