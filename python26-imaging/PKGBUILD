#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <jmc@chakra-project.org>
#

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_pkgname=Imaging
pkgname=python26-imaging
pkgver=1.1.7
pkgrel=1
pkgdesc="PIL. Provides image processing capabilities for Python."
arch=('i686' 'x86_64')
url="http://www.pythonware.com/products/pil/index.htm"
license=('custom:PIL')
depends=('python26' 'libjpeg' 'freetype2') 
makedepends=('tk')
optdepends=('tk')
provides=("pil-2.6=${pkgver}")
source=(http://effbot.org/downloads/${_pkgname}-${pkgver}.tar.gz)
md5sums=('fc14a54e1ce02a0225be8854bfba478e')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2.6 setup.py build_ext
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2.6 setup.py install --root="${pkgdir}"
  install -dm755 "${pkgdir}/usr/include/python2.6/"
  install -m644 -t "${pkgdir}/usr/include/python2.6/" libImaging/*.h 

  # do not have files ending in .py in /usr/bin
  for f in pildriver pilprint pilconvert pilfile pilfont; do
    mv ${pkgdir}/usr/bin/$f{.py,}
  done

  # Install license
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/README" \
                 "${pkgdir}/usr/share/licenses/${pkgname}/README"

  #
  # Rename some files to make them live well with other Python versions.
  #
  __to_fix=('pilconvert' 'pildriver' 'pilfile' 'pilfont' 'pilprint')
  for file in ${__to_fix[@]}; do
    mv "${pkgdir}/usr/bin/${file}" "${pkgdir}/usr/bin/${file}-2.6"
  done
}

# vim:set ts=2 sw=2 et:
