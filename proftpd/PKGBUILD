pkgname=proftpd
pkgver=1.3.5.a
_pkgver=1.3.5a
pkgrel=1
pkgdesc="A high-performance, scalable FTP server"
arch=('x86_64')
url="http://www.proftpd.org"
license=('GPL')
depends=('libldap' 'libmariadbclient' 'postgresql-libs')
backup=('etc/proftpd.conf' 'etc/conf.d/proftpd')
categories=('network')
install=proftpd.install
source=("ftp://ftp.proftpd.org/distrib/source/${pkgname}-${_pkgver}.tar.gz"
	'proftpd.service'
        'proftpd.logrotate'
	'proftpd.conf.d'
	'proftpd.tmpfiles')
md5sums=('b9d3092411478415b31d435f8e26d173'
         '2bf74515d84036cb0acd60406fcd25b0'
         'ddb09eb13131becdf0e081eef413116b'
         '2a1a9bab12655bd8694ac49d48c6f0e4'
         '907b149a120b046f05647c73502e23c9')
build() {
	cd ${srcdir}/${pkgname}-${_pkgver}

	./configure --prefix=/usr \
	    --mandir=/usr/share/man \
	    --libexecdir=/usr/lib \
	    --disable-pam \
	    --with-modules=mod_quotatab:mod_quotatab_file:mod_tls:mod_ldap:mod_sql:mod_sql_mysql:mod_sql_postgres \
	    --sysconfdir=/etc \
	    --localstatedir=/run/proftpd \
	    --enable-ctrls \
	    --enable-ipv6 \
	    --with-includes=/usr/include/mysql:/usr/include/postgresql \
	    --with-libraries=/usr/lib/mysql:/usr/lib/postgresql \
	    --enable-nls \
	    --with-systemd
	make
}

package() {
	cd ${srcdir}/${pkgname}-${_pkgver}

	make DESTDIR=${pkgdir} install
	install -Dm644 ../proftpd.logrotate ${pkgdir}/etc/logrotate.d/proftpd
	install -Dm644 ../proftpd.conf.d ${pkgdir}/etc/conf.d/proftpd
	install -Dm755 contrib/xferstats.holger-preiss ${pkgdir}/usr/bin/ftpstats
	
	install -d ${pkgdir}/usr/lib/systemd/system/
        install -m644 ${srcdir}/proftpd.service ${pkgdir}/usr/lib/systemd/system/
        install -Dm644 ${srcdir}/proftpd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/proftpd.conf
	rmdir ${pkgdir}/run/{proftpd,}
}
