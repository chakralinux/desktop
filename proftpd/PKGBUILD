#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Fabian Kosmale <0inkane@googlemail.com>
# Contributor (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config

pkgname=proftpd
pkgver=1.3.4
_pkgver=1.3.4b
pkgrel=2
pkgdesc="A high-performance, scalable FTP server"
arch=('i686' 'x86_64')
url="http://www.proftpd.org"
license=('GPL')
depends=('libldap' 'libmysqlclient' 'postgresql-libs')
backup=('etc/proftpd.conf' 'etc/conf.d/proftpd')
categories=('network')
source=(ftp://ftp.proftpd.org/distrib/source/${pkgname}-${_pkgver}.tar.gz
	'proftpd'
        'proftpd.logrotate'
	'proftpd.conf.d')
build() {
	cd ${srcdir}/${pkgname}-${_pkgver}

	./configure --prefix=/usr --mandir=/usr/share/man --disable-pam \
	    --with-modules=mod_quotatab:mod_quotatab_file:mod_tls:mod_ldap:mod_sql:mod_sql_mysql:mod_sql_postgres \
	    --sysconfdir=/etc --localstatedir=/var/run --enable-ctrls --enable-ipv6 \
	    --with-includes=/usr/include/mysql:/usr/include/postgresql \
	    --with-libraries=/usr/lib/mysql:/usr/lib/postgresql --enable-nls
	make
}

package() {
	cd ${srcdir}/${pkgname}-${_pkgver}

	make DESTDIR=${pkgdir} install
	install -Dm644 ../proftpd.logrotate ${pkgdir}/etc/logrotate.d/proftpd
	install -Dm644 ../proftpd.conf.d ${pkgdir}/etc/conf.d/proftpd
	install -Dm755 ../proftpd ${pkgdir}/etc/rc.d/proftpd
	install -Dm755 contrib/xferstats.holger-preiss \
	   ${pkgdir}/usr/bin/ftpstats
	cd ${pkgdir}/etc
	sed -i 's|nogroup|nobody|g' proftpd.conf
	rmdir ${pkgdir}/usr/libexec
}
md5sums=('0871e0b93c9c3c88ca950b6d9a04aed2'
         '03434e92fbf9250275bfb510a68f66bc'
         'ddb09eb13131becdf0e081eef413116b'
         '2a1a9bab12655bd8694ac49d48c6f0e4')
