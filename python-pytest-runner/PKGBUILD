
pkgbase=python-pytest-runner
pkgname=('python3-pytest-runner' 'python2-pytest-runner')
pkgver=4.0
pkgrel=1
pkgdesc="Invoke py.test as distutils command with dependency resolution"
arch=('any')
license=('MIT')
url="https://github.com/pytest-dev/pytest-runner"
makedepends=('python3-pytest' 'python2-pytest' 'python3-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/pytest-dev/pytest-runner.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pytest-runner{,-py2}
}

build() {
  cd "$srcdir/pytest-runner"
  python3 setup.py build

  cd "$srcdir/pytest-runner-py2"
  python2 setup.py build
}

package_python3-pytest-runner() {
  depends=('python3-pytest')

  cd pytest-runner
  python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pytest-runner() {
  depends=('python2-pytest')

  cd pytest-runner-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
