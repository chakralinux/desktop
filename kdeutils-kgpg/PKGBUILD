# Include global configuration
source ../kdeapps.conf

pkgname="kdeutils-kgpg"
_pkgname="kgpg"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=2
pkgdesc="GPG encryption and key management"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
depends=("kde-runtime" "kdelibs>=${_libsver}" "kdepimlibs4" "kdepim-runtime>=${_libsver}")
makedepends=('pkg-config' 'cmake' 'automoc4' "kdelibs>=${_libsver}" 'docbook-xsl')
conflicts=("kdeutils-common")
groups=("kde" "kdeutils" "kde-uninstall")
categories=('utils')
install=kdeutils.install
source=($_mirror/${_pkgname}-$_kdever.tar.xz 'fix-include.patch')
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`
            "1129aa5553d26edaded9cb258413844cf3c336b5f02da0a0167ca1ad77602682")

prepare() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    patch -p1 -i ${srcdir}/fix-include.patch
}

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    mkdir build
    cd build
    cmake .. \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}/build
    make DESTDIR=${pkgdir} install
}
