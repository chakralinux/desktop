#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Frenández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=darcs
pkgver=2.5
pkgrel=1
pkgdesc="Decentralized replacement for CVS with roots in quantum mechanics."
arch=(i686 x86_64)
url="http://darcs.net/"
license=('GPL')
depends=('curl' 'gmp' 'ncurses>=5.6-7')
makedepends=('ghc' 'haskell-tar' 'haskell-text' 'haskell-hashed-storage=0.5.3' 'haskell-haskeline' 'haskell-html' 'haskell-parsec=2.1.0.1' 'haskell-regex-compat')
source=("http://darcs.net/releases/$pkgname-$pkgver.tar.gz")
md5sums=('7de8b352d8b0ed50d71ac0c32d3b6d5c')

build() {
  cd $srcdir/$pkgname-$pkgver
  runhaskell Setup.lhs configure \
    --ghc \
    --prefix=/usr \
    --disable-library-for-ghci \
    --libsubdir=\$compiler/site-local/\$pkgid

  runhaskell Setup.lhs build
}

package() {
  cd $srcdir/$pkgname-$pkgver

  runhaskell Setup.lhs copy \
    --destdir=$pkgdir

  rm -r $pkgdir/usr/lib/
  chmod 755 $pkgdir/usr/share/man/man1/$pkgname.1
}

