# Contribution from Arch https://aur.archlinux.org/packages/darcs/

pkgname=darcs
pkgver=2.12.0
pkgrel=1
pkgdesc="Decentralized replacement for CVS with roots in quantum mechanics."
arch=('x86_64')
url="http://darcs.net"
license=('GPL')
depends=('curl' 'libffi')
makedepends=('ghc' 'cabal-install')
source=("http://darcs.net/releases/$pkgname-$pkgver.tar.gz")
sha1sums=('3921324eb15c09542e5124d3cfbf564a1b0db535')

build() {
   cd ${srcdir}/${pkgname}-${pkgver}

   # I've seen this cause a problem in the past. If necessary, do a
   # cabal update manually and comment it out here.
   cabal update

   cabal sandbox init
   cabal install --only-dependencies
   cabal configure -O --prefix=/usr
   cabal build
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}

   cabal copy --destdir=${pkgdir}

   # darcs is both a binary and a library. This packaging exists
   # solely to give the user the darcs binary. Unfortunately the only
   # way I found to prevent installation of these files was to blow
   # away this dir just prior to packaging:
   rm -r ${pkgdir}/usr/lib

   chmod 755 ${pkgdir}/usr/share/man/man1/${pkgname}.1
}
