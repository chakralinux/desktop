# Maintainer: Michael Haesel <michael[dot]haesel[at]gmail[dot]com>

pkgname=mumble
pkgver=1.2.8
pkgrel=1
arch=('x86_64')
pkgdesc="A voice chat application similar to TeamSpeak."
license=('BSD')
depends=('qt' 'speex' 'lsb-release' 'libxi' 'avahi' 'libsndfile' 'protobuf' 
         'libpulse' 'opus' "speech-dispatcher")
makedepends=('boost' 'mesa')
categories=('communication')
install=${pkgname}.install
url="http://mumble.sourceforge.net/"
screenshot="http://mumble.sourceforge.net/static/Mumble%20--%201.2.3.png"
source=("http://downloads.sourceforge.net/mumble/${pkgname}-${pkgver}.tar.gz"
        "speech-dispatcher.patch"
        "gcc49.patch::https://github.com/mumble-voip/mumble/commit/349436284b5f1baa61836c98ff0d518392140c5d.patch")
sha256sums=('4c2fc3dff49687bc1d977905958e8a9c35e109723aeed42c9ea124104febe159'
            'efa6108ce8c77b23b971504dbf916832b1ebd3ce52319292fd3da815cc05a54b'
            '00c14a5e41142510828d583e1e9a25a6d6d2614f3c7a86c75056be4321a0f798')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i "${srcdir}/gcc49.patch"
  patch -p1 -i "${srcdir}/speech-dispatcher.patch"
}

build() {
  cd ${pkgname}-${pkgver}

  # Building mumble
  qmake main.pro \
    CONFIG+="bundled-celt no-bundled-opus no-bundled-speex no-speechd no-g15 no-xevie no-server \
    no-embed-qt-translations no-update packaged" \
    DEFINES+="PLUGIN_PATH=/usr/lib/mumble"
  make release
}

package() {
  cd ${pkgname}-${pkgver}

  # bin stuff
  install -m755 -D release/mumble "${pkgdir}/usr/bin/mumble"
  install -m755 -D scripts/mumble-overlay "${pkgdir}/usr/bin/mumble-overlay"

  # lib stuff
  install -m755 -D release/libmumble.so.${pkgver} "${pkgdir}/usr/lib/mumble/libmumble.so.${pkgver}"
  ln -s libmumble.so.${pkgver} "${pkgdir}/usr/lib/mumble/libmumble.so"
  ln -s libmumble.so.${pkgver} "${pkgdir}/usr/lib/mumble/libmumble.so.1"
  ln -s libmumble.so.${pkgver} "${pkgdir}/usr/lib/mumble/libmumble.so.1.2"

  install -m755 -D release/plugins/liblink.so "${pkgdir}/usr/lib/mumble/liblink.so"
  install -m755 -D release/plugins/libmanual.so "${pkgdir}/usr/lib/mumble/libmanual.so"
  install -m755 -D release/libcelt* "${pkgdir}/usr/lib/mumble/"

  # other stuff
  install -m644 -D scripts/mumble.desktop "${pkgdir}/usr/share/applications/mumble.desktop"
  install -m644 -D scripts/mumble.protocol "${pkgdir}/usr/share/kde4/services/mumble.protocol"
  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m644 -D man/mum* "${pkgdir}/usr/share/man/man1/"
  install -m644 -D icons/mumble.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/mumble.svg"

  # License.
  install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
