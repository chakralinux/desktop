pkgname=mumble
pkgver=1.2.19
pkgrel=2
arch=('x86_64')
pkgdesc="A voice chat application similar to TeamSpeak."
license=('BSD')
depends=('qt' 'lsb-release' 'libxi' 'avahi' 'libsndfile' 'protobuf' 
         'libpulse' 'xdg-utils' 'speech-dispatcher' 'speex' 'opus')
makedepends=('boost' 'mesa')
categories=('communication')
install=${pkgname}.install
url="http://www.mumble.com/"
screenshot="http://mumble.sourceforge.net/static/Mumble%20--%201.2.3.png"
source=("https://github.com/mumble-voip/mumble/releases/download/${pkgver}/mumble-${pkgver}.tar.gz"
            AudioOutput.patch::https://github.com/mumble-voip/mumble/commit/e3871bb626f691e282085367d109b006f23e4389.patch)
sha1sums=('8ea9dafad14e6c730da72ae9b4625778e499d94f'
          'cf82fe36f33eb8df89e0c518d62250cc0f7479d0')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  patch -Np1 -i $srcdir/AudioOutput.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver

  # Building mumble
  qmake main.pro \
    CONFIG+="bundled-celt no-bundled-opus no-bundled-speex no-g15 no-xevie no-server \
    no-embed-qt-translations no-update packaged" \
    DEFINES+="PLUGIN_PATH=/usr/lib/mumble" \
    INCLUDEPATH+="/usr/include/speech-dispatcher" \
    LIBS+="-lpng16 -lfreetype -lXrender -lfontconfig"
  make release
}

package() {
  cd $srcdir/$pkgname-$pkgver

  # bin stuff
  install -m755 -D ./release/mumble $pkgdir/usr/bin/mumble
  install -m755 -D ./scripts/mumble-overlay $pkgdir/usr/bin/mumble-overlay

  # lib stuff
  install -m755 -D ./release/libmumble.so.$pkgver $pkgdir/usr/lib/mumble/libmumble.so.$pkgver
  ln -s libmumble.so.$pkgver $pkgdir/usr/lib/mumble/libmumble.so
  ln -s libmumble.so.$pkgver $pkgdir/usr/lib/mumble/libmumble.so.1
  ln -s libmumble.so.$pkgver $pkgdir/usr/lib/mumble/libmumble.so.1.2
  install -m755 -D ./release/plugins/liblink.so $pkgdir/usr/lib/mumble/liblink.so
  install -m755 -D ./release/plugins/libmanual.so $pkgdir/usr/lib/mumble/libmanual.so
  install -m755 -D ./release/libcelt* $pkgdir/usr/lib/mumble/

  # other stuff
  install -m644 -D ./scripts/mumble.desktop $pkgdir/usr/share/applications/mumble.desktop
  install -m755 -d $pkgdir/usr/share/man/man1
  install -m644 -D ./man/mum* $pkgdir/usr/share/man/man1/
  install -m644 -D ./icons/mumble.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/mumble.svg
  install -m644 -D ./LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
