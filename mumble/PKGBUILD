#
# <Repo> Packages for Chakra, part of chakra-project.org
#
# Maintainer: Michael Haesel <michael[dot]haesel[at]gmail[dot]com>

pkgname=mumble
pkgver=1.2.3
pkgrel=5
arch=('i686' 'x86_64')
pkgdesc="A voice chat application similar to TeamSpeak."
license=('GPL')
depends=('qt' 'speex' 'lsb-release' 'libxi' 'avahi' 'libsndfile' 'protobuf' 'libpulse')
makedepends=('boost' 'mesa' 'avahi')
options=('!libtool')
categories=('communication')
url="http://mumble.sourceforge.net/"
screenshot="http://mumble.sourceforge.net/static/Mumble%20--%201.2.3.png"
source=("http://downloads.sourceforge.net/mumble/${pkgname}-${pkgver}.tar.gz"
        "mumble11x.desktop"
        "mumble.protocol")
sha256sums=('05895122ae4abec3fb62ef24ed9d167ffd768e7080ed179c8bf3afca96d18a5c'
            '8f76b983cf497a4f4c8d68cc524a0c386657267b3da2fd013634092de906426f'
            'd1779e85055b707127b0136554508d7c404dae61462f769d883612be158f4bd9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Building mumble
  /usr/bin/qmake main.pro \
    CONFIG+="bundled-celt no-bundled-speex no-speechd no-g15 no-xevie no-server no-embed-qt-translations packaged" \
    DEFINES+="PLUGIN_PATH=/usr/lib/mumble"
  make release
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # bin stuff
  install -m755 -D ./release/mumble "${pkgdir}/usr/bin/mumble"
  install -m755 -D ./release/mumble11x "${pkgdir}/usr/bin/mumble11x"
  install -m755 -D ./scripts/mumble-overlay "${pkgdir}/usr/bin/mumble-overlay"

  # lib stuff
  install -m755 -D ./release/libmumble.so.${pkgver} "${pkgdir}/usr/lib/mumble/libmumble.so.${pkgver}"
  ln -s libmumble.so.${pkgver} "${pkgdir}/usr/lib/mumble/libmumble.so"
  ln -s libmumble.so.${pkgver} "${pkgdir}/usr/lib/mumble/libmumble.so.1"
  ln -s libmumble.so.${pkgver} "${pkgdir}/usr/lib/mumble/libmumble.so.1.2"

  install -m755 -D ./release/plugins/liblink.so "${pkgdir}/usr/lib/mumble/liblink.so"
  install -m755 -D ./release/plugins/libmanual.so "${pkgdir}/usr/lib/mumble/libmanual.so"
  install -m755 -D ./release/libcelt* "${pkgdir}/usr/lib/mumble/"

  # other stuff
  install -m644 -D ./scripts/mumble.desktop "${pkgdir}/usr/share/applications/mumble.desktop"
  install -m644 -D "${srcdir}/mumble11x.desktop" "${pkgdir}/usr/share/applications/mumble11x.desktop"
  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m644 -D ./man/mum* "${pkgdir}/usr/share/man/man1/"
  install -m644 -D ./icons/mumble.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/mumble.svg"
  install -m644 -D "${srcdir}/mumble.protocol" "${pkgdir}/usr/share/kde4/services/mumble.protocol"

  # License.
  install -m644 -D "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
