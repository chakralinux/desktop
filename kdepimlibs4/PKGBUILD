# Include global configuration
source ../kdeapps.conf

pkgname=kdepimlibs4
pkgver=4.14.10
_kdeappver=15.04.3
pkgrel=8
pkgdesc="KDE4 PIM Libraries"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdepimlibs'
license=('GPL' 'LGPL')
depends=('kdelibs' 'gpgme' 'libical' 'prison' 'qjson')
makedepends=('cmake' 'automoc4' 'boost' 'cyrus-sasl' 'docbook-xsl')
install='kdepimlibs4.install'
options=('!docs')
source=("http://download.kde.org/Attic/applications/${_kdeappver}/src/kdepimlibs-${pkgver}.tar.xz")
sha1sums=('8db2f59b8a33f4d1bbebfdff8b772d30669c9932')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdepimlibs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDEPIM_ONLY_KLEO=TRUE
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
