# Contributor : Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kcoreaddons
pkgver=${KFVersion}
pkgrel=1
pkgdesc='KCoreAddons'
arch=('i686' 'x86_64')
url='${KFServer}/kcoreaddons'
license=('LGPL')
depends=('qt5-base' 'shared-mime-info')
makedepends=('extra-cmake-modules')
groups=('kf5')
install=kcoreaddons.install
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('eb3acba6f7cf2407943ff97e02ca8427')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

