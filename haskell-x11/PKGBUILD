# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=X11
pkgname=haskell-x11
pkgver=1.6.1.1
pkgrel=1
pkgdesc="A binding to the X11 graphics library"
url="http://hackage.haskell.org/package/X11"
license=('custom:BSD3')
arch=('x86_64' 'i686')
makedepends=()
depends=('ghc=7.6.3-1' 'haskell-base>=3' 'haskell-base<5' 'haskell-data-default' 'libx11' 'libxrandr' 'libxext')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/X11/1.6.1.1/X11-1.6.1.1.tar.gz")
install="haskell-x11.install"
sha512sums=('d5f58a7ab2450e145e890187e8dc7b43e286d2a95414a605e549cbf48568ef0d89f662870b2010eecba3b8fdd2a3a57214d33fc8775426e7e23ffad727b4e144')

build() {
    cd "${srcdir}/X11-1.6.1.1"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix="/usr" \
        --docdir="/usr/share/doc/haskell-x11" \
        --libsubdir='$compiler/site-local/$pkgid'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/X11-1.6.1.1"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/haskell-x11/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/haskell-x11/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/haskell-x11/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/X11"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/haskell-x11/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/haskell-x11/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
