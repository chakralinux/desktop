pkgname=qtkeychain
pkgver=0.5.0
pkgrel=1
pkgdesc="Provides support for secure credentials storage."
arch=('x86_64')
url="https://github.com/frankosterfeld/qtkeychain"
license=('BSD')
depends=('qt')
makedepends=('cmake')
options=(!strip)
source=("${pkgname}-${pkgver}.zip::https://github.com/frankosterfeld/qtkeychain/archive/v${pkgver}.zip")
sha512sums=('61524e92adeabe19edda6d671fce7e61cea7efef7cdfc02cce425de5f1d686df3fe1e30960a18de21fae7583d2994123ac498da4da8430d127976c7e1c93a68d')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}

    if [[ -e ${srcdir}/${pkgname}-${pkgver}-build ]]; then rm -rf ${srcdir}/${pkgname}-${pkgver}-build; fi
    mkdir ${srcdir}/${pkgname}-${pkgver}-build
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}-build
    cmake -DBUILD_WITH_QT4=on \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
          -DCMAKE_BUILD_TYPE=Release \
          ../${pkgname}-${pkgver}
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}-build
    make DESTDIR=${pkgdir} install
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
