pkgbase=qtkeychain
pkgname=('qtkeychain-qt4' 'qtkeychain-qt5')
pkgver=0.6.1
pkgrel=1
pkgdesc="Provides support for secure credentials storage."
arch=('x86_64')
url="https://github.com/frankosterfeld/qtkeychain"
license=('BSD')
makedepends=('cmake' 'icu' 'qt5-tools' 'qt')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/frankosterfeld/$pkgbase/archive/v${pkgver}.tar.gz")
md5sums=('2cc49c62a387babdb74358b62d66e10f')

prepare() {
    mkdir -p build{4,5}
}

build() {
    cd build4
    cmake ../$pkgbase-$pkgver \
          -DBUILD_WITH_QT4=ON \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
          -DCMAKE_BUILD_TYPE=Release
    make
    
    cd ../build5
    cmake ../$pkgbase-$pkgver \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
          -DCMAKE_BUILD_TYPE=Release
    make
}

package_qtkeychain-qt4() {
    depends=('qt')
    provides=('qtkeychain')
    replaces=('qtkeychain')

    cd build4
    make DESTDIR="$pkgdir" install
    install -Dm644 "$srcdir"/$pkgbase-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_qtkeychain-qt5() {
    depends=('qt5-base')

    cd build5
    make DESTDIR="$pkgdir" install
    install -Dm644 "$srcdir"/$pkgbase-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
