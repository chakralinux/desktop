pkgname=qtkeychain
pkgver=0.8.0
pkgrel=1
pkgdesc="Provides support for secure credentials storage."
arch=('x86_64')
url="https://github.com/frankosterfeld/qtkeychain"
license=('BSD')
depends=('qt5-base')
makedepends=('cmake' 'icu' 'qt5-tools' 'extra-cmake-modules')
provides=("$pkgname-qt"{4,5})
replaces=("$pkgname-qt"{4,5})
source=("$pkgname-$pkgver.tar.gz::https://github.com/frankosterfeld/$pkgname/archive/v${pkgver}.tar.gz")
sha1sums=('d6ff0726245ee24e249e76ee9025b473239dd637')

prepare() {
    mkdir -p build
}

build() {
    cd $srcdir/build
    cmake ../$pkgbase-$pkgver \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
          -DCMAKE_BUILD_TYPE=Release \
          -DLIBSECRET_SUPPORT=OFF
    make
}

package() {
    cd $srcdir/build
    make DESTDIR="$pkgdir" install
    install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
