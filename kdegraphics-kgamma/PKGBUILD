# Maintainer:   Manuel Tortosa <manutortosa@chakra-project@org>
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Andrea Scarpino <andrea@archlinux.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdegraphics-kgamma
_pkgname=kgamma
pkgver=${_kdever}
pkgrel=3
pkgdesc='A monitor calibration tool'
arch=('x86_64')
url='http://kde.org/applications/graphics/kgamma/'
license=('GPL'
         'LGPL'
         'FDL')
# Note on libxxf86vm:
#     not detected by namcap because libgl depends on it
#     but nvidia providing libgl does not depend on libxxf86vm
depends=("kde-runtime>=${_kdever}"
         'libxxf86vm'
         "kdegraphics-libkexiv2>=${_kdever}"
         "kdegraphics-libkdcraw>=${_kdever}"
         "kdegraphics-libkipi>=${_kdever}"
         "kdegraphics-ksaneplugin>=${_kdever}"
         "kdegraphics-thumbnailers>=${_kdever}"
         "kdegraphics-strigi-analyzer>=${_kdever}"
         "kdegraphics-svgpart>=${_kdever}")
makedepends=('cmake'
             'automoc4')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)
groups=('kde'
        'kdegraphics'
        'kde-uninstall')
categories=('graphics')
provides=('kdegraphics-common')
replaces=('kdegraphics-common'
          'kdegraphics-doc')
conflicts=('kdegraphics-common')
options=('docs'
         '!header'
         'debug'
         'log')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
sha256sums=('d9e8a814a99c91b5f916882f8c604f045b9d10a7c8292241b062fb51e5d423ed')
