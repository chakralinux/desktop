pkgname=psi
pkgver=1.3
pkgrel=1
pkgdesc="Powerful XMPP (Jabber) client (Qt, C++) designed for power users"
url="https://psi-im.org/"
license=('GPL2')
arch=('x86_64')
depends=('hunspell' 'qca-qt5' 'qt5-svg' 'qt5-webengine' 'qt5-x11extras' 'libidn')
makedepends=('cmake' 'qt5-multimedia')
categories=('communication')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "chakralinux.patch"
        "no-qca-qt4.patch"
        "qt-5.11.patch")
sha256sums=('59debd16e61ab1d4ff88aca9f41b9caaaca8395f1576418fb99214d5e2c6fa8b'
            'e6c44c4a2d0ebdc05cb317954931bbb4a2aab1c1cd447fcd9a70e13d5832558d'
            'a6ea9a62100b6990b8a6c8590a6d0e9df095b99b6527bb5490a7fd3f6929ac23'
            'e56ec1a250488b2cf9d42887aac432ff2e0ef482eb7cc8500f3d52cb53949119')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/chakralinux.patch
  patch -p1 <"$srcdir"/no-qca-qt4.patch
  patch -p1 <"$srcdir"/qt-5.11.patch
}

build() {
  cd $pkgname-$pkgver
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/include/psi/plugins"
  install -m644 ../src/plugins/include/*.h "$pkgdir/usr/include/psi/plugins"
}
