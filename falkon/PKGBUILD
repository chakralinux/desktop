pkgname='falkon'
pkgver=3.0.0
pkgrel=1
pkgdesc="A very fast open source browser based on QtWebEngine"
url="https://github.com/KDE/falkon"
arch=('x86_64')
license=('GPL3')
depends=('qt5-webengine' 'qt5-x11extras' 'qt5-svg' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'kwallet' 'lcms2' 'extra-cmake-modules')
optdepends=('bash-completion: bash completion support'
            'kwallet: kf5 kwallet integration')
conflicts=('qupzilla' 'qupzilla-git' 'qupzilla-qt4')
replaces=('qupzilla' 'qupzilla-git' 'qupzilla-qt4')
install=${pkgname}.install
categories=('network')
source=("http://download.kde.org/stable/$pkgname/3.0/src/$pkgname-$pkgver.tar.xz"
        '0001-branded-speeddial.patch'
        'start-white.png'
        'falkon.desktop')
sha1sums=('ecc07f37e5e902aba11c049ca50a05841cfeca17'
          '0045ac3a37a6a16347acf82046b63cd772bbd377'
          '6ec67993bda3054d4210cc08177b124f873774e3'
          '30af0401f97e6465169bae2089d2a8626e08cadd')

prepare() {
  msg 'Applying browserUI patch...'
  cd $pkgname-$pkgver/src/lib/app/
  sed -e 's|"falkon:start"|"https://www.chakralinux.org/?welcome"|' \
      -e 's|"showStatusBar", true|"showStatusBar", false|' \
      -e 's|"showMenubar", true|"showMenubar", false|g' -i browserwindow.cpp
  cd $srcdir/$pkgname-$pkgver/src/lib/plugins/
  sed -e 's|"background", QString()|"file:///usr/share/pixmaps/start-white.png", QString()|' \
      -e 's|"backsize", "auto"|"backsize", "contain"|' -i speeddial.cpp
  cd $srcdir/$pkgname-$pkgver
  
  patch -Np1 -i ../0001-branded-speeddial.patch
  
  msg 'Applying preference patch'
  cd src/lib/preferences/
  sed -e 's|"homepage", QUrl(QSL("falkon:start"))|"homepage", QUrl(QSL("https://www.chakralinux.org/?welcome"))|' \
      -e 's|"showStatusBar", true|"showStatusBar", false|g' -i preferences.cpp
  msg 'Applying search engine patch...'
  cd ../opensearch/
  sed 's,\?q=\%s\&t=qupzilla,\?q=\%s\&t=chakra,g' -i searchenginesmanager.cpp
}
         
build() {
  export USE_WEBGL="true"
  export ENABLE_OPACITY_EFFECT="true"

  cd $pkgname-$pkgver
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INTEGRATION=ON \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DDISABLE_UPDATES_CHECK=ON
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm 644 ../start-white.png $pkgdir/usr/share/pixmaps/start-white.png
  
  # zsh completion
  install -Dm644 linux/completion/_$pkgname \
    $pkgdir/usr/share/zsh/site-functions/_$pkgname
} 
