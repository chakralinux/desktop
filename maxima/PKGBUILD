pkgname=maxima
pkgver=5.41.0
pkgrel=1
pkgdesc="Maxima - a sophisticated Computer Algebra System."
arch=('x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
# needs rebuild when bash and sbcl changes version
depends=('sbcl' 'texinfo' 'sh')
makedepends=('python2' 'emacs-nox')
optdepends=('gnuplot: plotting capabilities' \
            'rlwrap: readline support via /usr/bin/rmaxima' \
            'tk: graphical xmaxima interface')
options=('!makeflags' '!zipman') # don't zip info pages or they won't work inside maxima
install="maxima.install"
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        "${pkgname}.desktop")
md5sums=('972c51384d7895c88d78eb045c6aedb2'
         '24aa81126fbb8b726854e5a80d4c2415')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --libexecdir=/usr/lib \
              --enable-sbcl \
              --with-default-lisp=sbcl
  
  # help avoid (re)running makeinfo/tex
  touch doc/info/maxima.info
  
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR=$pkgdir emacsdir=/usr/share/emacs/site-lisp/maxima install

  # install some freedesktop.org compatibility
  install -Dm644 ../$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop

  # make sure we have a nice icon for the desktop file at the right place ;)
  install -d $pkgdir/usr/share/pixmaps
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
