pkgname=memcached
pkgver=1.5.9
pkgrel=1
pkgdesc="A distributed memory object caching system"
arch=('x86_64')
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
install=memcached.install
source=("http://www.memcached.org/files/$pkgname-$pkgver.tar.gz"
        "memcached.service")
sha256sums=('4af3577dbf71cb0a748096dc6562ccd587cddb7565c720f1fdb23e8a34241d06'
            'd0511361cec56c47bfb86590af8aa51a63ff6794513914170d5226f4e46e4386')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make test
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m 755 scripts/memcached-tool "$pkgdir"/usr/bin/memcached-tool
  install -D -m 644 ../memcached.service "$pkgdir"/usr/lib/systemd/system/memcached.service
}

