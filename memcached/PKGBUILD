pkgname=memcached
pkgver=1.4.31
pkgrel=1
pkgdesc="A distributed memory object caching system"
arch=('x86_64')
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
install=memcached.install
source=("http://www.memcached.org/files/$pkgname-$pkgver.tar.gz"
        "memcached.service")
sha256sums=('77ce5f820bafe94c33bf7bd86268d9bcdb3b76f68128d795b106db53d528707f'
          'd0511361cec56c47bfb86590af8aa51a63ff6794513914170d5226f4e46e4386')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m 755 scripts/memcached-tool "$pkgdir"/usr/bin/memcached-tool
  install -D -m 644 ../memcached.service "$pkgdir"/usr/lib/systemd/system/memcached.service
}

