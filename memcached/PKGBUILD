#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributors from Arch: Dan McGee <dan@archlinux.org>
#                         Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.4.15
pkgrel=1
pkgdesc="A distributed memory object caching system"
arch=(i686 x86_64)
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
backup=('etc/conf.d/memcached')
source=(http://memcached.googlecode.com/files/$pkgname-$pkgver.tar.gz
        memcached.service)
changelog=ChangeLog
sha256sums=('169721ab7a7531add6ae9f6b14b6b5641725fe0b1f0bdf5c3a4327725901e2b4'
            '2f45840b044e668fb790472c55dfbde89208733d55de39b737229302c0d17df9')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m 755 scripts/memcached-tool "$pkgdir"/usr/bin/memcached-tool
  install -D -m 644 ../memcached.service "$pkgdir"/usr/lib/systemd/system/memcached.service
}

