#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=w3m
pkgver=0.5.3
pkgrel=1
pkgdesc="A pager/text-based WWW browser"
arch=('i686' 'x86_64')
url="http://w3m.sourceforge.net/"
license=('custom')
depends=('openssl' 'gc>=7.0' 'ncurses' 'zlib')
makedepends=('imlib2')
optdepends=('imlib2: for graphics support') 
depends=('openssl' 'gc' 'ncurses')
categories=('network')
source=("http://downloads.sourceforge.net/sourceforge/w3m/${pkgname}-${pkgver}.tar.gz"
        'https-segfault.patch')
sha1sums=('444b6c8cf7094ee95f8e9de96b37f814b9d83237'
          '66affb2f695fe0bdde25cf774642bfd6a9404e88')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed 's/file_handle/file_handle_rofl/g' -i istream.*
	patch -p1 -i ../https-segfault.patch

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-image=x11,fb \
		--with-imagelib=imlib2 \
		--with-termlib=ncurses \
		--disable-w3mmailer \
		--disable-mouse \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	find doc/* | grep -v CVS | xargs -i install "{}" "${pkgdir}/usr/share/doc/${pkgname}"
	ln -s ../../doc/"${pkgname}"/README "${pkgdir}/usr/share/licenses/${pkgname}"
}
