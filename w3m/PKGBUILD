#
# Chakra Packages for Chakra, part of chakra-project.org
#
# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>

pkgname=w3m
pkgver=0.5.3
pkgrel=2
pkgdesc="A pager/text-based WWW browser"
arch=('x86_64')
url="http://w3m.sourceforge.net/"
license=('custom')
depends=('openssl' 'gc>=7.0' 'ncurses' 'zlib')
makedepends=('imlib2')
optdepends=('imlib2: for graphics support') 
depends=('openssl' 'gc' 'ncurses')
categories=('network')
source=("http://downloads.sourceforge.net/sourceforge/w3m/${pkgname}-${pkgver}.tar.gz"
        'form_unknown.patch'
        'file_handle.patch'
        'https.patch'
        'gc72.patch')
sha1sums=('444b6c8cf7094ee95f8e9de96b37f814b9d83237'
          '23d648033ad77f3c67d370c62cf7b7ed86b5c6f2'
          '457de43be7eabc652614461a7ab25e3ba76b2aff'
          '66affb2f695fe0bdde25cf774642bfd6a9404e88'
          '7baa06ca684fd140503a2adc65e2d606970c4662')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -p1 < "$srcdir"/form_unknown.patch
  patch -p1 < "$srcdir"/file_handle.patch
  patch -p1 < "$srcdir"/https.patch
  patch -p1 < "$srcdir"/gc72.patch
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-image=x11,fb \
    --with-imagelib=imlib2 \
    --with-termlib=ncurses \
    --disable-w3mmailer \
    --disable-mouse

  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
  find doc/* | grep -v CVS | xargs -i install "{}" "${pkgdir}/usr/share/doc/${pkgname}"
  ln -s ../../doc/"${pkgname}"/README "${pkgdir}/usr/share/licenses/${pkgname}"
}
