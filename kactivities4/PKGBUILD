# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=kactivities4
pkgname=('kactivities4' 'libkactivities4')
pkgver=4.13.3 # there is no 4.14 version!
pkgrel=4
arch=('x86_64')
pkgdesc='API for using and interacting with Activities'
url='http://kde.org/'
license=('GPL'
         'FDL')
depends=("kdelibs>=${_kdever}")
makedepends=('cmake'
             'automoc4')
options=('docs'
         '!header'
         'debug'
         'log')
source=($_mirror/kactivities-$pkgver.tar.xz)
#sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d" " -f1`)
sha256sums=("c7cb9d023f6e5cd01d76568c3590303ea3ecb4ebe9535b31862957846f5e898a")

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF
  make
}

package_kactivities4() {
  pkgdesc='The KActivity Manager Daemon'
  depends=('libkactivities4')
  provides=('kactivitymanagerd')

  cd build
  install -Dm755 src/service/kactivitymanagerd \
    "${pkgdir}"/usr/bin/kactivitymanagerd
}

package_libkactivities4() {
  pkgdesc='API for using and interacting with the Activity Manager'
  depends=('kdelibs' 'kactivitymanagerd')
  provides=('kactivities')
  replaces=('kactivities<=4.13.3-3')
  conflcits=('kactivities<=4.13.3-3')

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm "${pkgdir}"/usr/bin/kactivitymanagerd
  rmdir "${pkgdir}"/usr/bin
}
