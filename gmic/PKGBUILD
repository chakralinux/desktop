# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgbase=gmic
pkgname=(gmic zart gimp-plugin-gmic krita-plugin-gmic)
pkgver=2.3.3
pkgrel=1
arch=(x86_64)
url="https://gmic.eu/"
license=(custom:CeCILL)
makedepends=(gimp qt5-base fftw openexr opencv git)
source=(https://gmic.eu/files/source/gmic_$pkgver.tar.gz)
sha256sums=('46417d48d9dd87a155ba979667834a7d6899652f097cc25652e0680ee0b13c6a')

prepare() {
  sed -e 's|/etc/bash_completion.d|/usr/share/bash-completion/completions|g' -i gmic-$pkgver/src/Makefile
  sed -e 's|-Ofast|-O2 -fno-fast-math|g' -i gmic-$pkgver/gmic-qt/gmic_qt.pro
# fix overlinking
  sed -e 's/pkg-config opencv --libs ||//' -e 's/-lopencv_highgui/-lopencv_videoio/' -i gmic-$pkgver/src/Makefile 
}

build() {
  cd gmic-$pkgver
  make -C src cli lib libc WGET=/bin/true

  cd gmic-qt
  qmake GMIC_PATH=../src GMIC_DYNAMIC_LINKING=on HOST=none
  make
  qmake GMIC_PATH=../src GMIC_DYNAMIC_LINKING=on HOST=gimp
  make
  qmake GMIC_PATH=../src GMIC_DYNAMIC_LINKING=on HOST=krita
  make  

  cd ../zart
  qmake-qt5 GMIC_PATH=../src GMIC_DYNAMIC_LINKING=on
  make
}

package_gmic() {
  pkgdesc="GREYC's Magic Image Converter: image processing framework"
  depends=(opencv fftw curl)
  optdepends=('qt5-base: for the Qt UI')

  cd gmic-$pkgver/src
  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -r "$pkgdir"/usr/{bin/zart,lib/gimp,bin/gmic_krita_qt}
}

package_zart() {
  pkgdesc="A GUI for G'MIC real-time manipulations on the output of a webcam"
  depends=(gmic qt5-base)

  install -Dm755 gmic-$pkgver/zart/zart -t "$pkgdir"/usr/bin
  install -Dm644 gmic-$pkgver/zart/Licence_CeCILL_V2-en.html -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the G'MIC image processing framework"
  depends=(gimp gmic qt5-base)

  install -Dm755 gmic-$pkgver/gmic-qt/gmic_gimp_qt "$pkgdir"/usr/lib/gimp/2.0/plug-ins/gmic_gimp
  install -Dm644 gmic-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_krita-plugin-gmic() {
  pkgdesc="Krita plugin for the G'MIC image processing framework"
  depends=(krita gmic)

  install -Dm755 gmic-$pkgver/gmic-qt/gmic_krita_qt -t "$pkgdir"/usr/bin/
  install -Dm644 gmic-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
