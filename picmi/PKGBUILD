# Include global configuration

pkgname=picmi
pkgver=18.04.3
pkgrel=1
pkgdesc="A nonogram logic game for KDE"
arch=('x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegames')
conflicts=('kdegames-picmi')
replaces=('kdegames-picmi')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d44140eb4b5feca3f112c023e1c76b173975a28d89b18dff1617d406347a7574'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
