# Include global configuration

pkgname=kmag
arch=('x86_64')
pkgver=18.04.3
pkgrel=1
pkgdesc="Screen magnifier"
url="https://kde.org/applications/utilities/$pkgname"
license=('GPL' 'LGPL' 'FDL')
depends=(kio libqaccessibilityclient hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python3)
conflicts=(kdeaccessibility-kmag)
provides=(kdeaccessibility-kmag)
replaces=(kdeaccessibility-kmag)
groups=("kdeaccessibility" 'kde' 'kde-applications')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('61556d725df5001cc39e36ea9ffa58b1b18e806b2bf88b8c1700007249203a03'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
