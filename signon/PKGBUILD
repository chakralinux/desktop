# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/signon/

pkgname=signon
_pkgname=signond
pkgver=8.59
pkgrel=1
arch=('x86_64')
url="https://gitlab.com/accounts-sso/signond/"
license=('LGPL')
depends=('qt5-base' 'libproxy')
makedepends=('qt5-tools' 'doxygen' 'graphviz')
pkgdesc="Qt Framework to provide credential storage and authentication service"
options=('!emptydirs')
source=("https://gitlab.com/accounts-sso/signond/repository/archive.tar.bz2?ref=VERSION_$pkgver"
        'use_keyring.patch')
sha1sums=('e627143950b186ea2851383cad08d401b66282e2'
          '7f179d9260b3a67ccc8635c5bc5e50e282507896')

prepare() {
  cd $_pkgname-*
  # Use kwallet-keyring secrets storage if signon-keyring-extension is available
  patch -Np1 -i "$srcdir/use_keyring.patch"
}

build() {
  cd $_pkgname-*
  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd $_pkgname-*
  make INSTALL_ROOT="$pkgdir" install

  # Do not ship docs
  rm -rf "${pkgdir}/usr/share/doc"
}

