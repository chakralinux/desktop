# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/signon/

pkgname=signon
pkgver=8.57
pkgrel=5
arch=('x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('qt5-base' 'libproxy')
makedepends=('qt5-tools' 'doxygen')
pkgdesc="Qt Framework to provide credential storage and authentication service"
options=('!emptydirs')
source=("http://rsync.chakraos.org/sources/${pkgname}/$pkgname-$pkgver.tar.bz2"
        'use_keyring.patch')
sha1sums=('c9fa0567b68db434a7a954dcb303adf7aad1578b'
          '7f179d9260b3a67ccc8635c5bc5e50e282507896')

prepare() {
  cd $pkgname-$pkgver
  # Use kwallet-keyring secrets storage if signon-keyring-extension is available
  patch -Np1 -i "$srcdir/use_keyring.patch"
}

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  # Do not ship docs
  rm -rf "${pkgdir}/usr/share/doc"
}

