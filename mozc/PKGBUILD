# Maintainer: ponsfoot <cabezon dot hashimoto at gmail dot com>

## Mozc compile option
_bldtype=Release

_zipcoderel=201202
_protobuf_ver=2.4.1

pkgbase=mozc
pkgdesc="A Japanese Input Method for Chromium OS, Windows, Mac and Linux (the Open Source Edition of Google Japanese Input)"
pkgname=('mozc' 'fcitx-mozc')
pkgver=1.5.1053.102
_patchver=${pkgver}-1
pkgrel=1
arch=('i686' 'x86_64')
url="http://code.google.com/p/mozc/"
license=('BSD' 'custom')
makedepends=('pkg-config' 'python2' 'gtest' 'zinnia' 'qt' 'curl' 'fcitx')
source=(http://mozc.googlecode.com/files/mozc-${pkgver}.tar.bz2
        http://downloads.sourceforge.net/pnsft-aur/ken_all-${_zipcoderel}.zip
        http://downloads.sourceforge.net/pnsft-aur/jigyosyo-${_zipcoderel}.zip
	http://protobuf.googlecode.com/files/protobuf-${_protobuf_ver}.tar.bz2
	http://fcitx.googlecode.com/files/fcitx-mozc-${_patchver}.patch
)
sha1sums=('93a4918acc5115847efbaea91f05962c4ccf4bba'
          'f2dca1f3b4386d768b5b04ffe1f5e6103ef5bd08'
          'e52272cc5e75c59e8491d1caf00edc9b35fe44ae'
          'df5867e37a4b51fb69f53a8baf5b994938691d6d'
          '8bd15a29c13b49563265f113689d007a04223930')


build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  rm unix/fcitx -rf
  patch -Np2 -i ${srcdir}/fcitx-mozc-${_patchver}.patch || return 1

  # Generate zip code seed
  msg "Generating zip code seed..."
  python2 dictionary/gen_zip_code_seed.py --zip_code="${srcdir}/KEN_ALL.CSV" --jigyosyo="${srcdir}/JIGYOSYO.CSV" >> data/dictionary/dictionary09.txt
  msg "Done."

  # Copy protobuf to be linked statically
  cp -rf "${srcdir}/protobuf-${_protobuf_ver}" protobuf/files

  msg "Starting make..."

  # Get make -j option from $MAKEFLAGS
  _jobs=`sed -n -e "s/.*--jobs=\([0-9]\+\).*/\1/p" -e "s/.*-j *\([0-9]\+\).*/\1/p" <<< "$MAKEFLAGS"`
  _jobs=${_jobs:-1}

  _targets="server/server.gyp:mozc_server gui/gui.gyp:mozc_tool "
  _targets+="unix/fcitx/fcitx.gyp:fcitx-mozc "

  python2 build_mozc.py gyp --channel_dev=0
  python2 build_mozc.py build_tools -c $_bldtype -j $_jobs
  python2 build_mozc.py build -c $_bldtype -j $_jobs $_targets

  # Extract liccense part of mozc
  head -n 28 server/mozc_server.cc > LICENSE
}

package_mozc() {
  pkgdesc="A Japanese Input Method for Chromium OS, Windows, Mac and Linux (the Open Source Edition of Google Japanese Input)"
  arch=('i686' 'x86_64')
  groups=('mozc-im')
  depends=('curl' 'qt')
  replaces=('mozc-server' 'mozc-utils-gui')
  conflicts=('mozc-server' 'mozc-utils-gui')
  optdepends=('zinnia: hand-writing recognition support'
              'tegaki-models-zinnia-japanese: hand-writing recognition support')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -D -m 755 out_linux/${_bldtype}/mozc_server "${pkgdir}/usr/lib/mozc/mozc_server"
  install    -m 755 out_linux/${_bldtype}/mozc_tool   "${pkgdir}/usr/lib/mozc/mozc_tool"
  install -d "${pkgdir}/usr/lib/mozc/documents/"
  install    -m 644 data/installer/*.html "${pkgdir}/usr/lib/mozc/documents/"

  install -D -m 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}

package_fcitx-mozc() {
  pkgdesc="Fcitx module for Mozc"
  arch=('i686' 'x86_64')
  groups=('mozc-im')
  depends=("mozc=${pkgver}" 'fcitx>=4.2.1')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  for mofile in out_linux/${_bldtype}/obj/gen/unix/fcitx/po/*.mo
  do
      filename=`basename $mofile`
      lang=${filename/.mo/}
      install -D -m 644 "$mofile" "${pkgdir}/usr/share/locale/$lang/LC_MESSAGES/fcitx-mozc.mo"
  done

  install -D -m 755 out_linux/${_bldtype}/fcitx-mozc.so "${pkgdir}/usr/lib/fcitx/fcitx-mozc.so"
  install -D -m 644 unix/fcitx/fcitx-mozc.conf "${pkgdir}/usr/share/fcitx/addon/fcitx-mozc.conf"
  install -D -m 644 unix/fcitx/mozc.conf "${pkgdir}/usr/share/fcitx/inputmethod/mozc.conf"

  install -d "${pkgdir}/usr/share/fcitx/mozc/icon"
  install -m 644 data/images/product_icon_32bpp-128.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc.png"
  install -m 644 data/images/unix/ui-alpha_full.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc-alpha_full.png"
  install -m 644 data/images/unix/ui-alpha_half.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc-alpha_half.png"
  install -m 644 data/images/unix/ui-direct.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc-direct.png"
  install -m 644 data/images/unix/ui-hiragana.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc-hiragana.png"
  install -m 644 data/images/unix/ui-katakana_full.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc-katakana_full.png"
  install -m 644 data/images/unix/ui-katakana_half.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc-katakana_half.png"
  install -m 644 data/images/unix/ui-dictionary.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc-dictionary.png"
  install -m 644 data/images/unix/ui-properties.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc-properties.png"
  install -m 644 data/images/unix/ui-tool.png "${pkgdir}/usr/share/fcitx/mozc/icon/mozc-tool.png"
}
sha1sums=('af091cb8f563ccd482348e3c10ef40e742c5517d'
          'f2dca1f3b4386d768b5b04ffe1f5e6103ef5bd08'
          'e52272cc5e75c59e8491d1caf00edc9b35fe44ae'
          'df5867e37a4b51fb69f53a8baf5b994938691d6d'
          'd111b663525140840888f9989bcdc984d82c3bb0')
