# Maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributors: H W TovetjÃ¤rn (totte) <totte@tott.es>
# 		Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

## NOTE: when you update Haskell packages you'll have to update the entire stack to not break it ##

_hkgname=mtl
pkgname=haskell-mtl
pkgver=2.1.2
pkgrel=4
pkgdesc="Monad classes, using functional dependencies"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc=7.6.3-1' "haskell-transformers=0.3.0.0")
options=('strip' 'staticlibs')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
sha512sums=('ef8043adeb2746c770bebd4683c95c399bdc047c278b619d89230f342d13c33f0a9275c6be399e6da329f385ff209ff537e002b82d5075684202e8703fc5e29d')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix=/usr \
        --docdir=/usr/share/doc/haskell-mtl \
        --libsubdir=\$compiler/site-local/\$pkgid
    
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/${pkgname}/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/mtl"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
