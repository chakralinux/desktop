#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer : Mateusz Krawczuk <willingmagic[at]gmail[dot]com>

pkgname=blender
pkgver=2.63.1
_pkgver=2.63a
pkgrel=2
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.blender.org"
depends=('libpng' 'libtiff' 'openexr' 'sdl' 'python3' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils' 'glew' 'libxi' 'libxmu' 
'mesa' 'freetype2' 'openal' 'libsndfile' 'libsamplerate' 'ffmpeg' 'fftw' 'openimageio')
makedepends=('cmake' 'jack')
screenshot=('http://www.blender.org/index.php?eID=tx_cms_showpic&file=uploads%2Fpics%2Fblender_screenshot3_03.png&width=800m&height=600m&bodyTag=%3Cbody%20style%3D%22margin%3A0%3B%20background%3A%23fff%3B%22%3E&wrap=%3Ca%20href%3D%22javascript%3Aclose%28%29%3B%22%3E%20%7C%20%3C%2Fa%3E&md5=cddc8ef0fbe1e90d0d22e78b4911860b')
install=blender.install
source=(http://download.blender.org/source/$pkgname-$_pkgver.tar.gz)
md5sums=('31a60b3ce5466d965cb7e2648995e092')

build() {
  cd "$srcdir/$pkgname-$_pkgver"
  
  mkdir -p build
  cd build

 [[ $CARCH == i686 ]] && ENABLESSE2="-DSUPPORT_SSE2_BUILD:BOOL=OFF"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DWITH_INSTALL_PORTABLE:BOOL=OFF \
    -DWITH_PYTHON_INSTALL:BOOL=OFF \
    -DWITH_OPENCOLLADA:BOOL=OFF \
    -DWITH_GAMEENGINE:BOOL=ON \
    -DWITH_PLAYER:BOOL=ON \
    -DWITH_BUILTIN_GLEW:BOOL=On \
    -DWITH_CODEC_FFMPEG:BOOL=ON \
    -DPYTHON_VERSION:STRING=3.2 \
    -DPYTHON_LIBPATH:STRING=/usr/lib \
    -DPYTHON_LIBRARY:STRING=python3.2mu \
    -DPYTHON_INCLUDE_DIRS:STRING=/usr/include/python3.2mu \
    $ENABLESSE2

  make $MAKEFLAGS

  cp -rf "$srcdir"/${pkgname}-$_pkgver/release/plugins/* \
    "$srcdir"/${pkgname}-$_pkgver/source/blender/blenpluginapi/
  cd "$srcdir"/${pkgname}-$_pkgver/source/blender/blenpluginapi
  chmod 755 bmake
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver/build"
  make DESTDIR="${pkgdir}" install
  python3 -m compileall "${pkgdir}/usr/share/blender"

# install plugins
  install -d -m755 "$pkgdir"/usr/share/blender/${_pkgver%[a-z]}/plugins/{sequence,texture}
  cp "$srcdir"/${pkgname}-$_pkgver/source/blender/blenpluginapi/sequence/*.so \
    "$pkgdir"/usr/share/blender/${_pkgver%[a-z]}/plugins/sequence/
  cp "$srcdir"/${pkgname}-$_pkgver/source/blender/blenpluginapi/texture/*.so \
    "$pkgdir"/usr/share/blender/${_pkgver%[a-z]}/plugins/texture/
}
