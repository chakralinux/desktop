# Maintainer: U t G  <utg.chakra.linux[at]gmail[dot]com>
# Contributor: Neophytos Kolokotronis <tetris4@gmail.com

pkgname=blender
pkgver=2.70
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('x86_64')
license=('GPL')
url="http://www.blender.org"
depends=('libpng' 'libtiff' 'openexr' 'sdl' 'python3' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils' 'glew' 'freetype2' 'jack' 'openal' 'libsndfile' 'libsamplerate' 'ffmpeg' 'fftw' 'openimageio' 'boost-libs' 'opencollada' 'opencolorio' 'llvm' 'pcre')
makedepends=('cmake' 'boost' 'mesa' 'subversion')
screenshot=( # new screenshots: 2014
'http://www.blender.org/wp-content/uploads/2012/11/materials_screenshot-640x360.png' 
'http://www.blender.org/wp-content/uploads/2012/11/Flexible-UI-640x359.png'
'http://www.blender.org/wp-content/uploads/2012/11/camera-tracking-640x360.png')
install=blender.install
source=("http://download.blender.org/source/$pkgname-$pkgver.tar.gz")
sha1sums=('294d89855e4bd2226bfbd4311f835709379ec309')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  mkdir -p build
  cd build

#    -DWITH_LLVM=ON \
#    -DLLVM_VERSION=3.3 \
#    -DLLVM_STATIC=OFF \

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_INSTALL_PORTABLE=OFF \
    -DWITH_PYTHON_INSTALL=OFF \
    -DWITH_OPENCOLLADA=ON \
    -DOPENIMAGEIO_ROOT_DIR=/usr \
    -DWITH_GAMEENGINE=ON \
    -DWITH_JACK=ON \
    -DWITH_PLAYER=ON \
    -DWITH_CODEC_FFMPEG=ON \
    -DWITH_CODEC_SNDFILE=ON \
    -DWITH_CYCLES=ON \
    -DWITH_FFTW3=ON \
    -DWITH_MOD_OCEANSIM=ON \
    -DPYTHON_VERSION=3.3 \
    -DPYTHON_LIBPATH=/usr/lib \
    -DPYTHON_LIBRARY=python3.3m \
    -DPYTHON_INCLUDE_DIRS=/usr/include/python3.3m
    #-DOPENJPEG_INCLUDE_DIR=/usr/include/openjpeg-1.5
    # Remove the openjpeg line once blender accepts my patch

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR="${pkgdir}" install
  python3 -m compileall "${pkgdir}/usr/share/blender"
}

