# Maintainer: Jeff Huang <s8321414[at]gmail[dot]com>
# Contributor: UtG <utg.chakra.linux[at]gmail[dot]com>
# Contributor: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# Contributor: Samir Benmendil <ram-z[at]chakra-project[dot]org>
 
pkgname=mpv
pkgver=0.8.3
pkgrel=1
pkgdesc="A movie player and encoder for linux, based on MPlayer/mplayer2"
arch=('x86_64')
license=('GPL')
url="http://mpv.io"
depends=('pulseaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libgl' 'libbs2b' 
         'libxinerama' 'mpg123' 'libxv' 'libxkbcommon' 'libva'  'libass' 
         'lirc-utils' 'wayland' 'desktop-file-utils' 'hicolor-icon-theme' 
         'xdg-utils' 'lua' 'libdvdnav' 'libcdio-paranoia' 'libbluray'
         'enca' 'libguess' 'harfbuzz' 'graphite' 'libxrandr')
makedepends=('mesa' 'python3-docutils' 'ladspa' 'x264' 'jack'
             'samba')
options=('!emptydirs')
install=${pkgname}.install
source=("https://github.com/mpv-player/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('091038c802866848e3647f2a50215fa64ead0dc36f9114457db8b353528689cf')

prepare() {
  # point python to python3
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python3|' ${srcdir}/${pkgname}-${pkgver}/bootstrap.py
  # initialize
  cd ${pkgname}-${pkgver}
  ./bootstrap.py
  sed -i 's/vendor-completions/site-functions/' wscript_build.py

  # waf looking for python2...
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python3|' ${srcdir}/${pkgname}-${pkgver}/waf
}
 
build() {
  cd ${pkgname}-${pkgver}
 
  # disable SDL, since they are just legacy drivers
  ./waf configure \
    --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-joystick \
    --enable-libmpv-shared \
    --enable-cdda \
    --enable-zsh-comp \
    --enable-pulse \
    --disable-sdl2 \
    --disable-sdl1 \
    --enable-openal \
    --enable-wayland \
    --enable-gl-wayland \
    --enable-egl-x11 \
    --enable-libsmbclient \
    --enable-dvdread
  ./waf build
}
 
package() {
  cd ${pkgname}-${pkgver}
 
  ./waf install --destdir="${pkgdir}" 
  
  install -d "${pkgdir}/usr/share/doc/mpv/examples"
 
  # some extra useful stuff "make install" doesn't install
  install -m755 TOOLS/umpv \
    "${pkgdir}"/usr/bin/umpv
  install -m644 etc/{input,example}.conf \
    "${pkgdir}"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "${pkgdir}"/usr/share/doc/mpv
}
