# Maintainer:  UtG <utg.chakra.linux[at]gmail[dot]com>
# Contributor: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# Contributor: Samir Benmendil <ram-z[at]chakra-project[dot]org>
 
pkgname=mpv
pkgver=0.3.8
pkgrel=1
pkgdesc="A movie player and encoder for linux, based on MPlayer/mplayer2"
arch=('x86_64')
license=('GPL')
url="http://mpv.io"
depends=('pulseaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libquvi' 'libgl' 'libxinerama'  'mpg123' 'libxv' 'libxkbcommon' 'libva'  'libass' 'lirc-utils' 'wayland' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua' 'libdvdnav' 'libcdio-paranoia' 'libbluray')
makedepends=('mesa' 'python3-docutils' 'ladspa')
options=('!emptydirs' '!buildflags')
install=${pkgname}.install
source=("https://github.com/mpv-player/$pkgname/archive/v${pkgver}.tar.gz")
md5sums=('f7fc14f52c1958f6b603245b0595efa9')

prepare() {
  cd $pkgname-$pkgver
  ./bootstrap.py
}
 
build() {
  cd "$pkgname-$pkgver"
 
  ./waf configure --prefix=/usr \
      --confdir=/etc/mpv \
      --enable-joystick \
      --enable-pulse
  ./waf build
}
 
package() {
  cd "$srcdir/$pkgname-$pkgver"
 
  ./waf install --destdir="$pkgdir" 
  
  install -d ${pkgdir}/usr/share/doc/mpv/examples
 
  # some extra useful stuff "make install" doesn't install
  install -m644 etc/{input,example}.conf \
      "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
