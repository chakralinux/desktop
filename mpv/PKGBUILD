pkgname=mpv
pkgver=0.15.0
pkgrel=1
pkgdesc="A movie player and encoder for linux, based on MPlayer/mplayer2"
arch=('x86_64')
license=('LGPL3')
url="http://mpv.io"
depends=('pulseaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libgl' 'libvdpau'
         'libxinerama' 'libxv' 'libxkbcommon' 'libva'  'libass' 'uchardet' 
	 'wayland' 'desktop-file-utils' 'hicolor-icon-theme' 'v4l-utils'
         'xdg-utils' 'lua' 'libdvdnav' 'libcdio-paranoia' 'libbluray' 'libxss'
         'enca' 'libguess' 'harfbuzz' 'graphite' 'libxrandr' 'rubberband' 'smbclient')
makedepends=('mesa' 'python3-docutils' 'ladspa' 'x264' 'jack'
             'samba')
options=('!emptydirs')
provides=('mplayer')
install=${pkgname}.install
source=("https://github.com/mpv-player/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('7d31217ba8572f364fcea2955733f821374ae6d8c6d8f22f8bc63c44c0400bdc')

prepare() {
  # point python to python3
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python3|' ${srcdir}/${pkgname}-${pkgver}/bootstrap.py
  # initialize
  cd ${pkgname}-${pkgver}
  ./bootstrap.py
  sed -i 's/vendor-completions/site-functions/' wscript_build.py

  # waf looking for python2...
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python3|' ${srcdir}/${pkgname}-${pkgver}/waf
}
 
build() {
  cd ${pkgname}-${pkgver}
 
  # disable SDL, since they are just legacy drivers
  ./waf configure \
    --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-libmpv-shared \
    --enable-cdda \
    --enable-zsh-comp \
    --enable-alsa \
    --enable-pulse \
    --enable-openal \
    --enable-wayland \
    --enable-gl-wayland \
    --enable-egl-x11 \
    --enable-libsmbclient \
    --enable-dvdread \
    --enable-libv4l2 \
    --enable-vdpau \
    --enable-gpl3
  ./waf build
}
 
package() {
  cd ${pkgname}-${pkgver}
 
  ./waf install --destdir="${pkgdir}" 
  
  install -d "${pkgdir}/usr/share/doc/mpv/examples"
 
  # some extra useful stuff "make install" doesn't install
  install -m755 TOOLS/umpv \
    "${pkgdir}"/usr/bin/umpv
  install -m644 etc/input.conf \
    "${pkgdir}"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "${pkgdir}"/usr/share/doc/mpv
}
