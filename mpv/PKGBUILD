# Maintainer:  UtG <utg.chakra.linux[at]gmail[dot]com>
# Contributor: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# Contributor: Samir Benmendil <ram-z[at]chakra-project[dot]org>
 
pkgname=mpv
pkgver=0.5.3
pkgrel=1
pkgdesc="A movie player and encoder for linux, based on MPlayer/mplayer2"
arch=('x86_64')
license=('GPL')
url="http://mpv.io"
depends=('pulseaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libquvi' 'libgl' 
         'libxinerama' 'mpg123' 'libxv' 'libxkbcommon' 'libva'  'libass' 
         'lirc-utils' 'wayland' 'desktop-file-utils' 'hicolor-icon-theme' 
         'xdg-utils' 'lua' 'libdvdnav' 'libcdio-paranoia' 'libbluray'
         'enca' 'libguess' 'harfbuzz' 'graphite')
makedepends=('mesa' 'python3-docutils' 'ladspa')
options=('!emptydirs' '!buildflags')
install=${pkgname}.install
source=("https://github.com/mpv-player/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('04de4375f5ca69849927f23657358734ff1c02c54a15667ab3623ccb00ab6fa7')

prepare() {
  cd ${pkgname}-${pkgver}
  
  ./bootstrap.py
  sed -i 's/vendor-completions/site-functions/' wscript_build.py
}
 
build() {
  cd ${pkgname}-${pkgver}
 
  # disable wayland, as long, as the version is too old
  # it won't compile with 1.3.0
  ./waf configure \
    --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-joystick \
    --enable-libmpv-shared \
    --enable-cdda \
    --enable-zsh-comp \
    --enable-pulse \
    --disable-wayland \
    --disable-portaudio
  ./waf build
}
 
package() {
  cd ${pkgname}-${pkgver}
 
  ./waf install --destdir="${pkgdir}" 
  
  install -d "${pkgdir}/usr/share/doc/mpv/examples"
 
  # some extra useful stuff "make install" doesn't install
  install -m644 etc/{input,example}.conf \
    "${pkgdir}"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "${pkgdir}"/usr/share/doc/mpv
}
