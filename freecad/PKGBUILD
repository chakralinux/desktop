pkgname=freecad
_pkgname=FreeCAD
pkgver=0.17
_rollup=84d6128
pkgrel=1
pkgdesc='A general purpose 3D CAD modeler'
arch=('x86_64')
url='https://www.freecadweb.org/wiki'
license=('GPL')
depends=('boost-libs' 'curl' 'opencascade' 'xerces-c' 'libspnav' 'glu'
         'shared-mime-info' 'hicolor-icon-theme' 'jsoncpp' 'med' 'qt' 'netcdf'
         'python2-pyside' 'python2-matplotlib' 'python2-shiboken'
         'python2-pyside-tools' 'pyside-tools-common')
makedepends=('boost' 'eigen3' 'gcc-fortran' 'swig' 'xerces-c'
             'desktop-file-utils' 'cmake' 'coin' 'zlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FreeCAD/FreeCAD/archive/$pkgver.tar.gz"
        "$pkgname-$_rollup-rollup.patch::https://github.com/FreeCAD/FreeCAD/compare/$pkgver...$_rollup.patch"
	"${pkgname}.desktop"
	"${pkgname}.xml"
        'remove-qtwebkit.patch'
        'gcc8.patch' 'smesh-pthread.patch' 'qt5.11.patch')
sha256sums=('ae017393476b6dc7f1192bcaf91ceedc2f9b791f2495307ce7c45efadb5266fb'
            '321ee7669ad8cce5dd6d7d352b11952c18b37e4b178c94636a3b08079a3e27cb'
            '248918de7d3c2145b5cc4fbbc9e224d22f4a6ca7ead2680e8c3a32e91772482a'
            'de214a281d421d44b02b469ecf72e36c4d61f10c40431e511e09734aca316baf')

prepare() {
  cd "${srcdir}/FreeCAD-${pkgver}"

  patch -Np1 -i ../$pkgname-$_rollup-rollup.patch
  patch -Np1 -i ../gcc8.patch
  patch -Np1 -i ../smesh-pthread.patch
  patch -Np1 -i ../qt5.11.patch
}

build() {
  cd "${srcdir}/FreeCAD-${pkgver}"
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="/usr/lib/freecad" \
    -DCMAKE_INSTALL_DOCDIR="/usr/share/freecad/doc" \
    -DCMAKE_INSTALL_DATADIR="/usr/share/freecad" \
    -DFREECAD_USE_OCC_VARIANT="Official Version" \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DBUILD_QT5=ON \
    -DBUILD_QT5_WEBKIT=OFF \
    -DBUILD_START=OFF \
    -DBUILD_WEB=OFF
  make
}

package() {
  cd "${srcdir}/FreeCAD-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Symlink to /usr/bin
    install -dm755 "${pkgdir}/usr/bin"
    ln -srf "${pkgdir}/usr/lib/freecad/bin/FreeCAD" "${pkgdir}/usr/bin/freecad"
    ln -srf "${pkgdir}/usr/lib/freecad/bin/FreeCAD" "${pkgdir}/usr/bin/FreeCAD"
    ln -srf "${pkgdir}/usr/lib/freecad/bin/FreeCADCmd" "${pkgdir}/usr/bin/freecadcmd"
    ln -srf "${pkgdir}/usr/lib/freecad/bin/FreeCADCmd" "${pkgdir}/usr/bin/FreeCADCmd"

    # Install pixmaps and desktop shortcut
    desktop-file-install \
        --dir="${pkgdir}/usr/share/applications" \
        "${srcdir}/${pkgname}.desktop"
    for i in 16 32 48 64; do
        install -Dm644 "src/Gui/Icons/freecad-icon-${i}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/freecad.png"
    done
    install -Dm644 "src/Gui/Icons/freecad.svg" \
        "${pkgdir}/usr/share/icons/hicolor/scalable/apps/freecad.svg"

    # Mime info
    install -D -m644 "${srcdir}/freecad.xml" "${pkgdir}/usr/share/mime/packages/freecad.xml"
}
