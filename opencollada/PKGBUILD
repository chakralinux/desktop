pkgname=opencollada
pkgver=1.6.59
pkgrel=2
epoch=1
pkgdesc="Stream based reader and writer library for COLLADA files"
arch=('x86_64')
url="https://github.com/KhronosGroup/OpenCOLLADA"
license=('GPL')
depends=('libxml2' 'pcre' 'boost')
makedepends=('cmake')
source=("https://github.com/KhronosGroup/OpenCOLLADA/archive/v$pkgver.tar.gz"
        'opencollada.conf')
sha1sums=('17f3d2c7f3970163c11234b8e2fe824d41376ca9'
          '5fe78d788742428856633c65d864aae2977e82a5')

build() {
  cd "$srcdir"/OpenCOLLADA-$pkgver

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SHARED=ON \
    -DUSE_STATIC=OFF
  make
}

package() {
  cd "$srcdir"/OpenCOLLADA-$pkgver/build

  make DESTDIR=$pkgdir install

  install -Dm644 $srcdir/opencollada.conf $pkgdir/etc/ld.so.conf.d/opencollada.conf
}

