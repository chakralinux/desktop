#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# contributor Fabien <hcoohb@gmailcom>

pkgname=yate
pkgver=4.2.0
pkgrel=1
pkgdesc="Next-generation telephony engine, qt4 client for VoIP"
arch=('i686' 'x86_64')
url="http://yate.null.ro"
license=('GPL2')
depends=('qt' 'speex' 'gsm' 'postgresql-libs' 'libmysqlclient' \
         'alsa-lib')
makedepends=('pkgconfig' 'desktop-file-utils')
categories=('communication')
options=(!makeflags)
source=("http://yate.null.ro/tarballs/yate4/${pkgname}-${pkgver}-2.tar.gz"
        "yate.init"
        "yate.service"
        "yate.conf.d")
sha256sums=('1ec7ecfcac1b1d936957cd4c859f50a38d77c6e7927faabd73bb3da38d8d797f'
            '3797c4ea3e09ac8514349905a3a994233c9c86bca1feea1242ec5fa10fb44483'
            '224d8f15275717d078342c4a5d4b33c6d64dbf9772538656b590ef67a464dfec'
            '1b88b8e3573a68fb5f6673b1922318a4fa51c722def21dd27b4ef8ca002812e4')

build() {
  cd "${srcdir}/${pkgname}"
  
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-libpq \
              --with-mysql \
              --without-openh323 

  make
}

package() {
  cd "${srcdir}/${pkgname}"
  
  make DESTDIR="${pkgdir}" install
  
  # Fix desktop file
  desktop-file-edit --remove-key="Encoding" \
                    --set-icon="null_team-48" \
                    "${pkgdir}/usr/share/applications/yate-qt4.desktop"
  
  # Install init scripts
  install -Dm0755 "${srcdir}/yate.init" "${pkgdir}/etc/rc.d/yate"
  
  # Install system units
  install -Dm0644 "${srcdir}/yate.service" "${pkgdir}/usr/lib/systemd/system/yate.service"
  install -Dm0644 "${srcdir}/yate.conf.d" "${pkgdir}/etc/conf.d/yate"
}
