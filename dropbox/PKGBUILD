pkgname=dropbox
pkgver=8.4.19
pkgrel=1
pkgdesc="A free service that lets you bring your photos, docs, and videos anywhere and share them easily."
arch=('x86_64')
url="http://www.dropbox.com"
license=('custom')
depends=('dbus-glib' 'libsm' 'libxmu' 'libxslt'
         'qt5-base' 'qt5-declarative' 'qt5-webkit')
optdepends=('dolphin-plugins: for integration of dolphin under plasma 5')
options=('!strip' '!upx')
source=("https://dl-web.dropbox.com/u/17/${pkgname}-lnx.${arch}-${pkgver}.tar.gz"
        "dropbox.png"
        "dropbox.desktop"
        "LICENSE"
        "dropbox.service"
        "dropbox@.service")
sha1sums=('2d1e75e6915e848f865a9eef452ffd6745193ae0'
          '64073af76a2bbc7dba4a6886b82af0bb3e71a2a0'
          'f17894457eb9c32b249f19169386e423163f0e37'
          'b69765536f67fab1080bd623019c8d859edb75dc'
          '9bda78faca76d55d0cf1b16af782066bab3029fe'
          '0019b370924494390b071ed6e607e5f70c88fc12')

install="${pkgname}.install"

package() {
	install -d "$pkgdir/opt"
	cp -R "$srcdir"/.dropbox-dist/dropbox-lnx.$arch-$pkgver "$pkgdir"/opt/dropbox
 
	find "$pkgdir/opt/dropbox/" -type f -exec chmod 644 {} \;
	chmod 755 "$pkgdir/opt/dropbox/dropboxd"
	chmod 755 "$pkgdir/opt/dropbox/dropbox"
 
	install -d "$pkgdir/usr/bin"
	ln -s "/opt/dropbox/dropboxd" "$pkgdir/usr/bin/dropboxd"
 
	install -D -m 644 "$srcdir/dropbox.desktop" "$pkgdir/usr/share/applications/dropbox.desktop"
	install -D -m 644 "$srcdir/dropbox.png" "$pkgdir/usr/share/pixmaps/dropbox.png"
	install -D -m 644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 "$srcdir"/dropbox.service "$pkgdir"/usr/lib/systemd/user/dropbox.service
	install -Dm644 "$srcdir/dropbox@.service" "$pkgdir/usr/lib/systemd/system/dropbox@.service"

	rm -f "$pkgdir"/opt/dropbox/library.zip
	ln -s dropbox "$pkgdir"/opt/dropbox/library.zip

        # Works with the prefix `XDG_CURRENT_DESKTOP=Unity` under new simplesystemtray in testing
        cd ${pkgdir}/opt/dropbox/
        msg 'removing libQt5'
        rm -v libQt5*
        rm -vrf plugins/
        rm -v qt.conf
        msg 'removing other libraries'
        rm -v libicu*
        rm -v libffi*
        rm -v libGL*
        rm -v libpopt*
        rm -v libX11-xcb*
        rm -v libdrm*
}
