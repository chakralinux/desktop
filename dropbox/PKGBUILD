pkgname=dropbox
pkgver=54.4.90
pkgrel=1
pkgdesc="A free service that lets you bring your photos, docs, and videos anywhere and share them easily."
arch=('x86_64')
url="http://www.dropbox.com"
license=('custom')
depends=("libsm" "libxslt" "libxmu" "libxdamage" "libxrender" "libxxf86vm" "libxcomposite" "fontconfig" "dbus")
optdepends=('dolphin-plugins: for integration of dolphin under plasma 5')
options=('!strip' '!upx')
source=("https://clientupdates.dropboxstatic.com/dbx-releng/client/dropbox-lnx.x86_64-$pkgver.tar.gz"        
	"dropbox.png"
        "dropbox.desktop"
        "LICENSE"
        "dropbox.service"
        "dropbox@.service")
sha1sums=('e4a11487ab132ca2efef7cbd2514a853de35b65d'
          '64073af76a2bbc7dba4a6886b82af0bb3e71a2a0'
          'b30c97c01be656f9d392262c3cdeb2c0560e0475'
          'c2b5e46d5e135bc3c6fe89d7c7e464be47cae152'
          'ca22e7fcd94c45854e0b6516efa37ed30df56abf'
          '2950fb561a83d0687f86bb1a02116815f9142f9e')

install="${pkgname}.install"

package() {
	install -d "$pkgdir/opt"
	cp -R "$srcdir"/.dropbox-dist/dropbox-lnx.$arch-$pkgver "$pkgdir"/opt/dropbox
 
	find "$pkgdir/opt/dropbox/" -type f -exec chmod 644 {} \;
	chmod 755 "$pkgdir/opt/dropbox/dropboxd"
	chmod 755 "$pkgdir/opt/dropbox/dropbox"
 
	install -d "$pkgdir/usr/bin"
	ln -s ../../opt/dropbox/dropbox "$pkgdir"/usr/bin/dropbox
 
	install -D -m 644 "$srcdir/dropbox.desktop" "$pkgdir/usr/share/applications/dropbox.desktop"
	install -D -m 644 "$srcdir/dropbox.png" "$pkgdir/usr/share/pixmaps/dropbox.png"
	install -D -m 644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 "$srcdir/dropbox.service" "$pkgdir/usr/lib/systemd/user/dropbox.service"
	install -Dm644 "$srcdir/dropbox@.service" "$pkgdir/usr/lib/systemd/system/dropbox@.service"

	rm -f "$pkgdir"/opt/dropbox/library.zip
	ln -s dropbox "$pkgdir"/opt/dropbox/library.zip
}
