#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=firefox-qt
pkgver=9.0.1
pkgrel=2
pkgdesc="Web browser from mozilla.org, using Qt (experimental)"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('qt' 'mozilla-common' 'nss>=3.13.1' 'libxt' 'hunspell' 'startup-notification'
	 'mime-types' 'dbus-glib' 'alsa-lib' 'sqlite3' 'libnotify'
	 'desktop-file-utils' 'libvpx' 'libevent' 'hicolor-icon-theme' 'firefox-theme-ft-silverglow')
makedepends=('unzip' 'zip' 'python2' 'wireless_tools' 'yasm' 'mesa'
             'autoconf2.13' 'libidl2' 'xorg-server')
provides=("firefox=$pkgver")
conflicts=('firefox')
categories=('network')
url="http://www.mozilla.org/projects/firefox"
install=$pkgname.install
source=("ftp://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2"
	'mozconfig' 'firefox.desktop' 'firefox-install-dir.patch' 'vendor.js')
md5sums=('7cf2bd379792a9b232267c6a79680566'
	 'b28ad1b873a715ed4b9fa8875c51dae5'
	 'bdeb0380c7fae30dd0ead6d2d3bc5873'
	 '594523c536a9be951b52c90dd85aff18'
	 '0d053487907de4376d67d8f499c5502b')

build() {
  cd "$srcdir/mozilla-release"

  cp "$srcdir/mozconfig" .mozconfig
  patch -Np1 -i "$srcdir/firefox-install-dir.patch"

  # Fix PRE_RELEASE_SUFFIX
  sed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"

  LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
  #LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
  LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk MOZ_MAKE_FLAGS="$MAKEFLAGS"
  kill $! || true
}

package() {
  cd "$srcdir/mozilla-release"
  make -j1 -f client.mk DESTDIR="$pkgdir" install

  install -m644 "$srcdir/vendor.js" "$pkgdir/usr/lib/firefox/defaults/pref"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 browser/branding/official/default${i/x*/}.png \
        "$pkgdir/usr/share/icons/hicolor/$i/apps/firefox.png"
  done

  install -Dm644 "$srcdir/firefox.desktop" \
    "$pkgdir/usr/share/applications/firefox.desktop"

  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -sf /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
