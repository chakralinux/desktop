#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=musescore
pkgver=1.1
pkgrel=1
pkgdesc="A music score editor written in Qt4"
arch=('i686' 'x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('qt>=4.5.0' 'openssl' 'libsndfile' 'qtscriptgenerator')
makedepends=('cmake' 'doxygen')
optdepends=('portaudio' 'jack-audio-connection-kit')
conflicts=('musescore-svn' 'mscore')
categories=('multimedia')
source=(http://sourceforge.net/projects/mscore/files/mscore/MuseScore-${pkgver}/mscore-${pkgver}.tar.bz2 'paths.patch')
md5sums=('68b43af92093a16f7f074c0eb560a867'
         'e5011154d217f633f21b4bb70e589510')
         
build() {
  cd $srcdir/mscore-${pkgver}
  export QT_PLUGINS_DIR=/usr/lib/qt/plugins
  [ -d build ] && make clean
  patch -p1 < $srcdir/paths.patch
  sed -i 's+${Mscore_INSTALL_NAME}man+doc/mscore+' \
    "$srcdir"/mscore-$pkgver/mscore/manual/CMakeLists.txt
  make PREFIX=/usr release
}
package() {
  cd $srcdir/mscore-${pkgver}
  make DESTDIR="$pkgdir" install
  # avoid file conflict with "qtscriptgenerator" 
  for _i in gui network core uitools xml
  do 
    rm $pkgdir/usr/lib/qt/plugins/script/libqtscript_${_i}.so
  done
}
