pkgname=musescore
_pkgname=MuseScore
pkgver=2.3
pkgrel=1
pkgdesc="A music score editor written in Qt"
arch=('x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('libpulse' 'portaudio' 'qt5-quickcontrols' 'qt5-svg' 'qt5-tools' 'qt5-webkit' 'portmidi' )
makedepends=('cmake' 'doxygen' 'lame' 'qt5-script' 'texlive-core')
optdepends=('lame: MP3 export')
categories=('multimedia')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$_pkgname/archive/v$pkgver.tar.gz"
        'musescore.conf')
sha256sums=('28dee6854585763fc3b89744ad69afe22f9073d8918836bfceb8be8d9bb1b7c5'
            '397b5431137d468a64b768670efdf385c27cb7298ee9295a32323a2610427928')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_SYSTEM_FREETYPE=ON

  make lrelease
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
