#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=musescore
_pkgname=mscore
pkgver=1.3
pkgrel=1
pkgdesc="A music score editor written in Qt"
arch=('x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('qtwebkit' 'libsndfile' 'qtscriptgenerator' 'portaudio')
makedepends=('cmake' 'doxygen')
optdepends=('jack')
categories=('multimedia')
install=musescore.install
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"
        'paths.patch')
md5sums=('e5fde2bef0d40ee7542e1f515a3919d1'
         'a0419cbe08b655820d52ea440653b1d3')

build() {
  cd $srcdir/${_pkgname}-${pkgver}
  export QT_PLUGINS_DIR=/usr/lib/qt/plugins 
  [ -d build ] && make clean
  patch -p1 < $srcdir/paths.patch

  make PREFIX=/usr release 
}
package() {
  cd $srcdir/${_pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
# avoid file conflict with "qtscriptgenerator" 
  for _i in gui network core uitools xml
  do 
    rm $pkgdir/usr/lib/qt/plugins/script/libqtscript_${_i}.so
  done
}
