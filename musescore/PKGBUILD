pkgname=musescore
_pkgname=MuseScore
pkgver=2.1.0
pkgrel=1
pkgdesc="A music score editor written in Qt"
arch=('x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('libpulse' 'portaudio' 'qt5-quickcontrols' 'qt5-svg' 'qt5-tools' 'qt5-webkit')
makedepends=('cmake' 'doxygen' 'lame' 'qt5-script' 'texlive-core')
optdepends=('lame: MP3 export')
categories=('multimedia')
install=musescore.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$_pkgname/archive/v$pkgver.tar.gz"
        'musescore.conf')
sha256sums=('0581b8dd4e9bef51f863baf5b7f03b518f9784c79c0d92d6f0e33b180dd63c47'
            '397b5431137d468a64b768670efdf385c27cb7298ee9295a32323a2610427928')

build() {
  cd $_pkgname-$pkgver

  make PREFIX='/usr' release
}

package() {
  cd $_pkgname-$pkgver/build.release

  make DESTDIR=$pkgdir install
  install -Dm 644 ../assets/musescore-icon-round-512.png $pkgdir/usr/share/pixmaps/mscore.png
}
