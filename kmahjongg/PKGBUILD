# Include global configuration

pkgname=kmahjongg
pkgver=18.07.80
pkgrel=1
pkgdesc="Mahjongg solitaire"
arch=('x86_64')
url='http://kde.org/applications/games/kmahjongg/'
license=('GPL' 'LGPL' 'FDL')
groups=(kde kde-applications kdegames kde-uninstall)
depends=(libkdegames libkmahjongg)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegames-kmahjongg)
replaces=(kdegames-kmahjongg)
options=('docs' 'debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8e8c8391dae4402ecf528217f2890cfddfe4fd80096713b0ed3ed1cf6cb7ed74'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
