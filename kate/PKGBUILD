#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kate
source ../kdeapps.conf

pkgbase=kate
pkgname=('kwrite'
         'kate')
pkgver=${_kdever}
pkgrel=1
pkgdesc='Advanced editor component which is used in numerous KDE applications requiring a text editing component'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('knewstuff' 'ktexteditor' 'threadweaver' 'kded' 'kitemmodels')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'plasma-framework')
optdepends=('kio-extras: extra protocols support (sftp, fish and more)')
options=('docs' 'debug')
source=("$_mirror/${pkgbase}-$_kdever.tar.xz"
        'fix_close_and_dbus.patch')
sha256sums=("`grep ${pkgbase}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`"
            'dc2a82037145b6ded90c5e3fa63fef4bc95c597a8bcb5a00e5200dce61a281a8')

prepare() {
  mkdir build
  
  cd ${pkgbase}-${pkgver}
  patch -Np1 -i ${srcdir}/fix_close_and_dbus.patch
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver} 
  make
}

package_kwrite() {
  pkgdesc="Text Editor"
  groups=('kde')
  url="http://www.kde.org/applications/utilities/kwrite/"
  screenshot="https://www.kde.org/images/screenshots/kwrite.png"
  install='kwrite.install'
  replaces=('kde-baseapps-kwrite')
  conflicts=('kde-baseapps-kwrite')
  provides=('kde-baseapps-kwrite')

  cd build/kwrite
  make DESTDIR="${pkgdir}" install

  cd ../../build/doc/kwrite
  make DESTDIR="${pkgdir}" install
}

package_kate() {
  pkgdesc="Advanced Text Editor"
  groups=('kde')
  url="http://www.kde.org/applications/utilities/kate/"
  screenshot="https://www.kde.org/images/screenshots/kate.png"
  install='kate.install'
  replaces=('kdesdk-kate')
  conflicts=('kdesdk-kate')
  provides=('kdesdk-kate')
  optdepends=('konsole: open a terminal in Kate')

  cd build/kate
  make DESTDIR="${pkgdir}" install

  cd ../../build/doc/kate
  make DESTDIR="${pkgdir}" install

  cd ../../../build/addons
  make DESTDIR="${pkgdir}" install
}
