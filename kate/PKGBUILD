#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kate

pkgbase=kate
pkgname=('kwrite'
         'kate')
pkgver=18.04.3
pkgrel=1
pkgdesc='Advanced editor component which is used in numerous KDE applications requiring a text editing component'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('knewstuff' 'ktexteditor' 'threadweaver' 'kded' 'kitemmodels')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'plasma-framework')
optdepends=('kio-extras: extra protocols support (sftp, fish and more)')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgbase-$pkgver.tar.xz"{,.sig})
sha256sums=('e4ac4587881ca4a99ea972ffffd9015ea00a0d14fd1bcb129c1d22b930b02bf0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package_kwrite() {
  pkgdesc="Text Editor"
  groups=('kde')
  url="http://www.kde.org/applications/utilities/kwrite/"
  screenshot="https://www.kde.org/images/screenshots/kwrite.png"
  replaces=('kde-baseapps-kwrite')
  conflicts=('kde-baseapps-kwrite')
  provides=('kde-baseapps-kwrite')

  cd build/kwrite
  make DESTDIR="${pkgdir}" install

  cd ../../build/doc/kwrite
  make DESTDIR="${pkgdir}" install
}

package_kate() {
  pkgdesc="Advanced Text Editor"
  groups=('kde')
  url="http://www.kde.org/applications/utilities/kate/"
  screenshot="https://www.kde.org/images/screenshots/kate.png"
  replaces=('kdesdk-kate')
  conflicts=('kdesdk-kate')
  provides=('kdesdk-kate')
  optdepends=('konsole: open a terminal in Kate')

  cd build/kate
  make DESTDIR="${pkgdir}" install

  cd ../../build/doc/kate
  make DESTDIR="${pkgdir}" install

  cd ../../../build/addons
  make DESTDIR="${pkgdir}" install
}
