# Contributor: Oliver Goethel <deezy>

pkgname=med
pkgver=3.3.1
pkgrel=1
pkgdesc="MED stands for Modelisation et Echanges de Donnees, i.e. Data Modelization and Exchanges - MED is code-aster exchange module linked to hdf5"
url="https://www.salome-platform.org/downloads"
license=('LGPL')
depends=('hdf5' 'openmpi' 'swig')
makedepends=('gcc-fortran' 'coreutils' 'python2')
optdepends=('tk')
provides=()
conflicts=('med_fichier')
replaces=('med_fichier')
backup=()
arch=('x86_64')
source=("http://files.salome-platform.org/Salome/other/${pkgname}-${pkgver}.tar.gz"
        "hdf5-1.10-support.patch")
md5sums=('4981a02fa50b1a487a022804512631ff'
         '8fe2bc9bc5e6756351e1ff80e613dba3')

prepare() {
  cd "$pkgname-$pkgver"_SRC 
  patch -p1 -i ../hdf5-1.10-support.patch
  autoreconf -i
}

build() {
  export FFLAGS="-fopenmp -fPIC -fdefault-double-8 -fdefault-integer-8 -fdefault-real-8 -ffixed-line-length-0 ${CFLAGS}"
  export FCFLAGS="-fopenmp -fPIC -fdefault-double-8 -fdefault-integer-8 -fdefault-real-8 -ffixed-line-length-0 ${CFLAGS}"
  export CPPFLAGS="-DHAVE_F77INT64 ${CPPFLAGS}"
  export F77=mpif90
  export FC=mpif90
  
  export PYTHON="$(which python2)"

  cd "${pkgname}-${pkgver}"_SRC 

  ./configure --with-f90=mpif90 --prefix=/usr --datadir=/usr/share/med --with-swig=yes
  make
}
 
package() {
  cd "${pkgname}-${pkgver}"_SRC 
 
  make DESTDIR=${pkgdir} install

}

