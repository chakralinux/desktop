# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: SaultDon <sault.don gmail>
#               Lantald < lantald at gmx.com >
#               Thomas Dziedzic < gostrc at gmail >
#               dibblethewrecker dibblethewrecker.at.jiwe.dot.org
#               Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
#               Eric Forgeot < http://esclinux.tk >

pkgname=qgis
pkgver=2.6.0
pkgrel=1
pkgdesc='A Geographic Information System (GIS) that supports vector, raster and database formats'
url='http://qgis.org/'
license=('GPL')
arch=('x86_64')
depends=('bison' 
         'cfitsio'
         'curl'
         'flex'
         'gdal'
         'giflib'
         'jasper'
         'libmariadbclient'
         'libspatialite'
         'postgresql-libs'
         'pyqt4-python2'
         'python2'
         'qscintilla-python2'
         'python2-psycopg2'
         'qt'
         'qwt'
         'spatialindex'
         'sqlite3')
makedepends=('cmake'
             'fcgi'
             'grass'
             'gsl'
             'netcdf'
             'postgis'
             'python2-sip')
optdepends=('fcgi: qgis mapserver'
            'grass: grass plugin'
            'gsl: georeferencer'
            'python2-sip: python-support'
            'postgis: postgis support and SPIT plugin')
source=("http://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('8444ecb2d9d454b014e8631e152cba65ea7a6b50e4ddae78b58f115f60017748e33e2c27f15b5ee0cf9f5b99caefe9d24b81f5ab18dcdf1b902710d5d6d2bd7c')

prepare() {
    rm -rf ${srcdir}/${pkgname}-${pkgver}/build
    mkdir ${srcdir}/${pkgname}-${pkgver}/build
}

build() {
    #cd "${pkgname}-${pkgver}"
    #patch -Np1 -i "${srcdir}/rev-349a4c97.diff"
    #patch -Np1 -i "${srcdir}/QgsFieldValidator.diff"
    
    cd ${srcdir}/${pkgname}-${pkgver}/build
        #-DCMAKE_SKIP_RPATH=ON \
        #-DENABLE_TESTS=OFF \
        #-DQT_QMAKE_EXECUTABLE=qmake \
        #-DQWTPOLAR_INCLUDE_DIR=/usr/include/qwt \
        #-DQWTPOLAR_LIBRARY=/usr/lib/libqwtpolar.so \
        #-DQWT_INCLUDE_DIR=/usr/include/qwt \
        #-DQWT_LIBRARY=/usr/lib/libqwt.so \
        #-DWITH_INTERNAL_QWTPOLAR=FALSE \
        #-DWITH_MAPSERVER=TRUE
    cmake ../ \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DGRASS_PREFIX=/opt/grass \
        -DQGIS_MANUAL_SUBDIR=share/man \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_SITE_PACKAGES_DIR=/usr/lib/python2.7/site-packages \
        -DPYTHON_INCLUDE_PATH=/usr/include/python2.7
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}/build
    make DESTDIR=${pkgdir} install

    # create a more user-friendly application name link
    ln -s /usr/bin/qgis "${pkgdir}/usr/bin/quantum-gis"

    # install some freedesktop.org compatibility
    install -D -m644 "${srcdir}/${pkgname}-${pkgver}/debian/qgis.desktop" \
    "${pkgdir}/usr/share/applications/qgis.desktop"
    install -D -m644 "${srcdir}/${pkgname}-${pkgver}/debian/qbrowser.desktop" \
    "${pkgdir}/usr/share/applications/qbrowser.desktop"
    install -dm755 "${pkgdir}/usr/share/pixmaps" \
    "${pkgdir}/usr/share/mimelnk/application"
    for mime in "${srcdir}/${pkgname}-${pkgver}/debian/mime/application/"*.desktop
        do install -m644 "${mime}" \
            "${pkgdir}/usr/share/mimelnk/application"
        done
    ln -s /usr/share/qgis/images/icons/qgis-icon.png "${pkgdir}/usr/share/pixmaps/qgis-icon.png"
    ln -s /usr/share/qgis/images/icons/qgis-mime-icon.png "${pkgdir}/usr/share/pixmaps/qgis-mime-icon.png"

    # enable qgis for python by setting PYTHONPATH
    #install -Dm644 "${srcdir}/qgis.sh" \
    #"${pkgdir}/etc/profile.d/qgis.sh"
}
