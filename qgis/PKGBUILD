pkgname=qgis
pkgver=2.14.0
pkgrel=1
pkgdesc='A Geographic Information System (GIS) that supports vector, raster and database formats'
url='http://qgis.org/'
license=('GPL')
arch=('x86_64')
depends=('bison' 'cfitsio' 'curl' 'flex' 'gdal' 'jasper' 'libmariadbclient' 'libspatialite' 'libxslt' 
         'postgresql-libs' 'pyqt4-python2' 'python2' 'qscintilla-python2' 'python2-psycopg2'
         'qca' 'qt' 'qwt6' 'spatialindex' 'sqlite3')
makedepends=('cmake' 'fcgi' 'grass' 'gsl' 'netcdf' 'postgis' 'python2-sip')
optdepends=('fcgi: qgis mapserver'
            'grass: grass plugin'
            'gsl: georeferencer'
            'python2-sip: python-support'
            'postgis: postgis support and SPIT plugin')
source=("http://qgis.org/downloads/$pkgname-$pkgver.tar.bz2")
sha1sums=('9257ada87b63b1517edfb27ef9d4b6e55544cda0')


prepare() {
  cd $pkgname-$pkgver

  # Fix references to "python"
  sed -i 's/\(env \|\/usr\/bin\/\)python$/&2/' $(find . -iname "*.py")
  sed -i 's/python /python2 /' scripts/pyuic4-wrapper.sh

  # Remove mime types already defined by freedesktop.org
  sed -e '/type="image\/tiff"/,/<\/mime-type>/d' \
      -e '/type="image\/jpeg"/,/<\/mime-type>/d' \
      -e '/type="image\/jp2"/,/<\/mime-type>/d' \
      -e '/type="application\/x-adobe-mif"/,/<\/mime-type>/d' \
      -i debian/qgis.xml

  # Fix console.py for new pyqt build system
  sed -e '/from PyQt4.QtCore/ s/$/, QT_VERSION/' \
      -e '/import pyqtconfig/d' \
      -e 's/pyqtconfig.*qt_version/QT_VERSION/' \
      -i python/console/console.py

  [[ -d build ]] || mkdir build
}

build() {
  cd $pkgname-$pkgver/build

  cmake -G "Unix Makefiles" ../ \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_GRASS=OFF \
        -DGRASS_PREFIX=/opt/grass \
        -DGRASS_INCLUDE_DIR=/opt/grass/include/ \
        -DQGIS_MANUAL_SUBDIR=share/man \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DENABLE_TESTS=FALSE \
        -DCMAKE_SKIP_RPATH=TRUE
  make
}

package() {
    cd $pkgname-$pkgver/build
    make DESTDIR=$pkgdir install

    # create a more user-friendly application name link
    ln -s /usr/bin/qgis "$pkgdir/usr/bin/quantum-gis"

    # install some freedesktop.org compatibility
    install -D -m644 ../debian/qgis.desktop \
    "$pkgdir/usr/share/applications/qgis.desktop"
    install -D -m644 ../debian/qbrowser.desktop \
    "$pkgdir/usr/share/applications/qbrowser.desktop"
    install -dm755 "$pkgdir/usr/share/pixmaps" \
    "$pkgdir/usr/share/mimelnk/application"
    for mime in ../debian/mime/application/*.desktop
        do install -m644 "$mime" \
            "$pkgdir/usr/share/mimelnk/application"
        done
    ln -s /usr/share/qgis/images/icons/qgis-icon.png "$pkgdir/usr/share/pixmaps/qgis-icon.png"
    ln -s /usr/share/qgis/images/icons/qgis-mime-icon.png "$pkgdir/usr/share/pixmaps/qgis-mime-icon.png"
}
