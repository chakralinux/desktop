#
# Chakra Packages for Chakra, part of chakra-project.org
#

pkgname=qgis
pkgver=1.8.0
pkgrel=1
pkgdesc='A Geographic Information System (GIS) that supports vector, raster & database formats.'
url='http://qgis.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libmysqlclient' 'postgresql-libs' 'sqlite3' 'jasper' 'curl' 'qt' 'python2' 'pyqt' 'giflib' 'xerces-c' 'cfitsio' 'qwt' 'gdal' 'flex' 'bison' 'graphviz' 'libspatialite' 'spatialindex')
makedepends=('grass' 'gsl' 'postgis' 'netcdf' 'cmake' 'fcgi' 'sip')
optdepends=('postgis: postgis support and SPIT plugin'
            'fcgi: qgis mapserver'
            'sip: python-support'
            'grass: grass plugin'
            'gsl: georeferencer  ')
# options=('!makeflags') not needed anymore?
categories=('education')
source=("http://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2"
        'qgis.desktop')
md5sums=('1d60520f81d7763c026d0af887ac9a05'
         '8ab66039f2aba519b92f52272ec3c13e')

build() {
	cd ${srcdir}
	rm -rf build
	mkdir build
	cd build

	# -DCMAKE_SKIP_RPATH=ON commenting that out should fix compilation
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DGRASS_PREFIX=/opt/grass \
		-DQGIS_MANUAL_SUBDIR=share/man \
		-DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
		-DPYTHON_EXECUTABLE=/usr/bin/python2 \
		-DPYTHON_SITE_PACKAGES_DIR=/usr/lib/python2.7/site-packages \
		-DPYTHON_INCLUDE_PATH=/usr/include/python2.7/

	make
}

package() {
	cd build
	make DESTDIR=${pkgdir} install
	# install some freedesktop.org compatibility
	install -D -m644 ${srcdir}/${pkgname}.desktop \
	${pkgdir}/usr/share/applications/${pkgname}.desktop
}
