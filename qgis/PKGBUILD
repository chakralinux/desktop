pkgname=qgis
pkgver=2.16.3
pkgrel=2
pkgdesc='A Geographic Information System (GIS) that supports vector, raster and database formats'
url='http://qgis.org/'
license=('GPL')
arch=('x86_64')
depends=('bison' 'cfitsio' 'curl' 'flex' 'gdal' 'jasper' 'libmariadbclient' 'libspatialite' 'python2-pyspatialite' 'libxslt' 
         'postgresql-libs' 'pyqt4-python2' 'python2' 'qscintilla-python2' 'python2-psycopg2'
         'qca' 'qt' 'qwt6' 'spatialindex' 'sqlite3')
makedepends=('cmake' 'fcgi' 'grass' 'gsl' 'netcdf' 'postgis' 'python2-sip')
optdepends=('fcgi: qgis mapserver'
            'grass: grass plugin'
            'gsl: georeferencer'
            'python2-sip: python-support'
            'postgis: postgis support and SPIT plugin')
source=("http://qgis.org/downloads/$pkgname-$pkgver.tar.bz2")
sha1sums=('06feb38b462c5c0eafb305b349d78e4254796c9d')


prepare() {
  cd $pkgname-$pkgver
  [[ -d build ]] || mkdir build
}

build() {
  cd $pkgname-$pkgver/build
  cmake -G "Unix Makefiles" ../ \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_GRASS=OFF \
        -DGRASS_PREFIX=/opt/grass \
        -DGRASS_INCLUDE_DIR=/opt/grass/include/ \
        -DQGIS_MANUAL_SUBDIR=share/man \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DENABLE_TESTS=FALSE \
        -DCMAKE_SKIP_RPATH=TRUE
  make
}

package() {
    cd $pkgname-$pkgver/build
    make DESTDIR=$pkgdir install

    # create a more user-friendly application name link
    ln -s /usr/bin/qgis "$pkgdir/usr/bin/quantum-gis"

    # install some freedesktop.org compatibility
    install -Dm644 ../debian/qgis.desktop "$pkgdir/usr/share/applications/qgis.desktop"
    install -Dm644 ../debian/qbrowser.desktop "$pkgdir/usr/share/applications/qbrowser.desktop"
    install -dm755 "$pkgdir/usr/share/pixmaps" "$pkgdir/usr/share/mimelnk/application"
    for mime in ../debian/mime/application/*.desktop
        do install -m644 "$mime" \
            "$pkgdir/usr/share/mimelnk/application"
        done
    ln -s /usr/share/qgis/images/icons/qgis-icon.png "$pkgdir/usr/share/pixmaps/qgis-icon.png"
    ln -s /usr/share/qgis/images/icons/qgis-mime-icon.png "$pkgdir/usr/share/pixmaps/qgis-mime-icon.png"
}
