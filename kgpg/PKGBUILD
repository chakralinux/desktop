# Include global configuration

pkgname=kgpg
pkgver=18.07.80
pkgrel=1
pkgdesc='A GnuPG frontend'
url='http://kde.org/applications/utilities/kgpg/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(akonadi-contacts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python3 boost)
conflicts=(kdeutils-kgpg)
replaces=(kdeutils-kgpg)
options=(docs debug)
groups=(kde kde-uninstall kde-applications kdeutils)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('59207df2d8fd04e5b3dcdd938ffe833e9771af5d11720c73c88cc033c4da6dfa'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
