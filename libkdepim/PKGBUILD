# Include global configuration

pkgname=libkdepim
pkgver=18.08.2
pkgrel=1
pkgdesc="Lib for common kdepim apps."
arch=('x86_64')
url='https://projects.kde.org/projects/kde/pim/libkdepim'
license=('LGPL')
depends=(akonadi-contacts akonadi-search kldap kcmutils)
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools')
groups=(kde kde-applications kdepim kde-uninstall)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b297c762f3b8dae50be804aa6f70ae26f3c60b269ae4f4bb37583154e5075213'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
