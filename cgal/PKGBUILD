# Arch contribution https://www.archlinux.org/packages/community/x86_64/cgal/

pkgname=cgal
pkgver=4.10
pkgrel=1
pkgdesc="Computational Geometry Algorithms Library"
arch=('x86_64')
url="http://www.cgal.org"
license=('GPL' 'LGPL')
source=("https://github.com/CGAL/cgal/releases/download/releases%2FCGAL-$pkgver/CGAL-$pkgver.tar.xz")
depends=('mpfr' 'gmp' 'mesa' 'glu' 'boost-libs')
optdepends=('qt5-svg: for CGAL_Qt5'
            'eigen: for some packages, see the CGAL manual')
makedepends=('cmake' 'qt5-svg' 'eigen' 'boost')
md5sums=('0a60a756b6885580945122ee178dd8db')

# consider building with swig for python stuff
# https://gforge.inria.fr/frs/?group_id=52  for _pkgid

build() {
  cd "$srcdir/CGAL-$pkgver"
  mkdir build
  cd build
  cmake ../ -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "$srcdir/CGAL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  # The tarball still has all these licenses included
  for _license in LICENSE{,.FREE_USE,.GPL,.LGPL}; do
    install -D -m644 "$srcdir/CGAL-$pkgver/$_license" "$pkgdir/usr/share/licenses/$pkgname/$_license"
  done
}
