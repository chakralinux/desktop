# Arch contribution https://www.archlinux.org/packages/community/x86_64/cgal/

pkgname=cgal
pkgver=4.8
pkgrel=1
_pkgid=35139
pkgdesc="Computational Geometry Algorithms Library"
arch=('x86_64')
url="http://www.cgal.org"
license=('GPL' 'LGPL')
source=(https://github.com/CGAL/cgal/archive/releases/CGAL-$pkgver.tar.gz)
depends=('mpfr' 'boost-libs' 'gmp' 'mesa' 'glu')
optdepends=('qt: for CGAL_Qt4'
            'eigen3: for some packages, see the CGAL manual')
makedepends=('cmake' 'qt' 'eigen3' 'boost')
sha256sums=('98f9eafbf7faffc48d0a5c3a1217d8d8e80bb0a753c76d1634236ea033722e36')

# consider building with swig for python stuff
# https://gforge.inria.fr/frs/?group_id=52  for _pkgid

build() {
  cd "$srcdir/cgal-releases-CGAL-$pkgver"
  mkdir build
  cd build
  cmake ../ -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/cgal-releases-CGAL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  # The tarball still has all these licenses included
  cd ..
  install -D -m644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
