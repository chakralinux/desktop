# Include global configuration

pkgname="kcharselect"
arch=('x86_64')
pkgver=18.08.0
pkgrel=1
pkgdesc="Character selector"
url="http://kde.org/applications/utilities/kcharselect/"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
depends=('kbookmarks')
makedepends=('extra-cmake-modules' 'python3' 'kdoctools')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeutils')
conflicts=("kdeutils-kcharselect")
replaces=("kdeutils-kcharselect")
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('589922cbaefcdece28dbfde3e740a6e2862386d316e2723865ad3655e6ffdfbd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
