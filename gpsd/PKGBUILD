pkgname=gpsd
pkgver=3.18
pkgrel=2
pkgdesc="GPS daemon and library to support USB/serial GPS devices"
arch=('x86_64')
url="http://catb.org/gpsd/"
license=('BSD')
depends=('python3' 'libusb' 'bluez-libs')
optdepends=('php: generate a PHP status page for your GPS'
            'php-gd: image support for the PHP status page'
            'qt5-base: for Qgpsmm')
makedepends=('scons' 'docbook-xsl' 'qt5-base')
backup=('etc/default/gpsd')
source=("http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig}
        $pkgname.conf)
sha256sums=('48521f5158f5fda4c88a6f75e8bfc1ee67e999e3fc095e4a06cb8c2af56712f4'
            'SKIP'
            'dcfa71c4c730b486269995468bdcfa8e96bf3c282db1dadea9384d32015d277f')
validpgpkeys=('EED4A0893DCC705DB309E202CCF29C7238522905') # Gary E. Miller <gem@rellim.com>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export LINKFLAGS="${LDFLAGS}"
  scons prefix=/usr \
        systemd=yes \
        gpsd_group=uucp \
        sbindir=/usr/bin \
        qt_versioned=5
  scons build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix man pages path (Arch's FS#21715)
  sed -i 's|.so gps.1|.so man1/gps.1|' cgps.1 lcdgps.1 xgps.1 xgpsspeed.1
  # Fix path in systemd files
  sed -i 's|local/sbin|bin|' systemd/{gpsd.service,gpsdctl@.service}

  export DESTDIR="${pkgdir}"
  scons install
  install -Dm755 gpsinit -t "${pkgdir}"/usr/bin
  chmod 755 "${pkgdir}"/usr/bin/*

  install -Dm644 "$srcdir/gpsd.conf" "${pkgdir}"/etc/default/gpsd

  install -Dm644 "gpsd.rules" "${pkgdir}"/usr/lib/udev/rules.d/25-gpsd-usb.rules

  install -Dm755 gpsd.hotplug "${pkgdir}"/usr/lib/udev/gpsd.hotplug

  install -Dm644 packaging/X11/xgps.desktop \
    "${pkgdir}"/usr/share/applications/xgps.desktop
  install -Dm644 packaging/X11/xgpsspeed.desktop \
    "${pkgdir}"/usr/share/applications/xgpsspeed.desktop
  install -Dm644 packaging/X11/gpsd-logo.png \
    "${pkgdir}"/usr/share/gpsd/gpsd-logo.png

  install -Dm644 systemd/gpsd.service "${pkgdir}"/usr/lib/systemd/system/gpsd.service
  install -Dm644 systemd/gpsd.socket "${pkgdir}"/usr/lib/systemd/system/gpsd.socket
  install -Dm644 systemd/gpsdctl@.service "${pkgdir}"/usr/lib/systemd/system/gpsdctl@.service

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
