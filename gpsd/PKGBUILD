# Maintainer Bruce Liu <brli@users.sourceforge.net>

pkgname=gpsd
pkgver=3.16
pkgrel=2
pkgdesc="GPS daemon and library to support USB/serial GPS devices"
arch=('x86_64')
url="http://catb.org/gpsd/"
screenshot="http://www.catb.org/gpsd/gpsd2.png"
license=('BSD')
depends=('python2' 'libusb' 'bluez-libs' 'desktop-file-utils')
optdepends=('php: generate a PHP status page for your GPS'
            'php-gd: image support for the PHP status page')
makedepends=('scons' 'docbook-xsl' 'hardening-wrapper')
backup=('etc/conf.d/gpsd')
install="${pkgname}.install"
source=("http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha1sums=('f211a45eb6e8d067df2d02f7d9347e3977296ed9'
          'SKIP')
validpgpkeys=('41876B2F579463A499843D1DECC8208F8C6C738D') # Eric S. Raymond (Also known as 'ESR'.)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  scons prefix=/usr \
    systemd=yes \
    libQgpsmm=no
  scons build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg 'Fix man pages path '
  sed -i 's|.so gps.1|.so man1/gps.1|' cgps.1 lcdgps.1 xgps.1 xgpsspeed.1
  msg 'Installation'
  export DESTDIR="${pkgdir}"
  scons install

  sed -i 's|/lib/udev/gpsd|/usr/lib/udev/gpsd|' gpsd.rules

  install -Dm644 "gpsd.rules" "${pkgdir}/usr/lib/udev/rules.d/99-gpsd-usb.rules"

  sed -i 's|/etc/default/gpsd|/etc/conf.d/gpsd|' gpsd.hotplug
  install -Dm755 gpsd.hotplug "${pkgdir}/usr/lib/udev/gpsd.hotplug"

  install -D -m644 packaging/X11/xgps.desktop \
    "${pkgdir}/usr/share/applications/xgps.desktop"
  install -D -m644 packaging/X11/xgpsspeed.desktop \
    "${pkgdir}/usr/share/applications/xgpsspeed.desktop"
  install -D -m644 packaging/X11/gpsd-logo.png \
    "${pkgdir}/usr/share/gpsd/gpsd-logo.png"

  install -D -m644 systemd/gpsd.service "${pkgdir}/usr/lib/systemd/system/gpsd.service"
  install -D -m644 systemd/gpsd.socket "${pkgdir}/usr/lib/systemd/system/gpsd.socket"

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
