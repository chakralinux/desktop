pkgname=argon2
pkgver=20171227
pkgrel=1
pkgdesc="A password-hashing function (reference C implementation)"
arch=("x86_64" "i686")
url="https://github.com/P-H-C/phc-winner-argon2"
license=('Apache' 'custom:CC0')
depends=("glibc")
install="$pkgname.install"
source=("https://github.com/P-H-C/phc-winner-argon2/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "libargon2.pc")
sha256sums=('eaea0172c1f4ee4550d1b6c9ce01aab8d1ab66b4207776aa67991eb5872fdcd8'
            '4189ce6c183319ceaf2c4ec96facd424deeaa9e2819efb7511893bcf3ef5c836')

prepare() {
  cd phc-winner-$pkgname-$pkgver
  cp ../libargon2.pc libargon2.pc
  sed -i -e "s/@UPSTREAM_VER@/${pkgver}/" libargon2.pc
}

build() {
  cd phc-winner-$pkgname-$pkgver
  make OPTTARGET=none
}

check() {
  cd phc-winner-$pkgname-$pkgver
  make OPTTARGET=none test
}

package() {
  cd phc-winner-$pkgname-$pkgver
  make OPTTARGET=none DESTDIR=$pkgdir install
  install -D -m0644 LICENSE $pkgdir/usr/share/licenses/argon2/LICENSE
  install -Dm644 libargon2.pc $pkgdir/usr/lib/pkgconfig/libargon2.pc
} 
