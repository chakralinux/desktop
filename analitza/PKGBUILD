# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/analitza

pkgname=analitza
pkgver=18.12.0
pkgrel=1
pkgdesc="Shared math libraries from Kalgebra"
url="https://projects.kde.org/projects/kde/kdeedu/analitza"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('qt5-svg' 'qt5-declarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'eigen3' 'qt5-tools')
provides=('kdeedu-analitza')
replaces=('kdeedu-analitza')
conflicts=('kdeedu-analitza')
groups=("kde" "kdeedu" "kde-uninstall")
categories=('education')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2e3c78589e7d6054c2e98445b502c68c877f208a122b953506ec09cc92f9113d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
