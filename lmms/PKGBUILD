#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer abveritas@chakra-project.org
#

pkgname=lmms
pkgver=0.4.13
pkgrel=2
pkgdesc='The Linux MultiMedia Studio.'
url='http://lmms.sourceforge.net'
arch=('x86_64')
license=('GPL')
depends=('glib2' 'qt' 'sdl_sound' 'jack' 'libsamplerate' 'fluidsynth' 'libpng'
         'libvorbis' 'libxft' 'libxinerama' 'shared-mime-info')
optdepends=('wine: VST support (experimental)'
            'fftw: SpectrumAnalyzer plugin' )
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2' 'fftw')
categories=('multimedia')
install="lmms.install"
source=("http://downloads.sourceforge.net/sourceforge/lmms/${pkgname}-${pkgver}.tar.bz2"
        'zynaddsubfx_unlink.patch')
md5sums=('80db0dc5263041d443f474220410991f'
         'd2ea78ac0cf2979505f140c6845617cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/zynaddsubfx_unlink.patch

  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
