pkgname=lmms
pkgver=1.1.3
pkgrel=2
pkgdesc='The Linux MultiMedia Studio.'
url='http://lmms.sourceforge.net'
arch=('x86_64')
license=('GPL')
depends=('glib2' 'qt' 'sdl_sound' 'jack' 'libsamplerate' 'fluidsynth' 'libpng'
         'libvorbis' 'libxft' 'libxinerama' 'shared-mime-info' 'fltk')
optdepends=('wine: VST support (experimental)'
            'fftw: SpectrumAnalyzer plugin'
            'pulseaudio: PulseAudio output' )
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2' 'stk' 'fftw' 'gcc-multilib')
categories=('multimedia')
install="lmms.install"
source=("https://github.com/LMMS/${pkgname}/archive/v${pkgver}.tar.gz"
	"make-werrror-optional.patch")
md5sums=('88d9e66d240b711c37315e3c9da644a1'
         '2c6e51b665c34fa61dc8b23e129404a9')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake

  patch -Np1 -i ../make-werrror-optional.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DWANT_VST_NOWINE=ON
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
