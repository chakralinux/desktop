#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>
#

pkgname=lmms
pkgver=0.4.13
pkgrel=1
pkgdesc='The Linux MultiMedia Studio.'
url='http://lmms.sourceforge.net'
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'qt' 'sdl_sound' 'jack' 'libsamplerate' 'fluidsynth' 'libpng' 'libvorbis' 'libxft' 'libxinerama' 'shared-mime-info')
optdepends=('wine: VST support (experimental)'
            'fftw: SpectrumAnalyzer plugin' )
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2' 'fftw')
categories=('multimedia')
install="lmms.install"
source=("http://downloads.sourceforge.net/sourceforge/lmms/${pkgname}-${pkgver}.tar.bz2")
md5sums=('80db0dc5263041d443f474220410991f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
