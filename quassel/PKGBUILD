pkgname=quassel
pkgver=0.13.0
pkgrel=1
pkgdesc="Next-generation distributed IRC client"
arch=('x86_64')
url="http://quassel-irc.org"
license=('GPL')
depends=('qt5-base' 'knotifyconfig' 'qt5-webkit' 'qt5-script' 'qca-qt5' 'openssl')
makedepends=('cmake' 'extra-cmake-modules' 'qt5-tools' 'python3')
replaces=('quassel-qt4')
categories=('communication')
screenshot=('http://quassel-irc.org/files/images/snapshot13.preview.png')
source=("https://quassel-irc.org/pub/quassel-${pkgver}.tar.bz2"
	'quassel.service'
        'quassel.conf')
install=quassel.install
backup=(etc/conf.d/quassel.conf)
sha1sums=('e53400ad93b94ded5f04c74e21df72ec576795df'
          '40b801ad664e9e0f0d5f1260f7545b6842dd11e8'
          '575d09f165f12ba35e560be54c8e1cd92321bb4b')
         
build() {
  [[ ! -d build ]] && mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DUSE_QT5=ON \
    -DWITH_KDE=ON \
    -DHAVE_SSL=TRUE \
    -DCMAKE_BUILD_TYPE="Release" \
    ../${pkgname}-${pkgver}/ \
    -Wno-dev

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/$pkgname.service ${pkgdir}/usr/lib/systemd/system/$pkgname.service
  install -D -m644 ${srcdir}/$pkgname.conf ${pkgdir}/etc/conf.d/$pkgname
}
