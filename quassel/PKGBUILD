#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=quassel
pkgver=0.8.0
pkgrel=2
pkgdesc="QT4 irc client with a separated core"
url="http://quassel-irc.org"
arch=('x86_64')
license=('GPL')
depends=('kdelibs' 'oxygen-icons' 'hicolor-icon-theme' 'libindicate-qt')
makedepends=('cmake' 'automoc4')
categories=('communication')
screenshot=('http://quassel-irc.org/files/images/snapshot13.preview.png')
source=("http://quassel-irc.org/pub/quassel-${pkgver}.tar.bz2"
	'quassel.service'
        'quassel.conf')
install=quassel.install
backup=(etc/conf.d/quassel.conf)
md5sums=('546e2a950f44c4c0262c09d48f14e998'
         '95045bafa9bf965d0e2febd5b49514ea'
         '479229556ba0dd94e3ce28e31e5deea9')
         
build() {
  [[ ! -d build ]] && mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=1 \
    -DCMAKE_BUILD_TYPE="Release" \
    ../${pkgname}-${pkgver}/ \
    -Wno-dev

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/$pkgname.service ${pkgdir}/usr/lib/systemd/system/$pkgname.service
  install -D -m644 ${srcdir}/$pkgname.conf ${pkgdir}/etc/conf.d/$pkgname
}

