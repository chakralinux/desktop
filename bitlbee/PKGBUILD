#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=bitlbee
pkgver=3.0.5
pkgrel=3
pkgdesc="An IRC to IM gateway. Supports MSN, ICQ, Jabber, Yahoo!, and AIM"
arch=('i686' 'x86_64')
url="http://www.bitlbee.org/" 
screenshot="http://www.bitlbee.org/img/screenshot-irssi_sawfish.png"
license=('GPL')
depends=('openssl' 'glib2')
makedepends=('asciidoc' 'libotr')
optdepends=('xinetd: to run bitlbee through xinetd'
            'libotr: for OTR encryption support')
categories=('communication')
install=${pkgname}.install
backup=(etc/bitlbee/bitlbee.conf 
        etc/bitlbee/motd.txt 
        etc/xinetd.d/bitlbee)
source=("http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz"
        'sbindir.patch'
	'bitlbee.xinetd'
	'bitlbee.rcd'
        'bitlbee.tmpfiles')
sha256sums=('a2b18ae0355650b7ec326b8ea7082eb06bb8f8816ad3b4985fe97f729ce84e59'
            '2629b95b1dd2032412d0b840fc988c3d059317f1cc0e541bba8f35a61a518c33'
            '893616045f956ffbbaa299d81e2cf2af8b06c72663837eb4b61a88b02c58a3fe'
            '597a2c2118b11d4991347fd0c8d713d4e6f9d0a378466ff9202309b1401bc370'
            '47df3adcd6c3fa9e18e4a2b8433f49a717d1cb282f73cc293b64749f3a5e8b58')

build() { 
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/sbindir.patch"
	
  ./configure --prefix=/usr \
              --ssl=openssl \
              --etcdir=/etc/bitlbee \
              --pidfile=/var/run/bitlbee/bitlbee.pid \
              --ipcsocket=/var/run/bitlbee/bitlbee.sock \
              --systemdsystemunitdir=/usr/lib/systemd/system \
              --strip=0 \
              --otr=plugin
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install{,-etc,-dev}

  install -o65 -g65 -dm0770 "${pkgdir}/var/lib/bitlbee"
  install -Dm0644 "${srcdir}/bitlbee.xinetd" "${pkgdir}/etc/xinetd.d/bitlbee"
  install -Dm0755 "${srcdir}/bitlbee.rcd" "${pkgdir}/etc/rc.d/bitlbee"
  install -Dm0644 "${srcdir}/bitlbee.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/bitlbee.conf"
}
