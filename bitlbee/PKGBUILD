pkgname=bitlbee
pkgver=3.4.1
pkgrel=1
pkgdesc="An IRC to IM gateway. Supports MSN, ICQ, Jabber, Yahoo!, and AIM"
arch=('x86_64')
url="http://www.bitlbee.org/" 
screenshot="http://www.bitlbee.org/img/screenshot-irssi_sawfish.png"
license=('GPL')
depends=('gnutls' 'glib2')
makedepends=('asciidoc' 'libotr')
optdepends=('xinetd: to run bitlbee through xinetd'
            'libotr: for OTR encryption support')
categories=('communication')
install=${pkgname}.install
backup=(etc/bitlbee/bitlbee.conf 
        etc/bitlbee/motd.txt 
        etc/xinetd.d/bitlbee)
source=("http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz"
        'bitlbee.xinetd'
        'bitlbee.tmpfiles')
sha512sums=('fad297c7a792c932ba74daf7dc894883037842ae5d39a4c831c6615100580073001806c1af60555aa1c775399e8a484d99a4e601e96c6a075e12546e5d153532'
            'ff417d4e2b4d79273e8fac2704b609caa908d60bb2f9acbc35d9bd5ea59d9d674cac0c3ed81eaadc349bc1e0aa153faec82dcb68816424621c11bcdf33691ff7'
            'fe5ab700dfe77eab8366f2f1157bd3015898ede62ca41cc8f463c363a4ab1004023865ea38db6ac8e34b63da28aaeec929645bb11ebf5feffa4e5e088bdf7eca')

build() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
	
  ./configure --prefix=/usr \
              --ssl=gnutls \
              --etcdir=/etc/bitlbee \
              --pidfile=/var/run/bitlbee/bitlbee.pid \
              --ipcsocket=/var/run/bitlbee/bitlbee.sock \
              --systemdsystemunitdir=/usr/lib/systemd/system \
              --strip=0 \
              --otr=plugin \
	      --skype=plugin
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install{,-etc,-dev,-systemd}

  install -o65 -g65 -dm0770 "${pkgdir}/var/lib/bitlbee"
  install -Dm0644 "${srcdir}/bitlbee.xinetd" "${pkgdir}/etc/xinetd.d/bitlbee"
  install -Dm0644 "${srcdir}/bitlbee.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/bitlbee.conf"
}
