#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=bitlbee
pkgver=3.0.6
pkgrel=1
pkgdesc="An IRC to IM gateway. Supports MSN, ICQ, Jabber, Yahoo!, and AIM"
arch=('x86_64')
url="http://www.bitlbee.org/" 
screenshot="http://www.bitlbee.org/img/screenshot-irssi_sawfish.png"
license=('GPL')
depends=('openssl' 'glib2')
makedepends=('asciidoc' 'libotr')
optdepends=('xinetd: to run bitlbee through xinetd'
            'libotr: for OTR encryption support')
categories=('communication')
install=${pkgname}.install
backup=(etc/bitlbee/bitlbee.conf 
        etc/bitlbee/motd.txt 
        etc/xinetd.d/bitlbee)
source=("http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz"
	'bitlbee.xinetd'
        'bitlbee.tmpfiles')
sha256sums=('4f3e169f9a8e0cdf7976d2e6f41b9a8f6020a262b5731aa0f91fdb66d17446e8'
            '893616045f956ffbbaa299d81e2cf2af8b06c72663837eb4b61a88b02c58a3fe'
            '47df3adcd6c3fa9e18e4a2b8433f49a717d1cb282f73cc293b64749f3a5e8b58')

build() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
	
  ./configure --prefix=/usr \
              --ssl=openssl \
              --etcdir=/etc/bitlbee \
              --pidfile=/var/run/bitlbee/bitlbee.pid \
              --ipcsocket=/var/run/bitlbee/bitlbee.sock \
              --systemdsystemunitdir=/usr/lib/systemd/system \
              --strip=0 \
              --otr=plugin
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install{,-etc,-dev}

  install -o65 -g65 -dm0770 "${pkgdir}/var/lib/bitlbee"
  install -Dm0644 "${srcdir}/bitlbee.xinetd" "${pkgdir}/etc/xinetd.d/bitlbee"
  install -Dm0644 "${srcdir}/bitlbee.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/bitlbee.conf"
}
