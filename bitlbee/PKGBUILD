#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=bitlbee
pkgver=3.0.5
pkgrel=1
pkgdesc="An IRC to IM gateway. Supports MSN, ICQ, Jabber, Yahoo!, and AIM"
arch=('i686' 'x86_64')
url="http://www.bitlbee.org/" 
license=('GPL')
depends=('openssl' 'glib2')
makedepends=('asciidoc' 'libotr')
optdepends=('xinetd: to run bitlbee through xinetd'
            'libotr: for OTR encryption support')
categories=('communication')
install=${pkgname}.install
backup=(etc/bitlbee/bitlbee.conf etc/bitlbee/motd.txt etc/xinetd.d/bitlbee)
source=(http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz
	'bitlbee.xinetd'
	'bitlbee.rcd')
sha1sums=('74afdff87be49ce060771a6ae10d7643cd57b9b6'
          '5e0af27ba9cc4fe455e3381c75fc49a9326e2f17'
          '299776783902cc2e20be49e2fe729572ca656383')

build() { 
	cd ${srcdir}/${pkgname}-${pkgver}
	
	./configure --prefix=/usr \
		--ssl=openssl \
		--etcdir=/etc/bitlbee \
		--pidfile=/var/run/bitlbee/bitlbee.pid \
		--ipcsocket=/var/run/bitlbee/bitlbee.sock \
                 --otr=plugin 
	make 
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	install -o65 -g65 -dm0770 ${pkgdir}/var/lib/bitlbee
	install -Dm644 ${srcdir}/bitlbee.xinetd \
		${pkgdir}/etc/xinetd.d/bitlbee
	install -Dm755 ${srcdir}/bitlbee.rcd \
		${pkgdir}/etc/rc.d/bitlbee 
	make DESTDIR=${pkgdir} install 
	make DESTDIR=${pkgdir} install-etc 
	make DESTDIR=${pkgdir} install-dev 
}
