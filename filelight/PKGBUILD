# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdeutils-filelight/
# Include global configuration

pkgname=filelight
arch=('x86_64')
pkgver=18.08.3
pkgrel=1
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
pkgdesc="Creates a complex, but data-rich graphical representation of the files and directories on your computer"
depends=('kparts')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
conflicts=('kdeutils-filelight')
replaces=('kdeutils-filelight')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeutils')
categories=('utils')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c24203ad84f109a831cfe970d4e524a0c883d3a31a58aef3cf00c149914ab042'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
