#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf


pkgname=qlandkartegt
pkgver=1.1.2
pkgrel=1
pkgdesc="Use your GPS with Linux"
arch=('i686' 'x86_64')
url="http://www.qlandkarte.org"
license=('GPL')
depends=('gdal' 'gpsd' 'libdmtx' 'libexif' 'libmysqlclient' 'mesa' 'postgresql-libs' 'qt' 'shared-mime-info')
optdepends=('garmindev: to connect garmin devices')
makedepends=('cmake>=2.6' 'garmindev')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz \
        $pkgname-mimetypes.xml)
sha256sums=('8eedc414b4f89c9519e4b15c2ac7e5986b8e0834d237cde5fedc619c61ad0175'
            'e8d33948831f8a66edd752c71c653085d6c4d9f1969e70dd8c40b9c7ca37fba0')


build() {
  cd ${srcdir}/$pkgname-$pkgver

  install -d ${srcdir}/$pkgname-$pkgver/build
  cd ${srcdir}/$pkgname-$pkgver/build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make
}

package(){
  cd ${srcdir}/$pkgname-$pkgver/build

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/$pkgname-mimetypes.xml \
    ${pkgdir}/usr/share/mime/packages/$pkgname-mimetypes.xml
}
