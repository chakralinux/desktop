pkgname=otter-browser
pkgver=0.9.11
pkgrel=1
pkgdesc="Browser aiming to recreate classic Opera (12.x) UI using Qt5."
arch=('x86_64')
url="http://otter-browser.org"
license=('GPL3')
depends=('qt5-multimedia' 'qt5-webkit' 'qt5-webengine' 'qt5-script' 'hicolor-icon-theme' 'desktop-file-utils'
         'sonnet' 'libgcrypt' 'hunspell')
makedepends=('cmake' 'qt5-tools')
conflicts=('otter-browser-git')
install=otter.install
source=($pkgname-$pkgver.tar.gz::https://github.com/OtterBrowser/$pkgname/archive/v$pkgver.tar.gz)
sha1sums=('63c37c14e8b4b58c5a9a629ba39b118cbed639f9')

prepare() {
  cd $pkgname-$pkgver
  
  # Change DuckDuckGo's argument.
  sed -i 's/canonical/chakra/g' resources/searches/duckduckgo.xml
}

build() {
  cd $pkgname-$pkgver
  lrelease-qt5 resources/translations/*.ts
  cmake -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
