#Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
#Contributions from: https://aur.archlinux.org/packages/synaptiks/

pkgname=synaptiks
pkgver=0.8.1
pkgrel=3
pkgdesc="A touchpad tool for KDE."
arch=('x86_64')
url="http://synaptiks.readthedocs.org"
license=('BSD')
depends=('kde-workspace' 'kdebindings-pykde4' 'xf86-input-synaptics' 'python2-udev'
         'python2-dbus' 'python2-setuptools')
categories=('utils')
install=synaptiks.install
screenshot=('http://synaptiks.lunaryorn.de/en/0.8.1/_images/scrolling.png')
source=("http://pypi.python.org/packages/source/s/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'pyudev-fix.patch'
         'synaptics170.patch')
md5sums=('4137a5c7a6117cf8f2073bfdb05466d6'
         '9a1c930ff2d8fad6e986270141c3fb0a'
         'e04319829a73e26ca503452f290f3ec3')
         
prepare() {
cd "${srcdir}/${pkgname}-${pkgver}"

 #Fix issue with bluetooth mouse: https://bugs.archlinux.org/task/31328
 patch -p1 -i "${srcdir}"/pyudev-fix.patch
 #Fix https://bugs.archlinux.org/task/34637
  patch -p1 -i "${srcdir}"/synaptics170.patch
}

package() {
 cd "${srcdir}/${pkgname}-${pkgver}"

 python2 setup.py install --root "${pkgdir}" --optimize=1
 install -m755 -d "$pkgdir/usr/share/licenses/synaptiks/"
 install -m644 COPYING "$pkgdir/usr/share/licenses/synaptiks"
}

