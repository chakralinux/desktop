#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=jre
pkgver=6u21
pkgrel=1
pkgdesc="Sun's java runtime environment"
arch=('i686' 'x86_64')
  [ "$CARCH" = "i686" ]   && ARCH=i586
  [ "$CARCH" = "x86_64" ] && ARCH=amd64
url="http://java.sun.com"
depends=('glibc' 'libxtst')
install="jre.install"
source=(http://download.java.net/dlj/binaries/jdk-${pkgver}-dlj-linux-$ARCH.bin
        https://jdk-distros.dev.java.net/source/browse/*checkout*/jdk-distros/trunk/utils/construct.sh
        jre.profile)
[ "$CARCH" = "i686" ]   && md5sums=('b3fbbe55bafedb3f8f93e91d4e809390'
         			    '94065b612df0046d9ae758943f9f6a75'
	          		    '50b4f5ac4129097461d246645d73a622')
[ "$CARCH" = "x86_64" ] && md5sums=('7961051f95c014a9b94ffe3e95ea66e6'
			            '94065b612df0046d9ae758943f9f6a75'
	          		    '50b4f5ac4129097461d246645d73a622')
replaces=('j2re')
conflicts=('j2re' 'java-runtime')
provides=('j2re' 'java-runtime=6')
license=('custom')

build() {
	cd ${srcdir}

	mkdir unbundle-jdk
	cd unbundle-jdk

	sh ../jdk-${pkgver}-dlj-linux-$ARCH.bin --accept-license

	cd ..

	sh construct.sh unbundle-jdk linux-jdk linux-jre
}

package() {
	cd ${srcdir}

	mkdir -p ${pkgdir}/opt/java
	mv linux-jdk/jre ${pkgdir}/opt/java

	install -Dm755 ${srcdir}/${pkgname}.profile ${pkgdir}/etc/profile.d/${pkgname}.sh

	mkdir -p ${pkgdir}/usr/lib/mozilla/plugins

	if [ "$CARCH" = "i686" ]; then
	    ln -s /opt/java/jre/lib/i386/libnpjp2.so ${pkgdir}/usr/lib/mozilla/plugins
	else ln -s /opt/java/jre/lib/amd64/libnpjp2.so ${pkgdir}/usr/lib/mozilla/plugins
	fi

	mkdir -p ${pkgdir}/usr/share/licenses/jre
	cp ${pkgdir}/opt/java/jre/COPYRIGHT ${pkgdir}/usr/share/licenses/jre
	cp ${pkgdir}/opt/java/jre/LICENSE ${pkgdir}/usr/share/licenses/jre
	cp ${pkgdir}/opt/java/jre/THIRDPARTYLICENSEREADME.txt ${pkgdir}/usr/share/licenses/jre

	# Fix system prefs folder (FS#18872)
	install -dm755 ${pkgdir}/etc/.java/.systemPrefs
}
