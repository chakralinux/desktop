#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=jre
pkgver=7u1
pkgbuild=b08
pkgrel=1
pkgdesc="Sun's java runtime environment"
arch=('i686' 'x86_64')
  [ "$CARCH" = "i686" ]   && ARCH=i586
  [ "$CARCH" = "x86_64" ] && ARCH=x64
url="http://java.sun.com"
depends=('glibc' 'libxtst')
install="jre.install"
source=("http://download.oracle.com/otn-pub/java/jdk/${pkgver}-${pkgbuild}/${pkgname}-${pkgver}-linux-${ARCH}.tar.gz"
        'jre.profile')
noextract=("${pkgname}-${pkgver}-linux-${ARCH}.tar.gz")

[ "${CARCH}" = 'i686' ]   && md5sums=('d9b73cc5ccaa4f0b36cd6b8b62d07142'
                                      '50b4f5ac4129097461d246645d73a622')
[ "${CARCH}" = 'x86_64' ] && md5sums=('07bd73571b7028b73fc8ed19bc85226d'
                                      '50b4f5ac4129097461d246645d73a622')
replaces=('j2re')
conflicts=('j2re' 'java-runtime')
provides=('j2re' 'java-runtime=7')
license=('custom')

package() {
  msg "Extracting the Java Runtime Environment..."
  install -d "${pkgdir}/opt/java"
  tar -zxf "${srcdir}/${pkgname}-${pkgver}-linux-${ARCH}.tar.gz" -C "${pkgdir}/opt/java"
  mv "${pkgdir}/opt/java/jre1.7.0_01" "${pkgdir}/opt/java/jre"

  # Browser plugin
  [ "${CARCH}" = 'i686' ] && __arch='i386' || __arch='amd64'
  install -d "${pkgdir}/usr/lib/mozilla/plugins"
  ln -s "/opt/java/jre/lib/${__arch}/libnpjp2.so" "${pkgdir}/usr/lib/mozilla/plugins/libnpjp2.so"

  # License files and documentation
  install -d "${pkgdir}/usr/share/licenses/jre"
  install -m644 "${pkgdir}/opt/java/jre/COPYRIGHT" "${pkgdir}/usr/share/licenses/jre"
  install -m644 "${pkgdir}/opt/java/jre/LICENSE" "${pkgdir}/usr/share/licenses/jre"
  install -m644 "${pkgdir}/opt/java/jre/THIRDPARTYLICENSEREADME.txt" "${pkgdir}/usr/share/licenses/jre"

  # Environmental variables
  install -Dm755 "${srcdir}/${pkgname}.profile" "${pkgdir}/etc/profile.d/${pkgname}.sh"

  # Fix system prefs folder (FS#18872)
  install -dm755 "${pkgdir}/etc/.java/.systemPrefs"
}

# vim:set ts=2 sw=2 et:
