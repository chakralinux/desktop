pkgname=telegramqml
_pkgname=TelegramQML
pkgver=0.9.1
pkgrel=1
pkgdesc="Telegram API tools for QtQml and Qml"
arch=('x86_64')
url="https://github.com/Aseman-Land/TelegramQML"
license=('GPL')
depends=('qt5-quick1' 'qt5-webkit>=5.5' 'qt5-imageformats' 'qt5-graphicaleffects'
         'qt5-quickcontrols' 'qt5-declarative' 'qt5-multimedia' 'libqtelegram-ae>=6.0')
source=("https://github.com/Aseman-Land/TelegramQML/archive/v${pkgver}-stable.tar.gz")
sha512sums=('cc7317693f728a16da4353459ce5db0959a31ffac1977708354871f8b4d814b627aed8d53713e6739f4463557780558f4912faf24e4dfa77ef8b82ff1739e38d')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}-stable"
  sed -i 's#target.path = $$PREFIX/lib/$$LIB_PATH#target.path = $$PREFIX/lib#' telegramqml.pro
  
  mkdir -p build 
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}-stable/build"
  qmake-qt5 -r .. PREFIX=/usr INSTALL_LIBS_PREFIX=/usr/lib INSTALL_HEADERS_PREFIX=/usr/include BUILD_MODE+=lib
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}-stable/build"
  make INSTALL_ROOT="${pkgdir}" install
}
