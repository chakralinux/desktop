pkgname=telegramqml
_pkgname=TelegramQML
pkgver=0.9.2
pkgrel=1
pkgdesc="Telegram API tools for QtQml and Qml"
arch=('x86_64')
url="https://github.com/Aseman-Land/TelegramQML"
license=('GPL')
depends=('qt5-quick1' 'qt5-webkit>=5.5' 'qt5-imageformats' 'qt5-graphicaleffects'
         'qt5-quickcontrols' 'qt5-declarative' 'qt5-multimedia' 'libqtelegram-ae>=6.1')
source=("https://github.com/Aseman-Land/TelegramQML/archive/v${pkgver}.tar.gz")
sha256sums=('16d6429fe180531af167dc44bcc799e82b08c9cfa88f1f635155dfcef2558397')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  sed -i 's#target.path = $$PREFIX/lib/$$LIB_PATH#target.path = $$PREFIX/lib#' telegramqml.pro
  
  mkdir -p build 
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  qmake-qt5 -r .. PREFIX=/usr INSTALL_LIBS_PREFIX=/usr/lib INSTALL_HEADERS_PREFIX=/usr/include BUILD_MODE+=lib
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  make INSTALL_ROOT="${pkgdir}" install
}
