pkgname=telegramqml
_pkgname=TelegramQML
pkgver=0.8.0
pkgrel=2
pkgdesc="Telegram API tools for QtQml and Qml"
arch=('x86_64')
url="https://github.com/Aseman-Land/TelegramQML"
license=('GPL')
depends=('qt5-quick1' 'qt5-webkit>=5.5' 'qt5-imageformats' 'qt5-graphicaleffects'
         'qt5-quickcontrols' 'libqtelegram-ae>=5.0')
source=("https://github.com/Aseman-Land/TelegramQML/archive/v${pkgver}-stable.tar.gz")
sha512sums=('fabe1d944aaa60d61e9b728539919b395b3f25e85afb6d026e16916083e4e917e2fcd5eab763de23ae97fd0f58286f089bbe83d1b288a662e581cbcfb74cfe16')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}-stable"
  sed -i 's#target.path = $$PREFIX/lib/$$LIB_PATH#target.path = $$PREFIX/lib#' telegramqml.pro
  mkdir -p build 
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}-stable/build"
  qmake-qt5 -r .. PREFIX=/usr BUILD_MODE+=lib
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}-stable/build"
  make INSTALL_ROOT="${pkgdir}" install
}
