pkgname=libopenshot
pkgver=0.1.9
pkgrel=4
pkgdesc="A high quality, open-source video editing, animation, and playback library for C++, Python, and Ruby."
arch=('x86_64')
url="http://openshot.org/"
license=('LGPL3')
depends=('imagemagick' 'ffmpeg' 'libx264' 'libopenshot-audio' 'python3' 'qt5-base' 'qt5-multimedia' 'zeromq')
makedepends=('cmake' 'doxygen' 'swig' 'unittestpp')
source=("https://github.com/OpenShot/${pkgname}/archive/v${pkgver}.tar.gz"
https://github.com/OpenShot/libopenshot/commit/774eb365b3f663b1f53dd24c1650fafb3c445ea6.patch
ffmpeg-32-writer.patch ffmpeg-4.0.patch)
sha256sums=('2547969818092d77a1c742e27cf416b34b4d326abf6b089fc10b921f4a4141ec'
            'e0f56d40d4df0e4278d3cdb81923652b467c1a30e408bcff7a6b15327fb03548'
            '7dca1cfcdbc8c8cd039b6267623c822230e0d37968be2372520b36e22c782dd8'
            'bfc6aa2752a053774b0c46d0339b41df68f8e63765f1185f0df596ecbd3633a8')

prepare() {
	cd ${pkgname}-${pkgver}

	# patches for ffmpeg 3.2
	patch -p1 -i "$srcdir/774eb365b3f663b1f53dd24c1650fafb3c445ea6.patch"
	patch -p1 -i "$srcdir/ffmpeg-32-writer.patch"

	# patches for ffmpeg 4.0
	# TODO https://github.com/OpenShot/libopenshot/issues/87
	patch -p1 -i "$srcdir/ffmpeg-4.0.patch"
	sed -i \
		-e 's#FF_INPUT_BUFFER_PADDING_SIZE#AV_INPUT_BUFFER_PADDING_SIZE#g' \
		-e 's#CODEC_FLAG_GLOBAL_HEADER#AV_CODEC_FLAG_GLOBAL_HEADER#g' \
		src/FFmpegWriter.cpp src/FFmpegReader.cpp
}

build() {
	cd ${pkgname}-${pkgver}
	mkdir build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DMAGICKCORE_HDRI_ENABLE=1 -DMAGICKCORE_QUANTUM_DEPTH=16 \
              -DUSE_SYSTEM_JSONCPP=ON -DENABLE_RUBY=OFF ../
	make
}

package() {
	cd ${pkgname}-${pkgver}/build
	make DESTDIR="${pkgdir}" install
}
