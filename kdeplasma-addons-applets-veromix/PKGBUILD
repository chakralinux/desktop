# Maintainer: Manuel UtG <utg[dot]chakra.linux[at]gmail[dot]com>
# Original AUR Maintainer: Tom Debruyne <tomdebruyne at gmail dot com>

pkgname=kdeplasma-addons-applets-veromix
pkgver=4.53c5233
pkgrel=1
pkgdesc="A plasmoid mixer for the Pulseaudio sound server"
url="http://code.google.com/p/veromix-plasmoid/"
license=('GPL3')
arch=('x86_64')
makedepends=('zip' 'unzip')
depends=('kde-workspace' 'kdebindings-python2' 'pulseaudio' 'python2-xdg')
optdepends=('swh-plugins: equalizer and other effects support')
categories=('accessories')
source=("git+https://github.com/blue-shell/veromix.git")
sha1sums=('SKIP')
install="${pkgname}.install"
 
pkgver() {
  cd veromix
  echo $(git rev-list --count master).$(git rev-parse --short master)
}
 
build() {
  cd veromix
  make build      
}
package() {
  cd veromix
  make plasma-pkg

  install -dm755 "${pkgdir}/usr/share/apps/plasma/plasmoids/veromix-plasmoid"
  cd "${pkgdir}/usr/share/apps/plasma/plasmoids/veromix-plasmoid"
  unzip ${srcdir}/*veromix.plasmoid

  install -dm755 "${pkgdir}/usr/share/kde4/services/"
  ln -s /usr/share/apps/plasma/plasmoids/veromix-plasmoid/metadata.desktop ${pkgdir}/usr/share/kde4/services/plasma-widget-veromix.desktop

  cd ${pkgdir}
  python2 -m compileall .
}
