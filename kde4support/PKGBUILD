# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kde4support
pkgver=${KFVersion}
pkgrel=1
pkgdesc='KDE 4 Support'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kde4support'
license=('LGPL')
depends=('kio' 'kunitconversion' 'kdesignerplugin' 'libsm' 'qt5-tools')
makedepends=('extra-cmake-modules' 'kdoctools' )
checkdepends=("cmake")
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('a6d1781b71ab9060235c7b1086f99960')

prepare() {
  mkdir -p build
}

build() {
  # export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

check() {
  cd build
  msg "some tests are failing, do they need a GUI?"
  make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
