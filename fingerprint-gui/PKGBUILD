pkgname=fingerprint-gui
pkgver=1.09
pkgrel=3
pkgdesc="Application for fingerprint-based authentication, automatically support UPEK fingerprint readers with non-free library"
arch=('x86_64')
url="http://www.ullrich-online.cc/fingerprint/"
license=('GPL')
depends=('libfprint' 'libfakekey' 'polkit-qt5' 'qca-qt5' 'qt5-x11extras')
optdepends=('libusb: for libbsapi')
source=("https://github.com/maksbotan/${pkgname}/archive/v${pkgver}-qt5.tar.gz"
        "fingerprint-gui-udev-path.patch"
        "fingerprint-gui-udev-0050.patch")
sha1sums=('41649cfa5cb42f4ae5327095da0585425a6b3dd0'
          'f75eec87ab45d0fd6a2bcf3a6282ac19843c5380'
          'a9afa47bab503fa9f6798940a12365058b997b96')

prepare() {
  cd $pkgname-$pkgver-qt5
  patch -p1 -i ../fingerprint-gui-udev-path.patch
  patch -p1 -i ../fingerprint-gui-udev-0050.patch
}

build() {
  cd $pkgname-$pkgver-qt5
  qmake-qt5 PREFIX=/usr LIB=/usr/lib QMAKE_CFLAGS="${CFLAGS}" QMAKE_CXXFLAGS="${CXXFLAGS}"
  make
}

package() {
  cd $pkgname-$pkgver-qt5
  make INSTALL_ROOT=$pkgdir install
  make INSTALL_ROOT="${pkgdir}/" upek-rules
  make INSTALL_ROOT="${pkgdir}/" upek-cfg

  # upstream build rule wanna create group/change rights as well
  install -dm770 "${pkgdir}/var/upek_data"
  # we are using custom rule to define arch by using our mechanisms
  # (e.g. crossplatform build)
  install -Dm644 upek/lib64/libbsapi.so.* "${pkgdir}/usr/lib/"

  install -Dm644 "CHANGELOG" "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/"
}
