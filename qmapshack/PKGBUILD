pkgname=qmapshack
pkgver=1.12.0
pkgrel=1
pkgdesc='Plan your next outdoor trip'
arch=('x86_64')
url='https://bitbucket.org/maproom/qmapshack/'
license=('GPL3')
depends=('gdal' 'routino' 'qt5-declarative' 'qt5-webengine' 'proj' 'quazip')
makedepends=('cmake' 'qt5-tools')
source=("https://bitbucket.org/maproom/qmapshack/downloads/$pkgname-$pkgver.tar.gz")
sha1sums=('69c1e0c84137675c40e2e747f86dc3d3b3a49225')

prepare() {
    cd $pkgname-$pkgver
    [[ -d build ]] || mkdir build
}

build() {
    cd $pkgname-$pkgver/build
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd $pkgname-$pkgver/build
    make DESTDIR=$pkgdir install
}
