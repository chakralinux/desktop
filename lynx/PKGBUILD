# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributions from Arch: https://www.archlinux.org/packages/testing/x86_64/lynx/

pkgname=lynx
pkgver=2.8.8
pkgrel=2
pkgdesc="A text browser for the World Wide Web"
url="http://lynx.isc.org/"
arch=(x86_64)
license=('GPL')
depends=('ncurses' 'openssl')
source=(ftp://invisible-island.net/${pkgname}/tarballs/${pkgname}${pkgver}rel.2.tar.gz)
url="http://lynx.isc.org"
backup=('etc/lynx.cfg')
screenshot=('http://upload.wikimedia.org/wikipedia/commons/d/d5/Lynx-wikipedia.png')
md5sums=('7caa6773f94e136556c1c30629fe1431')

build() {
  cd ${srcdir}/${pkgname}2-8-8
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-ssl \
              --enable-nls \
              --mandir=/usr/share/man
  make
}

package() {
  cd ${srcdir}/${pkgname}2-8-8
  make DESTDIR=$pkgdir install
  
  # Fix bug https://bugs.archlinux.org/task/20404 - points to local help
  sed -i -e "s|^HELPFILE.*$|HELPFILE:file:///usr/share/doc/lynx/lynx_help/lynx_help_main.html|" ${pkgdir}/etc/lynx.cfg

  install -d ${pkgdir}/usr/share/doc/lynx
  cp -rf lynx_help ${pkgdir}/usr/share/doc/lynx
  
}

