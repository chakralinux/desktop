
pkgbase=python-cffi
pkgname=(python3-cffi python2-cffi)
pkgver=1.5.2
_revision=4640c7bfb3166763943b428bc7ae9d3585e45c34
pkgrel=1
pkgdesc="Foreign Function Interface for Python calling C code"
arch=('x86_64')
url="http://cffi.readthedocs.org/"
license=('MIT')
makedepends=('python3-setuptools' 'python2-setuptools' 'python3-pycparser' 'python2-pycparser' 'mercurial')
checkdepends=('python3-pytest' 'python2-pytest')
source=("hg+https://felixonmars@bitbucket.org/cffi/cffi#revision=$_revision")
sha512sums=('SKIP')

prepare() {
  cp -a cffi{,-py2}
}

build() {
  cd "$srcdir/cffi"
  python3 setup.py build

  cd "$srcdir/cffi-py2"
  python2 setup.py build
}

#check() {
#  ## Fails complaining that it can't import _cffi_backend, and I'm
#  ## not sure how to make it work
#  cd "$srcdir/cffi"
#  python3 setup_base.py build
#  PYTHONPATH=build/lib.linux-* py.test c/ testing/
#  
#  cd "$srcdir/cffi-py2"
#  python2 setup_base.py build
#  PYTHONPATH=build/lib.linux-* py.test c/ testing/
#}

package_python3-cffi() {
  depends=('python3-pycparser')

  cd "$srcdir/cffi"
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-cffi() {
  depends=('python2-pycparser')

  cd "$srcdir/cffi-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
