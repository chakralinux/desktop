# Contributions from Arch: https://www.archlinux.org/packages/kde-unstable/x86_64/kaccounts-integration/
# Include global configuration

pkgname=kaccounts-integration
pkgver=18.08.3
pkgrel=1
pkgdesc="Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, Jabber and others"
arch=('x86_64')
url="https://projects.kde.org/projects/kdereview/kaccounts-integration"
license=('GPL')
depends=('kcmutils' 'kio' 'signon-kwallet-extension' 'signon-plugin-oauth2' 'signon-ui' 'ki18n' 'libaccounts-glib')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
groups=('kde-telepathy')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('579bc40c0adb7eea27389a55f93e84f21fa837a6bc4fd6951d58d8d396d220de'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
