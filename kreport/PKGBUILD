# Contribution from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kreport
pkgver=3.1.0
pkgrel=1
pkgdesc="A framework for creation and generation of reports in multiple formats"
arch=(x86_64)
url="http://www.kexi-project.org/"
license=(GPL2)
depends=(kproperty qt5-declarative)
makedepends=(extra-cmake-modules python3 qt5-tools marble)
optdepends=('marble: support for maps in reports')
source=("https://download.kde.org/stable/$pkgname/src/$pkgname-$pkgver.tar.xz")
sha1sums=('d029cd1c6719952696121cd4e5095db4d09c794d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
