#
# Apps Packages for Chakra, part of chakraos.org
#
# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=grub2-editor
_pkgname=kcm-grub2
pkgver=0.7.0
pkgrel=1
pkgdesc="A KDE4 control module for configuring the GRUB2 bootloader."
arch=('x86_64')
_kdeapps=139643
url="http://kde-apps.org/content/show.php?content=${_kdeapps}"
screenshot="http://ksmanis.files.wordpress.com/2013/10/kcm_grub451.png"
license=('GPL3')
depends=('kcoreaddons' 'ki18n' 'kauth' 'kconfigwidgets' 'kio' 'solid'
         'imagemagick' 'hwinfo' 'librsvg' 'grub' 'os-prober')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('system')

source=("https://github.com/maz-1/grub2-editor/archive/${pkgver}.tar.gz")
md5sums=('d48dfa4d3e75cc031ea7555d746ae3ed')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}