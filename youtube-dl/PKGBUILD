#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=youtube-dl
pkgver=2011.08.04
pkgrel=1
pkgdesc='A small command-line program to download videos from YouTube.com'
arch=('any')
url='http://rg3.github.com/youtube-dl/'
license=('custom')
depends=('python2')
source=("https://github.com/rg3/${pkgname}/raw/${pkgver}/${pkgname}"
        support-playlist-urls.patch)
md5sums=('2538839c274fe7cd1568304e37545bde'
         '562f5a71679f8f6ee5b5653ff9b9874f')

package() {
  cd "${srcdir}"

  # Fix FS#24506: Unable to download playlists
  patch -Np1 -i "${srcdir}/support-playlist-urls.patch"

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' youtube-dl
  install -D -m755 youtube-dl "${pkgdir}/usr/bin/youtube-dl"
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  grep License youtube-dl  > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
