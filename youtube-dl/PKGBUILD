# Maintainer: Jeff Huang <s8321414[at]gmail[dot]com>
# maintainer: Francesco Marinucci <franzmari[at]chakra-project[dot]it>
# Maintainer: UtG  <utg[dot]chakra.linux[at]gmail[dot]com>

pkgname=youtube-dl
pkgver=2015.01.25
pkgrel=1
pkgdesc='A small command-line program to download videos from YouTube.com'
arch=('x86_64')
url='http://rg3.github.com/youtube-dl/'
license=('custom')
depends=('python3' 'python3-setuptools')
optdepends=('ffmpeg: for video post-processing'
            'rtmpdump: for rtmp streams support')
categories=('network')
source=(http://youtube-dl.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.gz
        http://youtube-dl.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig)
sha1sums=('b6c39c9c6babb2c9dcf15e19f7a4d13e30504017'
          'SKIP')

package() {
  cd "${srcdir}/${pkgname}"
  sed -i -e 's|etc/bash_completion.d|share/bash-completion/completions|' \
         -e 's|youtube-dl.bash-completion|youtube-dl|' setup.py
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
