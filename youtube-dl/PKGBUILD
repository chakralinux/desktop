#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=youtube-dl
pkgver=2013.01.13
pkgrel=1
pkgdesc='A small command-line program to download videos from YouTube.com'
arch=('x86_64')
url='http://rg3.github.com/youtube-dl/'
license=('custom')
depends=('python2')
makedepends=('python-distribute')
categories=('network')
source=("http://youtube-dl.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('99b580d1623d81f37bd022f39f1f45177b8e9c5f')

package() {
  cd "${srcdir}/${pkgname}"
  sed -i -e 's|etc/bash_completion.d|share/bash-completion/completions|' \
         -e 's|youtube-dl.bash-completion|youtube-dl|' setup.py
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}