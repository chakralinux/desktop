#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=mc
pkgver=4.8.5
pkgrel=1
pkgdesc="A filemanager/shell that emulates Norton Commander"
arch=('i686' 'x86_64')
url="http://www.ibiblio.org/mc/"
license=('GPL')
depends=('e2fsprogs' 'glib2' 'pcre' 'gpm>=1.20.4' 'slang')
makedepends=('libxt' 'libx11')
optdepends=('p7zip: support for 7zip archives')
screenshot=('http://ompldr.org/vOG9vNg')
options=('!emptydirs' '!makeflags')
source=("https://www.midnight-commander.org/downloads/${pkgname}-${pkgver}.tar.bz2")
md5sums=('87f03efc65cf962d775c3811d157e402')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure \
	    --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
	    --enable-background --enable-charset --enable-largefile \
	    --with-edit --with-gpm-mouse --with-mmap --enable-vfs-smb \
	    --with-screen=slang --with-subshell --with-vfs --with-x \
	    --without-debug --without-gnome --without-included-gettext \
	    --libexecdir=/usr/lib

	make 
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	# Fixes
	sed -i 's|op_has_zipinfo = 0|op_has_zipinfo = 1|' \
	    ${pkgdir}/usr/lib/mc/extfs.d/uzip

	rm "${pkgdir}/usr/lib/mc/extfs.d/u7z"

	sed 's#/usr/bin/env python#/usr/bin/python2#' \
		-i "${pkgdir}/usr/lib/mc/extfs.d/s3+"
}

