# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=cabal-install
pkgname=haskell-cabal-install
pkgver=1.16.0.2
pkgrel=1
pkgdesc="Command-line interface for Cabal and Hackage"
url="http://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('x86_64' 'i686')
makedepends=()
depends=('ghc=7.6.3-1' 'haskell-cabal>=1.16.0' 'haskell-cabal<1.18' 'haskell-http>=4000.0.2' 'haskell-http<4001' 'haskell-base>=2.0' 'haskell-base<2.2' 'haskell-filepath>=1.0' 'haskell-filepath<1.4' 'haskell-mtl>=2.0' 'haskell-mtl<3' 'haskell-network>=1' 'haskell-network<3' 'haskell-time>=1.1' 'haskell-time<1.5' 'haskell-unix>=1.0' 'haskell-unix<2.7' 'haskell-zlib>=0.4' 'haskell-zlib<0.6' 'gmp' 'zlib')
options=()
source=("http://hackage.haskell.org/packages/archive/cabal-install/1.16.0.2/cabal-install-1.16.0.2.tar.gz")
install="haskell-cabal-install.install"
sha512sums=('3fa29de625741d243778bd7309720fe492ee83ac0d9197155d3467bae956839cde4e387bd56d6633a1723197d81cd9104198ea7ec7fe42ccc9a86a5b2ed5a1df')

build() {
    cd "${srcdir}/cabal-install-1.16.0.2"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix="/usr" \
        --docdir="/usr/share/doc/haskell-cabal-install" \
        --libsubdir='$compiler/site-local/$pkgid'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/cabal-install-1.16.0.2"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/haskell-cabal-install/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/haskell-cabal-install/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/haskell-cabal-install/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/cabal-install"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/haskell-cabal-install/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/haskell-cabal-install/LICENSE"

    # Add bash completion
    install -d ${pkgdir}/usr/share/bash-completion/completions
    install -m644 bash-completion/cabal \
        ${pkgdir}/usr/share/bash-completion/completions

    runhaskell Setup copy --destdir="${pkgdir}"
}
