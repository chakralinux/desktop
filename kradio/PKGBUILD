#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

_pkgname=kradio4
pkgname=kradio
pkgver=4.0.6
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL2')
pkgdesc="KRadio is a comfortable KDE radio application."
url="http://kradio.sourceforge.net/"
depends=('lame' 'kdelibs' 'libsndfile' 'lirc-utils' 'libmms' 'ffmpeg')
makedepends=('pkgconfig' 'automoc4')
options=(!libtool)
categories=('multimedia')
screenshot=('http://kradio.sourceforge.net/screenshots/4.0.0/docking.png')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${_pkgname}-${pkgver}.tar.bz2")
md5sums=('ee9214156465e0416bb369f78b4a5eae')

build() {
  cd "${srcdir}"

  mkdir -p build
  cd build

  cmake "../${_pkgname}-${pkgver}" \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release 

  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
