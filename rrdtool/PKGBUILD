# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributors from Arch: Eric Belanger <eric@archlinux.org>
#                         Tom K <tom@archlinux.org>

pkgname=rrdtool
pkgver=1.6.0
pkgrel=2
pkgdesc="Data logging and graphing application"
arch=('x86_64')
url="http://www.rrdtool.org"
license=('GPL')
depends=('libpng' 'libxml2' 'pango')
makedepends=('intltool' 'ruby' 'python2' 'tcl' 'lua' 'groff')
optdepends=('tcl: to use corresponding binding' \
            'python2: to use corresponding binding' \
            'ruby: to use corresponding binding' \
            'lua: to use corresponding binding')
options=('!makeflags')
source=("http://oss.oetiker.ch/rrdtool/pub/${pkgname}-${pkgver}.tar.gz")
sha1sums=('9866b41bda9416188f236d61d24f185b173fd571')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix FS#28521 make ruby install to vendor_ruby instead of site_ruby
  sed -e 's/$(RUBY) ${abs_srcdir}\/ruby\/extconf.rb/& --vendor/' -i bindings/Makefile.am
  aclocal
  automake
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --localstatedir=/var --disable-rpath \
    --enable-perl --enable-perl-site-install --with-perl-options='INSTALLDIRS=vendor' \
    --enable-ruby --enable-ruby-site-install --enable-python \
    --enable-lua --enable-lua-site-install \
    --enable-tcl --disable-libwrap
  make LIBS+="-lglib-2.0"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" includedir=/usr/include install
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
