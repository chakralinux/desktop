# Contribution from Arch:
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>

pkgbase=python-py
pkgname=('python2-py' 'python3-py')
pkgver=1.4.34
pkgrel=1
pkgdesc="Python rapid development tools"
arch=('any')
license=('MIT')
url="http://pylib.readthedocs.org/en/latest/"
makedepends=("python2-setuptools" "python3-setuptools" )
checkdepends=('python2-pytest' 'python3-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/py/archive/$pkgver.tar.gz")
sha256sums=('522e37eb34a4decf1cc43f1522d4c18c9a6aa8caaddc401627aba5432ee08970')

prepare() {
  cp -a py-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/py-$pkgver
  python3 setup.py build

  cd "$srcdir"/py-$pkgver-py2
  python2 setup.py build
}

check() {
  # Known failure: https://github.com/pytest-dev/py/issues/104

  cd "$srcdir"/py-$pkgver
  python3 setup.py pytest || warning "Tests failed"

  cd "$srcdir"/py-$pkgver-py2
  python2 setup.py pytest || warning "Tests failed"
}

package_python3-py() {
  depends=('python3')

  cd py-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-py() {
  depends=('python2')

  cd py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

