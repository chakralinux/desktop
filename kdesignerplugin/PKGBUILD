# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=kdesignerplugin
pkgver=${KFVersion}
pkgrel=4
pkgdesc='KDesignerPlugin'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdesignerplugin'
license=('LGPL')
depends=('kio' 'kplotting' 'kdewebkit')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools')
checkdepends=('cmake')
groups=('kf5')
source=("${KFServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=( $(getSum ${pkgname}) )

prepare() {
  mkdir -p build
}

build() {
  # export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildTyp} \
    -DCMAKE_INSTALL_PREFIX=${KFInstalPrefix} \
    -DLIB_INSTALL_DIR=lib 
  make
}

check() {
  cd build
  make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
