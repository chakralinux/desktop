# Contributions from Arch: https://www.archlinux.org/packages/kde-unstable/any/kaccounts-providers/
# Include global configuration

pkgname=kaccounts-providers
pkgver=18.07.80
pkgrel=1
pkgdesc="Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, Jabber and others"
arch=('x86_64')
url="https://projects.kde.org/projects/playground/base/kde-accounts/kaccounts-providers"
license=('GPL')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdenetwork')
depends=('libaccounts-glib' 'kaccounts-integration')
makedepends=('extra-cmake-modules' 'intltool' 'python3')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('85bf3e16261246ea34859f91c9d4bde98cc10d2935b4bf19d7b549d5aef10222'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
