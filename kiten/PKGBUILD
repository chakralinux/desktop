# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdeedu-kiten/
# Include global configuration

pkgname=kiten
pkgver=18.12.2
pkgrel=1
pkgdesc="Japanese reference/study tool"
url="http://kde.org/applications/education/kiten/"
screenshot='https://www.kde.org/images/screenshots/kiten.png'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('khtml')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeedu')
replaces=('kdeedu-kiten')
conflicts=('kdeedu-kiten')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('189d3d692db070c5f4adcc411e01e9d06d511eb565d25aa4c51432da9afd3bf0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
