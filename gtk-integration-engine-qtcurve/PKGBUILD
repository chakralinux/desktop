#
# Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=gtk-integration-engine-qtcurve
pkgver=1.8.12
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc='QtCurve-Gtk2 Add-on for Gtk-integration.'
url='http://www.kde-look.org/content/show.php?content=40492'
depends=('gtk-integration' 'gtk2')
provides=('qtcurve-gtk2')
conflicts=('qtcurve-gtk2')
makedepends=('pkgconfig' 'cmake')
options=('!libtool')
source=("http://craigd.wikispaces.com/file/view/QtCurve-Gtk2-${pkgver}.tar.bz2")
md5sums=('e1b8b4a8444c0afe8ba291f623c6b713')

build() {
  cd "${srcdir}"

  mkdir -p build && cd build

  cmake "${srcdir}/QtCurve-Gtk2-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
