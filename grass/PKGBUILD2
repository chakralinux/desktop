
# build twice, when building on the server, j8 fails to honor without wx first time

pkgname=grass
pkgver=7.0.2
pkgrel=1
pkgdesc='Geographic Information System (GIS) used for geospatial data management and analysis, image processing, graphics/maps production, spatial modeling, and visualization.'
arch=('x86_64')
url='http://grass.itc.it/index.php'
license=('GPL')
depends=('gdal' 'tk' 'sqlite3' 'python2' 'mesa' 'proj' 'libjpeg-turbo' 'libpng' 'libtiff'
         'cfitsio' 'fftw' 'libxmu' 'postgresql' 'xorg-server' 'glu' 'tcl' 'zlib' 'cairo' 'geos')
makedepends=('mariadb' 'freetype2' 'r')
optdepends=('fftw: required for i.fft and i.ifft modules'
            'postgresql: PostgreSQL database interface'
            'r: R language interface'
            'lapack: required for GMATH library'
            'xorg-server: required for the graphical interface')
options=('!libtool' '!makeflags')
groups=('graphics')
#install='grass.install'
source=("http://grass.osgeo.org/grass70/source/${pkgname}-${pkgver}.tar.gz")
md5sums=('1f2c8f5cba2fe2d681a01b9519180e9e')

build() {
  cd ${pkgname}-${pkgver}
  sed -i '/^\s*INSTDIR/ s/".*"//' configure
  export CFLAGS="$CPPFLAGS $CFLAGS"
  export CXXFLAGS="$CPPFLAGS $CXXFLAGS"
  unset CPPFLAGS

  # see ${srcdir}/grass-6.4.0/REQUIREMENTS.html for options
  ./configure --enable-64bit \
    --prefix=/opt/grass \
    --with-mysql-includes=/usr/include/mysql \
    --with-mysql \
    --with-sqlite \
    --with-postgres \
    --with-fftw \
    --with-gdal=/usr/bin/gdal-config \
    --with-python \
    --with-blas \
    --with-lapack \
    --with-readline \
    --with-pthread \
    --with-netcdf \
    --with-nls \
    --with-geos \
    --with-cairo \
    --with-postgres \
    --with-proj-libs=/usr/lib \
    --with-proj-includes=/usr/include \
    --with-proj-share=/usr/share/proj \
    --with-fftw-includes=/usr/include \
    --with-fftw-libs=/usr/lib \
    --with-freetype \
    --with-freetype-includes=/usr/include/freetype2 \
    --without-glw \
    --without-wxwidgets \
    --without-htmldocs
   
  make -j4
}

package() {
  cd ${pkgname}-${pkgver}

  make \
    exec_prefix=$pkgdir/usr \
    INST_DIR=${pkgdir}/opt/grass \
    BINDIR=${pkgdir}/usr/bin \
    install

  install -D -m644 gui/icons/grass-48x48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/grass.png
  install -D -m644 gui/icons/grass-64x64.png $pkgdir/usr/share/icons/hicolor/64x64/apps/grass.png

  install -D -m644 ${srcdir}/grass.conf ${pkgdir}/etc/ld.so.conf.d/grass.conf
}