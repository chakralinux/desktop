# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/libkdcraw
# Include global configuration

pkgname=kamera
pkgver=18.04.2
pkgrel=1
pkgdesc="KDE integration for gphoto2 cameras"
url="http://kde.org/applications/graphics/kamera/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde kde-applications kdegraphics kde-uninstall)
depends=(kio libgphoto2)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegraphics-kamera)
replaces=(kdegraphics-kamera)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a84e0c53d374f3d8b62c9f1e2b0c20eb5460a4a2c59388d0fc9d0ee292b38709'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
