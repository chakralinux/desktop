pkgname=check
pkgver=0.12.0
pkgrel=1
pkgdesc="A unit testing framework for C"
arch=('x86_64')
url="https://libcheck.github.io/check/"
license=('LGPL')
depends=('awk')
categories=('programming')
install=$pkgname.install
source=(https://github.com/libcheck/check/archive/$pkgver.tar.gz)
sha256sums=('7816b4c38f6e23ff873786f18d966e552837677bfae144041e0587e7c39e04e8')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  # get rid of the package's info directory
  rm ${pkgdir}/usr/share/info/dir
  
  # svn log file is too big
  rm "$pkgdir"/usr/share/doc/check/*ChangeLog*
}

