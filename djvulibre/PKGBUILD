pkgname=djvulibre
pkgver=3.5.27
pkgrel=1
pkgdesc="Suite to create, manipulate and view DjVu ('déjà vu') documents"
arch=("x86_64")
license=('GPL')
url="http://djvu.sourceforge.net/"
depends=('gcc-libs' 'libtiff' 'bash' 'hicolor-icon-theme')
makedepends=('librsvg')
provides=("libdjvu=${pkgver}")
source=("https://sourceforge.net/projects/djvu/files/DjVuLibre/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('99c4f2c621c063bf8c8a1626030539fe5a8675f9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-desktopfiles
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  #Install icons for mime-types.
  install -m755 -d ${pkgdir}/usr/share/icons/hicolor/{22x22,32x32,48x48,64x64}/mimetypes
  for sz in 22 32 48 64; do
    install -m644 desktopfiles/prebuilt-hi${sz}-djvu.png "${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/mimetypes/image-vnd.djvu.mime.png"
  done
}
