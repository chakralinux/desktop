# Include global configuration
source ../kdeapps.conf

pkgname=libkgapi
pkgver=${_kdever}
pkgrel=2
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://projects.kde.org/projects/extragear/libs/libkgapi'
arch=('x86_64')
license=(GPL LGPL FDL)
depends=(kcalcore kcontacts qt5-webengine lcms2)
makedepends=(extra-cmake-modules kdoctools qt5-tools kdesignerplugin python3)
options=('debug')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"{,.sig}
        kdebug-388483.patch::"https://cgit.kde.org/libkgapi.git/patch/?id=2980ace5")
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
                        'SKIP'
            '193867c7ec72283614fb7795724dc50730d001b41892ba0a730aeb75e98f86a0')
validpgpkeys=(${Avalidpgpkeys[@]})

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-388483.patch # Fix Google authentication
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
