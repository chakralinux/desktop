# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>
#               Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=2.2.0
pkgrel=1
pkgdesc='A KDE-based library for accessing various Google services via their public API'
url='https://projects.kde.org/projects/kdereview/libkgapi'
arch=('x86_64')
license=('GPL'
         'LGPL'
         'FDL')
options=("debug")
depends=('kdepimlibs'
         'qjson')
makedepends=('cmake'
             'automoc4'
             'boost')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('c5f37aacb89243d7985d7064aa8941d3d2a6c26685113e876b38c737ea2e1126b40b0f88b0908e7ea8a7bcb2fade1dc528c2f73f22890b9e892c6d18d9d3bb42')

prepare() {
    [[ -d ${srcdir}/build ]] ||mkdir -p "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake "../${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE4_BUILD_TESTS=ON
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}
