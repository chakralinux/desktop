# Maintainer:
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=2.0.2
pkgrel=3
pkgdesc='A KDE-based library for accessing various Google services via their public API'
url='https://projects.kde.org/projects/kdereview/libkgapi'
arch=('x86_64')
license=('GPL'
         'LGPL'
         'FDL')
depends=('kdepimlibs'
         'qjson')
makedepends=('cmake'
             'automoc4'
             'boost')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha512sums=('da3a496afd99f25000cf146c0ff034e2a8b0a14b7fbaf89a83ed61460aec97d5f64669031ee3697c54d465dcf2a528a0bc9fde6cbf130bb62d96f683df677bea')

prepare() {
    mkdir "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake "../${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE4_BUILD_TESTS=OFF
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}
