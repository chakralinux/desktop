# Contributions from Arch: https://www.archlinux.org/packages/extra/i686/kdegames-kblocks/
# Include global configuration

pkgname=kblocks
pkgver=18.07.80
pkgrel=1
pkgdesc="The classic falling blocks game"
arch=('x86_64')
url='http://kde.org/applications/games/kblocks/'
screenshot="https://www.kde.org/images/screenshots/kblocks.png"
license=('GPL' 'LGPL' 'FDL')
depends=('knotifyconfig' 'libkdegames')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegames')
replaces=('kdegames-kblocks')
provides=('kdegames-kblocks')
options=('docs' 'debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('253b392e0a98aa10b059886552e6309ebb8968386031a66d418bb54b0f841b82'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
