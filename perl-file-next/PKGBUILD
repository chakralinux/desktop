_pkgname=file-next
pkgname=perl-${_pkgname}
pkgver=1.16
pkgrel=1
pkgdesc='File::Next - File-finding iterator'
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="https://github.com/petdance/${_pkgname}"
depends=('perl>=5.10.0')
options=(!emptydirs)
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=('8ee4c9826ba82ba4a91ee36bf178300a76ea3f400c837e2a14c478eab69ff600')

build() {
  cd "$srcdir/${_pkgname}-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"

  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}

