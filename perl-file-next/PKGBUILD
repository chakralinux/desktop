#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>


pkgname=perl-file-next
_pkgname=File-Next
pkgver=1.06
pkgrel=3
pkgdesc='File::Next - File-finding iterator'
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_pkgname}"
depends=('perl>=5.10.0')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/P/PE/PETDANCE/${_pkgname}-$pkgver.tar.gz)
md5sums=('8d901b18e40c477acd18a6ea0fd7de64')

build() {
  cd "$srcdir/${_pkgname}-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"

  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}
