#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Neo Kolokotronis <tetris4@gmail.com
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmygpo-qt
pkgver=1.0.6
pkgrel=1
pkgdesc="A C++/Qt client library for gpodder.net"
arch=('x86_64')
url="http://wiki.gpodder.org/wiki/Libmygpo-qt"
license=('LGPL2.1')
depends=('qjson')
makedepends=('cmake')
source=("http://stefan.derkits.at/files/${pkgname}/${pkgname}.${pkgver}.tar.gz")
md5sums=('dd07ccc88eac954a3860aeda96aab7b1')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake "../${pkgname}.${pkgver}"  \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}