pkgbase=python-mistune
_pkgname=mistune
pkgname=('python3-mistune' 'python2-mistune')
pkgver=0.7.1
pkgrel=2
pkgdesc="The fastest markdown parser in pure Python with renderer feature."
arch=('any')
url="https://github.com/lepture/mistune"
license=('BSD')
depends=('python3')
makedepends=('python3-setuptools' 'python2-setuptools')
source=("https://github.com/lepture/mistune/archive/v${pkgver}.tar.gz")
sha256sums=('d6684534174caa30e0169e106a7152aee14507796a610b76be9fe9b335b18410')

prepare() {
  cd "$srcdir"
  cp -r "$srcdir/$_pkgname-$pkgver" "$srcdir/${_pkgname}2-$pkgver"
}

package_python3-mistune() {
  cd "$srcdir/${_pkgname}-$pkgver"
  python3 setup.py install --root="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python3-mistune/LICENSE"
}

package_python2-mistune() {
  depends=('python2')
  cd "$srcdir/${_pkgname}2-$pkgver"
  python2 setup.py install --root="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-mistune/LICENSE"
}
