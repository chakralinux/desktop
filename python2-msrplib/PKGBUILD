#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <jmc@chakra-project.org>
#

_pkgname=python-msrplib
pkgname=python2-msrplib
pkgver=20120826
pkgrel=1
pkgdesc="Client library for MSRP protocol and its relay extension (RFC 4975 and RFC4976)."
arch=('any')
url="http://msrprelay.org"
license=('LGPL')
depends=('python2')
makedepends=('darcs')
replaces=('python-msrplib')

_darcstrunk="http://devel.ag-projects.com/repositories"
_darcsmod="${_pkgname}"

build() {
  msg "Starting Darcs pull..."

  cd "${srcdir}"

  if [ -d "${_darcsmod}/_darcs" ]; then
    (cd "${_darcsmod}" && darcs pull -a)
  else
    darcs get "${_darcstrunk}/${_darcsmod}" "${_darcsmod}"
  fi

  msg "Darcs pull done (or server timeout)."
}

package() {
  cd "${srcdir}/${_darcsmod}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
