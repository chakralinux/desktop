# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/telepathy-kde-contact-list/
# Include global configuration

pkgname=ktp-contact-list
pkgver=18.12.2
pkgrel=1
pkgdesc="Telepathy contact list application "
arch=('x86_64')
url="https://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('ktp-common-internals')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde-telepathy')
categories=('communication')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('37de84c9bd8448916463919102a1b95c1dae521e0143afc98aaf9ec555bb87b8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
