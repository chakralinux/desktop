#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=hotot
pkgver=0.9.8.10
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)"
arch=('x86_64')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('hicolor-icon-theme' 'kde-runtime')
makedepends=('cmake' 'intltool' 'python2')
install="hotot.install"
source=("https://github.com/lyricat/Hotot/archive/${pkgver}.tar.gz")
md5sums=('c817e486a4eec6063d6ce7a08964e4ca')

build() {
  cd ${srcdir}/Hotot-${pkgver}
  mkdir build 
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DWITH_GTK=OFF \
           -DWITH_GIR=OFF \
           -DWITH_QT=ON \
           -DWITH_KDE=ON \
           -DWITH_CHROME=OFF \
           -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd ${srcdir}/Hotot-${pkgver}/build

  make DESTDIR="$pkgdir/" install

  # remove google analytics tracking 
  find "$pkgdir" -name hotot.js -exec \
    sed -i '/\/\/ 7. run track code/,+12d' {} \;
}
