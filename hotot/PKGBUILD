# Maintainer: <franzmari[at]chakra-project[dot]it>

pkgname=hotot
pkgver=0.9.8.16
pkgrel=4
epoch=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)"
arch=('x86_64')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('hicolor-icon-theme' 'kde-runtime' 'qt5-base')
makedepends=('cmake' 'intltool' 'python2')
install="hotot.install"
source=("https://github.com/ManiacTwister/Hotot/archive/${pkgver}.tar.gz")
md5sums=('5cae5f3b757dfdd476f68697959121f2')

build() {
  cd ${srcdir}/Hotot-${pkgver}
  mkdir build 
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DWITH_GTK=OFF \
           -DWITH_GIR=OFF \
           -DWITH_QT=ON \
           -DWITH_KDE=ON \
           -DWITH_CHROME=OFF \
           -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd Hotot-$pkgver/build

  make DESTDIR="$pkgdir/" install

  # remove google analytics tracking 
  find "$pkgdir" -name hotot.js -exec \
    sed -i '/\/\/ 7. run track code/,+12d' {} \;

  # fix user retweets error
  find "$pkgdir" -name lib.twitter.js -exec \
    sed -i 's/1\/statuses\/retweeted_by/1.1\/statuses\/retweets_of/' {} \;
  find "$pkgdir" -name lib.twitter.js -exec \
    sed -i 's/1\/statuses\/retweeted_to_me/1.1\/statuses\/retweets_of_me/' {} \;  

  # remove double entry: 
  rm $pkgdir/usr/share/applications/hotot.desktop
}
