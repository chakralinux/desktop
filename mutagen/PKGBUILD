# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor from Arch: Eric Belanger <eric@archlinux.org>

pkgbase=mutagen
pkgname=('mutagen' 'python3-mutagen')
pkgver=1.37
pkgrel=1
arch=('any')
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
makedepends=('python2-setuptools' 'python3-setuptools')
checkdepends=('python2-pytest' 'python3-pytest')
source=($pkgname-$pkgver::https://github.com/quodlibet/mutagen/archive/release-${pkgver}.tar.gz)
sha1sums=('4c9725f0b613d54e8ae522dac3d716c36ef10095')

prepare() {
  cp -aT $pkgbase-release-$pkgver python3-$pkgbase-$pkgver
}

check() {
  cd $pkgbase-release-$pkgver
  LANG=en_US.UTF8 python2 setup.py test
  cd ../python3-$pkgbase-$pkgver
#  LANG=en_US.UTF8 python3 setup.py test
}

package_mutagen() {
  pkgdesc="An audio metadata tag reader and writer"
  depends=('python2')

  cd $pkgbase-release-$pkgver
  python2 setup.py install --root="${pkgdir}"
}

package_python3-mutagen() {
  pkgdesc="An audio metadata tag reader and writer library for Python 3"
  depends=('python3')

  cd python3-$pkgbase-$pkgver
  python3 setup.py install --root="${pkgdir}"

  rm -r "${pkgdir}"/usr/{bin,share}
}
