# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor from Arch: Eric Belanger <eric@archlinux.org>

pkgbase=mutagen
pkgname=('mutagen' 'python3-mutagen')
pkgver=1.40.0
pkgrel=1
arch=('any')
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
makedepends=(python2-setuptools python3-setuptools)
checkdepends=(python2-pytest      python3-pytest
              python2-hypothesis  python3-hypothesis
              python2-pyflakes    python3-pyflakes
	      python2-pycodestyle python3-pycodestyle)
source=(https://github.com/quodlibet/mutagen/releases/download/release-${pkgver}/${pkgbase}-${pkgver}.tar.gz{,.sig})
validgpgkeys=(0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC)
sha1sums=('3473555912d01026ed9c085b01d91ee0117b45f9'
          'SKIP')

prepare() {
  cp -aT $pkgbase-$pkgver python3-$pkgbase-$pkgver
}

check() {
  cd $pkgbase-$pkgver
  LANG=en_US.UTF8 python2 setup.py test
  cd ../python3-$pkgbase-$pkgver
#  LANG=en_US.UTF8 python3 setup.py test
}

package_mutagen() {
  pkgdesc="An audio metadata tag reader and writer"
  depends=('python2')

  cd $pkgbase-$pkgver
  python2 setup.py install --root="${pkgdir}"
}

package_python3-mutagen() {
  pkgdesc="An audio metadata tag reader and writer library for Python 3"
  depends=('python3')

  cd python3-$pkgbase-$pkgver
  python3 setup.py install --root="${pkgdir}"

  rm -r "${pkgdir}"/usr/{bin,share}
}
