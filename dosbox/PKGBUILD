pkgname=dosbox
pkgver=0.74
pkgrel=6
pkgdesc="An emulator with builtin DOS for running DOS Games"
arch=('x86_64')
url="http://dosbox.sourceforge.net/"
license=('GPL')
depends=('sdl_net' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'gcc-libs' 'zlib' 'glu')
makedepends=('mesa-libgl' 'gendesk')
categories=('games' 'system')
source=("http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz"
	'dosbox.png'
	'gcc46.patch')
md5sums=('b9b240fa87104421962d14eee71351e8'
         '2aac25fc06979e375953fcc36824dc5e'
         '3fba2e3c7c43290319b2928f40ed30e5')

prepare() {
  cd ${srcdir}
  gendesk --pkgname ${pkgname} --pkgdesc ${pkgdesc}
  
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i "$srcdir/gcc46.patch"
  sed -i 's/png_check_sig/png_sig_cmp/' configure 
}

build(){
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc/dosbox 
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 

  # install docs, make does not install them
  install -Dm644 README "$pkgdir"/usr/share/doc/$pkgname/README
  install -Dm644 docs/README.video "$pkgdir"/usr/share/doc/$pkgname/README.video

  install -Dm644 ${srcdir}/${pkgname}.png \
    ${pkgdir}/usr/share/pixmaps/${pkgname}.png 
  install -Dm644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop 
}
