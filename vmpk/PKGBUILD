pkgname=vmpk
pkgver=0.6.2a
_pkgver=0.6.2
pkgrel=1
pkgdesc="MIDI event generator and receiver"
arch=('x86_64')
url="http://sourceforge.net/projects/vmpk/"
depends=('qt5-x11extras' 'drumstick')
makedepends=('cmake' 'docbook-xsl' 'qt5-tools')
optdepends=('soundfont-fluidr3: for MIDI sound'
            'soundfont-personalcopy: for MIDI sound'
            'soundfont-unison: for MIDI sound')
license=('GPL3')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$_pkgver/$pkgname-$pkgver.tar.bz2")
sha256sums=('d1453497690bf4df9155a3414b149b79c6ce5e474f00985d8ccdd9be678ca908')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
  
  #fix missing translation files
  sed -i s!vmpk_gl.ts!! \
  translations/CMakeLists.txt
  sed -i s!vmpk_sr.ts!! \
  translations/CMakeLists.txt
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=release
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  sed -i s!"Categories=AudioVideo;Audio;Midi;Education;Music;"!"Categories=AudioVideo;Music;"! \
  ${pkgdir}/usr/share/applications/vmpk.desktop
}
