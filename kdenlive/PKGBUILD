#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdenlive
pkgver=0.8.1svn
_pkgver=0.8svn20110722
pkgrel=2
pkgdesc="A non-linear video editor for Linux"
arch=('i686' 'x86_64')
url="http://www.kdenlive.org/"
license=('GPL')
depends=('kdelibs' 'mlt' 'dvgrab' 'kdebase-runtime' 'frei0r-plugins' 'qjson' 'qimageblitz')
optdepends=("dvdauthor: for creating dvds"
	    "recordmydesktop: for desktop-recordings")
makedepends=('automoc4' 'cmake' 'gettext' 'docbook-xsl')
#source=(http://downloads.sourceforge.net/kdenlive/kdenlive-$pkgver.tar.gz)
source=("http://chakra-project.org/sources//kdenlive/kdenlive-${_pkgver}.tar.bz2")
md5sums=('390fcbce02274ee99b2a6d6a234bef0c')

build() {
  export LDFLAGS="$LDFLAGS -lX11"

  cd "${srcdir}"
  mkdir build || true
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=/usr/lib \
    -DLINK_LIBRARIES=-lX11
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}