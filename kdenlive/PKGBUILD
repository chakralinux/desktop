# Contributions from Arch: https://www.archlinux.org/packages/kde-unstable/x86_64/kdenlive/
# Include global configuration

pkgname=kdenlive
pkgver=18.08.0
pkgrel=1
pkgdesc="A non-linear video editor for Linux"
arch=('x86_64')
url="http://www.kdenlive.org/"
license=('GPL')
depends=('knewstuff' 'knotifyconfig' 'kplotting' 'mlt' 'glu' 'hicolor-icon-theme' 'kfilemetadata')
optdepends=("dvdauthor: for creating dvds"
	    "recordmydesktop: for desktop-recordings"
	    "dvgrab: for firewire capture"
	    "xine-ui: for DVD preview"
	    "ffmpeg: for FFmpeg plugin"
	    "cdrkit: for creation of DVD ISO images"
	    "libdv: for webcam capture (if FFmpeg is not installed)"
	    "vid.stab: for video stabilize plugin ")
makedepends=('extra-cmake-modules' 'kdoctools' 'v4l-utils' 'python')
categories=('multimedia')
options=('debug')
screenshot=('http://kdenlive.org/sites/default/files/kdenlive-090a_0.png')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('43247d070e6898c26271235b915b45422ee8668e512f38f6df711e5571dca019'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare(){
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
