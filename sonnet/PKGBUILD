# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=sonnet
pkgver=${KFVersion}
pkgrel=1
pkgdesc='Provides spell-checking capabilities to applications.'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/sonnet'
license=('LGPL')
depends=('qt5-base' 'enchant')
makedepends=('extra-cmake-modules')
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('a8ec604893ddcf211d056687f30487e8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib 
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
