pkgname=krename
pkgver=4.0.9
pkgrel=7
pkgdesc="A very powerfull batch file renamer for KDE"
arch=('x86_64')
url="http://www.krename.net"
license=('GPL')
depends=('exiv2' 'kde-runtime' 'taglib' 'xdg-utils')
makedepends=('cmake' 'automoc4')
options=('libtool')
categories=('utils')
install=$pkgname.install
source=("http://sourceforge.net/projects/krename/files/KDE4%20krename-stable/$pkgver/$pkgname-$pkgver.tar.bz2"
        krename-gcc6.patch)
md5sums=('21b2daeee6d81155bd3c0fa2bc1b9a63'
         'ac1fe8bc93a7524a21d28edef168e888')

prepare() {
  cd $pkgname-$pkgver
  # Fix build with GCC 6
  patch -p1 -i ../krename-gcc6.patch
}

build() {
  cd $pkgname-$pkgver
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_CXX_FLAGS="-I/usr/include/freetype2"
  sed -i 's|/share/locale|/share/locale/kde4|' po/cmake_install.cmake
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
}
