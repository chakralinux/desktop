# Contributions from ArchLinux https://git.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/krename

pkgname=krename
pkgver=5.0.0
pkgrel=1
pkgdesc="A very powerful batch file renamer"
arch=(x86_64)
url="http://www.krename.net"
license=(GPL)
depends=('kio' 'kjs' 'exiv2' 'podofo' 'taglib' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules')
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0a61761853787fd46b35f3a733cf87cde00de5df631728332a64c38c670bd28c'
            'SKIP')
validpgpkeys=(D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heirecka@exherbo.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
