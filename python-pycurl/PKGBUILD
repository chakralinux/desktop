# Maintainer: UtG  <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

_pypiname=pycurl
pkgbase=python-$_pypiname
pkgname=('python3-pycurl' 'python2-pycurl')
pkgver=7.43.0
pkgrel=2
arch=('x86_64')
url="http://pycurl.sourceforge.net"
license=('LGPL' 'MIT')
makedepends=('python3' 'python2' 'curl' 'git')
source=("git+https://github.com/pycurl/pycurl.git#tag=REL_${pkgver//./_}")
sha1sums=('SKIP')

prepare() {
  cp -r ${_pypiname} python2-${_pypiname}

  cd "${srcdir}/${_pypiname}"
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python3|" {} +
    sed -i 's/"share", "doc", PACKAGE/"share", "doc", "python3-pycurl"/' setup.py
  sed -i "s/nosetests/nosetests3/" Makefile

  cd "${srcdir}/python2-${_pypiname}"
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python2|" {} +
    sed -i 's/"share", "doc", PACKAGE/"share", "doc", "python2-pycurl"/' setup.py
  sed -i "s/nosetests/nosetests2/" Makefile
}

build() {
  cd "${srcdir}/${_pypiname}"
  make
  python3 setup.py build --with-ssl

  cd "${srcdir}/python2-${_pypiname}"
  make
  python2 setup.py build --with-ssl
}

check() {
  export PYCURL_VSFTPD_PATH=vsftpd

  cd "$srcdir/$_pypiname"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.4:$PYTHONPATH" make test || warning "Tests failed"

  cd "${srcdir}/python2-${_pypiname}"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" make test || warning "Tests failed"
}

package_python3-pycurl() {
  pkgdesc="A Python 3.x interface to libcurl"
  depends=('python3' 'curl')
  provides=('pycurl')

  cd ${_pypiname}
  python3 setup.py install -O1 --root="${pkgdir}"
  install -D -m644 COPYING-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pycurl() {
  pkgdesc="A Python 2.x interface to libcurl"
  depends=('python2' 'curl')
  provides=('pycurl' 'python-pycurl')
  conflicts=('python-pycurl')
  replaces=('python-pycurl')

  cd python2-${_pypiname}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -D -m644 COPYING-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
