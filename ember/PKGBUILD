#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=ember
pkgver=0.7.1
_mediaver=$pkgver
pkgrel=2
pkgdesc="Ember is a 3D client using the OGRE 3d library, valid for WorldForge server."
arch=('i686' 'x86_64')
url="http://wiki.worldforge.org/wiki/Ember"
license=('GPL3')
depends=('atlas_cpp' 'boost' 'cegui-0.7' 'curl' 'eris' 'freealut' 'libsigc++2.0' 'libwfut' 'mercator' 'ogre' 'openal' 'sdl' 'skstream' 'toluapp' 'varconf' 'wfmath')
categories=('games')
source=("http://downloads.sourceforge.net/worldforge/$pkgname-$pkgver.tar.bz2"
	"http://downloads.sourceforge.net/worldforge/$pkgname-media-$_mediaver.tar.bz2")
md5sums=('bfcd6f5a9980b05172c45791dba611cf'
         '773df69df42628c4d9933a74f109335c')

build() {
  cd $srcdir/$pkgbase-$pkgver

  sed -e 's#.reset(0)#.reset()#' \
      -i src/components/ogre/SceneManagers/EmberPagingSceneManager/src/OgrePagingLandScapeData2D.cpp \
      -i src/components/ogre/SceneManagers/EmberPagingSceneManager/src/OgrePagingLandScapeData2DManager.cpp
  
  ./configure \
    --enable-cegui-toluapp \
    --prefix=/usr \
    --with-toluaxx
  make
}

package() {
  cd $srcdir/$pkgbase-$pkgver

  make DESTDIR=$pkgdir install

  # Data.
  mkdir -p $pkgdir/usr/share/$pkgname/media/
  cp -R $srcdir/$pkgname-media-$pkgver/media/* $pkgdir/usr/share/$pkgname/media/
}
