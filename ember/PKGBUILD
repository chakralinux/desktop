#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=ember
pkgver=0.7.0
_mediaver=$pkgver
pkgrel=6
pkgdesc="Ember is a 3D client using the OGRE 3d library, valid for WorldForge server."
arch=('i686' 'x86_64')
url="http://wiki.worldforge.org/wiki/Ember"
license=('GPL3')
depends=('atlas_cpp' 'boost' 'cegui' 'curl' 'eris' 'freealut' 'libsigc++2.0' 'libwfut' 'mercator' 'ogre' 'openal' 'sdl' 'skstream' 'toluapp' 'varconf' 'wfmath')
categories=('games')
source=("http://downloads.sourceforge.net/worldforge/$pkgname-$pkgver.tar.bz2"
	"http://downloads.sourceforge.net/worldforge/$pkgname-media-$_mediaver.tar.bz2")
md5sums=('7e7932129b0c0dce354d471ac052596f'
         '29aa6930d71f5bdc6b45be036d2227ec')

build() {
  cd $srcdir/$pkgbase-$pkgver
  
  ./configure \
    --enable-cegui-toluapp \
    --prefix=/usr \
    --with-toluaxx
  make
}

package() {
  cd $srcdir/$pkgbase-$pkgver

  make DESTDIR=$pkgdir install

  # Data.
  mkdir -p $pkgdir/usr/share/$pkgname/media/
  cp -R $srcdir/$pkgname-media-$pkgver/media/* $pkgdir/usr/share/$pkgname/media/
}
