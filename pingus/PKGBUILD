#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=pingus
pkgver=0.7.4
pkgrel=2
pkgdesc="A Lemmings clone, i.e. a level-based puzzle game."
arch=('i686' 'x86_64')
url="http://pingus.seul.org"
license=('GPL')
depends=('gcc-libs' 'sdl_image' 'sdl_mixer' 'boost-libs>=1.46')
makedepends=('boost>=1.46' 'scons')
source=("http://pingus.seul.org/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('82742bc9906cad98338cfd3ea700b396')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  scons prefix=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install PREFIX="${pkgdir}/usr"

  # This is needed... the standard installation method it TOTALLY broken in this script creation.
  cat > "${pkgdir}/usr/bin/pingus" <<EOF
#!/bin/sh
exec "/usr/bin/pingus.bin" --datadir "/usr/share/pingus"
EOF
  chmod 755 "${pkgdir}/usr/bin/pingus"
}

# vim:set ts=2 sw=2 et:
