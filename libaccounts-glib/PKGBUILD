# Contributors from Arch: https://www.archlinux.org/packages/extra/x86_64/libaccounts-glib/

pkgname=libaccounts-glib
pkgver=1.22
_commit=8565136
pkgrel=1
pkgdesc="Glib-based client library for accessing the online accounts database"
arch=('x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('dbus-glib' 'libxml2' 'sqlite3')
makedepends=('python2-gobject' 'docbook-xsl' 'gtk-doc')
source=("https://gitlab.com/accounts-sso/libaccounts-glib/repository/archive.tar.bz2?ref=$_commit")
md5sums=('895a6f7bb14ddf44d2c358773ed3baa3')

build() {
  cd $pkgname-*
  ./autogen.sh
  PYTHON=/usr/bin/python2 HAVE_GCOV_FALSE='#' ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-gtk-doc
  make
}

package() {
  cd $pkgname-*
  make DESTDIR="$pkgdir" install
}
