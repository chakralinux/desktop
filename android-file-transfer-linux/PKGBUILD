pkgname=android-file-transfer-linux
pkgver=3.1
pkgrel=1
pkgdesc="An interactive MTP client implemented in C++ using Qt toolkit"
arch=('x86_64')
url="https://github.com/whoozle/android-file-transfer-linux"
license=('GPL3')
depends=('qt5-base' 'fuse' 'hicolor-icon-theme' 'xdg-utils' 'libxkbcommon-x11' 'file')
makedepends=('cmake' 'ninja' 'mesa')
install=$pkgname.install
source=("https://github.com/whoozle/android-file-transfer-linux/archive/v$pkgver.tar.gz"
"solid_aftl.desktop")
sha256sums=('d6350e273fa18c3c6d9e0a65c620ec35f41ef7e926acdf423a454f597e4314ec'
            '743a0099e38fd82443b51434b65e65f0a64b2791bd2af89392f4f4d991c3bfc9')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -p build
}

build() {
  cd "$pkgname-$pkgver/build"

  cmake -G Ninja .. \
    -DCMAKE_INSTALL_PREFIX=/usr

  ninja
}

package() {
  cd "$pkgname-$pkgver"/build

  DESTDIR="$pkgdir" ninja install

  install -Dm644 ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  
  # added plasma5 integration
  install -D ../../solid_aftl.desktop $pkgdir/usr/share/solid/actions/solid_aftl.desktop
}
