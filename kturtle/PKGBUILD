# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdeedu-kturtle/
# Include global configuration

pkgname=kturtle
pkgver=18.08.3
pkgrel=1
pkgdesc="Educational programming environment"
url="http://kde.org/applications/education/kturtle/"
arch=('x86_64')
screenshot='https://www.kde.org/images/screenshots/kturtle.png'
license=('GPL' 'LGPL' 'FDL')
depends=('knewstuff' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeedu')
replaces=('kdeedu-kturtle')
conflicts=('kdeedu-kturtle')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8d7a83016ee726824f5cc44e5306895138335c0ec3f1fb245c3fba665c50cddc'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
