# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/libksane/
# Include global configuration
source ../kdeapps.conf

pkgname=kdegraphics-libksane
_pkgname=libksane
pkgver=${_kdever}
pkgrel=1
pkgdesc='SANE library interface for KDE'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdegraphics/libs/libksane'
license=('GPL' 'LGPL' 'FDL')
depends=("kdelibs>=${_libsver}" 'sane')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdegraphics' 'kde-uninstall')
provides=('libksane')
options=('docs' 'debug')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
