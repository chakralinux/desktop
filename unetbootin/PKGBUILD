pkgname=unetbootin
pkgver=657
pkgrel=1
pkgdesc="Allows the creation of bootable Live USB drives."
url="http://unetbootin.sourceforge.net/"
license=('GPL3')
arch=('x86_64')
depends=('kde-runtime' 'libpng' 'mtools' 'p7zip' 'qt' 'syslinux') 
groups=('system')
screenshot=('http://sourceforge.net/dbimage.php?id=300347')
# kde-runtime is added as dependency so kdesu is used to ask for password.
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-source-$pkgver.tar.gz")
md5sums=('50ad69c1d703e635d30c3aa4305093f7')

build() {
  lupdate $pkgname.pro
  lrelease $pkgname.pro
  qmake
  make
}

package() {
  # Executable installation.
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname

  # Translations installation.
  mkdir -p $pkgdir/usr/share/$pkgname
  install -m644 $pkgname_*.qm $pkgdir/usr/share/$pkgname/

  # Desktop integration.
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 ${pkgname}_48.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
