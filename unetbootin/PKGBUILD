#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>

pkgname=unetbootin
pkgver=563
pkgrel=1
pkgdesc="Allows the creation of bootable Live USB drives."
url="http://unetbootin.sourceforge.net/"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('kdebase-runtime' 'libpng' 'mtools' 'p7zip' 'qt' 'syslinux') 
# kdebase-runtime is added as dependency so kdesu is used to ask for password.
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-source-$pkgver.tar.gz)
md5sums=('d91b510eeb260f839bacbda7979ecfa5')

build() {
  lupdate $pkgname.pro
  lrelease $pkgname.pro
  qmake
  make
}

package() {
  # Executable installation.
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname

  # Translations installation.
  mkdir -p $pkgdir/usr/share/$pkgname
  install -m644 $pkgname_*.qm $pkgdir/usr/share/$pkgname/

  # Desktop integration.
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 ${pkgname}_48.png $pkgdir/usr/share/pixmaps/$pkgname.png
}