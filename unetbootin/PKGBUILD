# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=unetbootin
pkgver=585
pkgrel=1
pkgdesc="Allows the creation of bootable Live USB drives."
url="http://unetbootin.sourceforge.net/"
license=('GPL3')
arch=('x86_64')
depends=('kde-runtime' 'libpng' 'mtools' 'p7zip' 'qt' 'syslinux') 
categories=('system')
screenshot=('http://sourceforge.net/dbimage.php?id=300347')
# kdebase-runtime is added as dependency so kdesu is used to ask for password.
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-source-$pkgver.tar.gz")
md5sums=('483cc1c32d93141fe8f6e40dc8798881')

build() {
  lupdate $pkgname.pro
  lrelease $pkgname.pro
  qmake
  make
}

package() {
  # Executable installation.
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname

  # Translations installation.
  mkdir -p $pkgdir/usr/share/$pkgname
  install -m644 $pkgname_*.qm $pkgdir/usr/share/$pkgname/

  # Desktop integration.
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 ${pkgname}_48.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
