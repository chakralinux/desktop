# Chakra Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf
 

pkgname=privoxy
pkgver=3.0.17
pkgrel=1
pkgdesc='A web proxy with advanced filtering capabilities.'
arch=('i686' 'x86_64')
url='http://www.privoxy.org'
license=('GPL')
depends=('pcre' 'zlib')
makedepends=('autoconf')
categories=('network')
backup=('etc/conf.d/privoxy'
        'etc/privoxy/'{config,trust,default.action,user.filter,default.filter}
        'etc/privoxy/'{match-all,user}.action
        'etc/logrotate.d/privoxy')
install='privoxy.install'
source=("http://downloads.sourceforge.net/ijbswa/${pkgname}-${pkgver}-stable-src.tar.gz"
        'privoxy'
        'privoxy.logrotate.d'
        'privoxy.conf.d')
md5sums=('9d363d738a3f3d73e774d6dfeafdb15f'
         'a799e4d4f0ebb7f08190fa2bc961f439'
         'a364c34c8dc6eb78c9a6b777237932de'
         '27830ef79418e277b90c1c1fa933f876')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"

  autoheader
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc/privoxy

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"

  make prefix="${pkgdir}/usr" MAN_DEST="${pkgdir}/usr/share/man/man1" GROUP=42 \
    CONF_BASE="${pkgdir}/etc/privoxy" VAR_DEST="${pkgdir}/var" install

  # Don't overwrite existing log files!
  rm "${pkgdir}/var/log/privoxy/"*

  install -Dm0755 "${srcdir}/privoxy" "${pkgdir}/etc/rc.d/privoxy"
  install -Dm0644 "${srcdir}/privoxy.logrotate.d" "${pkgdir}/etc/logrotate.d/privoxy"
  install -Dm0644 "${srcdir}/privoxy.conf.d" "${pkgdir}/etc/conf.d/privoxy"

  # Fix config paths.
  sed -i -e 's#^confdir.*$#confdir /etc/privoxy#' -e 's#^logdir.*$#logdir /var/log/privoxy#' \
    -e '/^user-manual/s#.*#\#user-manual /usr/share/doc/privoxy/user-manual/#' \
    "${pkgdir}/etc/privoxy/config"

  # Fix group ownership and permissions.
  chgrp -R 42 "${pkgdir}/etc/privoxy/"
  chgrp -R 42 "${pkgdir}/var/log/privoxy/"

  find "${pkgdir}/etc/privoxy/" -type d | xargs chmod 0770
  find "${pkgdir}/etc/privoxy/" -type f | xargs chmod 0660
  chmod 0770 "${pkgdir}/var/log/privoxy"
}
