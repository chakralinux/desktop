#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=python2-shiboken
_pkgname=shiboken
_pkgname2=Shiboken
_pkgname3=pyside-shiboken
pkgver=1.2.1
_pyver=2.7
pkgrel=1
pkgdesc="Support library for Python bindings."
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.pyside.org"
depends=("python2" 'qt>=4.7' 'openssl')
makedepends=('cmake' 'automoc4')
provides=("apiextractor" "generatorrunner" "shiboken" "shiboken-generator")
replaces=("apiextractor" "generatorrunner" "shiboken" "shiboken-generator")
conflicts=("apiextractor" "generatorrunner" "shiboken" "shiboken-generator")
source=("${_pkgname}-${pkgver}.tar.gz::https://qt.gitorious.org/pyside/shiboken/archive/${pkgver}.tar.gz")
md5sums=('aef741d012e7d988cc39c064e82736c1')

build() {
  cd "${srcdir}/${_pkgname3}"
  mkdir -p build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    ..
  make shiboken
  make libshiboken
  make generator
}

package(){
  cd "${srcdir}/${_pkgname3}/build/libshiboken"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/${_pkgname3}/build/generator"
  make DESTDIR="${pkgdir}" install

  mkdir -m 755 -p "${pkgdir}/usr/lib/cmake/${_pkgname2}-${pkgver}"
  install -m 755 ../data/ShibokenConfig.cmake "${pkgdir}/usr/lib/cmake/${_pkgname2}-${pkgver}"
  install -m 755 ../data/ShibokenConfigVersion.cmake "${pkgdir}/usr/lib/cmake/${_pkgname2}-${pkgver}"
  install -m 755 "../data/ShibokenConfig-python${_pyver}.cmake" "${pkgdir}/usr/lib/cmake/${_pkgname2}-${pkgver}"

  mkdir -m 755 -p "${pkgdir}/usr/lib/pkgconfig"
  install -m 755 "../data/${_pkgname}.pc" "${pkgdir}/usr/lib/pkgconfig"
}
