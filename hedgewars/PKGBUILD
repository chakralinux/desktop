#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrian Chaves Fernandez (Gallaecio) <adriyetichaves@gmail.com>

pkgname=hedgewars
pkgver=0.9.19
_pkgver=${pkgver}
pkgrel=1
pkgdesc="Free turn based strategy game similar to Worms"
arch=('i686' 'x86_64')
url="http://hedgewars.org"
license=('GPL2')
depends=('ffmpeg' 'lua' 'qt' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'ttf-dejavu')
makedepends=('fpc' 'cmake' 'ghc' 'haskell-bytestring-show' 'haskell-utf8-string' 'haskell-dataenc' 'haskell-network' 
'haskell-hslogger' 'haskell-random' 'haskell-vector')
categories=('games')
source=(http://download.gna.org/$pkgname/$pkgname-src-${_pkgver}.tar.bz2)
md5sums=('51d84ad6ea00e9894e6119b118b4a523')

build() {
  cd $srcdir/$pkgname-src-$pkgver
  
  # Patches.
  sed -e "/^instance NFData B.ByteString\$/d" -i "gameServer/Actions.hs"
  
  mkdir -p build && cd build
  cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        ..
  make
}
 
package() {
  cd $srcdir/$pkgname-src-$pkgver/build
  make DESTDIR=$pkgdir install

  # DejaVu Font.
  rm $pkgdir/usr/share/$pkgname/Data/Fonts/DejaVuSans-Bold.ttf
  ln -s /usr/share/fonts/TTF/DejaVuSans-Bold.ttf $pkgdir/usr/share/$pkgname/Data/Fonts/DejaVuSans-Bold.ttf
}
