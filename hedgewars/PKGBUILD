pkgname=hedgewars
pkgver=0.9.22
pkgrel=3
pkgdesc="Free turn based strategy game similar to Worms"
arch=('x86_64')
url="http://hedgewars.org"
license=('GPL2')
depends=('ffmpeg' 'lua51' 'qt' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'ttf-dejavu' 'freeglut' 'physfs')
makedepends=('fpc' 'cmake'
	     # haskell specific
	     'ghc'
	     'haskell-network'
	     'haskell-bytestring-show'
	     'haskell-utf8-string'
	     #'haskell-dataenc'
	     'haskell-hslogger'
	     'haskell-vector'
	     'haskell-entropy'
	     'haskell-sha'
	     'haskell-zlib'
	     'haskell-random')
categories=('games')
source=("http://download.gna.org/$pkgname/$pkgname-src-${pkgver}.tar.bz2"
	hedgewars.png)
md5sums=('3718947ea69f57a76a3511678fb8173b'
         'eeb14d50df39063549ac5eca9dbc65d1')

prepare() {
  cd $pkgname-src-*
  sed -i 's|instance NFData (Chan a)$|instance NFData (Chan a) where rnf x = seq x ()|' gameServer/CoreTypes.hs
}	 

build() {
  cd $srcdir/$pkgname-src-*
  mkdir -p build && cd build
  #Don't know why ld can't link to haskell-sha correctly,disable it.
  cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE="Release" \
        -DDATA_INSTALL_DIR=/usr/share/hedgewars \
	-DNOSERVER=1 \
        ..
  make
}
 
package() {
  cd $srcdir/$pkgname-src-*/build
  make DESTDIR=$pkgdir install
  install -D -m644 $srcdir/hedgewars-src-*/Fonts_LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/Fonts_LICENSE.txt
  install -D -m644 $srcdir/hedgewars.png $pkgdir/usr/share/pixmaps/hedgewars.png
  install -D -m644 $srcdir/hedgewars-src-*/share/hedgewars/Data/misc/hedgewars.desktop $pkgdir/usr/share/applications/hedgewars.desktop
}
