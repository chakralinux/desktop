#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrian Chaves Fernandez (Gallaecio) <adriyetichaves@gmail.com>

pkgname=hedgewars
pkgver=0.9.18
_pkgver=${pkgver}-3
pkgrel=2
pkgdesc="Free turn based strategy game similar to Worms"
arch=('i686' 'x86_64')
url="http://hedgewars.org"
license=('GPL2')
depends=('lua' 'qt' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'ttf-dejavu')
makedepends=('fpc' 'cmake' 'ghc' 'haskell-bytestring-show' 'haskell-utf8-string' 'haskell-dataenc' 'haskell-network' 
'haskell-hslogger' 'haskell-vector')
categories=('games')
source=(http://download.gna.org/$pkgname/$pkgname-src-${_pkgver}.tar.bz2)
md5sums=('02465736fdcf48feea1cacaa0aa5960e')

build() {
  cd $srcdir/$pkgname-src-$pkgver
  
  # Patches.
  sed -e "/^instance NFData B.ByteString\$/d" -i "gameServer/Actions.hs"
  
  mkdir -p build && cd build
  cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        ..
  make
}
 
package() {
  cd $srcdir/$pkgname-src-$pkgver/build
  make DESTDIR=$pkgdir install

  # DejaVu Font.
  rm $pkgdir/usr/share/$pkgname/Data/Fonts/DejaVuSans-Bold.ttf
  ln -s /usr/share/fonts/TTF/DejaVuSans-Bold.ttf $pkgdir/usr/share/$pkgname/Data/Fonts/DejaVuSans-Bold.ttf
}
