pkgname=kmymoney
pkgver=4.8.0
pkgrel=4
pkgdesc="A finance manager for KDE4 with HBCI support"
arch=('x86_64')
url="http://kmymoney2.sourceforge.net"
license=('GPL')
depends=('kde-runtime' 'shared-mime-info' 'aqbanking' 'libofx>=0.9.10' 'libalkimia' 'boost-libs' 'kdepimlibs4')
makedepends=('cmake' 'automoc4' 'docbook-xml' 'docbook-xsl' 'boost' 'doxygen')
categories=('office')
install=$pkgname.install
source=(http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz
        4.8_build.patch
	kmymoney-gpgme-1.7.patch)
sha256sums=('c2a635710aafa6925a49803c480e043e0bf4bc5b62430689aa5eb95619f48ac2'
            '21cd9d7fece1b38b87ade8e4fc7f01397cf44a7fe0bc44cd08dda01d05207215'
            '5a240ebedd8e6f80892187fc6543d58125c4832aff0698c0aa53ba6302033535')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p build
  patch -Np1 -i "${srcdir}"/4.8_build.patch
  patch -Np1 -i "${srcdir}"/kmymoney-gpgme-1.7.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RELEASE -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}" install
}
