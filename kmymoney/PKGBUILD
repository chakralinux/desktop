# Maintainer: abveritas@chakra-project.org

pkgname=kmymoney
pkgver=4.6.4
pkgrel=1
pkgdesc="An finance manager for KDE4 with HBCI support"
arch=('x86_64')
url="http://kmymoney2.sourceforge.net"
license=('GPL')
depends=('kde-workspace' 'aqbanking-qt4' 'shared-mime-info' 'boost-libs' 'pulseaudio'
         'libofx' 'alkimia')
makedepends=('cmake' 'automoc4' 'gwenhywfar' 'docbook-xsl' 'boost' 'doxygen')
conflicts=(kmymoney2)
replaces=(kmymoney2)
categories=('office')

source=("http://sourceforge.net/projects/kmymoney2/files/KMyMoney-KDE4/$pkgver/$pkgname-$pkgver.tar.xz/download")
sha1sums=('3e4b17574684fd717fc75963d0a0c4d31aa81b71')

prepare() {
  cd "${srcdir}"
  rm -rf build
  mkdir build
}

build() {
  cd ${srcdir}/build

  cmake "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RELEASE \
        -DCMAKE_SKIP_RPATH=YES \
        -DKDE4_BUILD_TESTS=OFF
  make VERBOSE=1
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
