pkgname=mp3diags
pkgver=1.2.03
pkgrel=1
pkgdesc="GUI-based application that allows end-users to identify issues with their MP3 files, fix some of the issues and make other changes."
url="http://sourceforge.net/projects/mp3diags/"
license=('GPL')
arch=('x86_64')
depends=('qt' 'boost-libs')
makedepends=('boost')
optdepends=('mp3gain: MP3 normalization support')
categories=('multimedia')
install=mp3diags.install
source=("http://downloads.sourceforge.net/project/mp3diags/mp3diags-src/MP3Diags-${pkgver}.tar.gz")
sha512sums=('0f7a5d300a3366a2b5ed3da78e1b1bf892618e19e6ea0de444cffea0926d8370f8309fae4fececcea4bf494b1b4b51ec6c382dbb7e46d8286c7580da18675fcf')

build() {
  cd ${srcdir}/MP3Diags-$pkgver
  msg 'Building main application...'
  ./AdjustMt.sh
  echo 'QMAKE_MOC = $$QMAKE_MOC -DBOOST_TT_HAS_OPERATOR_HPP_INCLUDED -DBOOST_NO_TEMPLATE_PARTIAL_SPECIALIZATION' >> src/src.pro
  qmake
  make

  msg 'Building translation...'
  ./MakeTranslations.sh
}

package() {
  cd ${srcdir}/MP3Diags-$pkgver
  install -D bin/MP3Diags ${pkgdir}/usr/bin/MP3Diags
  install -D desktop/MP3Diags.desktop ${pkgdir}/usr/share/applications/MP3Diags.desktop
  for i in "16" "22" "24" "32" "36" "40" "48"; do
		install -d "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps"
		install -p -m644 "desktop/MP3Diags${i}.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/MP3Diags.png"
  done
  install -D ${srcdir}/MP3Diags-$pkgver/src/images/logo.svg ${pkgdir}/usr/share/pixmaps/MP3Diags.svg
  install -d ${pkgdir}/usr/share/${pkgname}/translations/
  install -Dm644 ${srcdir}/MP3Diags-$pkgver/src/translations/*.qm ${pkgdir}/usr/share/${pkgname}/translations/
}
