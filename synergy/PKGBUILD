# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributor: Ernesto Manriquez <alejandronova@gmail.com>

pkgname=synergy
pkgver=1.6.2
pkgrel=2
pkgdesc="Share your mouse and keyboard between several computers"
url="http://synergy-foss.org"
arch=('x86_64')
license=('GPL2')
depends=('gcc-libs' 'libxtst' 'libxinerama' 'crypto++' 'libxkbcommon' 'avahi')
makedepends=('libxt' 'cmake' 'qt5-base' 'unzip' 'subversion')
optdepends=('qt5-base: gui support')
license=('GPL2')
source=("synergy-${pkgver}.tar.gz::https://github.com/synergy/synergy/archive/${pkgver}.tar.gz"
        "synergys.socket"
        "synergys.service"
        "unfuck-cryptopp-thanks-gentoo.patch")
sha1sums=('e3cab850da2ac63c1d7649c5a9fa0c7c204ef9a4'
          '7ec33221725fc496b807e0f435c5e87b590beb5d'
          '79fe22835b88a3454048fe16b76edff1c9d68145'
          '8e321e664ae4b7a763175524dd938a88d85c7909')

build() {
  cd "${srcdir}/synergy-${pkgver}"

  # Unfuck the bundled cryptopp stuff. Thanks a lot, Gentoo!
  # You and Fedora are our only friends in this crazy world.
  patch -Np1 < "${srcdir}/unfuck-cryptopp-thanks-gentoo.patch"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_FLAGS="${CXXFLAGS} -pthread" .
  make -j1

  cd src/gui
  qmake-qt5
  make
}

package() {
  cd "${srcdir}/synergy-${pkgver}"

  # install binary
  install -Dm755 bin/synergy "$pkgdir/usr/bin/synergy"
  install -Dm755 bin/synergyc "$pkgdir/usr/bin/synergyc"
  install -Dm755 bin/synergys "$pkgdir/usr/bin/synergys"

  # install config
  install -Dm644 "doc/${pkgname}.conf.example" "${pkgdir}/etc/${pkgname}.conf.example" 
  install -Dm644 "doc/${pkgname}.conf.example-advanced" "${pkgdir}/etc/${pkgname}.conf.example-advanced"
  install -Dm644 "doc/${pkgname}.conf.example-basic" "${pkgdir}/etc/${pkgname}.conf.example-basic" 

  # install manfiles
  install -Dm644 "doc/${pkgname}c.man" "${pkgdir}/usr/share/man/man1/${pkgname}c.1"
  install -Dm644 "doc/${pkgname}s.man" "${pkgdir}/usr/share/man/man1/${pkgname}s.1"

  # install systemd service and socket
  install -Dm644 "$srcdir/synergys.service" "$pkgdir/usr/lib/systemd/system/synergys@.service"
  install -Dm644 "$srcdir/synergys.socket" "$pkgdir/usr/lib/systemd/system/synergys@.socket"

  # install desktop/icon stuff
  install -Dm644 "res/synergy.ico" "$pkgdir/usr/share/icons/synergy.ico"
  install -Dm644 "res/synergy.desktop" "$pkgdir/usr/share/applications/synergy.desktop"
}
