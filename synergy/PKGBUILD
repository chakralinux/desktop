# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# contributor: Ernesto Manriquez <alejandronova@gmail.com>

pkgname=synergy
pkgver=1.4.15
pkgrel=3
pkgdesc="Share your mouse and keyboard between several computers"
url="http://synergy-foss.org"
arch=('x86_64')
license=('GPL2')
depends=('gcc-libs' 'libxtst' 'libxinerama' )
makedepends=('libxt' 'cmake' 'qt' 'unzip' )
optdepends=('qt: gui support')
provides=('synergy-plus')
categories=('utils')
backup=('etc/synergy.conf')
source=("http://${pkgname}.googlecode.com/files/${pkgname}-${pkgver}-Source.tar.gz"
        "synergys.socket"
        "synergys.service")
sha256sums=('643745888ec6150e74cb6da9813f8c7572f2f87cf35f0bd4f64cc1e467ef3550'
            'aeb357ba161131524aa70a5303a17f78831c9a1ab17dbc7c97e4781a5c4bac44'
            'af2d1d7622ea189d773577a92d13164ca6a33f052f39de3fe466314265e59e0f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"
  
 # Unzip the crypto library
  unzip -d tools/cryptopp562 tools/cryptopp562.zip
  
  cmake -D CMAKE_INSTALL_PREFIX=/usr .
  make
  
  cd src/gui
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source/bin"
  
  #make DESTDIR="${pkgdir}" install # doesn't work atm
  
  # install binary
  install -Dm755 synergy "$pkgdir/usr/bin/synergy"
  install -Dm755 synergyc "${pkgdir}/usr/bin/"
  install -Dm755 synergys "${pkgdir}/usr/bin/"
    
  # Install systemd service and socket
  install -d "${pkgdir}/usr/lib/systemd/system"
  install -Dm644 "${srcdir}/synergys.service" "${pkgdir}/usr/lib/systemd/system/"
  install -Dm644 "${srcdir}/synergys.socket" "${pkgdir}/usr/lib/systemd/system/"
  
  # install config
  install -m755 -d "${pkgdir}/etc"
  install -m644 "${srcdir}/${pkgname}-${pkgver}-Source/doc/${pkgname}.conf.example" "${pkgdir}/etc/"
  
   # install desktop/icon stuff
  cd ../res
  sed -i 's/icons/pixmaps/' synergy.desktop
  install -Dm644 "synergy.ico" "$pkgdir/usr/share/pixmaps/synergy.ico"
  install -Dm644 "synergy.desktop" "$pkgdir/usr/share/applications/synergy.desktop"
}
