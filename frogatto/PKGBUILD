pkgname=frogatto
pkgver=1.3.1
pkgrel=4
pkgdesc="An old-school 2D platformer game, starring a certain quixotic frog."
arch=('x86_64')
url="http://www.frogatto.com"
license=("custom: GPL3 code, non-free data")
depends=('boost-libs' 'glew' 'icu' 'libgl' 'libpng' 'mesa' 'sdl' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
makedepends=('boost')
categories=('games')
source=("https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('f3206fbe1395ea2878092fbd4ed2c591'
         'e31563b04748a39292a59aaad633ff58')

build() {
  cd $pkgname-$pkgver

  sed -i 's/ccache //g' Makefile
  sed -i 's/-lprofiler//g' Makefile
  sed -i 's/\.io_service/\.get_io_service/g' src/server.cpp
  sed -i 's/\%lu\\n/\%zu\\n/g' src/server.cpp
  sed -i 's/-lboost_regex-mt/-lboost_regex/g' Makefile
  sed -i 's/-lboost_system-mt/-lboost_system/g' Makefile
  sed -i 's/-lboost_thread-mt/-lboost_thread/g' Makefile
  sed -i 's/-lboost_iostreams-mt/-lboost_iostreams/g' Makefile
  CXXFLAGS="$CXXFLAGS -std=gnu++98" make game server
}

package() {
  cd $pkgname-$pkgver

  # Binaries:
  install -Dm755 game $pkgdir/usr/share/$pkgname/game
  install -Dm755 server $pkgdir/usr/share/$pkgname/server
  
  # Data:
  mkdir -p $pkgdir/usr/share/frogatto/
  cp -r data $pkgdir/usr/share/frogatto/
  cp -r images $pkgdir/usr/share/frogatto/
  cp -r modules $pkgdir/usr/share/frogatto/
  cp -r music $pkgdir/usr/share/frogatto/
  install -D -m644 FreeMono.ttf $pkgdir/usr/share/frogatto/FreeMono.ttf
  install -D -m644 DejaVuSans.ttf $pkgdir/usr/share/frogatto/DejaVuSans.ttf

  # Run script.
  mkdir -p $pkgdir/usr/bin
  echo "#!/bin/sh"				>  $pkgdir/usr/bin/$pkgname
  echo "cd /usr/share/frogatto"			>> $pkgdir/usr/bin/$pkgname
  echo 'exec /usr/share/frogatto/game $*'	>> $pkgdir/usr/bin/$pkgname
  chmod +x $pkgdir/usr/bin/$pkgname

  # License: 
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  # Desktop integration and execution script:
  cd $srcdir
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
