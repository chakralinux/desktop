
pkgname=libakonadi-qt4
pkgver=1.13.0
pkgrel=8
pkgdesc="Qt4 akonadi libraries"
arch=('x86_64')
url='http://community.kde.org/KDE_PIM/Akonadi'
license=('LGPL')
depends=('qt')
makedepends=('cmake' 'automoc4' 'boost' 'postgresql' 'mariadb' 'libxslt')
conflicts=('akonadi<15')
install="${pkgname}.install"
source=("http://download.kde.org/stable/akonadi/src/akonadi-${pkgver}.tar.bz2"
        dont-leak-old-external-payload-files.patch)
md5sums=('84eb2e471bd6bdfe54a2a2f1d858c07d'
         '9711e300dde3775a38637daa3c20befe')

prepare() {
  mkdir -p build

  cd akonadi-$pkgver
  # https://bugs.kde.org/show_bug.cgi?id=341884
  patch -p1 -i ../dont-leak-old-external-payload-files.patch
}

build() {
  cd build
  cmake ../akonadi-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_QSQLITE_IN_QT_PREFIX=TRUE \
    -DDATABASE_BACKEND=SQLITE \
    -DWITH_SOPRANO=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# conflicts with akonadi
  rm -r "$pkgdir"/usr/{bin,share}
}
