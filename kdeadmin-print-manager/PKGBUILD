# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdeadmin-print-manager
_pkgname=print-manager
pkgver=${_kdever}
pkgrel=1
pkgdesc="A KDE tool for managing print jobs and printers"
url="https://projects.kde.org/projects/playground/base/print-manager"
arch=('x86_64')
license=('GPL'
         'LGPL'
         'FDL')
depends=("kde-workspace>=${_workspace_ver}"
         "libcups"
         "system-config-printer>=1.3.12")
makedepends=('cmake'
             'automoc4')
install=${pkgname}.install
groups=("kde"
        "kdeadmin"
        "kde-uninstall")
provides=('print-manager')
replaces=('kdeadmin-system-config-printer'
          'print-manager'
          'kdeutils-printer-applet')
conflicts=('print-manager')
options=('docs'
         '!header'
         'debug'
         'log')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz"
        'libcups2_version.diff')
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`
            '3fe146c93765f20d9e8156c82132a3fadaaee6481b5a0bb33599d38bbdb72106')

prepare (){
    cd ${srcdir}/${_pkgname}-${pkgver}
    patch -p1 -i "${srcdir}/libcups2_version.diff"
}

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
