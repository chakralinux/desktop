# Include global configuration

pkgname=ktp-call-ui
pkgver=18.12.2
pkgrel=1
pkgdesc="Telepathy call handler "
arch=('x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kconfig' 'kdeclarative' 'kiconthemes' 'ki18n' 'knotifications' 'kxmlgui' 'kcmutils'
         'ktp-common-internals' 'telepathy-farstream'
         'telepathy-mission-control' 'telepathy-gabble' 'gst-plugins-base'
         'gst-plugins-good' 'qt5-gstreamer' 'gst-libav')
makedepends=('boost' 'extra-cmake-modules' 'kdoctools')
groups=(kde kde-applications kde-telepathy kde-uninstall)
options=('docs' 'debug')

source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a77285cc623d1b50584259bb6b98b04a2fdf026470c6d31f124e0323b0934274'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}

