# Maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributors: H W TovetjÃ¤rn (totte) <totte@tott.es>
#		Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

## NOTE: when you update Haskell packages you'll have to update the entire stack to not break it ##

_pkgname=vector
pkgname=haskell-vector
pkgver=0.10.9.1
pkgrel=2
pkgdesc="Efficient Arrays"
url="http://hackage.haskell.org/package/vector"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc=7.6.3-1' 'haskell-deepseq>=1.1' 'haskell-deepseq<1.4' 'haskell-primitive>=0.5.0.1' 'haskell-primitive<0.6')
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('205656e9741aad406e427e988dbf1665bf5dbdf3e9d574e309c274be4e06bde5')


build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/${pkgname}/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/vector"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
