# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W TovetjÃ¤rn (totte) <totte@tott.es>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

_pkgname=vector
pkgname=haskell-vector
pkgver=0.10.9.1
pkgrel=1
pkgdesc="Efficient Arrays"
url="http://hackage.haskell.org/package/vector"
license=('custom:BSD3')
arch=('x86_64')
makedepends=()
depends=('ghc=7.6.3-1' 'haskell-base>=4.0' 'haskell-deepseq>=1.1' 'haskell-deepseq<1.4' 'haskell-ghc-prim' 'haskell-primitive>=0.5.0.1' 'haskell-primitive<0.6')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
install="haskell-vector.install"
sha512sums=('8eab2fb1d225f7a53d82721147bf78a05714bc1484e9b7bdd1d1bc4720e42d71bda0ffe92e60b8c6c65287b64c3bd738ff597ea8caa5b05e15a822b8563aba3c')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix="/usr" \
        --docdir="/usr/share/doc/haskell-vector" \
        --libsubdir='$compiler/site-local/$pkgid'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/haskell-vector/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/haskell-vector/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/haskell-vector/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/vector"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/haskell-vector/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/haskell-vector/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
