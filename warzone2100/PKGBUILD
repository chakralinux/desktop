pkgname=warzone2100
pkgver=3.2.3
pkgrel=1
pkgdesc="3D real-time strategy game where you fight to rebuilt the world destroyed by nuclear war."
arch=('x86_64')
url="https://wz2100.net/"
license=('GPL')
categories=('games')
depends=('libgl' 'openal' 'libjpeg' 'libpng' 'libvorbis' 'libtheora' 'libmad'
	 'physfs' 'mesa' 'quesoglc' 'popt' 'ttf-dejavu' 'qt5-base' 'qt5-script'
	 'qt5-x11extras' 'glew' 'sdl2')
makedepends=('gawk' 'bison' 'flex' 'zip' 'unzip' 'libxrandr' 'asciidoc')
conflicts=('warzone2100-dev')
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz"
	fix-qt-build.patch)
sha256sums=('fcab9e860203e7dacda6ee60384da490a9d44aeafc07aa31ff25f9c548be7582'
            '59529cb62e5985ed9934fdf3621a98ecf2b3b54d4bd7d0793b5317156934712f')

build() {
  cd ${pkgname}-${pkgver}

  # Fix build
  patch -Np1 -i ${srcdir}/fix-qt-build.patch

  ./configure \
    --prefix=/usr \
    --with-backend=qt \
    --with-distributor="Chakra GNU/Linux"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
