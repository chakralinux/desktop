pkgname=warzone2100
pkgver=3.2.1
pkgrel=1
pkgdesc="3D real-time strategy game where you fight to rebuilt the world destroyed by nuclear war."
arch=('x86_64')
url="http://wz2100.net/"
license=('GPL')
groups=()
categories=('games')
depends=('libgl' 'openal' 'libjpeg' 'libpng' 'libvorbis' 'libtheora' 'libmad'
	 'physfs' 'mesa' 'quesoglc' 'popt' 'ttf-dejavu' 'qt5-base' 'qt5-script'
	 'qt5-x11extras' 'glew' 'sdl2')
makedepends=('gawk' 'bison' 'flex' 'zip' 'unzip' 'libxrandr' 'asciidoc')
conflicts=('warzone2100-dev')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz"
	warzone2100.appdata.xml)
sha256sums=('0f66b76c50c598abcc895134bd49aab5dd46dcf8135645f2505e93077402a6d9'
            'd6fd6bf40bd72d57a2872b3e439bed04f4cd78e0a7b2993de580ed765b80cfc4')

build() {
  cd ${pkgname}-${pkgver}

  # Add missing file from the tarball
  cp ${srcdir}/warzone2100.appdata.xml icons/

  ./configure \
    --prefix=/usr \
    --with-backend=qt \
    --with-distributor="Chakra GNU/Linux"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
