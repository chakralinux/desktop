#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=polkit
pkgver=0.99
pkgrel=2
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i686 x86_64)
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/Policykit"
depends=('glib2>=2.25.15' 'pam' 'expat>=2.0.1')
makedepends=('intltool>=0.41.1' 'gobject-introspection>=0.9.5' 'docbook-xsl')
replaces=('policykit')
options=('!libtool')
source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
        polkit.pam
        disable-gtk-doc.patch)
md5sums=('fcc4d7b19c08ad54d3ce0eae0ab12398'
         '6564f95878297b954f0572bc1610dd15'
	 'ad88086ef2b04fec5b69118ca3d47801')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/disable-gtk-doc.patch"
  libtoolize --force
  autoreconf
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-1 \
      --disable-static --disable-gtk-doc --with-os-type=chakra || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m644 "${srcdir}/polkit.pam" "${pkgdir}/etc/pam.d/polkit-1" || return 1
}
