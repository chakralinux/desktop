#
# Chakra Packages for Chakra, part of chakra-project.org
#
# Maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>
#

pkgname=polkit
pkgver=0.98
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i686 x86_64)
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/Policykit"
depends=('glib2>=2.25.15' 'pam' 'expat>=2.0.1')
makedepends=('intltool>=0.41.1' 'gobject-introspection>=0.9.5' 'docbook-xsl')
replaces=('policykit')
options=('!libtool')
source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
        polkit-git-fixes.patch
        polkit.pam
        disable-gtk-doc.patch
	configure-fix.patch)
md5sums=('96e583a1177ba5436f034a2fee55f5fa'
	 'c5aaa13d6eeda5cec224b273acba9420'
         '6564f95878297b954f0572bc1610dd15'
	 'ad88086ef2b04fec5b69118ca3d47801'
	 '9649eaeac5293903f9e4831c41bcd6c1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/polkit-git-fixes.patch"
  patch -Np1 -i "${srcdir}/disable-gtk-doc.patch"
  libtoolize --force
  autoreconf
  patch -Np1 -i "${srcdir}/configure-fix.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-1 \
      --disable-static --disable-gtk-doc || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m644 "${srcdir}/polkit.pam" "${pkgdir}/etc/pam.d/polkit-1" || return 1
}
