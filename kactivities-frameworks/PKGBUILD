source ../frameworks.conf

pkgname=kactivities-frameworks
_pkgname=kactivities
pkgver=${KFVersion}
pkgrel=1
pkgdesc="Core components for the KDE's Activities"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
depends=('kcmutils' 'kdeclarative')
makedepends=('extra-cmake-modules' 'boost' 'python')
options=("debug")
groups=('kf5')
source=("$KFServer/${pkgver}/${_pkgname}-${pkgver}.tar.xz")
sha256sums=( $(getSum ${_pkgname}) )

prepare() {
  mkdir -p ${srcdir}/build
}

build() {
  cd ${srcdir}/build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib
  make
}

check() {
  cd ${srcdir}/build
  make test
}

package() {
  cd ${srcdir}/build
  make DESTDIR="${pkgdir}" install
}
