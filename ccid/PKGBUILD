# contribution from Arch https://www.archlinux.org/packages/community/x86_64/ccid/

pkgname=ccid
pkgver=1.4.23
_code=112
pkgrel=1
pkgdesc="A generic USB Chip/Smart Card Interface Devices driver"
arch=('x86_64')
url="http://pcsclite.alioth.debian.org/ccid.html"
license=('LGPL' 'GPL')
depends=('pcsclite')
backup=(etc/reader.conf.d/libccidtwin)
source=("https://alioth.debian.org/frs/download.php/latestfile/${_code}/ccid-${pkgver}.tar.bz2")
sha256sums=('0836adceb6eee2925e9933e377a4c8cc86a285525c2b54520ad0da16d113ec68')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-twinserial \
              --enable-serialconfdir=/etc/reader.conf.d
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
    # move the configuration file in /etc and create a symbolic link
  mv "${pkgdir}/usr/lib/pcsc/drivers/ifd-ccid.bundle/Contents/Info.plist" "${pkgdir}/etc/libccid_Info.plist"
  ln -s /etc/libccid_Info.plist ${pkgdir}/usr/lib/pcsc/drivers/ifd-ccid.bundle/Contents/Info.plist
}
