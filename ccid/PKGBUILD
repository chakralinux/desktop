pkgname=ccid
pkgver=1.4.18
_code=4111
pkgrel=1
pkgdesc="A generic USB Chip/Smart Card Interface Devices driver"
arch=('x86_64')
url="http://pcsclite.alioth.debian.org/ccid.html"
license=('LGPL' 'GPL')
depends=('pcsclite')
backup=(etc/reader.conf.d/libccidtwin)
source=("https://alioth.debian.org/frs/download.php/file/${_code}/$pkgname-$pkgver.tar.bz2")
sha512sums=('ff7e331431909fe9fffd4f7aae0a2395b3b755da9c898aa6a6b629f7f516c240b820669146791deb443c646dc8fc56f9100c9f987b3e7d13c24f24f81f06b1f0')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure \
    --enable-twinserial \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 src/92_pcscd_ccid.rules  $pkgdir/usr/lib/udev/rules.d/85-pcscd-ccid.rules
}
