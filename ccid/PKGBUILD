# contribution from Arch https://www.archlinux.org/packages/community/x86_64/ccid/

pkgname=ccid
pkgver=1.4.24
_code=112
pkgrel=2
pkgdesc="A generic USB Chip/Smart Card Interface Devices driver"
arch=('x86_64')
url="http://pcsclite.alioth.debian.org/ccid.html"
license=('LGPL' 'GPL')
depends=('pcsclite')
backup=(etc/reader.conf.d/libccidtwin)
source=("https://alioth.debian.org/frs/download.php/latestfile/${_code}/ccid-${pkgver}.tar.bz2")
sha256sums=('62cb73c6c009c9799c526f05a05e25f00f0ad86d50f82a714dedcfbf4a7e4176')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-twinserial \
              --enable-serialconfdir=/etc/reader.conf.d
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
    # move the configuration file in /etc and create a symbolic link
  mv "${pkgdir}/usr/lib/pcsc/drivers/ifd-ccid.bundle/Contents/Info.plist" "${pkgdir}/etc/libccid_Info.plist"
  ln -s /etc/libccid_Info.plist ${pkgdir}/usr/lib/pcsc/drivers/ifd-ccid.bundle/Contents/Info.plist
}
