# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# (c) 2010 Drake Justice

pkgname=cbundle
pkgver=0.4.7
pkgrel=1
depends=('squashfs-tools' 'fakechroot' 'unionfs-fuse' 'kmod' 'sh')
makedepends=('qt' 'automoc4' 'kde-workspace')
pkgdesc="A shared library for using Chakra software bundles"
arch=("i686" "x86_64")
license=('GPL')
url="http://chakra-project.org/"

source=("http://chakra-linux.org/sources/cinstall/${pkgname}-${pkgver}.tar.xz")
md5sums=('bae926383224c35c6da3f833b9bb6220')

# create tarball: source PKGBUILD && mksource

mksource() {
  git clone -n "git://gitorious.org/chakra/${pkgname}.git"

  cd "${pkgname}"

  git archive --prefix="${pkgname}-${pkgver}/" --format=tar "${pkgver}" \
    | xz > "../${pkgname}-${pkgver}.tar.xz"

  cd ../

  md5sum "${pkgname}-${pkgver}.tar.xz"
}

build() {
  cd "${srcdir}"

  mkdir -p build && cd build

  cmake "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
  || return 1

  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
