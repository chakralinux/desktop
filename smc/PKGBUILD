#
# Games Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=smc
pkgver=1.9
_musicver=5.0
pkgrel=10
pkgdesc="Secret Maryo Chronicles"
arch=('i686' 'x86_64')
url="http://www.secretmaryo.org/"
license=('GPL3')
depends=('sdl_image' 'sdl_ttf' 'sdl_mixer' 'cegui>=0.7' 'boost' 'libpng')
makedepends=('autoconf' 'automake')
categories=('games')
source=(http://downloads.sourceforge.net/smclone/$pkgname-$pkgver.tar.bz2
        http://downloads.sourceforge.net/smclone/SMC_Music_${_musicver}_high.zip
        smc-for-cegui-v0-7.diff
        smc.desktop
        smc.png)
md5sums=('75ab7826303c49aec25b052a8b90287f'
         '0bccac29052d3eed93e88535312eaaa2'
         '51dfea21b741ad19df14e738df52f1cf'
         'aca433b9b28a4959d3bc94516547a267'
         'efca7580e28748625eb676c9d24ee122')

build() {
  cd $srcdir/$pkgname-$pkgver

  # to remove when smc will be compatible with cegui-0.7.x
  patch -Np1 -i ${srcdir}/smc-for-cegui-v0-7.diff

  # fix for boost-1.46
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  ./autogen.sh
  LIBS+="-lX11 -lboost_system" \
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make install LDADD=-lpng DESTDIR=$pkgdir

  # Desktop Integration.
  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png

  cp -R $srcdir/data/music $pkgdir/usr/share/smc/
}
