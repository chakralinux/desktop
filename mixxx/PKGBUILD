#contributions from Arch: https://www.archlinux.org/packages/community/x86_64/mixxx/

pkgname=mixxx
pkgver=2.1.0
pkgrel=1
pkgdesc="Digital DJ mixing software with cpu optimization enabled"
arch=('x86_64')
url="http://www.mixxx.org/"
license=('GPL2')
depends=('glu' 'fftw' 'libsndfile' 'libogg' 'taglib' 'libid3tag' 'libmad' 'libvorbis' 'portaudio'
         'portmidi' 'flac' 'opus' 'faad2' 'libmp4v2' 'chromaprint' 'rubberband' 'qt' 'upower'
         'libshout' 'vamp-plugin-sdk' 'libusbx' 'protobuf' 'opusfile' 'soundtouch')
makedepends=('mesa' 'scons' 'pkgconfig')
categories=('multimedia')
screenshot=("http://www.mixxx.org/images/splash1.png")
source=("https://github.com/${pkgname}dj/${pkgname}/archive/release-${pkgver}.tar.gz")
sha256sums=('148b0ab222fa1c310e62b566d87c0a3d43fdee9435150d40d62055dbc228d396')

build() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"
  export SCONSFLAGS="-j $(nproc)"
  scons build=release optimize=portable virtualize=0 localecompare=1 qt_sqlite_plugin=0 opus=1 \
    qtdir=/usr/lib/qt prefix=/usr faad=1
}

package() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"
  scons qtdir=/usr/lib/qt prefix=/usr install_root=$pkgdir/usr install 
}
