
pkgname=openscenegraph
_pkgname=OpenSceneGraph
pkgver=3.6.3
pkgrel=1
pkgdesc="FOSS high performance real-time graphics toolkit."
arch=('x86_64')
license=('custom:OSGPL')
url="http://www.openscenegraph.org"
depends=('curl' 'giflib' 'jasper' 'librsvg' 'pth' 'xine-lib')
makedepends=('cmake' 'libvncserver' 'qt5-base' 'ffmpeg' 'mesa' 'gdal')
optdepends=('libvncserver' 'gdal' 'openexr' 'poppler-glib' 'qt5-base')
categories=('games')
source=("https://github.com/$pkgname/$_pkgname/archive/$_pkgname-$pkgver.tar.gz")
md5sums=('3bc355c7dcd3f9eef84d4dc82dd0b19d')

build() {
  cd $_pkgname-$_pkgname-$pkgver

  mkdir -p build && cd build
 
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..

  make
}

package() {
  cd $_pkgname-$_pkgname-$pkgver/build
  make DESTDIR=$pkgdir install

  # Fix installation directory.
  mv $pkgdir/usr/lib64 $pkgdir/usr/lib

  # License.
  install -D -m644 ../LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}
