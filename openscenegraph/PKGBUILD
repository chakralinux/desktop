#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=openscenegraph
_pkgname=OpenSceneGraph
pkgver=3.0.1
pkgrel=1
pkgdesc="FOSS high performance real-time graphics toolkit."
arch=('i686' 'x86_64')
license=('custom:OSGPL')
url="http://www.openscenegraph.org"
depends=('curl' 'giflib' 'jasper' 'librsvg' 'pth' 'xine-lib')
makedepends=('cmake' 'libvncserver')
optdepends=('libvncserver' 'gdal' 'openexr' 'poppler-glib')
source=($url/downloads/stable_releases/$_pkgname-$pkgver/source/$_pkgname-$pkgver.zip)
md5sums=('c43a25d023e635c3566b2083d8e6d956')

build() {
  cd $srcdir/$_pkgname-$pkgver
  # Using “Release” instead of “RelWithDebInfo” on purpose, else there are problems.
  mkdir build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver/build
  make DESTDIR=$pkgdir install

  # Fix installation directory.
  [ -d $pkgdir/usr/lib64 ] && mv $pkgdir/usr/lib64 $pkgdir/usr/lib || true

  # License.
  install -D -m644 ../LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}
