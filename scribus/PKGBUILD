# maintainer abveritas@chakra-project.org

pkgname=scribus
pkgver=1.4.6
pkgrel=2
pkgdesc="A desktop publishing program"
arch=('x86_64')
url="http://www.scribus.net"
license=('GPL')
install=${pkgname}.install
depends=('libcups' 'lcms' 'qt' 'ghostscript' 'hyphen' 'podofo'
         'libart-lgpl' 'python2' 'libxml2' 'cairo' 'boost-libs'
         'desktop-file-utils' 'shared-mime-info' 'ruby' 'hunspell')
makedepends=('cmake' 'boost')
screenshot=('http://upload.wikimedia.org/wikipedia/commons/f/f4/Scribus-1.3-Linux.png')
options=(!libtool)
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('f04a2eecfb531af4beb99645f85165be161b00a3')

prepare() {
  cd ${pkgname}-${pkgver}

  # fix icon path in .desktop file
  sed 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' -i scribus.desktop
}

build() {
	mkdir -p build
	cd build
	
	cmake ../${pkgname}-${pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWITH_ASPELL=0 -DWANT_HUNSPELL=1 \
	   -DWANT_CAIRO=1 \
	   -DLIB_SUFFIX=""
	make 
}

package() {
	cd build
	
	make DESTDIR="${pkgdir}" install
	
	install -Dm644 "${srcdir}/${pkgname}-${pkgver}/scribus.desktop" \
	   "${pkgdir}/usr/share/applications/scribus.desktop"
}
