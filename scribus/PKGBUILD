#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=scribus
pkgver=1.3.9.8
_pkgver=1.4.0.rc6
pkgrel=1
pkgdesc="A desktop publishing program"
arch=('i686' 'x86_64')
url="http://www.scribus.net"
license=('GPL')
install=scribus.install
depends=('libcups>=1.3.11' 'lcms>=1.18' 'qt' 'ghostscript>=8.70' \
         'libart-lgpl>=2.3.20' 'python2' 'libxml2>=2.7.3' 'cairo' \
         'desktop-file-utils' 'shared-mime-info' 'ruby')
makedepends=('cmake')
options=(!libtool !makeflags)
source=(http://sourceforge.net/projects/scribus/files/scribus-devel/${_pkgver}/${pkgname}-${_pkgver}.tar.bz2)
sha1sums=('1962052d5faf52dcb6988e4e0ff87d5d626351d1')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${_pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_CAIRO=1 \
	   -DLIB_SUFFIX=""
	make 
}

package() {
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install

	# fix icon path in .desktop file
	sed -i 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' \
	   ${srcdir}/${pkgname}-${_pkgver}/scribus.desktop

	install -Dm644 ${srcdir}/${pkgname}-${_pkgver}/scribus.desktop \
	   ${pkgdir}/usr/share/applications/scribus.desktop
}
