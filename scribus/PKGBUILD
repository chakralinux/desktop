pkgname=scribus
pkgver=1.4.7
pkgrel=2
pkgdesc="A desktop publishing program"
arch=('x86_64')
url="https://www.scribus.net"
license=('GPL')
install=$pkgname.install
depends=('libcups' 'lcms' 'qt' 'ghostscript' 'hyphen' 'podofo'
         'libart-lgpl' 'python2' 'libxml2' 'cairo' 'boost-libs'
         'desktop-file-utils' 'shared-mime-info' 'ruby' 'hunspell')
makedepends=('cmake' 'boost')
screenshot=('http://upload.wikimedia.org/wikipedia/commons/f/f4/Scribus-1.3-Linux.png')
source=("https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('42d335b4a59c26c8ae1e3f601676baa3c42b035b8cde326d195f7a30078e5fec')

build() {
	mkdir -p build
	cd build
	
	cmake ../$pkgname-$pkgver \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWITH_ASPELL=0 -DWANT_HUNSPELL=1 \
	   -DWANT_CAIRO=1 \
	   -DLIB_SUFFIX=""
	make 
}

package() {
	cd build
	
	make DESTDIR=$pkgdir install
	
	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/scribus.desktop "${pkgdir}/usr/share/applications/scribus.desktop"
	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/resources/icons/AppIcon.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps/scribus.png"
}
