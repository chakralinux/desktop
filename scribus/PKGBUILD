#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=scribus
pkgver=1.4.1
pkgrel=2
pkgdesc="A desktop publishing program"
arch=('x86_64')
url="http://www.scribus.net"
license=('GPL')
install=scribus.install
depends=('libcups' 'lcms' 'qt' 'ghostscript' \
         'libart-lgpl' 'python2' 'libxml2' 'cairo' \
         'desktop-file-utils' 'shared-mime-info' 'ruby')
makedepends=('cmake')
screenshot=('http://upload.wikimedia.org/wikipedia/commons/f/f4/Scribus-1.3-Linux.png')
options=(!libtool !makeflags)
source=("http://sourceforge.net/projects/scribus/files/scribus/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('fb48bc492d79abc2275b1903692defe9b3931adf')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../Scribus \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_CAIRO=1 \
	   -DLIB_SUFFIX=""
	make 
}

package() {
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install

	# fix icon path in .desktop file
	sed -i 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' \
	   ${srcdir}/Scribus/scribus.desktop

	install -Dm644 ${srcdir}/Scribus/scribus.desktop \
	   ${pkgdir}/usr/share/applications/scribus.desktop
}
