#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=scribus
pkgver=1.4.1
pkgrel=1
pkgdesc="A desktop publishing program"
arch=('i686' 'x86_64')
url="http://www.scribus.net"
license=('GPL')
install=scribus.install
depends=('libcups' 'lcms' 'qt' 'ghostscript' \
         'libart-lgpl' 'python2' 'libxml2' 'cairo' \
         'desktop-file-utils' 'shared-mime-info' 'ruby')
makedepends=('cmake')
screenshot=('http://upload.wikimedia.org/wikipedia/commons/f/f4/Scribus-1.3-Linux.png')
options=(!libtool !makeflags)
source=(http://sourceforge.net/projects/scribus/files/scribus/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha1sums=('50601946c7ffe55090900784b51c98a156da8f13')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_CAIRO=1 \
	   -DLIB_SUFFIX=""
	make 
}

package() {
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install

	# fix icon path in .desktop file
	sed -i 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' \
	   ${srcdir}/${pkgname}-${pkgver}/scribus.desktop

	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/scribus.desktop \
	   ${pkgdir}/usr/share/applications/scribus.desktop
}
