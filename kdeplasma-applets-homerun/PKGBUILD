# maintainer: Manuel 'UtG' <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributions from AUR: https://aur.archlinux.org/packages/kdeplasma-applets-homerun/

pkgname=kdeplasma-applets-homerun
_pkgname=homerun
pkgver=1.2.4
pkgrel=1
pkgdesc="An application launcher for KDE Plasma desktop"
arch=('x86_64')
url="http://userbase.kde.org/Homerun"
license=('GPL' 'LGPL' 'BSD')
depends=('kde-workspace' 'kde-baseapps')
makedepends=('cmake' 'automoc4')
conflicts=('kdeplasma-applets-homerun-git')
install=$pkgname.install  
screenshot=('https://lh5.googleusercontent.com/-axerTR3fGvY/UExd_g_qByI/AAAAAAAAhlE/wKm_W5XtwWY/homerun.png')
source=("http://download.kde.org/stable/$_pkgname/src/$_pkgname-$pkgver.tar.xz"
        'icon.diff'
        'kicker-icon.diff')
md5sums=('fad9bca1772ae6cf17cd1e646c0d6839'
         'a57402bf4e353fa1ec8f1ae4972c9587'
         '551a8fc1b77eedf2d9cc21929e707678')

build() {
  cd "$srcdir"/$_pkgname-$pkgver
  patch -p1 -i "${srcdir}/icon.diff"
  patch -p1 -i "${srcdir}/kicker-icon.diff"
  cd "$srcdir"
  mkdir build
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$_pkgname-$pkgver/LICENSE.BSD" "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}
