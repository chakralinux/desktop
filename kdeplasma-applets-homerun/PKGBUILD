# maintainer: Manuel 'UtG' <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributions from AUR: https://aur.archlinux.org/packages/kdeplasma-applets-homerun/

pkgname=kdeplasma-applets-homerun
_pkgname=homerun
pkgver=1.2.5
pkgrel=1
pkgdesc="An application launcher for KDE Plasma desktop"
arch=('x86_64')
url="http://userbase.kde.org/Homerun"
license=('GPL' 'LGPL' 'BSD')
depends=('kde-workspace' 'kde-baseapps')
makedepends=('cmake' 'automoc4')
conflicts=('kdeplasma-applets-homerun-git')
install=$pkgname.install  
screenshot=('https://lh5.googleusercontent.com/-axerTR3fGvY/UExd_g_qByI/AAAAAAAAhlE/wKm_W5XtwWY/homerun.png')
source=("http://download.kde.org/stable/$_pkgname/src/$_pkgname-$pkgver.tar.xz"
        'icon.diff'
        'kicker-icon.diff')
md5sums=('86ebece69eae72c548adf40da02aa354'
         'a57402bf4e353fa1ec8f1ae4972c9587'
         '551a8fc1b77eedf2d9cc21929e707678')

prepare() {
  cd "$srcdir"/$_pkgname-$pkgver
  patch -p1 -i "${srcdir}/icon.diff"
  patch -p1 -i "${srcdir}/kicker-icon.diff"

  [[ -d build ]] && rm -rf build
  mkdir build
}

build() {
  cd "$srcdir/$_pkgname-$pkgver/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"/build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$_pkgname-$pkgver/LICENSE.BSD" "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}
