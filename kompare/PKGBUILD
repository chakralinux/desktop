# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdesdk-kompare/
# Include global configuration

pkgname="kompare"
arch=('x86_64')
pkgver=18.04.2
pkgrel=1
pkgdesc="A KDE visual diff viewer"
screenshot="http://www.kde.org/images/screenshots/kompare.png"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
depends=('libkomparediff2' 'ktexteditor' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python3' 'kdoctools')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kde-devel' 'kdesdk')
provides=('kdesdk-kompare')
conflicts=('kdesdk-kompare')
replaces=('kdesdk-kompare')
install=kdesdk.install
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('89ade212c8e08a7859615dd01d8de7a74cbe2c9af9dcdaf55507e1b6b2da202d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
