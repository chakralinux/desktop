# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>

# Note:
#   GHC places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when GHC is rebuilt.

pkgname=ghc
pkgver=7.6.3
pkgrel=1
pkgdesc="The Glasgow Haskell Compiler"
arch=('i686' 'x86_64')
url="http://www.haskell.org/ghc/"
license=('custom')
# Depends on base-devel (gcc, docbook-xsl, ... )
# libffi might be needed in the future: http://hackage.haskell.org/trac/ghc/ticket/4496,
# http://ghc.haskell.org/trac/ghc/changeset/3005e90936c47c1f71672bf6c84fff20cb14014b/ghc,
# http://ghc.haskell.org/trac/ghc/changeset/7ee5bedc5e5039c9bb3ba06b50a4395e579a4b0e/ghc
depends=('perl' 'gmp>=5.0')
# Make depends on libedit > 2.6.9
makedepends=('ghc>=7.0.1' 'perl>=5.6' 'libxslt' 'sed>=4.2.1' 'libedit')
checkdepends=('python2')
install="ghc.install"
# Keep this, otherwise a lot of tests will show as unexpected failures
# (investigate why using -jn for n > 1 does this).
#options=('!makeflags')
# Missing haskell-rtl?
provides=('haskell-array=0.4.0.1'
          'haskell-base=4.6.0.1'
          'haskell-bin-package-db=0.0.0.0'
          'haskell-binary=0.5.1.1'
          'haskell-bytestring=0.10.0.2'
          'haskell-cabal=1.16.0'
          'haskell-containers=0.5.0.0'
          'haskell-deepseq=1.3.0.1'
          'haskell-directory=1.2.0.1'
          'haskell-filepath=1.3.0.1'
          'haskell-ghc-prim=0.3.0.0'
          'haskell-haskell2010=1.1.1.0'
          'haskell-haskell98=2.0.0.2'
          'haskell-hoopl=3.9.0.0'
          'haskell-hpc=0.6.0.0'
          'haskell-integer-gmp=0.5.0.0'
          'haskell-old-locale=1.0.0.5'
          'haskell-old-time=1.1.0.1'
          'haskell-pretty=1.1.1.0'
          'haskell-process=1.1.0.2'
          'haskell-template-haskell=2.8.0.0'
          'haskell-time=1.4.0.1'
          'haskell-unix=2.6.0.1')
replaces=('haskell-array<0.4.0.1'
          'haskell-base<4.6.0.1'
          'haskell-bin-package-db<0.0.0.0'
          'haskell-binary<0.5.1.1'
          'haskell-bytestring<0.10.0.2'
          'haskell-cabal<1.16.0'
          'haskell-containers<0.5.0.0'
          'haskell-deepseq<1.3.0.1'
          'haskell-directory<1.2.0.1'
          'haskell-filepath<1.3.0.1'
          'haskell-ghc-prim<0.3.0.0'
          'haskell-haskell2010<1.1.1.0'
          'haskell-haskell98<2.0.0.2'
          'haskell-hoopl<3.9.0.0'
          'haskell-hpc<0.6.0.0'
          'haskell-integer-gmp<0.5.0.0'
          'haskell-old-locale<1.0.0.5'
          'haskell-old-time<1.1.0.1'
          'haskell-pretty<1.1.1.0'
          'haskell-process<1.1.0.2'
          'haskell-template-haskell<2.8.0.0'
          'haskell-time<1.4.0.1'
          'haskell-unix<2.6.0.1')
source=("http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-src.tar.bz2"
        "http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-testsuite.tar.bz2"
        "build.mk")
md5sums=('986d1f90ca30d60f7b2820d75c6b8ea7'
         '66aa6177a31cc4b9d7eeb55cb1514918'
         'c367ef26300648ee9b8aca3dee5c9669')

build() {
    cd "ghc-${pkgver}"
    cp "${srcdir}/build.mk" "mk/build.mk"
    ./configure \
        --prefix=/usr
    make
}

check() {
    # Keep this, otherwise a lot of tests will show as unexpected failures
    # (investigate why using -jn for n > 1 does this).
    unset MAKEFLAGS

    cd "ghc-${pkgver}"

    # Python 2 rename
    sed -e "s/PYTHON = python/&2/" -i \
        testsuite/mk/boilerplate.mk
    sed -e "s_#!/usr/bin/env python_&2_" -i \
        testsuite/timeout/calibrate testsuite/timeout/timeout.py

    # Upstream known failures on both i686 and x86_64:
    #   ghci.debugger/scripts   break011 [bad stderr] (ghci)
    #   perf/compiler           parsing001 [stat not good enough] (normal)
    #   simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
    # Upstream known failures on i686 only:
    #   numrun012 test fails on i686 works on x86_64 
    #       (http://hackage.haskell.org/trac/ghc/ticket/5856)
    make test

    # Enabling threads to speed up causes more failures than make test,
    # need to investigate.
    # Upstream known failures on both i686 and x86_64:
    #   perf/compiler           parsing001 [stat not good enough] (normal)
    #   simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
    # Upstream known failures on i686 only:
    #   numrun012 test fails on i686 works on x86_64
    #       (http://hackage.haskell.org/trac/ghc/ticket/5856)
    #make THREADS=9 test

    # fulltest will most likely cause lots of failures. Enable this when
    # upstream has 0 test failures from a simple "make test".
    #   Zero unexpected failures on all tier 1 platforms
    #       (http://hackage.haskell.org/trac/ghc/ticket/5757)
    #make fulltest
}

package() {
    cd "ghc-${pkgver}"
    make DESTDIR="${pkgdir}/" install

    # License
    install -d "${pkgdir}/usr/share/licenses/ghc"
    install -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/ghc"
}
