#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=xine-ui
pkgver=0.99.7
pkgrel=1
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xine-project.org"
depends=('xine-lib' 'curl' 'libxtst' 'libxinerama' 'libxv' 'libpng' 'libxft'
	 'xdg-utils' 'shared-mime-info' 'hicolor-icon-theme'
	 'lirc-utils')
makedepends=('libxt')
categories=('multimedia')
options=('!emptydirs' '!strip')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/xine/${pkgname}-${pkgver}.tar.xz)
sha1sums=('ca82e09cf8245d038813bc4dbd8384ba79cbf197')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --with-x \
    --enable-lirc \
    --without-aalib \
    --enable-debug 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install 
}
