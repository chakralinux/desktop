# Maintainer: Ernesto Manriquez <alejandronova@gmail.com>

pkgname=perl-exiftool
_srcname=Image-ExifTool
pkgver=9.27
pkgrel=2
license=('GPL')
pkgdesc="Reader and rewriter of EXIF informations that supports raw files"
url="http://www.sno.phy.queensu.ca/~phil/exiftool/"
arch=(any)
depends=('perl')
optdepends=('perl-archive-zip: decode compressed zip information')
source=(http://www.sno.phy.queensu.ca/~phil/exiftool/${_srcname}-${pkgver}.tar.gz)
options=(!emptydirs)
md5sums=('c61f8f79c8e9f375e5be1ca0d13cc5fa')

build() {
  cd ${srcdir}/${_srcname}-${pkgver}
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor 
  make
}

package() {
  cd ${srcdir}/${_srcname}-${pkgver}
  make DESTDIR=${pkgdir} install
  # remove *.pod, .packlist, and empty dirs:
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
