pkgname=syslog-ng
pkgver=3.8.1
pkgrel=1
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('x86_64')
license=('GPL2')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glib2' 'eventlog' 'openssl' 'libcap' 'awk' 'systemd' 'python2')
makedepends=('flex' 'pkg-config')
optdepends=('logrotate: for rotating log files')
provides=('logger')
backup=('etc/syslog-ng/modules.conf' 'etc/syslog-ng/scl.conf' \
        'etc/syslog-ng/syslog-ng.conf' 'etc/logrotate.d/syslog-ng' 'etc/logrotate.d/syslog-ng')
source=("https://github.com/balabit/syslog-ng/releases/download/syslog-ng-$pkgver/syslog-ng-$pkgver.tar.gz"
        'syslog-ng.conf'
        'syslog-ng.conf.d'
        'syslog-ng.logrotate')
sha1sums=('e66cc7538eec245b80dda4624671a61932c856c3'
          '98074e0facfc6ef036202662cc86d04b38a2c142'
          'eb2aa25737e0cb9453c7b058f0e2dcf16abf21cd'
          '91ed7a6acfe45b38d6539ba744c58d2d397458ad')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/syslog-ng \
    --libexecdir=/usr/lib \
    --localstatedir=/var/lib/syslog-ng \
    --datadir=/usr/share/syslog-ng \
    --with-pidfile-dir=/run \
    --disable-spoof-source \
    --enable-ipv6 \
    --enable-systemd \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --sbindir=/usr/bin
  make
}

check() {
  cd "$pkgname-$pkgver"
  
  # 1 fail, because test_systemd_journal.c
  # https://github.com/algernon/syslog-ng/commit/ed0976f87ddf7f9986e22a0b64dc3aef9249046a
  make check | true
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/var/lib/syslog-ng" "$pkgdir/etc/syslog-ng/patterndb.d"
  install -Dm644 "$srcdir/syslog-ng.conf" "$pkgdir/etc/syslog-ng/syslog-ng.conf"
  install -Dm644 "$srcdir/syslog-ng.logrotate" "$pkgdir/etc/logrotate.d/syslog-ng"
  install -Dm644 "$srcdir/syslog-ng.conf.d" "$pkgdir/etc/conf.d/syslog-ng"
}
