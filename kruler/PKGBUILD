# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegraphics-kruler/
# Include global configuration

pkgname=kruler
pkgver=18.12.0
pkgrel=1
pkgdesc='Screen ruler for measuring pixels'
url='http://kde.org/applications/graphics/kruler/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('knotifications' 'kxmlgui')
makedepends=('extra-cmake-modules' 'python3' 'kdoctools')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegraphics')
replaces=('kdegraphics-kruler')
conflicts=('kdegraphics-kruler')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0781366f7b55d0c88f57b0638726b98492f6d692f2e0baf6a629e48c6c394857'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
