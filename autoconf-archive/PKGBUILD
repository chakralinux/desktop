# Arch contributor: https://projects.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/autoconf-archive

pkgname=autoconf-archive
pkgver=2019.01.06
pkgrel=1
epoch=1
pkgdesc="A collection of freely re-usable Autoconf macros"
arch=('any')
url="https://www.gnu.org/software/autoconf-archive/"
license=('GPL')
depends=('autoconf')
optdepends=('automake: macros for use with it')
validpgpkeys=('1A4F63A13A4649B632F65EE141BC28FE99089D72')
source=(http://ftpmirror.gnu.org/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
md5sums=('d46413c8b00a125b1529bae385bbec55'
         'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
