#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Frenández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=haskell-binary
pkgver=0.5.0.2
pkgrel=4
pkgdesc="Binary serialisation for Haskell values using lazy ByteStrings"
url="http://hackage.haskell.org/package/binary"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=(ghc=6.12.3 sh)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/binary/$pkgver/binary-$pkgver.tar.gz)
install=haskell-binary.install
md5sums=('6bf8f3d1441602c9ab09a75e3bd6e926')

build() {
    cd ${srcdir}/binary-$pkgver
    runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname} -p --enable-split-objs --enable-shared
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
}

package() {
    cd ${srcdir}/binary-0.5.0.2
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/binary
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
