pkgbase=tlp
pkgname=('tlp' 'tlp-rdw')
pkgver=1.1
pkgrel=1
arch=('x86_64')
url='http://linrunner.de/en/tlp/tlp.html'
license=('GPL2' 'GPL3')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/linrunner/TLP/archive/$pkgver.tar.gz"
'default')
sha256sums=('76e1a091eed1055f7af75f49621be51ed41b1e8f872eeb9506c0749060fd1019'
            '7f86f31ee86235eac6a57e4d647d6f41489b3eaa10037cca1460eb3ff091b567')

package_tlp() {
  pkgdesc='Linux Advanced Power Management'
  depends=('hdparm' 'iw' 'pciutils' 'usbutils' 'util-linux')
  optdepends=('acpi_call: Sandy Bridge and newer ThinkPad battery functions'
              'ethtool: Disable Wake On Lan'
              'lsb-release: Display LSB release version in tlp-stat'
              'smartmontools: Display S.M.A.R.T. data in tlp-stat'
              'tp_smapi: ThinkPad battery functions'
              'x86_energy_perf_policy: Set energy versus performance policy on x86 processors')
  conflicts=('laptop-mode-tools' 'pm-utils')
  backup=('etc/default/tlp')

  cd TLP-$pkgver

  export TLP_SBIN='/usr/bin'
  export TLP_ULIB='/usr/lib/udev'
  export TLP_SYSD='/usr/lib/systemd/system'
  export TLP_NO_INIT='1'
  export TLP_NO_PMUTILS='1'
  export TLP_WITH_SYSTEMD='1'

  make DESTDIR=$pkgdir install-tlp
}

package_tlp-rdw() {
  pkgdesc='Linux Advanced Power Management - Radio Device Wizard'
  depends=('networkmanager' 'tlp')

  cd TLP-$pkgver

  make DESTDIR=$pkgdir install-rdw
}
