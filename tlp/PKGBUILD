pkgbase=tlp
pkgname=('tlp' 'tlp-rdw')
pkgver=0.9
pkgrel=1
arch=('any')
url='http://linrunner.de/en/tlp/tlp.html'
license=('GPL2' 'GPL3')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/linrunner/TLP/archive/${pkgver}.tar.gz"
'default')
sha256sums=('887b226d443f930398fda5793ac690e1e1ef7dd2187db7be0278359315aa7a76'
            '92cc56c67267f0221be09c398c15a7f46b932887da4bc3a8f50f57876a48c548')

package_tlp() {
  pkgdesc='Linux Advanced Power Management'
  depends=('hdparm' 'iw' 'pciutils' 'rfkill' 'usbutils' 'util-linux')
  optdepends=('acpi_call: Sandy Bridge and newer ThinkPad battery functions'
              'ethtool: Disable Wake On Lan'
              'lsb-release: Display LSB release version in tlp-stat'
              'smartmontools: Display S.M.A.R.T. data in tlp-stat'
              'tp_smapi: ThinkPad battery functions'
              'x86_energy_perf_policy: Set energy versus performance policy on x86 processors')
  conflicts=('laptop-mode-tools' 'pm-utils')
  backup=('etc/default/tlp')

  cd TLP-${pkgver}

  export TLP_SBIN='/usr/bin'
  export TLP_ULIB='/usr/lib/udev'
  export TLP_SYSD='/usr/lib/systemd/system'
  export TLP_NO_INIT='1'
  export TLP_NO_PMUTILS='1'
  export TLP_WITH_SYSTEMD='1'

  make DESTDIR="${pkgdir}" install-tlp
}

package_tlp-rdw() {
  pkgdesc='Linux Advanced Power Management - Radio Device Wizard'
  depends=('networkmanager' 'tlp')

  cd TLP-${pkgver}

  make DESTDIR="${pkgdir}" install-rdw
}
