# Contributor: MCMic <come.bernigaud@laposte.net>

pkgname=valyria-tear
pkgver=1.0.0
pkgrel=2
pkgdesc="An attempt at creating a free (as meant in the Gnu Public License) J-RPG game."
arch=('x86_64')
url="http://valyriatear.blogspot.fr"
license=('GPL')
depends=('sdl' 'sdl_image' 'sdl_ttf' 'openal' 'libogg' 'libvorbis' 'glu' 'libpng' 'libjpeg' 'gettext' 'lua' 'glew' 'boost-libs')
makedepends=('cmake' 'boost' 'git')
conflicts=('valyria-tear')
source=("git+https://github.com/Bertram25/ValyriaTear.git#tag=${pkgver}-gcc5"
        "valyriatear.sh")
md5sums=('SKIP'                                                                                                                                                 
         '5e783c05fb31d6125f6b771035d3808b')  

build() {
  cd $srcdir/ValyriaTear
  git submodule update --init --remote
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DLUA_LIBRARIES=/usr/lib/liblua.so.5.1 .
  make
}

package() {
  cd $srcdir/ValyriaTear
  make install DESTDIR=$pkgdir
  install -d $pkgdir/usr/lib
  # Provide wrapper to load data from /usr/share/valyriatear https://github.com/ValyriaTear/ValyriaTear/issues/505
  mv $pkgdir/usr/{bin,lib}/valyriatear
  install -m755 $srcdir/valyriatear.sh $pkgdir/usr/bin/valyriatear
}

