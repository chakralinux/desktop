#
# Chakra Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=mesa-git
pkgname=('mesa-git' 'libgl-git' 'ati-dri-git' 'intel-dri-git' 'unichrome-dri-git' 'mach64-dri-git' 'mga-dri-git' 'r128-dri-git' 'savage-dri-git' 'sis-dri-git' 'tdfx-dri-git' 'nouveau-dri-git')
pkgver=20101121
pkgrel=1
arch=(i686 x86_64)
makedepends=('glproto-git' 'pkgconfig' 'libdrm-git' 'libxxf86vm>=1.1.0' 'libxdamage>=1.1.3' 'expat>=2.0.1' 'libx11>=1.3.5' 'libxt>=1.0.8' 
             'gcc-libs>=4.5' 'dri2proto-git' 'python' 'talloc' 'libxml2' 'imake')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=(http://chakra-project.org/sources/mesa/gl-manpages-1.0.1.tar.bz2
        LICENSE)
md5sums=('6ae05158e678f4594343f32c2ca50515'
         '5c65a0fe315dd347e09b1f2826a1df5a')

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

build() {
  msg 'Connecting to git.freedesktop.org GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
  else
    git clone ${_gitroot}
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd ${srcdir}

  # Cleanup and prepare the build dir
  [ -d build ] && rm -rf build
  cp -r ${_gitname} build
  cd build

  # python2 build fixes
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" $(find $srcdir -name '*.py')
  sed -i -e "s|PYTHON2 = python|PYTHON2 = python2|" "${srcdir}"/build/configs/{default,autoconf.in}
  sed -i -e "s|python|python2|" "${srcdir}"/build/src/gallium/auxiliary/Makefile

  ./autogen.sh --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --disable-egl \
    --enable-gallium-radeon \
    --enable-gallium-nouveau \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --with-state-trackers=dri,glx \
    --disable-glut
  make

  cd "${srcdir}/gl-manpages-1.0.1"
  ./configure --prefix=/usr
  make
}

package_libgl-git() {
  depends=('libdrm-git' 'libxxf86vm>=1.1.0' 'libxdamage>=1.1.3' 'expat>=2.0.1')
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"
  provides=('libgl=7.9')

  cd "${srcdir}"/build
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"

  bin/minstall lib/libGL.so* "${pkgdir}/usr/lib/"

  cd src/mesa/drivers/dri
  make -C swrast DESTDIR="${pkgdir}" install
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  install -m755 -d "${pkgdir}/usr/share/licenses/libgl"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgl/"
}

package_mesa-git() {
  depends=('libgl-git' 'libx11>=1.3.5' 'libxt>=1.0.8' 'gcc-libs>=4.5' 'dri2proto-git' 'libdrm-git' 'glproto-git')
  pkgdesc="Mesa 3-D graphics libraries and include files"
  provides=('mesa=7.9')
  conflicts=('mesa')

  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/lib/libGL.so"*
  rm -rf "${pkgdir}/usr/lib/xorg"
  rm -f "${pkgdir}/usr/include/GL/glew.h"
  rm -f "${pkgdir}/usr/include/GL/glxew.h"
  rm -f "${pkgdir}/usr/include/GL/wglew.h"

  cd "${srcdir}/gl-manpages-1.0.1"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}

package_ati-dri-git() {
  depends=('libgl-git')
  conflicts=('ati-dri')
  provides=('ati-dri=7.9')
  pkgdesc="Mesa DRI + Gallium3D r300 drivers for AMD/ATI Radeon"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C radeon DESTDIR="${pkgdir}" install
  make -C r200 DESTDIR="${pkgdir}" install
  # classic mesa driver for R300 r300_dri.so
# make -C r300 DESTDIR="${pkgdir}" install  <------- depricated
  # gallium3D driver for R300 r300_dri.so
  make -C ${srcdir}/build/src/gallium/targets/dri-r300 DESTDIR="${pkgdir}" install
  make -C r600 DESTDIR="${pkgdir}" install
}

package_intel-dri-git() {
  depends=("libgl-git")
  conflicts=('intel-dri')
  provides=('intel-dri=7.9')
  pkgdesc="Mesa DRI drivers for Intel"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C i810 DESTDIR="${pkgdir}" install
  make -C i915 DESTDIR="${pkgdir}" install
  make -C i965 DESTDIR="${pkgdir}" install
}

package_unichrome-dri-git() {
  depends=('libgl-git')
  conflicts=('unichrome-dri')
  provides=('unichrome-dri=7.9')
  pkgdesc="Mesa DRI drivers for S3 Graphics/VIA Unichrome"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C unichrome DESTDIR="${pkgdir}" install
}

package_mach64-dri-git() {
  depends=('libgl-git')
  conflicts=('mach64-dri')
  provides=('mack64-dri=7.9')
  pkgdesc="Mesa DRI drivers for ATI Mach64"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C mach64 DESTDIR="${pkgdir}" install
}

package_mga-dri-git() {
  depends=('libgl-git')
  conflicts=('mga-dri')
  provides=('mga-dri=7.9')
  pkgdesc="Mesa DRI drivers for Matrox"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C mga DESTDIR="${pkgdir}" install
}

package_r128-dri-git() {
  depends=('libgl-git')
  conflicts=('r128-dri')
  provides=('r128-dri=7.9')
  pkgdesc="Mesa DRI drivers for ATI Rage128"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C r128 DESTDIR="${pkgdir}" install
}

package_savage-dri-git() {
  depends=('libgl-git')
  conflicts=('savage-dri')
  provides=('savage-dri=7.9')
  pkgdesc="Mesa DRI drivers for S3 Sraphics/VIA Savage"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C savage DESTDIR="${pkgdir}" install
}

package_sis-dri-git() {
  depends=('libgl-git')
  conflicts=('sis-dri')
  provides=('sis-dri=7.9')
  pkgdesc="Mesa DRI drivers for SiS"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C sis DESTDIR="${pkgdir}" install
}

package_tdfx-dri-git() {
  depends=('libgl-git')
  conflicts=('tdfx-dri')
  provides=('tdfx-dri=7.9')
  pkgdesc="Mesa DRI drivers for 3dfx"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C tdfx DESTDIR="${pkgdir}" install
}

package_nouveau-dri-git() {
  depends=('libgl-git')
  conflicts=('nouveau-dri')
  provides=('nouveau-dri=7.9')
  pkgdesc="Mesa classic DRI + Gallium3D drivers for Nouveau"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  # classic mesa driver for nv10 , nv20 nouveau_vieux_dri.so
  make -C nouveau DESTDIR="${pkgdir}" install
  # gallium3D driver for nv30 - nv40 - nv50 nouveau_dri.so
  make -C ${srcdir}/build/src/gallium/targets/dri-nouveau DESTDIR="${pkgdir}" install
}