pkgbase=avidemux
pkgname=('avidemux-cli' 'avidemux')
pkgver=2.6.10
pkgrel=1
pkgdesc='Simple video editor for cutting, filtering and encoding tasks.'
arch=('x86_64')
license=('GPL')
url="http://fixounet.free.fr/avidemux/"
screenshot="http://upload.wikimedia.org/wikipedia/commons/7/77/Avidemux_Screenshot_Qt.png"
makedepends=('cmake' 'libxslt' 'qt5-base' 'qt5-tools' 'qt5-script' 'qt' 'jack' 'libvorbis' 'sdl' 'libxv' 'alsa-lib' 'lame'
             'faad2' 'faac' 'x264' 'x265' 'libsamplerate' 'opencore-amr' 'yasm' 'mesa' 'libvpx' 'sqlite3' 'xvidcore'
             'pulseaudio' 'libva' 'desktop-file-utils' 'libdca' 'fribidi' 'glu')
# 'aften' 'dcaenc' 'twolame' will be added to makedepends
# and avidemux will be rebuilt
categories=('multimedia')
options=("!makeflags")
source=("http://downloads.sourceforge.net/avidemux/avidemux_${pkgver}.tar.gz")
sha512sums=('f9414810925b2eeb0b6dc0094ac9a42809ab11274d2cd591eef753538d5719df893dc2352b85c8a1047b890bb03079678e6619af9bb25834a2428cfdae5005ef')
         
build() {
  cd "${srcdir}/${pkgbase}_${pkgver}"
 # build
  bash ./bootStrap.bash --with-core \
                        --with-cli \
                        --enable-qt5 \
                        --without-gtk \
                        --with-plugins

  # Create qm files
  cd po
  lrelease-qt5 *.po
}

package_avidemux-cli() {
  pkgdesc="A graphical tool to edit video (filter/re-encode/split)- command line version"
  depends=('faac' 'faad2' 'fontconfig' 'fribidi' 'jack' 'lame' 'libdca' 
  'pulseaudio' 'libva' 'libxv' 'libvpx' 'libxml2' 'opencore-amr'  'xvidcore'
  'sdl' 'sqlite3' 'x264' 'x265')
  # 'aften' 'dcaenc' 'twolame' same thing as makepends
  optdepends=('avidemux: graphical interface')

  cd "${srcdir}/${pkgbase}_${pkgver}"
  (cd buildCli; make DESTDIR="${pkgdir}" install)
  (cd buildCore; make DESTDIR="${pkgdir}" install)
  (cd buildPluginsCLI; make DESTDIR="${pkgdir}" install)
  (cd buildPluginsCommon; make DESTDIR="${pkgdir}" install)
  
  install -D -m644 "${srcdir}/${pkgbase}_${pkgver}/avidemux_icon.png" "${pkgdir}/usr/share/pixmaps/avidemux.png" 
  install -D -m644 "${srcdir}/${pkgbase}_${pkgver}/man/avidemux.1" "${pkgdir}/usr/share/man/man1/avidemux.1" 
}

package_avidemux() {
  pkgdesc="A graphical tool to edit video (filter/re-encode/split)"
  depends=('avidemux-cli' 'qt5-base' 'mesa')

  cd "${srcdir}/${pkgbase}_${pkgver}"
  
  (cd buildQt5; make DESTDIR="${pkgdir}" install)
  (cd buildPluginsQt5; make DESTDIR="${pkgdir}" install)

  install -D -m644 avidemux2.desktop "${pkgdir}/usr/share/applications/avidemux-qt.desktop"
  sed -i 's|2_gtk|3_qt5|' "${pkgdir}/usr/share/applications/avidemux-qt.desktop"
}
