# Include global configuration

pkgname=kdialog
pkgver=18.07.80
pkgrel=1
pkgdesc="A utility for displaying dialog boxes from shell scripts"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
depends=(kdelibs4support)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin python3)
conflicts=(kde-baseapps-kdialog)
replaces=(kde-baseapps-kdialog)
options=(docs debug)
groups=(kde kde-uninstall kde-applications kde-baseapps)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('9c02938a597065333494009ba0da0ed115bc81131dc4fe914d78cfd6b129548d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
