#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-apc
pkgver=3.1.14
pkgrel=2
arch=('x86_64')
pkgdesc='A free, open, and robust framework for caching and optimizing PHP intermediate code.'
url="http://pecl.php.net/package/APC"
depends=('php')
license="PHP"
source=("http://pecl.php.net/get/APC-${pkgver}.tgz")
backup=('etc/php/conf.d/apc.ini')
md5sums=('4e9111f9e2bdc219107e98beffc3751e')

build() {
  cd "${srcdir}/APC-${pkgver}"
  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/APC-${pkgver}"
  make install INSTALL_ROOT="${pkgdir}"
  echo ';extension=apc.so' > apc.ini
  install -D -m644 apc.ini "${pkgdir}/etc/php/conf.d/apc.ini"
  install -D -m644 apc.php "${pkgdir}/usr/share/php-apc/apc.php"
  install -D -m644 INSTALL "${pkgdir}/usr/share/doc/php-apc/install.txt"
}

# vim:set ts=2 sw=2 et:
