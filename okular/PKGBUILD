# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegraphics-okular/
# Include global configuration

pkgname=okular
pkgver=18.07.90
pkgrel=1
pkgdesc='Document Viewer'
arch=(x86_64)
url="http://kde.org/applications/graphics/okular/"
license=(GPL LGPL FDL)
depends=(chmlib djvulibre libspectre libkexiv2 poppler-qt5 qca-qt5 kpty khtml kactivities threadweaver)
makedepends=(extra-cmake-modules ebook-tools kdegraphics-mobipocket kdoctools python3)
optdepends=('ebook-tools: mobi and epub support'
            'kdegraphics-mobipocket: mobi support')
conflicts=(kdegraphics-okular)
replaces=(kdegraphics-okular)
options=(docs debug)
groups=(kde kde-uninstall kde-applications kdegraphics)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6c588523cd7cf1ef8c4fa67ca1ea0fad978bdce5f714d16ab4f864e093b17cd9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
