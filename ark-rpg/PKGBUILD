#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=ark-rpg
_pkgname=ark
pkgver=0.1
pkgrel=1
pkgdesc="Single player three dimensional role playing game set in a fantasy world."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/ark-rpg/"
screenshot="http://sourceforge.net/dbimage.php?id=313983"
license=('GPL2')
depends=('bullet' 'cegui' 'lua' 'ogre' 'openal')
makedepends=('scons')
categories=('games')
source=(http://downloads.sourceforge.net/project/$pkgname/$_pkgname-$pkgver.tar.bz2)
md5sums=('027d1f9af6a3c213f174db0f5fd84afb')

build() {
  cd $srcdir/$_pkgname-$pkgver

  scons  
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  # Note: it’s a dirty installation, game is not too packaging-friendly right now.

  # Install the whole thing.
  install -d $pkgdir/usr/share/$pkgname
  cp -R * $pkgdir/usr/share/$pkgname

  # Remove unneeded stuff.
  for PATTERN in 'ChangeLog' 'config.log' 'INSTALL' 'NEWS' 'README' 'SConstruct' '*.cbp' '*.cpp' \
		 '*.cxx' '*.depend' '*.h' '*.i' '*.o' '*.sh'
  do
    find $pkgdir/usr/share/$pkgname -type f -name $PATTERN -exec rm {} \;
  done
  find $pkgdir/usr/share/$pkgname -depth -empty -type d -exec rmdir {} \;

  # Execution script.
  install -d $pkgdir/usr/bin
  echo '#!/bin/bash'		 > $pkgdir/usr/bin/$pkgname
  echo "cd /usr/share/$pkgname"	>> $pkgdir/usr/bin/$pkgname
  echo "exec ./$_pkgname"		>> $pkgdir/usr/bin/$pkgname
  chmod +x $pkgdir/usr/bin/$pkgname

  # License.
  install -d $pkgdir/usr/share/licenses/$pkgname
  mv $pkgdir/usr/share/$pkgname/AUTHORS $pkgdir/usr/share/licenses/$pkgname/authors.txt
  mv $pkgdir/usr/share/$pkgname/COPYING $pkgdir/usr/share/licenses/$pkgname/license.txt
  
}
