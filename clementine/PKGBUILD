pkgname=clementine
#_pkgname=Clementine
pkgver=1.3.0
pkgrel=11
pkgdesc="A modern music player and library organiser and a port of Amarok 1.4, with some features rewritten to take advantage of Qt4."
url="http://www.clementine-player.org/"
screenshot="http://images.clementine-player.org/screenshots/clementine-1.0-3.png"
license="GPL"
arch=('x86_64')
install=${pkgname}.install
depends=('gstreamer' 'taglib' 'glew' 'libgpod' 'libmtp' 'libplist' 'liblastfm-qt4'
         'hicolor-icon-theme' 'qtwebkit' 'libimobiledevice' 'qjson' 'protobuf' 'crypto++'
         'libcdio' 'qca' 'qca-ossl' 'chromaprint' 'libmygpo-qt' 'libechonest-qt4')
makedepends=('cmake' 'boost' 'mesa' 'sparsehash')
optdepends=('gst-plugins-base: for more open formats'
            'gst-plugins-good: for use with "Good" plugin libraries'
            'gst-plugins-bad: for use with "Bad" plugin libraries'
            'gst-plugins-ugly: for use with "Ugly" plugin libraries'
            'libspotify: add support for Spotify, due to license restriction, please build it 
                         with ccr yourself'
            'dropbox: add support for Dropbox')
categories=('multimedia')
source=("https://github.com/clementine-player/Clementine/releases/download/1.3/${pkgname}-${pkgver}.tar.xz")
sha1sums=('6224ac0aabb85e973102e6b60a1cc0938bd6d4f8')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's,<lastfm/,<lastfm4/,g' -i src/internet/lastfm/* -i src/core/*.cpp
#  patch -Np1 -i "../clementine-udisks-headers.patch" || return 1
  mkdir -p build
}

build() {
  cd ${pkgname}-${pkgver}/build
  cmake ../ -DLASTFM_INCLUDE_DIRS=/usr/include/liblastfm4 \
            -DLASTFM1_INCLUDE_DIRS=/usr/include/liblastfm4 \
            -DCMAKE_INSTALL_PREFIX=/usr \
            -DCMAKE_BUILD_TYPE=Release \
            -DBUILD_WERROR=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make install DESTDIR="${pkgdir}"
}
