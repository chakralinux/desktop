# Contributor: Giuseppe Cal√É  <jiveaxe@gmail.com>
# Maintainer: Bruce Liu <rainman59118@gmail.com>

pkgname=clementine
_pkgname=Clementine
pkgver=1.2.3
pkgrel=4
pkgdesc="A modern music player and library organiser and a port of Amarok 1.4, with some features rewritten to take advantage of Qt4."
url="http://www.clementine-player.org/"
screenshot="http://images.clementine-player.org/screenshots/clementine-1.0-3.png"
license="GPL"
arch=('x86_64')
install=${pkgname}.install
depends=('gstreamer0.10-base' 'taglib' 'glew>=1.10.0' 'liblastfm' 'libgpod' 'libmtp' 'libplist'
         'hicolor-icon-theme' 'qtwebkit' 'libimobiledevice' 'qjson' 'glew' 'protobuf'
         'libcdio' 'qca' 'qca-ossl' 'chromaprint')
makedepends=('cmake' 'boost' 'mesa')
optdepends=('gstreamer0.10-base-plugins: for more open formats'
            'gstreamer0.10-good-plugins: for use with "Good" plugin libraries'
            'gstreamer0.10-bad-plugins: for use with "Bad" plugin libraries'
            'gstreamer0.10-ugly-plugins: for use with "Ugly" plugin libraries')
categories=('multimedia')
source=("https://github.com/clementine-player/${_pkgname}/archive/${pkgver}.tar.gz"
        "udisks-headers.patch")
sha1sums=('2a27eda3a13185ba9f0f7b75dcf7173e6828384a'
          '72f73076a6384c9ce387ec5856a63e599a6e8474')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 -i "../udisks-headers.patch"
}

build() {
  cd ${_pkgname}-${pkgver}
  mkdir -p build && cd build
  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr \
            -DCMAKE_BUILD_TYPE=Release \
            -DBUILD_WERROR=OFF \
            -DSTATIC_SQLITE=ON
  make
}

package() {
  cd ${_pkgname}-${pkgver}/build
  make install DESTDIR="${pkgdir}"
}
