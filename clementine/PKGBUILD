#
# Apps packages for Chakra, part of chakra-project.org
#
# Contributor: abveritas@chakra-project.org
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>
# Maintainer: Bruce Liu <rainman59118@gmail.com>

pkgname=clementine
pkgver=1.2.1
pkgrel=2
pkgdesc="A modern music player and library organiser and a port of Amarok 1.4, with some features rewritten to take advantage of Qt4."
url="http://www.clementine-player.org/"
screenshot="http://images.clementine-player.org/screenshots/clementine-1.0-3.png"
license="GPL"
arch=('x86_64')
install=${pkgname}.install
depends=('gstreamer0.10-base' 'taglib' 'glew' 'liblastfm' 'libgpod' 'libmtp' 'libplist'
         'hicolor-icon-theme' 'qtwebkit' 'libimobiledevice' 'qjson' 'glew' 'protobuf'
         'libcdio' 'qca' 'qca-ossl' 'chromaprint')
makedepends=('cmake' 'boost' 'mesa')
optdepends=('gstreamer0.10-base-plugins: for more open formats'
            'gstreamer0.10-good-plugins: for use with "Good" plugin libraries'
            'gstreamer0.10-bad-plugins: for use with "Bad" plugin libraries'
            'gstreamer0.10-ugly-plugins: for use with "Ugly" plugin libraries')
categories=('multimedia')
source=("http://clementine-player.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
sha1sums=('15dcb7e6b4f08ba36abd105ed019b61b7e8578bb')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build && cd build
  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr \
            -DCMAKE_BUILD_TYPE=Release \
            -DSTATIC_SQLITE=ON
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make install DESTDIR="${pkgdir}"
}
