pkgname=clementine
pkgver=1.3.1
pkgrel=8
pkgdesc="A modern music player and library organiser and a port of Amarok 1.4, with some features rewritten to take advantage of Qt4."
url="http://www.clementine-player.org/"
screenshot="http://images.clementine-player.org/screenshots/clementine-1.0-3.png"
license=("GPL")
arch=('x86_64')
depends=('gstreamer' 'gst-plugins-base-libs' 'taglib' 'glew' 'libgpod' 'libmtp' 'libplist' 'liblastfm-qt4'
         'hicolor-icon-theme' 'qjson' 'protobuf' 'crypto++'
         'libcdio' 'qca' 'qca-ossl' 'chromaprint' 'libmygpo-qt' 'libechonest-qt4')
makedepends=('cmake' 'boost' 'mesa' 'sparsehash')
optdepends=('gst-plugins-base: for more open formats'
            'gst-plugins-good: for use with "Good" plugin libraries'
            'gst-plugins-bad: for use with "Bad" plugin libraries'
            'gst-plugins-ugly: for use with "Ugly" plugin libraries'
            'libspotify: add support for Spotify, due to license restriction, please build it 
                         with ccr yourself'
            'dropbox: add support for Dropbox')
categories=('multimedia')
source=("https://github.com/clementine-player/Clementine/releases/download/1.3.1/${pkgname}-${pkgver}.tar.xz"
        'clementine-gcc6.patch'
        'add-missing-functional-includes-5630.patch'
        'clementine-1.3.1-chromaprint-1.4.0.patch'
        #'clementine-cryptopp6.patch'
        'https://github.com/clementine-player/Clementine/commit/75f9439843a0e9cdc26cd739d5e1dbd93bd974bc.patch')
sha1sums=('67f3438d674e075d15859daedfc9f82b638243f7'
          '41dfe9c6b22d60d6d6e83c3a1389a523a52e7946'
          '633f88598a257fcac21610cdced1fbecfe144a10'
          '9d6a8bc211f94728ff424de680ad3b042176f570'
          '2d45444f011613d92acb4a247a4f9c653d34e655'
          '579063e7eaad30af93609bb88854ce49728014f6')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's,<lastfm/,<lastfm4/,g' -i src/internet/lastfm/* -i src/core/*.cpp

  # Fix build with GCC 6
  patch -p1 -i ../clementine-gcc6.patch
  # https://github.com/clementine-player/Clementine/pull/5630
  patch -p1 -i ../add-missing-functional-includes-5630.patch
  # Fix build with chromaprint 1.4.0
  patch -p1 -i ../clementine-1.3.1-chromaprint-1.4.0.patch
  # Fix build with crypto++ 6.0.0 due to https://github.com/weidai11/cryptopp/issues/442
  #patch -p1 -i ../clementine-cryptopp6.patch
  # FS#58413
  patch -Np1 -i ../75f9439843a0e9cdc26cd739d5e1dbd93bd974bc.patch

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd ${pkgname}-${pkgver}/build
  cmake ../ -DLASTFM_INCLUDE_DIRS=/usr/include/lastfm4 \
            -DLASTFM1_INCLUDE_DIRS=/usr/include/lastfm4 \
            -DCMAKE_INSTALL_PREFIX=/usr \
            -DCMAKE_BUILD_TYPE=Release \
            -DBUILD_WERROR=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make install DESTDIR="${pkgdir}"
}
