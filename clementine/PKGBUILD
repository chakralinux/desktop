#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=clementine
pkgver=1.0.1
pkgrel=1
pkgdesc="A modern music player and library organiser and a port of Amarok 1.4, with some features rewritten to take advantage of Qt4."
url="http://www.clementine-player.org/"
license="GPL"
arch=('i686' 'x86_64')
depends=('gstreamer0.10' 'taglib' 'glew' 'liblastfm' 'libgpod' 'libmtp' 'libplist' 'hicolor-icon-theme' 'qt' 'libimobiledevice' 'qjson' 'glew')
makedepends=('cmake' 'boost' 'gdk-pixbuf2')
optdepends=('gstreamer0.10-base-plugins: for more open formats'
            'gstreamer0.10-good-plugins: for use with "Good" plugin libraries'
            'gstreamer0.10-bad-plugins: for use with "Bad" plugin libraries'
            'gstreamer0.10-ugly-plugins: for use with "Ugly" plugin libraries')
categories=('multimedia')
source=("http://clementine-player.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('54dc47eb2de5960cd1654e1249a59bc6')
install="clementine.install"

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   mkdir -p build && cd build
   cmake ../ -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DENABLE_GIO=OFF -DENABLE_PLASMARUNNER=ON
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}/build"
   make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
