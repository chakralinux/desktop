pkgname=higan-qt
_pkgname=higan
pkgver=101
pkgrel=1
pkgdesc='Nintendo multi-system emulator'
arch=('x86_64')
url='http://byuu.org/'
license=('GPL3')
depends=('libao' 'libpulse' 'openal' 'qt' 'sdl' 'dialog')
makedepends=('mesa')
optdepends=('beat: Delta patcher')
provides=("higan=${pkgver}")
conflicts=('higan-gtk' 'higan')
source=("http://download.byuu.org/higan_v${pkgver}-source.7z"
        '0001-higan-flags.patch')
sha1sums=('c877108f2554c74e8a8aafea84b432bd8e988d9d'
          '875c78770b845880d3786b5c2a4f7926c5211e14')

prepare() {
  cd ${srcdir}/higan_v${pkgver}-source

  patch -Np1 -i ../0001-higan-flags.patch
}

build() {
  cd ${srcdir}/higan_v${pkgver}-source

  export hiro=qt
  make -C icarus
  make -C higan
}

package() {
  cd ${srcdir}/higan_v${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,higan,pixmaps}}
  install -m 755 icarus/out/icarus "${pkgdir}"/usr/bin/
  install -m 755 higan/out/higan "${pkgdir}"/usr/bin/
  install -m 644 higan/data/higan.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 higan/data/higan.png "${pkgdir}"/usr/share/pixmaps/
  cp -dr --no-preserve='ownership' higan/systems/* higan/data/cheats.bml "${pkgdir}"/usr/share/higan/

}
