#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=jdk
pkgver=7u1
pkgbuild=b08
pkgrel=1
pkgdesc="Sun's Java Development Kit"
arch=('i686' 'x86_64')
  [ "${CARCH}" = "i686" ]   && ARCH=i586
  [ "${CARCH}" = "x86_64" ] && ARCH=x64
url="http://java.sun.com"
depends=('glibc' 'jre')
install="jdk.install"
source=("http://download.oracle.com/otn-pub/java/jdk/${pkgver}-${pkgbuild}/${pkgname}-${pkgver}-linux-${ARCH}.tar.gz"
        'jdk.profile'
        'jdk.profile.csh'
        'java-control-panel.desktop'
        'java-monitoring-and-management-console.desktop'
        'java-policy-settings.desktop'
        'java-visualvm.desktop'
        'java-web-start.desktop')
noextract=("${pkgname}-${pkgver}-linux-${ARCH}.tar.gz")

md5sums=('661c426a167ef31c121c51f8f57dc7ea'
         'fc2bf7c855629c5a07145dfe72fea914'
         'e4d814c0f310d77ed6990c731bccd0fb'
         '4bc2b0be93f76ed07b471e3c89eb5a8a'
         '8e3cec7ab641ec449cabd068be583f5c'
         '9ba148ebabce8ed3351b813a9a66b6e5'
         '568451cf45fc8c7adb83a222a61ca2c6')

[ "${CARCH}" = 'i686' ]   && md5sums=('7267759f93bc6fb23046dd42d40d1c2f' "${md5sums[@]}")
[ "${CARCH}" = 'x86_64' ] && md5sums=('9707049b591f47e5c3988a9f029c015e' "${md5sums[@]}")

replaces=('j2sdk')
conflicts=('java-environment' 'j2sdk')
provides=('java-environment=7' 'j2sdk')
license=('custom')

package() {
  msg "Extracting the Java Development Kit..."
  install -d "${pkgdir}/opt/java"
  tar -zxf "${srcdir}/${pkgname}-${pkgver}-linux-${ARCH}.tar.gz" -C "${pkgdir}/opt/java"
  mv "${pkgdir}/opt/java/jdk1.7.0_01" "${pkgdir}/opt/java/jdk"

  # Icon
  install -Dm644 "${pkgdir}/opt/java/jdk/lib/desktop/icons/hicolor/48x48/apps/sun-java.png" "${pkgdir}/usr/share/pixmaps/java.png"

  # Environmental variables
  install -D "${srcdir}/jdk.profile" "${pkgdir}/etc/profile.d/jdk.sh"
  install -D "${srcdir}/jdk.profile.csh" "${pkgdir}/etc/profile.d/jdk.csh"

  # License and documentation
  install -d "${pkgdir}/usr/share/licenses/jdk"
  install -m644 "${pkgdir}/opt/java/jdk/COPYRIGHT" "${pkgdir}/usr/share/licenses/jdk"
  install -m644 "${pkgdir}/opt/java/jdk/LICENSE" "${pkgdir}/usr/share/licenses/jdk"
  install -m644 "${pkgdir}/opt/java/jdk/THIRDPARTYLICENSEREADME.txt" "${pkgdir}/usr/share/licenses/jdk"

  # Desktop entries
  install -d "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/java-control-panel.desktop" "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/java-monitoring-and-management-console.desktop" "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/java-policy-settings.desktop" "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/java-visualvm.desktop" "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/java-web-start.desktop" "${pkgdir}/usr/share/applications"
}

# vim:set ts=2 sw=2 et:
