# Maintainer: Daniele Cocca <jmc@chakra-project.org>
pkgname=peazip
pkgver=5.5.0
pkgrel=1
pkgdesc="Free, Open Source file and archive manager. Supports 7Z, ACE, ARC, CAB, ISO, PAQ, PEA, RAR, TAR, ZIP and many more archive formats."
arch=('x86_64')
url="http://peazip.org/"
license=('GPL')
depends=('zip' 'unzip' 'unrar' 'xz' 'p7zip' 'unarj' 'fpc-qtbindings')
makedepends=('lazarus')
optdepends=('rar: support for RAR archive compression')
categories=('utils')
source=("http://sourceforge.net/projects/peazip/files/${pkgver}/${pkgname}-${pkgver}.src.zip")
md5sums=('854377293beaad8d5371aa63e1b6bf0c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}.src
  lazbuild -B --verbose -r project_peach.lpi \
	   --lazarusdir=/usr/lib/lazarus \
	   --widgetset=qt \
	   --primary-config-path=${srcdir}/config
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}.src
  install -Dm755 peazip ${pkgdir}/usr/bin/peazip

  # preparing for desktop installation
  cd ${srcdir}/${pkgname}-${pkgver}.src/FreeDesktop_integration/

  # Move the Dolohin integration
  for i in peazipadd.desktop \
           peazipext.desktop \
           peazipextfolder.desktop \
           peazipexthere.desktop \
           peazipopen.desktop ; do 
    install -Dm755 kde4-dolphin/usr/share/kde4/services/ServiceMenus/${i} \
		   ${pkgdir}/usr/share/kde4/services/ServiceMenus/${i}
  done

  # Move mimetypes and icons
  install -Dm755 peazip.desktop ${pkgdir}/usr/share/applications/peazip.desktop
  install -Dm755 peazip.png ${pkgdir}/usr/share/icons/hicolor/256x256/apps/peazip.png
}
