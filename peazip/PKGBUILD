pkgname=peazip
pkgver=5.7.2
pkgrel=1
pkgdesc="Free, Open Source file and archive manager. Supports 7Z, ACE, ARC, CAB, ISO, PAQ, PEA, RAR, TAR, ZIP and many more archive formats."
arch=('x86_64')
url="http://peazip.org/"
license=('GPL')
depends=('zip' 'unzip' 'unrar' 'xz' 'p7zip' 'unarj' 'fpc-qtbindings')
makedepends=('lazarus')
optdepends=('rar: support for RAR archive compression')
categories=('utils')
source=("https://github.com/giorgiotani/PeaZip/releases/download/${pkgver}/${pkgname}-${pkgver}.src.zip"
        'altconf.txt'
        'peazip.desktop')
sha512sums=('315eb019cf78b360c0cf430b09edbcf197fbe49b56a4f367fcb7762f3ae7293c77f6946f8f4bf372c273962adab5c38ac84447a2e21c093678224612a0caa5d0'
         'cbf521241954b242ed002449a0ad249fec2e434ee676b4dd51fcc768f0ba443dbde2d7dfa2b42627fa8384cd61179346d0d303865273cc0043d1f8d91166073f'
         'e9625c790e41d6b805f57d228114760cd2f8efc9ce5eff0328ae75805110d2241dc8de4eeb97391cfb7a56cd1b1b330d4496f03a753aba290479a9e9ee9ed370')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}.src
  for i in project_pea.lpi project_peach.lpi project_gwrap.lpi ; do 
   lazbuild -B --cpu=${arch} --verbose ${i} \
 	   --lazarusdir=/usr/lib/lazarus \
 	   --widgetset=qt
  done
}

package() {
  # Since the dev are lazy, we introduce several path for easier description
  _srcdir=${srcdir}/${pkgname}-${pkgver}.src
  _datadir=usr/share/${pkgname}
  cd ${_srcdir}
  install -dm755 ${pkgdir}/${_datadir}

  # Move language files and utilities
  cp -rv res/themes ${pkgdir}/${_datadir}/res/
  cp -rv res/lang ${pkgdir}/${_datadir}/res/
  install -Dm644 ${srcdir}/altconf.txt ${pkgdir}/${_datadir}/res/altconf.txt
  
  # Link tools
  for i in 7z upx ; do
    install -dm755 ${pkgdir}/${_datadir}/res/${i}
    ln -s /usr/bin/${i} ${pkgdir}/${_datadir}/res/${i}/${i}
  done

  # Install binary to datadir/res 
  for i in pea pealauncher ; do
    install -Dm755 ${i} ${pkgdir}/${_datadir}/res/${i}
  done
  
  # Install peazip to datadir because at start need to read res/altconf.txt
  install -Dm755 peazip ${pkgdir}/${_datadir}/peazip
  install -dm755 ${pkgdir}/usr/bin
  ln -s /${_datadir}/peazip ${pkgdir}/usr/bin/peazip

  # Preparing for desktop installation
  cd ${_srcdir}/FreeDesktop_integration/
 
  # Move the Dolohin integration
  for i in peazipadd.desktop \
           peazipext.desktop \
           peazipextfolder.desktop \
           peazipexthere.desktop \
           peazipopen.desktop ; do 
    install -Dm755 kde4-dolphin/usr/share/kde4/services/ServiceMenus/${i} \
		   ${pkgdir}/usr/share/kde4/services/ServiceMenus/${i}
  done
  
  #use own .desktop file to fix error name
  install -D ../../peazip.desktop ${pkgdir}/usr/share/applications/peazip.desktop
  # Move icons
  install -Dm755 peazip.png ${pkgdir}/usr/share/icons/hicolor/256x256/apps/peazip.png
  
  # License file
  for r in copying.txt copying_we.txt legal.txt; do
    install -Dm644 ${_srcdir}/${r} ${pkgdir}/usr/share/licenses/${pkgname}/${r}
  done
}
