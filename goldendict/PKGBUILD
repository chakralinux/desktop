pkgname=goldendict
pkgver=1.5.0rc2
pkgrel=7
pkgdesc="Feature-rich dictionary lookup program"
arch=('x86_64')
url="http://goldendict.berlios.de/"
license=('GPL3')
depends=('hunspell' 'libxtst' 'libzip' 'libao' 'qt5-webkit' 'qt5-svg'
         'qt5-x11extras' 'qt5-tools' 'phonon-qt5' 'ffmpeg')
provides=('stardict')
source=($pkgname-$pkgver.tar.gz::https://github.com/goldendict/goldendict/archive/1.5.0-RC2.tar.gz)
sha256sums=('bccee0a3d3902f8fa31e439e220a405065fff774e5f8c581be2b0743d3f83fde')

prepare(){
  cd "${srcdir}"/${pkgname}-1.5.0-RC2
}

build(){
  cd "${srcdir}"/${pkgname}-1.5.0-RC2

  qmake-qt5 "CONFIG+=no_epwing_support" PREFIX="/usr" 
  make
}

package() {
  cd "${srcdir}"/${pkgname}-1.5.0-RC2

  make INSTALL_ROOT="${pkgdir}" install

# languages
  install -d "${pkgdir}"/usr/share/$pkgname/locale
  install -m644 locale/*.qm -t "${pkgdir}"/usr/share/$pkgname/locale

# removing unneeded
  rm -rf "${pkgdir}"/usr/share/app-install
}
