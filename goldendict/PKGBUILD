pkgname=goldendict
pkgver=1.5.0rc
pkgrel=6
pkgdesc="Feature-rich dictionary lookup program"
arch=('x86_64')
url="http://goldendict.berlios.de/"
license=('GPL3')
depends=('hunspell' 'ffmpeg' 'libxtst' 'libzip' 'phonon' 'libao' 'libxslt')
provides=('stardict')
source=($pkgname-$pkgver.tar.gz::https://github.com/goldendict/goldendict/archive/1.5.0-RC.tar.gz
        $pkgname-paths.patch)
sha256sums=('5705d5197509596221faf16a806965c799de7371a41fe2d2c4dc00b737ffe22e'
            '6aa0961a6f64f4f41b30890793c66b9354e768dcef0bad543c9e578015d64c76')

prepare(){
  cd "${srcdir}"/${pkgname}-1.5.0-RC

  patch -Np0 -i "${srcdir}"/$pkgname-paths.patch
}

build(){
  cd "${srcdir}"/${pkgname}-1.5.0-RC

  qmake PREFIX="/usr"
  make
}

package() {
  cd "${srcdir}"/${pkgname}-1.5.0-RC

  make INSTALL_ROOT="${pkgdir}" install

# languages
  install -d "${pkgdir}"/usr/share/$pkgname/locale
  install -m644 locale/*.qm -t "${pkgdir}"/usr/share/$pkgname/locale

# removing unneeded
  rm -rf "${pkgdir}"/usr/share/app-install
}