
pkgname=kdepim-addons
pkgver=18.07.80
pkgrel=1
arch=('x86_64')
pkgdesc="Addons for kdepim apps (as kmail addons/ kmail theme/ kaddressbook addons etc.)"
url="https://github.com/KDE/kdepim-addons"
license=('GPL' 'LGPL' 'FDL')
depends=(incidenceeditor ktnef libksieve akonadi-notes akonadi-import-wizard kparts poppler-qt5 kitinerary)
makedepends=(extra-cmake-modules kdoctools boost)
groups=('kde' 'kde-applications' 'kdepim' 'kde-uninstall')
options=('docs' 'debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('09ba601a855bb749f7ebe277cb76b4b14faf791b2950ec3b6798a67edc61cace'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}

