pkgname=redshift
pkgver=1.12
pkgrel=1
pkgdesc='Adjusts the color temperature of your screen according to your surroundings'
arch=('x86_64')
url='http://jonls.dk/redshift/'
license=('GPL3')
depends=('libxxf86vm' 'libdrm' 'libxcb' 'geoclue')
makedepends=('python3' 'intltool')
source=("https://github.com/jonls/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('d2f8c5300e3ce2a84fe6584d2f1483aa9eadc668ab1951b2c2b8a03ece3a22ba')
 
build() {
  cd $pkgname-$pkgver
  ./configure --disable-gui \
              --disable-ubuntu \
              --enable-drm \
              --enable-randr \
              --enable-vidmode \
              --enable-geoclue2 \
              --prefix=/usr
  make
}
 
package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
  rm $pkgdir/usr/lib/systemd/user/$pkgname-gtk.service
}
 
