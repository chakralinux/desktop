#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=qemu-kvm
_pkgname=kvm
pkgver=1.2.0
pkgrel=1
pkgdesc="Generic and open source processor emulator using dynamic translation to improve speed."
arch=('x86_64')
license=('GPL2' 'LGPL2.1')
url="http://www.linux-kvm.org"
depends=('alsa-lib' 'bluez' 'curl' 'gnutls>=2.4.1' 'libjpeg-turbo' 'libpng' 'pulseaudio'
         'libsasl' 'sdl' 'util-linux-ng' 'vde2')
makedepends=('git' 'iasl' 'perl' 'python2' 'texi2html')
backup=('etc/qemu/target-x86_64.conf')
provides=('qemu')
conflicts=('qemu')
categories=('system')
options=(!strip)
install='qemu-kvm.install'
source=("http://downloads.sourceforge.net/$_pkgname/$pkgname-$pkgver.tar.gz"
        "65-kvm.rules")
md5sums=('d7b18b673c48abfee65a9c0245df0415'
         'b316a066d2f1bb57d8f5b7ea1d0d1caf')

build() {
  cd $srcdir/$pkgname-$pkgver
    
  # Fix esound building.
  ./configure\
    --prefix=/usr \
    --python=/usr/bin/python \
    --sysconfdir=/etc \
    --audio-drv-list=alsa,sdl,oss,pa \
    --audio-card-list=ac97,sb16,es1370,hda \
    --enable-docs

    # Use latest seabios version
    cd "${srcdir}/"
    git clone git://git.seabios.org/seabios.git
    cd seabios
    find 'tools/' 'contrib' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
    make clean
    make

}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # Use latest seabios version
    cp "${srcdir}/seabios/out/bios.bin" "${pkgdir}/usr/share/qemu/bios.bin"

    # symbolic link for backwards compatibility
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/qemu-kvm"
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/qemu"
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/kvm"
    # fix man page
    mv "${pkgdir}/usr/share/man/man1/qemu.1" \
                     "${pkgdir}/usr/share/man/man1/qemu-kvm.1"
    # install udev rules
    install -D -m644 "${srcdir}/65-kvm.rules" \
                     "${pkgdir}/lib/udev/rules.d/65-kvm.rules"
    # strip scripts directory
    find "${pkgdir}/usr/bin"  -type f -perm -u+w 2>/dev/null | while read binary ; do
      case "$(file -bi "$binary")" in
        *application/x-executable*) # Binaries
        /usr/bin/strip $STRIP_BINARIES "$binary";;
      esac
    done

}
