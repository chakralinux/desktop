#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=freetype2-ubuntu
_pkgname=freetype
_pkgname2=freetype2
pkgver=2.4.11
_pkgver=2.4.11-0
pkgrel=1
pkgdesc="TrueType font rendering library, with Ubuntu's LCD rendering patches."
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntu/oneiric/+source/freetype"
license=('GPL')
depends=('zlib')
conflicts=($_pkgname2)
provides=("$_pkgname2=$pkgver")
categories=('system')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.bz2
        https://launchpad.net/ubuntu/+archive/primary/+files/${_pkgname}_${_pkgver}ubuntu1.diff.gz
        apple-offset.patch
	enable-valid.patch)
md5sums=('b93435488942486c8d0ca22e8f768034'
         'd31b655036ff49d814fdad1ca981edf9'
         '1c964f90ae90aa391af574ceb0814cda'
         '214119610444c9b02766ccee5e220680')

build() {
  cd $srcdir/$_pkgname-$pkgver

  # Patch from Ubuntu.
  patch -Np1 -i $srcdir/${_pkgname}_${_pkgver}ubuntu1.diff

  # Patches adjustments.
  sed -i -e "s#\ssrc# freetype-$pkgver/src#g" debian/patches-freetype/freetype-2.1.7-backwards.compat.patch
  sed -i -e "s/-p[0-9]\|.*otvalid\.patch//g" debian/patches-freetype/series
  
  # Patches.
  for _f in $(cat debian/patches-freetype/series) ; do    
    patch -Np1 -i debian/patches-freetype/$_f    
  done

  # Patches from Arch Linux.
  patch -Np1 -i $srcdir/enable-valid.patch

  # Configure and build.
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  make DESTDIR=$pkgdir install
}
