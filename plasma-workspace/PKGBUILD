#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/plasma-workspace
source ../plasma.conf

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'drkonqi')
pkgver=${PVersion}
pkgrel=2
pkgdesc='KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('knewstuff' 'kjsembed' 'knotifyconfig' 'libxdamage' 'kwayland' 'kglobalaccel'
         'libksysguard' 'libkscreen-kf5' 'ktexteditor' 'kxmlrpcclient' 'libqalculate' 'networkmanager-qt' 'kfilemetadata'
         'qt5-tools' 'kded' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot' 'xorg-apps' 'xorg-utils'
         'prison-kf5' 'milou')
makedepends=('extra-cmake-modules' 'kdoctools' 'kwin' 'gpsd' 'baloo-kf5' 'krunner' 'kdewebkit')
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PSubVersion}.tar.xz"
        kde.pam
        qt4pluginpath.sh
        dbus-update-environment.patch
        startkde.patch)
sha256sums=( $(getSum ${pkgname} | head -n1)             
            '44a7c7acee30547dd41d0a593486b29df4eaa5c3707287b87e23b1104de3964a'
            '498aef1b2e5573d82634e9a359c5cdcc6c8b41e803aa648e02ee02cde11b53fd'
            '85efd33aee50703df1afe08df1485738b7f0c6f144dd32fb3d189380d81ee313'
            '850c1057e335215dc6cba4e9c713e1dc61a41570a738fa87ce06c1322dd1ae19')

prepare() {
  mkdir -p build
  
  cd ${pkgbase}-${pkgver}
  patch -Np1 -i ../startkde.patch
  # be sure to use the Qt5 version of qtpaths
  sed -i 's:qtpaths:qtpaths-qt5:' startkde/startkde.cmake
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver}
  make
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS support for geolocation')

  cd build
  make DESTDIR="${pkgdir}" install

  install -D "${srcdir}"/kde.pam \
    "${pkgdir}"/etc/pam.d/kde
  
  # enable QT4 style integration in KDE4 apps
  install -D "${srcdir}"/qt4pluginpath.sh \
    "${pkgdir}"/etc/profile.d/qt4pluginpath.sh

  # Remove conflicts with drkonqi
  rm "${pkgdir}"/usr/lib/drkonqi
  rm -r "${pkgdir}"/usr/share/drkonqi
}

package_drkonqi() {
  pkgdesc='KDE crash handler'
  depends=('kdewebkit')

  cd build/drkonqi
  make DESTDIR="${pkgdir}" install
}
