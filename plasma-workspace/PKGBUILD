source ../plasma.conf

pkgname=plasma-workspace
pkgver=${PVersion}
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('knewstuff' 'baloo-kf5' 'kjsembed' 'knotifyconfig' 'libxcursor'
         'libksysguard' 'libkscreen2' 'ktexteditor' 'libqalculate'
         'qt5-tools' 'kded' 'kde-cli-tools' 'kio-extras' 'milou' 'breeze'
         'xorg-xrdb' 'xorg-xsetroot' 'xorg-apps')
makedepends=('extra-cmake-modules' 'kdoctools' 'kwin' 'gpsd')
optdepends=('plasma-workspace-wallpapers: additional wallpapers'
            'kwin: KDE Window Manager'
            'gpsd: GPS support for geolocation')
conflicts=('kde-workspace')
checkdepends=("cmake")
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PSubVersion}.tar.xz"
        '10-kf5')
sha256sums=( $(getSum ${pkgname} | head -n1)             
            '024c222ad603b0bd69886d83ad970b834de0a7dc93afda24880c4fb9002e2a03')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make
}

check() {
  cd build
  make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  sed 's|qtpaths|qtpaths-qt5|g' -i "$pkgdir/usr/bin/startkde"
  install -Dm755 "${srcdir}/10-kf5" "${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5"
}
