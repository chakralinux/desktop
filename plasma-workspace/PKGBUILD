#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/plasma-workspace
source ../plasma.conf

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'drkonqi')
pkgver=${PVersion}
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('knewstuff' 'kjsembed' 'knotifyconfig' 'libxdamage' 'kwayland' 'kglobalaccel>=5.7.0'
         'libksysguard' 'libkscreen-kf5' 'ktexteditor' 'libqalculate'
         'qt5-tools' 'kded' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot' 'xorg-apps' 'xorg-utils')
makedepends=('extra-cmake-modules' 'kdoctools' 'kwin' 'gpsd' 'baloo-kf5' 'krunner')
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${PSubVersion}.tar.xz"
        'kde.pam')
sha256sums=( $(getSum ${pkgname} | head -n1)             
            '44a7c7acee30547dd41d0a593486b29df4eaa5c3707287b87e23b1104de3964a')

prepare() {
  mkdir -p build
  
  cd ${pkgbase}-${pkgver}
  # be sure to use the Qt5 version of qtpaths
  sed -i 's:qtpaths:qtpaths-qt5:' startkde/startkde.cmake
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver}
  make
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS support for geolocation')
  conflicts=('kde-workspace')

  cd build
  make DESTDIR="${pkgdir}" install

  install -D "${srcdir}"/kde.pam \
    "${pkgdir}"/etc/pam.d/kde

  # Remove conflicts with drkonqi
  rm "${pkgdir}"/usr/lib/drkonqi
  rm -r "${pkgdir}"/usr/share/drkonqi
  rm "${pkgdir}"/usr/lib/libKF5XmlRpcClientPrivate.*
}

package_drkonqi() {
  pkgdesc='KDE crash handler'
  depends=('kdewebkit')

  cd build/drkonqi
  make DESTDIR="${pkgdir}" install
}
