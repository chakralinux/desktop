source ../plasma.conf

pkgname=plasma-workspace
pkgver=${PVersion}
pkgrel=1
pkgdesc="Plasma Workspace"
arch=('x86_64')
url='http://projects.kde.org'
license=('LGPL')
depends=('qt5-base' 'qt5-declarative' 'qt5-script' 'plasma-framework' 'kdelibs4support' 'kemoticons' 'kdesu' 'knewstuff' 'krunner' 'kcmutils' 'kjs' 'kjsembed' 'knotifyconfig' 'ksysguard' 'libkscreen2' 'kwin' 'ktexteditor' 'akonadi-qt5' 'kf5-baloo')
makedepends=('extra-cmake-modules' 'kapidox')
checkdepends=("cmake")
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        '10-kf5')
sha256sums=( $(getSum ${pkgname} | head -n1)             
            '024c222ad603b0bd69886d83ad970b834de0a7dc93afda24880c4fb9002e2a03')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${PBuildType} \
    -DCMAKE_INSTALL_PREFIX=${PInstallPrefix} \
    -DLIB_INSTALL_DIR=lib 
  make
}

check() {
  cd build
  make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  sed 's|qtpaths|qtpaths-qt5|g' -i "$pkgdir/usr/bin/startkde"
  install -Dm755 "${srcdir}/10-kf5" "${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5"
}
