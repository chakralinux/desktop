# Contributor: speps <speps dot aur dot archlinux dot org>

pkgname=pumpa
pkgver=0.9.2
pkgrel=1
pkgdesc="A simple pump.io client written in C++ and Qt"
arch=('x86_64')
url="https://pumpa.branchable.com/"
license=('GPL3')
depends=('qt5-base' 'qjson' 'tidyhtml' 'aspell')
install="$pkgname.install"
source=("https://saz.im/software/downloads/pumpa/pumpa-${pkgver}.tar.xz")
sha256sums=('3ff51a0dc8a966e06d3472b0424c86980c34fc18fede3c59a442d95505922315')

# A small comentary on the source URL. I'm avoiding using the git tags from
# pumpa.branchable.com because it would imply much more work (cloning the repo
# specifying the tag, etc.). The source URL points to the personal server of the
# main developer of Pumpa, "Sazius".

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make INSTALL_ROOT="$pkgdir/" install
  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 icons/512x512/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
    
  # man
  install -Dm644 docs/$pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
}
