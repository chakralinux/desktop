# Maintainer: Francesco Marinucci <franzmari[at]chakra-project[dot]it>
# Contributor: speps <speps dot aur dot archlinux dot org>

pkgname=pumpa
pkgver=0.8.2
pkgrel=1
pkgdesc="A simple pump.io client written in C++ and Qt"
arch=('x86_64')
url="http://gitorious.org/pumpa"
license=('GPL3')
depends=('qt' 'qjson')
install="$pkgname.install"
source=("$url/pumpa/archive-tarball/v$pkgver")
md5sums=('177917ea49384b6c15d8275b8e3d177c')

prepare() {
  cd "$srcdir/$pkgname-$pkgname"

  # desktop file paths fix
  sed -e '/Exec/s/\./\/usr\/bin/' \
      -e '/Icon/s/\.\/pumpa\/images/\/usr\/share\/pixmpas/' \
      -i $pkgname.desktop
}

build() {
  cd "$srcdir/$pkgname-$pkgname"
  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgname"

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 images/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
