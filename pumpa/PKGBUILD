# Contributor: speps <speps dot aur dot archlinux dot org>

pkgname=pumpa
pkgver=0.9
pkgrel=1
pkgdesc="A simple pump.io client written in C++ and Qt"
arch=('x86_64')
url="http://gitorious.org/pumpa"
license=('GPL3')
depends=('qt' 'qjson' 'tidyhtml' 'aspell')
install="$pkgname.install"
source=("${url}/pumpa/archive/v$pkgver.tar.gz")
sha512sums=('034e88ec6b75c02a781fe985ed19f6475e4bf2be73d9d18d3a808392abbed82507d6925bcbc6ad138aba61b93161735bf59dd1afa6a665879cc85e3f831038b8')

prepare() {
  cd "$srcdir/$pkgname-$pkgname"

  # desktop file paths fix
  sed -e '/Exec/s/\./\/usr\/bin/' \
      -e '/Icon/s/\.\/pumpa\/images/\/usr\/share\/pixmpas/' \
      -i $pkgname.desktop
}

build() {
  cd "$srcdir/$pkgname-$pkgname"
  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgname"

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 images/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
    
  # man
  install -Dm644 docs/$pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
}
