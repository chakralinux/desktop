#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>
# contributor: Homey
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdeplasma-applets-stackfolder
pkgver=0.1.9
pkgmin=12
pkgrel=3
pkgdesc="Browse the stack of folders"
url="http://www.rosalab.ru"
arch=('x86_64' 'i686')
license=('GPL')
depends=('kdebase-workspace' 'libjpeg')
makedepends=('rpmextract' 'cmake' 'automoc4')
categories=('accessories')
source=("ftp://ftp.pbone.net/mirror/carroll.cac.psu.edu/pub/linux/distributions/mandrakelinux/devel/cooker/SRPMS/main/release/plasma-applet-stackfolder-${pkgver}-${pkgmin}.src.rpm")
md5sums=('011de531e7b17abc4cb7a3c692a3ab17')
 
build() {
  cd "${srcdir}"
  rpmextract.sh "${srcdir}/plasma-applet-stackfolder-${pkgver}-${pkgmin}.src.rpm"
  tar -xjvf "plasma-applet-stackfolder-${pkgver}.tar.bz2"
 
  mkdir -p "plasma-applet-stackfolder-${pkgver}-build"
  cd "plasma-applet-stackfolder-${pkgver}-build"
  cmake -DCMAKE_RELEASE_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
        "${srcdir}/plasma-applet-stackfolder-${pkgver}"
  make
}
 
package() {
  cd "${srcdir}/plasma-applet-stackfolder-${pkgver}-build"
 
  make DESTDIR="${pkgdir}" install
}


