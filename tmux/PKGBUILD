pkgname=tmux
pkgver=2.1
pkgrel=1
pkgdesc='A terminal multiplexer, intended as an alternative to GNU screen'
arch=('x86_64')
url='http://sourceforge.net/projects/tmux/'
screenshot='http://tmux.sourceforge.net/tmux3.png'
license=('BSD')
depends=('ncurses' 'libevent' 'libutempter')
source=("https://github.com/tmux/tmux/releases/download/${pkgver}/tmux-${pkgver}.tar.gz"
        LICENSE)
sha256sums=('31564e7bf4bcef2defb3cb34b9e596bd43a3937cad9e5438701a81a5a9af6176'
            'b5de80619e4884ced2dfe0a96020e85dcfb715a831ecdfdd7ce8c97b5a6ff2cc')

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    
    install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"

    install -dm755 "$pkgdir/usr/share/tmux/"
    install -m644 examples/* "$pkgdir/usr/share/tmux/"
    install -Dm644 examples/tmux.vim "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"

    install -d $pkgdir/usr/share/bash-completion/completions/
    mv $pkgdir/usr/share/tmux/bash_completion_tmux.sh $pkgdir/usr/share/bash-completion/completions/tmux
}
