# Maintainer: Viech <viech unvanquished net>
# Contributor: Gereon Schomber
# Contributor: Martin F. Schumann <mfs@mfs.name>

pkgname=unvanquished
pkgver=0.33.1
pkgrel=2
pkgdesc='A team-based fps/rts hybrid game which pits aliens against humans. Monthly release that can be played on official servers.'
arch=('x86_64')
url='http://www.unvanquished.net'
license=('GPL3')
makedepends=('cmake')
depends=('curl'               'freetype2' 'glew'               'gmp'
         'libjpeg-turbo'      'ncurses'   'libogg'             'libpng'
         'sdl2'               'libvorbis' 'zlib'               'libwebp>=0.2.0'
         'libtheora'          'nettle'    'speex'              'xvidcore'
         'openal'             'xdg-utils' 'desktop-file-utils' 'shared-mime-info'
         'hicolor-icon-theme' 'geoip'     'opusfile')
provides=('unvanquished')
conflicts=('unvanquished-maps' 'unvanquished-git')
options=('emptydirs')
backup=('etc/unvanquished/main/server.cfg' 'etc/unvanquished/main/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=("unvanquished::git+https://github.com/Unvanquished/Unvanquished.git#tag=v${pkgver}"
        'ChangeLog'                   'unvanquished.install' 'unvanquished.sh'   'unvanquished-tty.sh'
        'unvanquished-update-paks.sh' 'unvanquished.service' 'unvanquished.conf' 'unvanquished.desktop')
md5sums=('SKIP'
         'ce9371c1cd22e58ddf744f64b96583b6'
         'a5246cf3bed53798ddc4d95c6b8c1b37'
         'ac9c54f828edbbf236156431991f6c82'
         '7a10dca92e352e6f47d99a0434681f4e'
         '2e82941e47422fdb3cee4ccfca346458'
         '1842789de67de55b8027c51cad22f778'
         'ce04c7e8423b2da0ea0d8df8be4c8ea4'
         'ac69d49b3c665d274d0ab58870220522')

build() {
	cd $srcdir/unvanquished

	cmake -D GAME_QVM=OFF -D BUILD_TTY_CLIENT=ON .
	make
}

package() {
	# create installation directories
	cd $pkgdir

	install -d     etc/conf.d \
	               etc/unvanquished/main \
	               usr/bin \
	               usr/lib/systemd/system \
	               usr/lib/unvanquished/main \
	               usr/share/applications \
	               usr/share/icons/hicolor/128x128/apps \
	               usr/share/licenses/unvanquished \
	               var/cache/unvanquished/update-paks \
	               var/lib/unvanquished/main \
	               var/lib/unvanquished-server/main

	# install content
	cd $srcdir/unvanquished

	install -m 755 daemon*                 "${pkgdir}/usr/lib/unvanquished/"
	install -m 755 *.so                    "${pkgdir}/usr/lib/unvanquished/"
	install -m 755 game-nacl-native-exe    "${pkgdir}/usr/lib/unvanquished/"
	install -m 755 nacl_helper_bootstrap   "${pkgdir}/usr/lib/unvanquished/"
	install -m 755 sel_ldr                 "${pkgdir}/usr/lib/unvanquished/"
	install -m 755 irt_core-x86*.nexe      "${pkgdir}/usr/lib/unvanquished/"
	install -m 644 debian/unvanquished.png "${pkgdir}/usr/share/icons/hicolor/128x128/apps/"
	install -m 644 COPYING.txt             "${pkgdir}/usr/share/licenses/unvanquished/"

	# install starters
	cd $srcdir

	install -m 644 unvanquished.conf           $pkgdir/etc/conf.d/
	install -m 755 unvanquished.sh             $pkgdir/usr/bin/unvanquished
	install -m 755 unvanquished-tty.sh         $pkgdir/usr/bin/unvanquished-tty
	install -m 755 unvanquished-update-paks.sh $pkgdir/usr/bin/unvanquished-update-paks
	install -m 644 unvanquished.service        $pkgdir/usr/lib/systemd/system/
	install -m 644 unvanquished.desktop        $pkgdir/usr/share/applications/
	
	# setup server home directory
	cd $pkgdir/var/lib/unvanquished-server/main

	ln -s ../../../../etc/unvanquished/main configuration.pk3dir
}

