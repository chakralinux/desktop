# Contribution Arch https://aur.archlinux.org/packages/traverso/

pkgname=traverso
pkgver=0.49.5
pkgrel=1
pkgdesc="Digital Audio Workstation with an innovative User Interface"
arch=('x86_64')
url="http://traverso-daw.org/"
license=('GPL')
depends=('jack' 'fftw' 'wavpack' 'lame' 'libmad' 'qt5-base' 'shared-mime-info' 'lilv')
makedepends=('cmake')
categories=('multimedia')
source=("http://traverso-daw.org/${pkgname}-${pkgver}.tar.gz")
install=traverso.install
sha256sums=('75980d93e600d9cca0ee086819e2c8c07740631c4da096b78aec00e432d62d99')

build() {
	cd "$pkgname-$pkgver"
	[[ -d bld ]] || mkdir bld && cd bld
	cmake .. -DCMAKE_INSTALL_PREFIX="/usr" \
        -DWANT_MP3_ENCODE=ON
	make
}

package(){
  cd "$pkgname-$pkgver"
  cd bld; make DESTDIR="$pkgdir/" install
  cd ../resources
  # desktop file
  sed '/Path/d' -i traverso.desktop 
  install -Dm644  traverso.desktop "$pkgdir"/usr/share/applications/traverso.desktop

  # icons
  install -dm755 "$pkgdir"/usr/share/icons/hicolor
  cp -a freedesktop/icons/* "$pkgdir"/usr/share/icons/hicolor

  # mime & docs
  install -Dm644 x-traverso.xml "$pkgdir"/usr/share/mime/packages/x-traverso.xml
  install -Dm644 help.text "$pkgdir"/usr/share/doc/traverso/help.txt
}
