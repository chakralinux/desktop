# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegames-kdiamond/
# Include global configuration

pkgname=kdiamond
pkgver=18.07.80
pkgrel=1
pkgdesc="A single player puzzle game"
arch=('x86_64')
url='http://kde.org/applications/games/kdiamond/'
screenshot='https://www.kde.org/images/screenshots/kdiamond.png'
license=('GPL' 'LGPL' 'FDL')
depends=('knotifyconfig' 'libkdegames')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegames')
replaces=('kdegames-kdiamond')
conflicts=('kdegames-kdiamond')
options=('docs' 'debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8b7a2e3d93bc5485dca67267ed907280500125d50cb0b65edf8e9c441288ac5b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
