#
# Chakra Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=libdrm-git
pkgver=20101120
pkgrel=1
pkgdesc='Userspace interface to kernel DRM services.'
arch=('i686' 'x86_64')
url='http://dri.freedesktop.org/'
license=('custom')
depends=('glibc')
makedepends=('git')
provides=('libdrm=2.4.22')
conflicts=('libdrm')
options=('!libtool')
source=('no-pthread-stubs.patch'
        'COPYING')
md5sums=('9b5ec3bbebe86921e2057694c42f65b8'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')

_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='drm'

build() {
  msg 'Connecting to git.freedesktop.org GIT server....'

  if [ -d $_gitname ] ; then
    cd $_gitname
    git pull origin
    msg 'The local files are updated.'
  else
    git clone $_gitroot
  fi

  msg 'GIT checkout done or server timeout'

  msg 'Creating build directory'
  cd ${srcdir}
  rm -rf $_gitname-build
  cp -R $_gitname $_gitname-build

  msg 'Starting make...'
  cd $_gitname-build

  msg 'Applying patches ...'
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch"

  sh autogen.sh \
    --prefix=/usr \
    --enable-nouveau-experimental-api \
    --enable-radeon \
    --enable-udev

  make
}

package() {
  cd $_gitname-build

  make DESTDIR="${pkgdir}" install

  # Install license file
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
