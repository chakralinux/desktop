pkgname=sqlcipher
pkgver=3.4.2
pkgrel=1
pkgdesc="SQLite extension that provides transparent 256-bit AES encryption of database files"
arch=('x86_64')
url="https://www.zetetic.net/sqlcipher/"
license=('BSD')
makedepends=('openssl' 'sqlite3' 'tcl')
source=($pkgname-$pkgver.tar.gz::https://github.com/sqlcipher/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('69897a5167f34e8a84c7069f1b283aba88cdfa8ec183165c4a5da2c816cfaadb')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-tempstore=yes \
                CFLAGS="$CFLAGS -DSQLITE_HAS_CODEC" LDFLAGS="-lcrypto"
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
