# Maintainer: Neo Kolokotronis <tetris4@gmail.com
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmygpo-qt5
_pkgname=libmygpo-qt
pkgver=1.1.0
pkgrel=1
pkgdesc="A C++/Qt client library for gpodder.net"
arch=('x86_64')
url="http://wiki.gpodder.org/wiki/Libmygpo-qt"
license=('LGPL2.1')
depends=('qt5-base')
makedepends=('cmake')
source=("http://stefan.derkits.at/files/${_pkgname}/${_pkgname}.${pkgver}.tar.gz")
md5sums=('02c67ce51f025fcc5f71fbb4d6d029d6')

prepare() {
  cd "${srcdir}"
  [[ -e build ]] && rm -rf build
  mkdir build
}

build() {
  cd build
  cmake "../${_pkgname}.${pkgver}"  \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT4=OFF
  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
