# Contributors from Arch: https://www.archlinux.org/packages/extra/x86_64/libaccounts-qt4/

pkgbase=libaccounts-qt
pkgname=('libaccounts-qt4' 'libaccounts-qt5')
pkgver=1.13
pkgrel=1
arch=('x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
makedepends=('qt' 'libaccounts-glib' 'doxygen' 'qt5-base')
options=('!docs' '!emptydirs')
source=("https://sources.archlinux.org/other/packages/${pkgbase}/accounts-qt-$pkgver.tar.bz2")
sha1sums=('df4f51ea3dac547e0cbdda04d60f6c501d6280f2')

prepare() {
  cd accounts-qt-$pkgver
  sed -i 's|SUBDIRS  += Accounts tests|SUBDIRS += Accounts|' accounts-qt.pro
  cd ..

  cp -r accounts-qt-${pkgver} accounts-qt-${pkgver}-qt4

}

build() {
  cd ${srcdir}/accounts-qt-$pkgver
  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make

  cd ${srcdir}/accounts-qt-$pkgver-qt4
  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_libaccounts-qt4() {
  pkgdesc="Qt4-based client library for accessing the online accounts database"
  depends=('qt' 'libaccounts-glib')

  cd accounts-qt-$pkgver-qt4
  make INSTALL_ROOT="$pkgdir" install
}

package_libaccounts-qt5() {
  pkgdesc="Qt5-based client library for accessing the online accounts database"
  depends=('qt5-base' 'libaccounts-glib')

  cd accounts-qt-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
