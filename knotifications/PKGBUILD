# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=knotifications
pkgver=${KFVersion}
pkgrel=1
pkgdesc='Event notifier'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/knotifications'
license=('LGPL')
depends=('kwindowsystem' 'libdbusmenu-qt5' 'libxslt' 'kservice' 'kiconthemes' 'phonon-qt5')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
options=('debug')
source=("${KFServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=( $(getSum ${pkgname}) )

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib
  make
}

check() {
  cd build
  msg "does not work currently, needs gui?"
  make test || return 0
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
