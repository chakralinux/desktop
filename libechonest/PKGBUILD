pkgname=libechonest
pkgver=2.3.1
pkgrel=5
pkgdesc="C++ library for interfacing with Echo Nest"
arch=('x86_64')
url="https://projects.kde.org/projects/playground/libs/libechonest"
license=('GPL')
depends=('qt5-base')
makedepends=('cmake')
conflicts=('libechonest-qt4')
source=(http://files.lfranchi.com/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('9d7245c71e707651a7054ce6f0d90b9a62004b23')

prepare() {
  rm -rf "build"
  mkdir "build"
  
}

build() {
  cd "build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DECHONEST_BUILD_TESTS=off \
        -DBUILD_WITH_QT4=OFF \
        "../${pkgname}-${pkgver}"
  make
}

package() {
  cd "build"
  make DESTDIR="${pkgdir}" install
}
