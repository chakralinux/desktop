# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/libkscreen-frameworks

source ../plasma.conf

pkgname=libkscreen-kf5
_pkgname=libkscreen
pkgver=${PVersion}
pkgrel=1
pkgdesc='KDE screen management software'
arch=('x86_64')
url='https://projects.kde.org/projects/extragear/libs/libkscreen'
license=('LGPL')
depends=('qt5-x11extras' 'libxrandr')
makedepends=('extra-cmake-modules')
groups=('plasma')
options=("debug")
source=("${PServer}/${pkgver}/${_pkgname}-${PSubVersion}.tar.xz")
sha256sums=( $(getSum ${_pkgname}) )

replaces=('libkscreen2')
provides=('libkscreen2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
