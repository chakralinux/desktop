# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/mpd

pkgname=mpd
pkgver=0.19.10
pkgrel=1
pkgdesc='Flexible, powerful, server-side application for playing music'
url='http://www.musicpd.org/'
license=('GPL')
arch=('x86_64')
depends=('audiofile' 'avahi' 'curl' 'faad2' 'ffmpeg' 'jack' 'libao' 'libid3tag'
         'libmad' 'libmms' 'libmodplug' 'libpulse' 'libshout' 'sqlite3' 'wavpack'
         'libmpdclient' 'yajl' 'opus' 'libbluray' 'libgme' 'icu' 'libupnp' 'libnfs'
         'x264')
makedepends=('boost' 'doxygen')
source=("http://www.musicpd.org/download/mpd/stable/${pkgname}-${pkgver}.tar.xz"
        'tmpfiles.d'
        'mpd.conf')
backup=('etc/mpd.conf')
install=install
sha512sums=('037e1ccd69456bd7d388cbcba26de83144816684fb266995b173df960051e84203d3fc7255d7766eee5dbe0629dbb7745b9821664486158d33da4f29189edef8'
          '3608f8b0418aa5527917c35308aeca80357c3cf1834cceeade2eaab7fa736117c0b3143cf225478441ffc533b45ff1e8c5579a2e1aa432a4db5ca4cef2dd04e1'
          '25a823740d92da8e186916701413114142eb6ad91a172c592e68b569c8e4f50fa99580e555ccf6cd31fc4f55a09bfe0278efa46e4e76ee0fe02846292fadf3c1')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-libmpdclient \
        --enable-jack \
        --enable-soundcloud \
        --enable-pipe-output \
        --enable-pulse \
        --disable-sidplay \
        --with-systemdsystemunitdir=/usr/lib/systemd/system
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -d -g 45 -o 45 "${pkgdir}"/var/lib/mpd/playlists
    install -Dm644 ../mpd.conf "${pkgdir}"/etc/mpd.conf
    install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/mpd.conf

    install -Dm644 "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
    sed '/\[Service\]/a User=mpd' -i "${pkgdir}"/usr/lib/systemd/system/mpd.service
    sed '/WantedBy=/c WantedBy=default.target' -i "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
}
