pkgname=mpd
pkgver=0.20.23
pkgrel=1
pkgdesc='Flexible, powerful, server-side application for playing music'
url='https://www.musicpd.org/'
license=('GPL')
arch=('x86_64')
depends=('audiofile' 'avahi' 'curl' 'faad2' 'ffmpeg' 'jack' 'libao' 'libid3tag'
         'libmad' 'libmms' 'libmodplug' 'libpulse' 'libshout' 'sqlite3' 'wavpack'
         'libmpdclient' 'yajl' 'opus' 'libbluray' 'libgme' 'icu' 'libupnp' 'libnfs'
         'x264' 'smbclient' 'libcdio-paranoia' 'fluidsynth' 'mpg123' 'libmpcdec'
         'wildmidi' 'twolame' 'openal' 'zziplib')
makedepends=('boost' 'doxygen')
source=("https://www.musicpd.org/download/mpd/0.20/${pkgname}-${pkgver}.tar.xz"
        'tmpfiles.d'
        'sysusers.d'
        'conf')
backup=('etc/mpd.conf')
sha256sums=('503e5f9f237290f568ff7956ab2f9aed563594bf749f19b8fe994fb21434afea'
            '2faa85c12449a5b3ca422ff1c1fa06d057c7e262a74bfa6298c914a92d6f2e7a'
            '0b74c6e5db08daab3091dc15a6b0c75210ba2f9e98fa074f6cfd605a461056b6'
            'f40f68205834ca53cea3372e930bfe6c2f9ecc9df3b1605df2fec63a658b2e03')
build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-libmpdclient \
        --enable-jack \
        --enable-pipe-output \
        --enable-pulse \
        --enable-soundcloud \
        --enable-zzip \
        --disable-sidplay \
        --enable-systemd-daemon \
        --with-systemduserunitdir=/usr/lib/systemd/user \
        --with-systemdsystemunitdir=/usr/lib/systemd/system
    make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../conf "${pkgdir}"/etc/mpd.conf
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/mpd.conf
	install -Dm644 ../sysusers.d "${pkgdir}"/usr/lib/sysusers.d/mpd.conf

	sed '/\[Service\]/a User=mpd' -i "${pkgdir}"/usr/lib/systemd/system/mpd.service
	sed '/WantedBy=/c WantedBy=default.target' -i "${pkgdir}"/usr/lib/systemd/system/mpd.service
}
