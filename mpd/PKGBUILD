# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/mpd

pkgname=mpd
pkgver=0.18.4
pkgrel=1
pkgdesc='Flexible, powerful, server-side application for playing music'
url='http://mpd.wikia.com/wiki/Music_Player_Daemon_Wiki'
license=('GPL')
arch=('x86_64')
depends=('audiofile' 'avahi' 'curl' 'faad2' 'ffmpeg' 'jack' 'libao' 'libid3tag' 'libmad' 'libmms' 'libmodplug'
         'libpulse' 'libshout' 'sqlite3' 'wavpack' 'libmpdclient' 'yajl' 'opus')
makedepends=('doxygen')
source=("http://www.musicpd.org/download/mpd/stable/${pkgname}-${pkgver}.tar.xz"
        'tmpfiles.d') 
backup=('etc/mpd.conf')
install=install
sha512sums=('f12e3bca60be59ad45493c03ef49284807ab5ed5401a476407361f69d093e71ffa22004b55c18b633833e982524815335b14cce5bdfdee90b82abac3a3c347da'
            '3608f8b0418aa5527917c35308aeca80357c3cf1834cceeade2eaab7fa736117c0b3143cf225478441ffc533b45ff1e8c5579a2e1aa432a4db5ca4cef2dd04e1')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-jack \
        --enable-soundcloud \
        --enable-pulse \
	--enable-libmpdclient \
        --disable-sidplay \
        --with-systemdsystemunitdir=/usr/lib/systemd/system 
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    sed \
        -e '/^#playlist_directory/c playlist_directory "/var/lib/mpd/playlists"' \
        -e '/^#db_file/c db_file "/var/lib/mpd/mpd.db"' \
        -e '/^#pid_file/c pid_file "/run/mpd/mpd.pid"' \
        -e '/^#state_file/c state_file "/var/lib/mpd/mpdstate"' \
        -e '/^#user/c user "mpd"' \
        -i doc/mpdconf.example
    install -d -g 45 -o 45 "${pkgdir}"/var/lib/mpd/playlists
    install -Dm644 doc/mpdconf.example "${pkgdir}"/etc/mpd.conf
    install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/mpd.conf
    install -d "${pkgdir}"/usr/lib/systemd/user
    ln -s ../system/mpd.service "${pkgdir}"/usr/lib/systemd/user/mpd.service

}
