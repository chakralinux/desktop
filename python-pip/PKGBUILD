# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Neophytos Kolokotronis (tetris4) <tetris4@gmail.com>
#               Stephen McIntosh <stephenmac7@gmail.com>
#               Dan McGee <dan@archlinux.org>
#               Sebastien Binet <binet@lblbox>

pkgname=('python3-pip' 'python2-pip')
pkgver=1.4.1
pkgrel=1
pkgdesc="An easy_install replacement for installing pypi python packages"
url="http://www.pip-installer.org/"
arch=('any')
license=('MIT')
makedepends=('python3' 'python3-distribute' 'python2' 'python2-distribute')
source=(http://pypi.python.org/packages/source/p/pip/pip-${pkgver}.tar.gz)
md5sums=('6afbb46aeb48abac658d4df742bff714')

package_python3-pip() {
    depends=('python3' 'python3-distribute')

    cd "$srcdir/pip-$pkgver"
    python3 setup.py build
    python3 setup.py install \
        --prefix=/usr \
        --root="$pkgdir"

    mv "$pkgdir/usr/bin/pip" "$pkgdir/usr/bin/pip3"
    sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python3|" \
        ${pkgdir}/usr/lib/python3.3/site-packages/pip/__init__.py

    install -D -m644 LICENSE.txt \
        "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pip() {
    depends=('python2' 'python2-distribute')

    cd "$srcdir/pip-$pkgver"
    python2 setup.py build
    python2 setup.py install \
        --prefix=/usr \
        --root="$pkgdir"

    ln "$pkgdir/usr/bin/pip" "$pkgdir/usr/bin/pip2"

    install -D -m644 LICENSE.txt \
        "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
