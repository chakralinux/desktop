pkgname=krecipes
pkgver=2.1.0
pkgrel=1
pkgdesc="A tool designed to make organizing your personal recipes collection fast and easy."
arch=('x86_64')
url="http://extragear.kde.org/apps/krecipes/"
license=('GPL')
depends=('kde-runtime' 'qimageblitz' 'hicolor-icon-theme')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
optdepends=('mariadb' 'postgresql')
categories=('utils')
install=${pkgname}.install
source=(http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz)
md5sums=('422ff3df8ab5aac56617d87942762e88')

build() { 
  cd $pkgname-$pkgver
  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR=${pkgdir} install
}
