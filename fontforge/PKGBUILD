# Contribution from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/fontforge

pkgname=fontforge
pkgver=20161012
pkgrel=2
pkgdesc="An outline and bitmap font editor"
arch=('x86_64')
url="http://www.fontforge.org"
license=('BSD')
depends=('libtool' 'libxkbui' 'libxi' 'pango' 'giflib' 'libtiff' 'libspiro>=0.5' 'libxml2' 'libunicodenames'
         'zeromq' 'python3' 'desktop-file-utils' 'hicolor-icon-theme' )
install=${pkgname}.install
source=("https://github.com/fontforge/fontforge/archive/${pkgver}.tar.gz"
        'http://fontforge.org/cidmaps.tgz')
sha512sums=('7ce8b7b6350ffbf03ddfdbc81f12798ea7c02e326fa163b1b1a06ff1f97ce508c725d1c0084f2a0befebbf8edda75ea0151a37b7d51e4578858a0df854a3090d'
            'a85daeb16b6650179f54ff9e35915c4c7bb5e2efca0455262a271063849484dbba08c4b493c4ca08552365205995cad5c3770ba7b3cadb2e5c386626dc46636c')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LIBZMQ_LIBS=-lzmq
	./bootstrap --force
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-python-extension
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/fontforge"
	install -m644 ../Adobe-* "${pkgdir}/usr/share/fontforge"

	install -d "${pkgdir}/usr/share/icons"
	cp -a desktop/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 desktop/fontforge.desktop "${pkgdir}/usr/share/applications/fontforge.desktop"
}
