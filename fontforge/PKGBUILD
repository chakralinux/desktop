#contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/fontforge

pkgname=fontforge
_pkgver=2014-11-26
pkgver=${_pkgver//-/}
pkgrel=2
pkgdesc="An outline and bitmap font editor"
arch=('x86_64')
url="http://www.fontforge.org"
license=('BSD')
depends=('libtool' 'libxkbui' 'libxi' 'pango' 'giflib' 'libtiff' 'libspiro' 'libxml2' 'libunicodenames'
         'zeromq' 'python2' 'desktop-file-utils' 'hicolor-icon-theme' )
install=${pkgname}.install
source=("http://github.com/fontforge/fontforge/releases/download/${pkgver}/fontforge-${_pkgver}-Unix-Source.tar.gz"
        'http://fontforge.org/cidmaps.tgz')
sha1sums=('ecd776480a47cdcbe1b30ce275172d7d52288e77'
          'efbc7c9d3e95159f5600dc71f9cccb370e46bb94')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	rm -fr libltdl
	for i in m4/*.m4; do
		if [[ ! -L $i ]]; then continue; fi
		j=$(readlink "$i")
		ln -sf "${j/aclocal\//aclocal\/lt}" "$i"
	done
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LIBZMQ_LIBS=-lzmq
	export PYTHON=python2
	./bootstrap --force
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-python-extension
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/fontforge"
	install -m644 ../Adobe-* "${pkgdir}/usr/share/fontforge"

	install -d "${pkgdir}/usr/share/icons"
	cp -a desktop/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 desktop/fontforge.desktop "${pkgdir}/usr/share/applications/fontforge.desktop"
}
