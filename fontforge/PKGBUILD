# Contribution from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/fontforge

pkgname=fontforge
pkgver=20170731
pkgrel=2
pkgdesc="An outline and bitmap font editor"
arch=('x86_64')
url="https://fontforge.github.io/"
license=('BSD')
depends=('libtool' 'libxkbui' 'libxi' 'pango' 'giflib' 'libtiff' 'libspiro>=0.5' 'libxml2' 'libunicodenames'
         'zeromq' 'python3' 'hicolor-icon-theme')
source=("https://github.com/fontforge/fontforge/archive/${pkgver}.tar.gz"
        'https://fontforge.github.io/cidmaps.tgz')
sha256sums=('89203db05658e79418104fb6442b3feffb20762198c2d8b0343db49a07769302'
            '1bf9c7eb8835e6ed94e62cb49f1141bc046c562849e52e6c3c7f1d7cfc95c7b3')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LIBZMQ_LIBS=-lzmq
	./bootstrap --force
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-python-extension
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/fontforge"
	install -m644 ../Adobe-* "${pkgdir}/usr/share/fontforge"

	install -d "${pkgdir}/usr/share/icons"
	cp -a desktop/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 desktop/fontforge.desktop "${pkgdir}/usr/share/applications/fontforge.desktop"
}
