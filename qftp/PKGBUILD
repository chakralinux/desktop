#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qftp
pkgver=1.4.3
pkgrel=1
pkgdesc="A Qt client for FTP file transfers."
arch=('i686' 'x86_64')
url="http://www.phenix.bnl.gov/WWW/publish/hpereira/software/index.php?page=package&package_list=software_list_qt4&package=qftp-qt4&full=1"
license=('GPL')
depends=('qt')
categories=('network')
source=("http://www.phenix.bnl.gov/WWW/publish/hpereira/software/tgz/${pkgname}-${pkgver}.tar.gz")
md5sums=('fc22583f2b5c97f6919bf2600b796cd8')

build() {
  cd "${srcdir}"

  mkdir -p build
  cd build

  # The default LDFLAGS="-Wl,--hash-style=gnu -Wl,--as-needed" specified
  # in /etc/makepkg.conf will break the linkage procedure.
  unset LDFLAGS

  cmake "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${srcdir}/build"
  make install DESTDIR="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
