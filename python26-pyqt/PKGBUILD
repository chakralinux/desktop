# Maintainer: Daniele Cocca <jmc@chakra-project.org>

_pkgname=PyQt-x11-gpl
pkgname=python26-pyqt
pkgver=4.8.4
pkgrel=1
pkgdesc="A set of Python bindings for the Qt toolkit."
arch=('i686' 'x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
depends=('python26-sip' 'python26-dbus-python' 'qt')
makedepends=('phonon' 'python26-opengl' 'qt-assistant-compat')
optdepends=('phonon: enable audio and video in PyQt applications'
        'python26-opengl: enable OpenGL 3D graphics in PyQt applications'
        'qscintilla: QScintilla API'
        'qt-assistant-compat: add PyQt online help in Qt Assistant')
provides=("python26-pyqt4=${pkgver}")
replaces=('python26-pyqt4')
conflicts=('python26-pyqt4')
source=("http://riverbankcomputing.co.uk/static/Downloads/PyQt4/${_pkgname}-${pkgver}.tar.gz")
md5sums=('97c5dc1042feb5b3fe20baabad055af1')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python2.6 configure.py \
    --confirm-license \
    --qsci-api

  # Thanks Gerardo for the rpath fix
  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  #
  # Rename or move away files that might conflict with other PyQt installations.
  #
  __to_fix=('pylupdate4' 'pyrcc4' 'pyuic4')
  for file in ${__to_fix[@]}; do
    mv "${pkgdir}/usr/bin/${file}" "${pkgdir}/usr/bin/${file}-2.6"
  done

  mv "${pkgdir}/usr/share/qt/qsci/api/python/PyQt4.api" \
    "${pkgdir}/usr/share/qt/qsci/api/python/PyQt4-2.6.api"
}

# vim:set ts=2 sw=2 et:
