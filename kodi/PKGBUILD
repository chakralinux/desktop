pkgname=kodi
pkgver=16.0
_codename=Jarvis
pkgrel=1
arch=('x86_64')
url="http://kodi.tv"
license=('GPL2')
pkgdesc="A software media player and entertainment hub for digital media"
screenshot=http://kodi.tv/wp-content/uploads/kodi_about.jpg
  # depends expected for kodi plugins:
  # 'python2-pillow' 'python2-pybluez' 'python2-simplejson'
  # depends expeced in FEH.py
  # 'mesa-demos' 'xorg-xdpyinfo'
depends=('python2-pillow' 'python2-simplejson' 'mesa-demos' 'bluez-libs' 'fribidi' 'glew' 'hicolor-icon-theme'
         'libcdio' 'libjpeg-turbo' 'libmariadbclient' 'libmicrohttpd' 'libpulse' 'libssh' 'libva' 'libxrandr'
         'libxslt' 'lzo2' 'sdl2' 'smbclient' 'taglib' 'tinyxml' 'yajl' 'libusb' 'nettle' 'dcadec' 'crossguid-git')
optdepends=(
    'bluez: Blutooth support'
    'libnfs: NFS shares support'
    'libplist: AirPlay support'
    'libcec: Pulse-Eight USB-CEC adapter support'
    'lirc: Remote controller support'
    'pulseaudio: PulseAudio support'
    'udisks: Automount external drives'
    'unrar: Archives support'
    'unzip: Archives support'
    'upower: Display battery level')
makedepends=(
  'autoconf' 'automake' 'libusb'
  'bluez-libs' 'boost' 'cmake' 'curl' 'doxygen' 'git' 'glew'
  'gperf' 'hicolor-icon-theme' 'jasper' 'java-runtime' 'libass' 'libaacs'
  'libbluray' 'libcdio' 'libcec' 'libgl' 'libmariadbclient' 'libmicrohttpd'
  'libmodplug' 'libmpeg2' 'libnfs' 'libplist' 'libpulse' 'libssh' 'libva'
  'libvdpau' 'libxrandr' 'libxslt' 'lzo2' 'nasm' 'nss-mdns' 'python2-pillow'
  'python2-simplejson' 'rtmpdump' 'sdl2' 'sdl_image'
  'smbclient' 'swig' 'taglib' 'tinyxml' 'unzip' 'upower' 'yajl' 'zip'
)
provides=('xbmc')
conflicts=('xbmc')
replaces=('xbmc')
source=("$pkgname-$pkgver-$_codename.tar.gz::https://github.com/xbmc/xbmc/archive/$pkgver-$_codename.tar.gz")
sha256sums=('0421ea1337cdee674f8a36d995f54152b5ddc4100e53410b3aeeb3b3f7d53946')

prepare() {
  cd "$srcdir/xbmc-$pkgver-$_codename"

  find -type f -name *.py -exec sed 's|^#!.*python$|#!/usr/bin/python2|' -i "{}" +
  sed 's|^#!.*python$|#!/usr/bin/python2|' -i tools/depends/native/rpl-native/rpl
	sed 's/python/python2/' -i tools/Linux/kodi.sh.in
  sed 's/shell python/shell python2/' -i tools/EventClients/Makefile.in
}

build() {
  cd "$srcdir/xbmc-$pkgver-$_codename"

  # Bootstrapping
  MAKEFLAGS=-j1 ./bootstrap

  # Configuring Kodi
  export PYTHON_VERSION=2  # external python v2
  ./configure --prefix=/usr --exec-prefix=/usr \
    --disable-debug \
    --enable-optimizations \
    --enable-libbluray \
    --disable-gtest \
    --with-lirc-device=/run/lirc/lircd \
    ac_cv_type__Bool=yes

  # Now (finally) build
  make
}

package() {
  cd "$srcdir/xbmc-$pkgver-$_codename"
  # Running make install
  make DESTDIR="$pkgdir" install

  # Licenses
	install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  for licensef in LICENSE.GPL copying.txt; do
		mv ${pkgdir}/usr/share/doc/kodi/${licensef} \
			${pkgdir}/usr/share/licenses/${pkgname}
  done
}
