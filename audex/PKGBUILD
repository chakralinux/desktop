pkgname=audex
pkgver=0.79+84+g25d4ca1
pkgrel=1
pkgdesc="A CDDA extraction tool with a ftp upload function for KDE"
arch=('x86_64')
url="http://kde-apps.org/content/show.php?content=77125"
license=('GPL')
depends=('libkcddb' 'hicolor-icon-theme' 'kcmutils')
makedepends=('extra-cmake-modules' 'git')
optdepends=('vorbis-tools: OGG encoder'
            'faac: MP4/M4A/AAC encoder'
            'flac: FLAC encoder'
            'lame: MP3 encoder')
_commit=596f6c7ca41e3953e721bc00b321c6ae9ce3e8b3 #master
source=("git://git.kde.org/audex.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/-/+/g' -e 's/v//'
}

prepare() {
  cd $pkgname
  mkdir build
}

build() {
  cd $pkgname/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname/build

  make DESTDIR=$pkgdir install
}
