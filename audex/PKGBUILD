#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=audex
pkgver=0.76b2
pkgrel=1
pkgdesc="A CDDA extraction tool with a ftp upload function for KDE"
arch=('x86_64')
url="http://kde.maniatek.de/audex/"
license=('GPL')
depends=('kde-runtime' 'libkcddb' 'libkcompactdisc' 'cdparanoia')
makedepends=('cmake' 'automoc4')
optdepends=('vorbis-tools: OGG encoder'
            'faac: MP4/M4A/AAC encoder'
            'flac: FLAC encoder'
            'lame: MP3 encoder')
categories=('multimedia')
install=${pkgname}.install
source=("http://kde.maniatek.com/audex/files/${pkgname}-${pkgver}.tar.xz")
md5sums=('c971bbfa113e4d54b0dcaae8afbf70fb')

build() {
  cd ${srcdir}
  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
