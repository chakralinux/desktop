# $Id: PKGBUILD 13455 2008-09-27 11:03:58Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libcanberra
pkgname=(libcanberra libcanberra-pulse)
pkgver=0.26
pkgrel=1
pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification without gtk support"
arch=(i686 x86_64)
license=('LGPL')
depends=('libvorbis>=1.3.1' 'gstreamer0.10>=0.10.30' 'libtool' 'alsa-lib>=1.0.23' 'tdb>=1.2.1')
makedepends=('libpulse')
options=(!emptydirs)
url=http://0pointer.de/lennart/projects/libcanberra
source=(http://0pointer.de/lennart/projects/${pkgbase}/${pkgbase}-${pkgver}.tar.gz)
md5sums=('ee2c66ada7c851a4e7b6eb1682285a24')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
      --disable-static --with-builtin=dso --enable-null --disable-oss \
      --enable-alsa --enable-gstreamer --enable-pulse --disable-gtk
  make
}

package_libcanberra() {
  install=libcanberra.install

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/lib/libcanberra-gtk.la"
  rm -f "${pkgdir}/usr/lib/gtk-2.0/modules/"*.la
  
  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"

  # Split libcanberra-pulse
  mkdir pulse-plugin
  mv "${pkgdir}"/usr/lib/${pkgbase}-${pkgver}/${pkgbase}-pulse.* pulse-plugin
}

package_libcanberra-pulse() {
  pkgdesc="PulseAudio plugin for libcanberra"
  depends=("$pkgbase=$pkgver-$pkgrel" 'libpulse')
  groups=('pulseaudio')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  mkdir -p "${pkgdir}/usr/lib/${pkgbase}-${pkgver}"
  mv pulse-plugin/* "${pkgdir}/usr/lib/${pkgbase}-${pkgver}" 
}

