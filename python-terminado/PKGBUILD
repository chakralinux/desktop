pkgbase=python-terminado
pkgname=('python3-terminado' 'python2-terminado')
_pkgname=terminado
pkgver=0.5
pkgrel=1
pkgdesc="Terminals served to term.js using Tornado websockets"
url="https://github.com/takluyver/terminado"
arch=('any')
license=('BSD')
depends=('python3-tornado' 'python3-ptyprocess')
makedepends=('python3-setuptools' 'python2-setuptools'
             'python3-tornado' 'python3-ptyprocess'
             'python2-tornado' 'python2-ptyprocess')
source=("http://pypi.python.org/packages/source/t/$_pkgname/$_pkgname-$pkgver.tar.gz"
        'https://raw.githubusercontent.com/takluyver/terminado/master/LICENSE.txt')
sha256sums=('63e893eff1ba84f1ee7c4bfcca7676ba1de6394538bb9aa80cbbc8866cb875b6'
         '61aac07b6aa6373a93f43def1f86d404238eef1f9e297f268462b9c59dd60e2a')

prepare() {
  cd "$srcdir"
  cp -r $_pkgname-$pkgver ${_pkgname}2-$pkgver
}

package_python3-terminado() {
  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 "$srcdir/LICENSE.txt" "$pkgdir/usr/share/licenses/python-$_pkgname/LICENSE"
}

package_python2-terminado() {
  depends=('python2-tornado' 'python2-ptyprocess')
  cd "$srcdir/${_pkgname}2-$pkgver"
  python2 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 "$srcdir/LICENSE.txt" "$pkgdir/usr/share/licenses/python2-$_pkgname/LICENSE"
}
