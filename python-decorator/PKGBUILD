pkgbase=python-decorator
pkgname=('python2-decorator' 'python3-decorator')
pkgver=4.1.2
pkgrel=1
pkgdesc='Python Decorator module'
arch=('any')
url='http://pypi.python.org/pypi/decorator'
license=('BSD')
makedepends=('python2' 'python3' 'python2-setuptools' 'python3-setuptools')
source=('https://pypi.python.org/packages/bb/e0/f6e41e9091e130bf16d4437dabbac3993908e4d6485ecbc985ef1352db94/decorator-4.1.2.tar.gz'
    'LICENSE.txt')
sha1sums=('ab3cd33cb26480d58a34942b81771a33a3b9b957'
          'c4c7f1ac4772ee0fca420a65533520af261ef8b3')

build() {
  cp -r decorator-$pkgver decorator2-$pkgver

  cd "$srcdir/decorator-$pkgver"

  python3 setup.py build

  cd "$srcdir/decorator2-$pkgver"

  python2 setup.py build
}

package_python2-decorator() {
  depends=('python2')
  replaces=('python3-decorator<=3.3.2-1')
  cd "${srcdir}/decorator2-${pkgver}"

  python2 setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 "${srcdir}/LICENSE.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
package_python3-decorator() {
  depends=('python3')
  cd "${srcdir}/decorator-${pkgver}"

  python3 setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 "${srcdir}/LICENSE.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
