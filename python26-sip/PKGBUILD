#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <jmc@chakra-project.org>
#

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_pkgname=sip
pkgname=python26-sip
pkgver=4.12.2
pkgrel=1
pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries."
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
depends=('python26' 'gcc-libs')
source=("http://www.riverbankcomputing.com/static/Downloads/sip4/${_pkgname}-${pkgver}.tar.gz")
md5sums=('9df80f88e0e4022cdd8a8891c6c38048')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  #
  # Rename the destination executable, so that it will not conflict with
  # SIP package for more up-to-date Python versions.
  #
  sed -i 's|"sip_bin":          os.path.join(opts.sipbindir, "sip"),|"sip_bin":          os.path.join(opts.sipbindir, "sip-2.6"),|' configure.py

  python2.6 configure.py --sipdir="/usr/share/sip-2.6"
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  #
  # Rename the executable so that it matches the above fix.
  #
  mv "${pkgdir}/usr/bin/sip" "${pkgdir}/usr/bin/sip-2.6"
}

# vim:set ts=2 sw=2 et:
