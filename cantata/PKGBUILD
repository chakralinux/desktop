pkgname=cantata
pkgver=2.0.1
pkgrel=1
pkgdesc='Qt5 client for the music player daemon (MPD)'
arch=(x86_64)
url='https://code.google.com/p/cantata/'
license=(GPL)
depends=(qt5-svg libmtp libcddb libmusicbrainz5 mpg123 vlc taglib-extras cdparanoia media-player-info udisks2)
optdepends=('perl-uri: dynamic playlist' 'mpd: playback' 'ffmpeg: ReplayGain support')
makedepends=(cmake qt5-tools ffmpeg)
install=$pkgname.install
source=("https://github.com/CDrummond/cantata/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2" "fix.patch")
md5sums=('dacab1b6bf7639e3d46876db8883fbb2'
         '9acb7ffd4f279f8115fdf5d3d441cfad')

prepare() {
  mkdir -p build
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix.patch
}

build() {
  cd build

  PATH=/usr/lib/qt5/bin:$PATH
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_HTTP_STREAM_PLAYBACK=ON \
    -DENABLE_KDE=OFF -DENABLE_QT5=ON \
    -DENABLE_UDISKS2=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
