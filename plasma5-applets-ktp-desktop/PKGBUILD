# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/telepathy-kde-desktop-applets/
# Include global configuration

pkgname='plasma5-applets-ktp-desktop'
_pkgname='ktp-desktop-applets'
pkgver=18.08.3
pkgrel=1
pkgdesc="The KDE-Telepathy Plasma desktop applets"
arch=('x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('ktp-common-internals' 'ktp-accounts-kcm')
makedepends=('extra-cmake-modules' 'plasma-framework')
replaces=('ktp-desktop-applets')
conflicts=('ktp-desktop-applets')
provides=('ktp-desktop-applets')
groups=('kde-telepathy' 'plasma5-applets')
source=("https://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f51f4e7cabed7cb239a767f2073a75ac9338d78fa61fee045b3552e24f8c3e67'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
