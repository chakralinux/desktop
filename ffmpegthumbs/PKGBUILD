
pkgname=ffmpegthumbs
pkgver=18.04.2
pkgrel=1
pkgdesc='FFmpeg-based thumbnail creator for video files'
url='https://projects.kde.org/ffmpegthumbs'
arch=(x86_64)
license=(GPL LGPL FDL)
#groups=(kde-applications kdemultimedia)
depends=(kio ffmpeg)
makedepends=(extra-cmake-modules git)
conflicts=(kdemultimedia-ffmpegthumbs kdemultimedia-thumbnailers kdemultimedia-mplayerthumbs kffmpegthumbnailer)
replaces=(kdemultimedia-ffmpegthumbs kdemultimedia-thumbnailers kdemultimedia-mplayerthumbs kffmpegthumbnailer)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('678e6a9a1701ea5be61057567f2eb18b5353883fe114d4e2f1e88cad26e08a06'
                        'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver} 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
