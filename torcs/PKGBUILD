#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Shahar Weiss <sweiss4@gmx.net>

pkgbase=torcs
pkgname=(torcs torcs-data)
pkgver=1.3.3
pkgrel=1
pkgdesc="A 3D racing cars simulator"
url="http://torcs.sourceforge.net"
license=("GPL2")
arch=('i686' 'x86_64')
depends=('freealut' 'freeglut' 'libpng' 'libvorbis' 'libxi' 'libxmu' 'libxrandr')
makedepends=('plib')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('0319c996dac4480f1182c03d2331b14a')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Fix installation to /games
  sed \
    -e "s#/games##" \
    -i Make-config.in

  unset LDFLAGS
  ./configure \
    --prefix=/usr \
    --x-includes=/usr/include \
    --x-libraries=/usr/lib

  make
}

package_torcs() {
  depends=(${depends[@]} "torcs-data=$pkgver")

  cd $srcdir/$pkgname-$pkgver

  # Basic installation
  make DESTDIR=${pkgdir} install

  # Desktop Integration.
  install -Dm644 Ticon.png              $pkgdir/usr/share/pixmaps/$pkgbase.png
  install -Dm644 $pkgbase.desktop       ${pkgdir}/usr/share/applications/$pkgbase.desktop

  # Fix permissions.
  find ${pkgdir} -type d -exec chmod 755 {} \;
} 

package_torcs-data() {
  pkgdesc="Data files for TORCS."
  arch=(any)
  unset $depends
  
  cd $srcdir/$pkgname-$pkgver

  # Data installation.
  make DESTDIR=${pkgdir} datainstall
  find ${pkgdir} -type d -exec chmod 755 {} \;
}
