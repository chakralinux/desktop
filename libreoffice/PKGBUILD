#
# Chakra Packages for Chakra, part of chakra-project.org
#

pkgbase="libreoffice"
pkgname=('libreoffice-common'
	'libreoffice-base'
	'libreoffice-calc'
	'libreoffice-draw'
	'libreoffice-impress'
	'libreoffice-kde4'
	'libreoffice-math'
	'libreoffice-sdk'
	'libreoffice-sdk-doc'
	'libreoffice-writer'
	'libreoffice-en-US'
	'libreoffice-postgresql-connector'
	'libreoffice-extension-wiki-publisher'
	'libreoffice-extension-nlpsolver')
_LOver=4.2.5.2
pkgver=4.2.5
pkgrel=1
arch=('x86_64')
license=('LGPL3')
url="http://www.libreoffice.org/"
screenshot="http://wstaw.org/m/2012/04/20/libre2.png"
makedepends=( # makedepends
             'sane' 'perl-archive-zip' 'zip' 'unzip' 'unixodbc' # boost -> internal 
             'apache-ant' 'gperf' 'poppler' 'kdelibs' 'cppunit' 'glu'
             'beanshell'  'vigra' 'libldap' 'libmythes' 'junit' 'libwpg' 'imagemagick'
             'mesa' 'gst-plugins-base' 'java-runtime' 'postgresql-libs' 'clucene' 'doxygen' 'libvisio' 'bluez' 'junit'
             # libreoffice main pkg
             "curl>=7.20.0" "hunspell>=1.2.8" 'python3' 'libwpd>=0.9.2' 'libwps' 'libxaw' "neon>=0.28.6"
             'pango' 'nss' 'libjpeg' 'libxrandr' 'libgl' 'dbus-glib' "icu>=4.6" 'libxslt'
             'redland' 'graphite' 'hyphen' 'lpsolve' 'gcc-libs' 'sh'  'libtextcat' 'librsvg' 'lcms2'
             'hicolor-icon-theme' 'desktop-file-utils' 'shared-mime-info' 'translate-toolkit' 'xdg-utils' 'ttf-dejavu' 'libcups'  'libmwaw' 'ttf-liberation')

_mirror="http://download.documentfoundation.org/libreoffice/src/${pkgver}"
#_mirror="http://dev-builds.libreoffice.org/pre-releases/src"
_additional_source_url="http://dev-www.libreoffice.org/src"
_additional_source_url2="http://dev-www.libreoffice.org/extern"
source=(${_mirror}/${pkgbase}{,-help,-translations}-${_LOver}.tar.xz
	${_additional_source_url}/15cb8c0803064faef0c4ddf5bc5ca279-boost_1_54_0.tar.bz2
	${_additional_source_url}/1f24ab1d39f4a51faf22244c94a6203f-xmlsec1-1.2.14.tar.gz
	${_additional_source_url}/35c94d2df8893241173de1d16b6034c0-swingExSrc.zip
	${_additional_source_url}/798b2ffdc8bcfe7bca2cf92b62caf685-rhino1_5R5.zip
	${_additional_source_url}/a7983f859eafb2677d7ff386a023bc40-xsltml_2.1.2.zip
	${_additional_source_url}/3c219630e4302863a9a83d0efde889db-commons-logging-1.1.1-src.tar.gz
	${_additional_source_url}/625ff5f2f968dd908bca43c9469d6e6b-commons-lang-2.4-src.tar.gz
	${_additional_source_url}/2c9b0f83ed5890af02c0df1c1776f39b-commons-httpclient-3.1-src.tar.gz
	${_additional_source_url}/2e482c7567908d334785ce7d69ddfff7-commons-codec-1.6-src.tar.gz
	${_additional_source_url}/eeb2c7ddf0d302fba4bfc6e97eac9624-libbase-1.1.6.zip
	${_additional_source_url}/39bb3fcea1514f1369fcfc87542390fd-sacjava-1.3.zip
	${_additional_source_url}/3404ab6b1792ae5f16bbd603bd1e1d03-libformula-1.1.7.zip
	${_additional_source_url}/97b2d4dba862397f446b217e2b623e71-libloader-1.1.6.zip
	${_additional_source_url}/f94d9870737518e3b597f9265f4e9803-libserializer-1.1.6.zip
	${_additional_source_url}/8ce2fcd72becf06c41f7201d15373ed9-librepository-1.1.6.zip
	${_additional_source_url}/d8bd5eed178db6e2b18eeed243f85aa8-flute-1.1.6.zip
	${_additional_source_url}/3bdf40c0d199af31923e900d082ca2dd-libfonts-1.1.6.zip
	${_additional_source_url}/ace6ab49184e329db254e454a010f56d-libxml-1.1.7.zip
	${_additional_source_url}/db60e4fde8dd6d6807523deb71ee34dc-liblayout-0.2.10.zip
	${_additional_source_url}/ba2930200c9f019c2d93a8c88c651a0f-flow-engine-0.9.4.zip
	${_additional_source_url}/aa5ca9d1ed1082890835afab26400a39-mdds_0.10.3.tar.bz2
	${_additional_source_url}/ae330b9493bd4503ac390106ff6060d7-libexttextcat-3.4.3.tar.bz2
	${_additional_source_url}/22f8a85daf4a012180322e1f52a7563b-libcmis-0.4.1.tar.gz
	${_additional_source_url}/ea2acaf140ae40a87a952caa75184f4d-liborcus-0.5.1.tar.bz2
	${_additional_source_url}/36271d3fa0d9dec1632029b6d7aac925-liblangtag-0.5.1.tar.bz2
	${_additional_source_url}/0168229624cfac409e766913506961a8-ucpp-1.3.2.tar.gz
	${_additional_source_url}/17410483b5b5f267aa18b7e00b65e6e0-hsqldb_1_8_0.zip
	${_additional_source_url2}/185d60944ea767075d27247c3162b3bc-unowinreg.dll
	${_additional_source_url}/libcdr-0.0.15.tar.bz2
	${_additional_source_url}/libmspub-0.0.6.tar.bz2
	${_additional_source_url}/libmwaw-0.2.0.tar.bz2
	${_additional_source_url}/libfreehand-0.0.0.tar.bz2
	${_additional_source_url}/Firebird-2.5.2.26540-0.tar.bz2
	${_additional_source_url}/libatomic_ops-7_2d.zip
	${_additional_source_url}/libe-book-0.0.3.tar.bz2
	${_additional_source_url}/libabw-0.0.2.tar.bz2
	${_additional_source_url}/language-subtag-registry-2014-03-27.tar.bz2
	${_additional_source_url}/libetonyek-0.0.4.tar.bz2 # must be removed when 0.1.1 is compatible with libreoffice
	${_additional_source_url}/libodfgen-0.0.4.tar.bz2
	make-pyuno-work-with-system-wide-module-install.diff)
        #icu53.diff
	#icu53_crashfix.diff)

md5sums=('eb44cc33bb9331db4c1b6ae0b03fff9f'
         '867c10167b05ba19162b78032405524b'
         '6bcd61a3f977f78c64b1eaa13a40524c'
         '15cb8c0803064faef0c4ddf5bc5ca279'
         '1f24ab1d39f4a51faf22244c94a6203f'
         '35c94d2df8893241173de1d16b6034c0'
         '798b2ffdc8bcfe7bca2cf92b62caf685'
         'a7983f859eafb2677d7ff386a023bc40'
         '3c219630e4302863a9a83d0efde889db'
         '625ff5f2f968dd908bca43c9469d6e6b'
         '2c9b0f83ed5890af02c0df1c1776f39b'
         '2e482c7567908d334785ce7d69ddfff7'
         'eeb2c7ddf0d302fba4bfc6e97eac9624'
         '39bb3fcea1514f1369fcfc87542390fd'
         '3404ab6b1792ae5f16bbd603bd1e1d03'
         '97b2d4dba862397f446b217e2b623e71'
         'f94d9870737518e3b597f9265f4e9803'
         '8ce2fcd72becf06c41f7201d15373ed9'
         'd8bd5eed178db6e2b18eeed243f85aa8'
         '3bdf40c0d199af31923e900d082ca2dd'
         'ace6ab49184e329db254e454a010f56d'
         'db60e4fde8dd6d6807523deb71ee34dc'
         'ba2930200c9f019c2d93a8c88c651a0f'
         'aa5ca9d1ed1082890835afab26400a39'
         'ae330b9493bd4503ac390106ff6060d7'
         '22f8a85daf4a012180322e1f52a7563b'
         'ea2acaf140ae40a87a952caa75184f4d'
         '36271d3fa0d9dec1632029b6d7aac925'
         '0168229624cfac409e766913506961a8'
         '17410483b5b5f267aa18b7e00b65e6e0'
         '185d60944ea767075d27247c3162b3bc'
         'fbcd8619fc6646f41d527c1329102998'
         '1120705cd0f0d9bd5506360bf57b6c2e'
         'd794625f156a9fb1c53b3f8a8aa13b5e'
         '496dd00028afcc19f896b01394769043'
         '21154d2004e025c8a3666625b0357bb5'
         'c0b86562d5aa40761a87134f83e6adcf'
         '2f1ceaf2ac8752ed278e175447d9b978'
         '40fa48e03b1e28ae0325cc34b35bc46d'
         '504af523f5d1a5590bbeb6a4b55e8a97'
         '3c50bc60394d1f2675fbf9bd22581363'
         'e5483d1f0b71e64c367c1194b54b0f53'
         '07d2bf5ad839a394cca008b3ec6e7bb1')

noextract=(
	15cb8c0803064faef0c4ddf5bc5ca279-boost_1_54_0.tar.bz2
	185d60944ea767075d27247c3162b3bc-unowinreg.dll
	aa5ca9d1ed1082890835afab26400a39-mdds_0.10.3.tar.bz2
	798b2ffdc8bcfe7bca2cf92b62caf685-rhino1_5R5.zip
	1f24ab1d39f4a51faf22244c94a6203f-xmlsec1-1.2.14.tar.gz
	ae330b9493bd4503ac390106ff6060d7-libexttextcat-3.4.3.tar.bz2
	22f8a85daf4a012180322e1f52a7563b-libcmis-0.4.1.tar.gz
	35c94d2df8893241173de1d16b6034c0-swingExSrc.zip
	a7983f859eafb2677d7ff386a023bc40-xsltml_2.1.2.zip
	3c219630e4302863a9a83d0efde889db-commons-logging-1.1.1-src.tar.gz
	625ff5f2f968dd908bca43c9469d6e6b-commons-lang-2.4-src.tar.gz
	2c9b0f83ed5890af02c0df1c1776f39b-commons-httpclient-3.1-src.tar.gz
	2e482c7567908d334785ce7d69ddfff7-commons-codec-1.6-src.tar.gz
	eeb2c7ddf0d302fba4bfc6e97eac9624-libbase-1.1.6.zip
	39bb3fcea1514f1369fcfc87542390fd-sacjava-1.3.zip
	3404ab6b1792ae5f16bbd603bd1e1d03-libformula-1.1.7.zip
	97b2d4dba862397f446b217e2b623e71-libloader-1.1.6.zip
	f94d9870737518e3b597f9265f4e9803-libserializer-1.1.6.zip
	8ce2fcd72becf06c41f7201d15373ed9-librepository-1.1.6.zip
	d8bd5eed178db6e2b18eeed243f85aa8-flute-1.1.6.zip
	3bdf40c0d199af31923e900d082ca2dd-libfonts-1.1.6.zip
	ace6ab49184e329db254e454a010f56d-libxml-1.1.7.zip
	db60e4fde8dd6d6807523deb71ee34dc-liblayout-0.2.10.zip
	ba2930200c9f019c2d93a8c88c651a0f-flow-engine-0.9.4.zip
	ea2acaf140ae40a87a952caa75184f4d-liborcus-0.5.1.tar.bz2
	36271d3fa0d9dec1632029b6d7aac925-liblangtag-0.5.1.tar.bz2
	0168229624cfac409e766913506961a8-ucpp-1.3.2.tar.gz
	17410483b5b5f267aa18b7e00b65e6e0-hsqldb_1_8_0.zip
	libcdr-0.0.15.tar.bz2
	libmspub-0.0.6.tar.bz2
	libmwaw-0.2.0.tar.bz2
	libfreehand-0.0.0.tar.bz2
	Firebird-2.5.2.26540-0.tar.bz2
	libatomic_ops-7_2d.zip
	libe-book-0.0.3.tar.bz2
	libabw-0.0.2.tar.bz2
	libodfgen-0.0.4.tar.bz2
	language-subtag-registry-2014-03-27.tar.bz2
	libetonyek-0.0.4.tar.bz2)


prepare() {
    cd ${srcdir}/libreoffice-$_LOver
    
    # fix exttextcat build error
    # /usr/include/features.h:330:4: error: #warning _FORTIFY_SOURCE requires compiling with optimization (-O) [-Werror=cpp]
    unset CPPFLAGS

	# move external sources into place
	mkdir -p ${srcdir}/ext_sources &&	pushd ${srcdir}/ext_sources
	for source in "${noextract[@]}"; do
	  ln -sf ${srcdir}/$source .
	done
	popd

    # unowinreg.dll must be a file not a symlink or the result will become a broken symlink
    # /usr/share/libreoffice/sdk/classes/win/unowinreg.dll -> /build/libreoffice/src/185d60944ea767075d27247c3162b3bc-unowinreg.dll
    rm ${srcdir}/ext_sources/185d60944ea767075d27247c3162b3bc-unowinreg.dll
    cp -f ${srcdir}/185d60944ea767075d27247c3162b3bc-unowinreg.dll ${srcdir}/ext_sources
   
    # fix not upstreamable pyuno paths - patch taken from Debian
    patch -Np1 -i ${srcdir}/make-pyuno-work-with-system-wide-module-install.diff
    
    # upstream fixes for icu 53
    #patch -Np1 -i ${srcdir}/icu53.diff
    #patch -Np1 -i ${srcdir}/icu53_crashfix.diff

    
	#use the CFLAGS but remove the LibO overridden ones
	for i in $CFLAGS; do
		case "$i" in
			-O?|-pipe|-Wall|-g|-fexceptions) continue;;
		esac
        ARCH_FLAGS="$ARCH_FLAGS $i"
	done


}


build() {

    cd libreoffice-$_LOver

    ./autogen.sh --with-build-version="${_LOver} Chakra GNU/Linux-${pkgrel}" --with-vendor="Chakra GNU/Linux" \
		--enable-split-app-modules \
		--with-parallelism=${MAKEFLAGS/-j/} \
		--with-external-tar="${srcdir}/ext_sources" \
		--disable-fetch-external \
		--disable-verbose \
		--enable-release-build \
		--prefix=/usr --exec-prefix=/usr --sysconfdir=/etc \
		--libdir=/usr/lib --mandir=/usr/share/man \
		--with-lang="" \
		--enable-cairo-canvas \
		--enable-crashdump \
		--enable-avahi \
		--enable-dbus \
		--disable-evolution2 \
		--enable-graphite\
		--enable-gio\
		--disable-gnome-vfs\
		--disable-kde\
		--disable-gtk \
		--disable-gconf \
		--enable-kde4 \
		--enable-gstreamer \
		--enable-lockdown\
		--enable-mergelibs \
		--enable-opencl \
		--enable-opengl \
		--enable-openssl \
		--enable-odk \
		--enable-python=system \
		--enable-scripting-beanshell \
		--enable-scripting-javascript \
		--enable-ext-wiki-publisher \
		--enable-ext-nlpsolver \
		--without-fonts \
		--without-afms \
		--without-ppds \
		--without-system-libwps \
		--without-system-libcdr \
		--without-system-jfreereport \
		--without-system-apache-commons \
		--without-system-mdds \
		--without-myspell-dicts \
                --with-system-libvisio \
                --without-system-libcmis \
                --without-system-libmspub \
		--without-system-libexttextcat \
		--without-system-orcus \
		--without-system-liblangtag \
		--without-system-npapi-headers \
		--without-system-libetonyek \
		--without-system-libfreehand \
		--without-system-firebird \
		--without-system-libatomic-ops \
		--without-system-libebook \
		--without-system-libabw \
		--with-system-dicts \
		--with-external-dict-dir=/usr/share/hunspell \
		--with-external-hyph-dir=/usr/share/hyphen \
		--with-external-thes-dir=/usr/share/mythes \
		--with-system-beanshell \
                --with-system-cppunit \
                --with-system-graphite \
		--with-system-libwpg \
		--with-system-libwps \
		--without-system-libodfgen \
		--with-system-redland \
		--with-ant-home="/usr/share/java/apache-ant"\
		--with-system-boost \
		--with-system-cairo \
		--with-system-libs \
		--with-system-mythes \
		--with-system-headers \
		--without-system-hsqldb \
		--with-alloc=system\
		--with-system-clucene
        
        touch src.downloaded
	#./download
	make
	# fake installation to create split file lists
	mkdir $srcdir/fakeinstall
	make DESTDIR=${srcdir}/fakeinstall distro-pack-install
}

package_libreoffice-common() {

 pkgdesc="common files for LibreOffice - a productivity suite that is compatible with other major office suites"
 install=${pkgbase}.install
 depends=('libreoffice-langpack' "hunspell" "python3" "neon"
          'nspr' "icu" 'libsm' 'redland' 'hyphen' 'graphite' 'lcms2' 'poppler'
          'hicolor-icon-theme' 'desktop-file-utils' 'shared-mime-info' 'xdg-utils' 'clucene' 'libodfgen' 'libmwaw' 'lpsolve' 'libmythes'
          'libvisio')
          #'saxon'
 optdepends=('libreoffice-langpack: additional language support'
             'java-runtime:     adds java support'
             'libcups:          adds printing support'
             'nss:              adds support for signed files/macros'
             'pstoedit:         translates PostScript and PDF graphics into other vector formats'
             'libmythes:        for use in thesaurus'
             'beanshell:        interactive java -- good for prototyping /macros'
             'vigra:            C++ computer vision library, usable in Basebmp'
             'libmspack:        library for Microsoft compression formats for use in FontOOo'
             'libwpg:           library for importing and converting Corel WordPerfect(tm) Graphics images'
             'sane:             for scanner access'
             'unixodbc:         adds ODBC database support'
             'gst-plugins-base: + some gstr-plugins to support multimedia content, e.g. in impress')
 backup=(etc/libreoffice/sofficerc
         etc/libreoffice/bootstraprc
         etc/libreoffice/psprint.conf)
groups=('libreoffice')

provides=('libreoffice-extension-pdfimport'
          'libreoffice-extension-presenter-screen'
          'libreoffice-scripting-beanshell'
          'libreoffice-scripting-javascript'
          'libreoffice-extensions-report-builder'
          'libreoffice-extension-scripting-python'
          'libreoffice-extension-presentation-minimizer')

 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/common_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/common_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
 
 # put configuration files into place
 install -dm755 ${pkgdir}/etc/libreoffice
 install -m644 ${pkgdir}/usr/lib/libreoffice/program/{bootstraprc,sofficerc} ${pkgdir}/etc/libreoffice/
 install -m644 ${pkgdir}/usr/lib/libreoffice/share/psprint/psprint.conf ${pkgdir}/etc/libreoffice/
 # install dummy links to make them found by LibO
 cd ${pkgdir}/usr/lib/libreoffice/program/
 ln -vsf /etc/libreoffice/{bootstraprc,sofficerc} .
 cd ${pkgdir}/usr/lib/libreoffice/share/psprint/
 ln -vsf /etc/libreoffice/psprint.conf .
 
 # move bash-completion files to its new place
 install -dm755 ${pkgdir}/usr/share/bash-completion/completions
 mv ${pkgdir}/etc/bash_completion.d/libreoffice.sh ${pkgdir}/usr/share/bash-completion/completions/libreoffice.sh
 rm -rf ${pkgdir}/etc/bash_completion.d

 # remove conflicting files
 rm -f ${pkgdir}/usr/lib/libreoffice/program/classes/ScriptProviderForJavaScript.jar
 rm -f ${pkgdir}/usr/lib/libreoffice/program/classes/js.jar
 rm -f ${pkgdir}/usr/lib/libreoffice/program/services/scriptproviderforjavascript.rdb

 # workaround all packages now depend on that file
 cp ${srcdir}/fakeinstall/usr/lib/libreoffice/share/registry/writer.xcd ${pkgdir}/usr/lib/libreoffice/share/registry/writer.xcd

 # add missing files from Beanshell provider
 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/orig/gid_Module_Optional_Extensions_Script_Provider_For_BS`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/orig/gid_Module_Optional_Extensions_Script_Provider_For_BS`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file || /bin/true # some double file entries make it fail
 done
 # move dicts to en-US langpack to avoid file conflicts with en-GBen-ZA packages
 rm ${pkgdir}/usr/lib/libreoffice/share/wordbook/en-GB.dic
 mv ${pkgdir}/usr/lib/libreoffice/share/wordbook/en-US.dic ${srcdir}/
}

package_libreoffice-base() {
 pkgdesc="GUI Spreadsheet Applicationdatabase front-end for LibreOffice. Allows creation and management of databases through a GUI."
 depends=('libreoffice-common' 'libreoffice-kde4')
 optdepends=('libreoffice-postgresql-connector')
 backup=()
 groups=('libreoffice')
 replaces=('libreoffice')

 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/base_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/base_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
}

package_libreoffice-postgresql-connector() {
 pkgdesc="A PostgreSQL connector for the database front-end for LibreOffice"
 depends=('libreoffice-base' 'postgresql-libs')
 groups=('libreoffice')
 replaces=('libreoffice')

 # workaround double entries in the file list
 mv ${srcdir}/libreoffice-$_LOver/file-lists/postgresql_list.txt ${srcdir}/libreoffice-$_LOver/file-lists/postgresql_list.txt.orig
 sort ${srcdir}/libreoffice-$_LOver/file-lists/postgresql_list.txt.orig | uniq 1>& ${srcdir}/libreoffice-$_LOver/file-lists/postgresql_list.txt

 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/postgresql_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/postgresql_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
}


package_libreoffice-calc() {
 pkgdesc="Spreadsheet application for LibreOffice."
 depends=('libreoffice-common' 'libreoffice-kde4' 'lpsolve' )
 optdepends=()
 backup=()
 groups=('libreoffice')
 replaces=('libreoffice')
 
 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/calc_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/calc_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
}

package_libreoffice-draw() {
 pkgdesc="Drawing Application for LibreOffice."
 depends=('libreoffice-common' 'libreoffice-kde4')
 optdepends=()
 backup=()
 groups=('libreoffice')
 replaces=('libreoffice')
 
 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/draw_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/draw_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
}

package_libreoffice-impress() {
 pkgdesc="Presentation Application for LibreOffice."
 depends=('libreoffice-common' 'libreoffice-kde4')
 optdepends=('mesa: for the OGLTrans extension')
 backup=()
 groups=('libreoffice')
 replaces=('libreoffice')
 
 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/impress_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/impress_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
}

package_libreoffice-kde4() {
 pkgdesc="Plug-in for LibreOffice that enables integration into the KDE4 desktop environment."
 depends=('libreoffice-common' 'kdelibs')
 optdepends=()
 backup=()
 groups=('libreoffice')
 
 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/kde4_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/kde4_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
}

package_libreoffice-math() {
 pkgdesc="Equation Editor Application for LibreOffice."
 depends=('libreoffice-common' 'libreoffice-kde4')
 optdepends=()
 backup=()
 groups=('libreoffice')
 replaces=('libreoffice')
 
 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/math_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/math_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
}

package_libreoffice-sdk() {
 pkgdesc="Software Development Kit for LibreOffice."
 depends=('libreoffice-common' 'libreoffice-kde4' 'gcc-libs' 'sh' 'make' 'zip' 'gcc' 'java-environment')
 optdepends=()
 backup=()
 groups=('libreoffice')
 
 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/sdk_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/sdk_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
}

package_libreoffice-sdk-doc() {
 pkgdesc="Software Development Kit documentation for LibreOffice"
 depends=('libreoffice-common' 'libreoffice-sdk')
 optdepends=()
 backup=()
 groups=('libreoffice')
 
 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/sdk_doc_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/sdk_doc_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done

 #fix permissions
 find ${pkgdir}/usr/lib/libreoffice/sdk/examples -type f -exec chmod -x {} \;
}

package_libreoffice-writer() {
 pkgdesc="Word Processor Applicationfor LibreOffice."
 depends=('libreoffice-common' 'libreoffice-base' 'libreoffice-kde4' 'libwpd' 'libwps' 'libxml2')
 optdepends=('libwpg: library for importing and converting Corel WordPerfect(tm) Graphics images')
 backup=()
 groups=('libreoffice')
 replaces=('libreoffice')

 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/writer_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/writer_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done

 # workaround this file belongs now to common
 rm -vf ${pkgdir}/usr/lib/libreoffice/share/registry/writer.xcd
}

package_libreoffice-en-US() {
  pkgdesc="English (US) language pack for LibreOffice"
  #arch=('any')
  provides=('libreoffice-langpack')

 # create directories from *list.txt file
 for directory in `grep ^%dir ${srcdir}/libreoffice-$_LOver/file-lists/lang_en_US_list.txt`; do
   install -dm755 ${pkgdir}/${directory/\%dir/}
 done
 # install files into the pkg from fakeinstall dir
 for file in `grep -v ^%dir $srcdir/libreoffice-$_LOver/file-lists/lang_en_US_list.txt`; do
   dirname=`dirname $file`
   # check if directory has been already been created - some are missing like manpages
   [ -d ${pkgdir}/$dirname ] || install -dm755 ${pkgdir}/$dirname
   # mv file from fakeinstall to pkgdir
   mv ${srcdir}/fakeinstall${file} ${pkgdir}$file
 done
 # en-US.dic
 install -Dm644 ${srcdir}/en-US.dic ${pkgdir}/usr/lib/libreoffice/share/wordbook/en-US.dic
}

package_libreoffice-extension-wiki-publisher() {

 pkgdesc="This extension enables you to create Wiki articles on MediaWiki servers without having to know the syntax of the MediaWiki markup language"
 #arch=('any')
 depends=('libreoffice-common' 'java-environment')
 groups=('libreoffice-extensions')

	install -dm755 ${pkgdir}/usr/lib/libreoffice/share/extensions
	unzip -q ${srcdir}/libreoffice-$_LOver/workdir/Extension/wiki-publisher.oxt -d ${pkgdir}/usr/lib/libreoffice/share/extensions/wiki-publisher
}

package_libreoffice-extension-nlpsolver() {

 pkgdesc="This extension integrates into Calc and offers new Solver engines to use for optimizing nonlinear programming models"
 #arch=('any')
 depends=('libreoffice-common' 'java-environment')
 groups=('libreoffice-extensions')

	install -dm755 ${pkgdir}/usr/lib/libreoffice/share/extensions
	unzip -q ${srcdir}/libreoffice-$_LOver/workdir/Extension/nlpsolver.oxt -d ${pkgdir}/usr/lib/libreoffice/share/extensions/nlpsolver
}

