# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdemultimedia-kscd/
# Include global configuration
source ../kdeapps.conf

pkgname=kdemultimedia-kscd
_pkgname=kscd
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE CD player"
url='http://kde.org/applications/multimedia/kscd/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=("kde-runtime" 'libmusicbrainz3')
makedepends=('cmake' 'automoc4')
groups=("kde" "kdemultimedia" "kde-uninstall")
provides=('kscd')
options=('docs' 'debug')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz"{,.sig})
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
                        'SKIP')
validpgpkeys=(${Avalidpgpkeys[@]})

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install 
}
