# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="krfb"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="Desktop Sharing Server"
# note on libxdamage:
#       not detected by namcap because libgl depends on it
#       but nvidia providing libgl does not depend on libxdamage
depends=("kde-runtime>=${_kdever}" "kdelibs>=${_kdever}" 'libvncserver' 'libxdamage')
groups=("kde" "kdenetwork" "kde-uninstall")
categories=('network')
conflicts=("kdenetwork-common" "kdenetwork-doc" "kdenetwork-krfb")
replaces=("kdenetwork-krfb")
provides=("kdenetwork-krfb")
install=kdenetwork.install
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' '!header' 'debug' 'log')
makedepends=('pkg-config' 'cmake' 'automoc4' 'docbook-xsl')
source=("$_mirror/$pkgname-$_kdever.tar.xz")
sha256sums=(`grep $pkgname-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/$pkgname-$pkgver
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make 
}

package() {
    cd ${srcdir}/$pkgname-$pkgver
    make DESTDIR=${pkgdir} install
}
