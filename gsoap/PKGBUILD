# Arch contributor: https://projects.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/gsoap

pkgname=gsoap
pkgver=2.8.75
pkgrel=1
pkgdesc="Offers an XML language binding to ease the development of SOAP/XML Web services in C and C/C++"
url="https://www.genivia.com/dev.html"
arch=('i686' 'x86_64')
license=('GPL' 'custom')
depends=('openssl' 'zlib' 'gcc-libs')
makedepends=('autoconf' 'automake')
options=('staticlibs')
source=("https://sourceforge.net/projects/gsoap2/files/${pkgname}-${pkgver%.*}/${pkgname}_${pkgver}.zip"
	LICENSE)
md5sums=('7f616f6b35404dfd3929a45300292236'
         '27aaa3f5166db94d44044c11a7b2c37b')

build() {
  cd $srcdir/gsoap-2.8
  # needed for programs which use gsoap and have -fPIC enabled
  # https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=353724
  export CFLAGS+=" -fPIC"
  export CXXFLAGS+=" -fPIC"
  ./configure --prefix=/usr \
              --enable-ipv6
  make -j1
}

package() {
  cd $srcdir/gsoap-2.8
  make DESTDIR=$pkgdir install
  mkdir -p $pkgdir/usr/share/gsoap/import
  mkdir -p $pkgdir/usr/share/gsoap/WS
  cp -R gsoap/import $pkgdir/usr/share/gsoap/
  cp -R gsoap/WS $pkgdir/usr/share/gsoap/
  install -D ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
