# Include global configuration

pkgname=poxml
pkgver=18.07.90
pkgrel=1
pkgdesc='Translates DocBook XML files using gettext po files'
url='https://projects.kde.org/projects/kde/kdesdk/poxml'
arch=(x86_64)
depends=(qt5-base gettext)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdesdk-poxml)
replaces=(kdesdk-poxml)
groups=(kde-applications kdesdk)
license=('GPL' 'LGPL' 'FDL')
options=('docs' 'debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b36683c977b4337b33f5deaef773a921083b38bf06cc97d756fc1176b9b972ba'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
