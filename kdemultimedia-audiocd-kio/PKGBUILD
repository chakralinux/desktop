# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kdemultimedia-audiocd-kio
# Include global configuration
source ../kdeapps.conf

pkgname=kdemultimedia-audiocd-kio
_pkgname=audiocd-kio
pkgver=${_kdever}
pkgrel=1
pkgdesc="Kioslave for accessing audio CDs"
url='https://projects.kde.org/projects/kde/kdemultimedia/audiocd-kio'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=("libkcddb>=${_kdever}" "libkcompactdisc>=${_kdever}" 'lame')
makedepends=('cmake' 'automoc4')
groups=("kde" "kdemultimedia" "kde-uninstall")
provides=('audiocd-kio')
conflicts=('kdemultimedia-common')
replaces=('kdemultimedia-common')
options=('docs' '!header' 'debug' 'log')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
