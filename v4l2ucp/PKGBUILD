# Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/v4l2ucp

pkgname=v4l2ucp
pkgver=2.0.2
pkgrel=3
pkgdesc="A universal control panel for Video for Linux Two (V4L2) devices."
arch=("x86_64")
url="http://$pkgname.sourceforge.net"
license=('GPL2')
depends=('qt' 'v4l-utils')
optdepends=('mplayer: video preview.')
makedepends=('cmake')
categories=('utils')
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
        $pkgname.desktop)
md5sums=('e5bc6e71e2cd3ab123c277b2f7154b4f'
         'db20c209857533aeeb00e30c4872efd3')

build() {
  cd $pkgname-$pkgver

  # Remove old v4l header.
  sed -i 's|\(videodev\)\.|\12\.|' src/v4l2ctrl.c

  # Configure and build.
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  # Installation.
  make DESTDIR=$pkgdir install

  # Desktop Integration.
  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
