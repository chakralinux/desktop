# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kdevelop
source ../kdeapps.conf

pkgname=kdevelop
pkgver=5.0.1
_pkgver=$pkgver
pkgrel=2
pkgdesc='A C/C++ development environment for KDE'
arch=(x86_64)
url='http://www.kdevelop.org/'
license=(GPL)
option=('debug')
depends=(kdevplatform clang libksysguard)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt clang llvm qt5-tools plasma-framework krunner okteta)
optdepends=('konsole: embedded terminal'
	'git: Git support'
	'subversion: SVN support'
	'cvs: CVS support'
	'gdb: GNU Debugger support'
	'qt5-doc: Qt documentation integration'
	'cmake: cmake integration'
	'qt5-tools: qthelp plugin'
 	'okteta: hex editor integration'
	'plasma-framework: for the plasma addons')
conflicts=(kdevelop-qmljs)
replaces=(kdevelop-qmljs)
install="$pkgname.install"
source=("http://download.kde.org/stable/$pkgname/$_pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('8ee5a8668f4eea303a86b482e3c97ae069a51e2d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make 
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
