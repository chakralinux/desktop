# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kdevelop
source ../kdeapps.conf

pkgname=kdevelop
pkgver=$_kdevver
pkgrel=2
pkgdesc='A C/C++ development environment for KDE'
arch=(x86_64)
url='http://www.kdevelop.org/'
license=(GPL)
option=('debug')
depends=(clang libksysguard kitemmodels knotifyconfig ktexteditor grantlee libkomparediff2 qt5-webengine)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt llvm qt5-tools plasma-framework krunner okteta python3 mesa
             boost purpose subversion)
optdepends=('konsole: embedded terminal'
	'git: Git support'
	'subversion: SVN support'
	'cvs: CVS support'
	'gdb: GNU Debugger support'
	'qt5-doc: Qt documentation integration'
	'cmake: cmake integration'
	'qt5-tools: qthelp plugin'
 	'okteta: hex editor integration'
	'plasma-framework: for the plasma addons')
conflicts=(kdevplatform)
replaces=(kdevplatform)
install="$pkgname.install"
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=("`grep ${pkgname}-${pkgver}.tar.xz  ../kdevelop.checksums | cut -d " " -f1`"
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6')

prepare() {
  mkdir -p build
  sed -i 's|qdbus|qdbus-qt5|g' "${pkgname}-${pkgver}/app/kdevelop!"
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver}
  make 
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
