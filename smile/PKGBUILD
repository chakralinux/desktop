#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>


pkgname=smile
pkgver=1.0
pkgrel=3
_lang=all   #Available languages: { all, de, en, es, fr, it, pl, pt, ru }
pkgdesc="Slideshow Maker In Linux Environnement"
url="http://smile.tuxfamily.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('qtwebkit' 'sox' 'mplayer' 'imagemagick' 'perl')
makedepends=('mesa')
categories=('multimedia')
install='smile.install'
source=("http://ftp.pardus.org.tr/pub/source/2009/smile-1.0.tar.gz"
        'smile.desktop')
md5sums=('7f1fa1b1e0ab661d5a74ac4b8ee02511'
         'aefd88b769e18f5406647539ed953937')

build() {
  cd ${srcdir}/${pkgname}
  /usr/bin/qmake
  make clean
  make 
}

package() {
  cd ${srcdir}/${pkgname}
  mkdir -p ${pkgdir}/opt/smile
  cp -R BIB_ManSlide Interface smile fake.pl ${pkgdir}/opt/smile
  mkdir -p ${pkgdir}/usr/bin
  ln -sf /opt/smile/smile ${pkgdir}/usr/bin/smile

  # Installing language(s)
  if [ ${_lang} != "fr" ]; then
	  if [ ${_lang} = "all" ]; then
		  cp ${srcdir}/${pkgname}/smile_??.qm ${pkgdir}/opt/smile/
	  else
		  install -D -m644 ${srcdir}/${pkgname}/smile_${_lang}.qm ${pkgdir}/opt/smile
	  fi
  fi

  # Creating menu item
  install -D -m644 ${srcdir}/smile/Interface/Theme/logostart.png ${pkgdir}/usr/share/pixmaps/smile.png
  install -D -m644 ${srcdir}/smile.desktop ${pkgdir}/usr/share/applications/smile.desktop
}
