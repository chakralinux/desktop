# Contributions from Arch:
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=geoclue
pkgver=2.5.0
pkgrel=1
pkgdesc="Modular geoinformation service built on the D-Bus messaging system"
arch=(x86_64)
url="https://gitlab.freedesktop.org/geoclue/geoclue/wikis/home"
license=(LGPL)
depends=(libsoup json-glib modemmanager avahi libnotify)
makedepends=(systemd gobject-introspection vala meson gtk-doc)
backup=(etc/geoclue/geoclue.conf)
source=("https://gitlab.freedesktop.org/${pkgname}/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        0001-meson-Make-sure-all-install-paths-are-absolute.patch)
sha256sums=('282e19d55ad96dcb0ae4c0dcd8472ded1b7205c621853d8ea8f5a118882bca4f'
            '35c95ab25b607922e97c1f71ff3ac0a8f0b1df84899202bbc76afc7ea7f8cda9')

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact totte@chakralinux.org for
# more information.
_google_api_key=AIzaSyDjyg8EmaRUIsewzdjZXFZ0O8N5ARDUDGU

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Chakra Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact totte@chakralinux.org for
# more information.
_mozilla_api_key=bf05f841-e0bd-4644-81f5-3c132755f2e9

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../0001-meson-Make-sure-all-install-paths-are-absolute.patch
  sed -e "s/key=geoclue/key=$_mozilla_api_key/" \
      -e "s/key=YOUR_KEY/key=$_google_api_key/" \
      -i data/geoclue.conf.in
}

build() {
  chakra-meson ${pkgname}-${pkgver} build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

