pkgname=opensubdiv
pkgver=3.0.5
pkgrel=1
pkgdesc="An Open-Source subdivision surface library"
arch=('x86_64')
url="http://graphics.pixar.com/opensubdiv"
license=('apache')
depends=('zlib' 'ptex' 'intel-tbb')
makedepends=('cmake' 'doxygen' 'glfw' 'glew' 'python2' 'python2-pygments' 'python2-docutils' 'opencl-headers')
source=("https://github.com/PixarAnimationStudios/OpenSubdiv/archive/v${pkgver//./_}.tar.gz")
sha1sums=('089ab9e1042a4ac6c219034d2ef8db15b636a1a8')

prepare() {
    cd "OpenSubdiv-${pkgver//./_}"

    find . -name \*.py | xargs sed -i "s/env python/env python2/g"
}

build() {
    cd "OpenSubdiv-${pkgver//./_}"

    rm -rf build
    mkdir build && cd build

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
    cd "OpenSubdiv-${pkgver//./_}"/build

    make DESTDIR="$pkgdir/" install

    rm -rf ${pkgdir}/usr/bin
}
