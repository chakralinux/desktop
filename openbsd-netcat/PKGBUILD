# Apps Packages for Chakra, part of chakra-project.org
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributions from ArchLinux: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/openbsd-netcat

pkgname=openbsd-netcat
_pkgver=1.105
_pkgver_debrel=7
pkgver="${_pkgver}.${_pkgver_debrel}"
pkgrel=2
pkgdesc="TCP/IP swiss army knife. OpenBSD variant."
arch=('i686' 'x86_64')
url="http://packages.debian.org/sid/netcat-openbsd"
license=('BSD')
depends=('glib2' 'libbsd')
makedepends=('gcc' 'make')
source=("http://ftp.debian.org/debian/pool/main/n/netcat-openbsd/netcat-openbsd_${_pkgver}.orig.tar.gz"
        "http://ftp.debian.org/debian/pool/main/n/netcat-openbsd/netcat-openbsd_${_pkgver}-${_pkgver_debrel}.debian.tar.gz")
md5sums=('7e67b22f1ad41a1b7effbb59ff28fca1'
         'e914f8eb7eda5c75c679dd77787ac76b')

build() {
  cd "${srcdir}/netcat-openbsd-${_pkgver}"
  for i in $(cat ../debian/patches/series); do
    echo "** patch ${i}" 1>&2
    cat "../debian/patches/${i}"
  done | patch -p1
  make CFLAGS="${CFLAGS} -DDEBIAN_VERSION=\"\\\"${_pkgver_debrel}\\\"\" -I/usr/include/libbsd" \
    LDFLAGS="$LDFLAGS -lbsd"
}

package() {
  cd "$srcdir/netcat-openbsd-${_pkgver}"
  install -Dm0755 nc $pkgdir/usr/bin/nc
  ln -s nc $pkgdir/usr/bin/netcat
  install -Dm0644 nc.1 $pkgdir/usr/share/man/man1/nc.1
  install -dm0755 $pkgdir/usr/share/licenses/$pkgname/
  head -n28 netcat.c | tail -n+2 >$pkgdir/usr/share/licenses/$pkgname/LICENSE
}
