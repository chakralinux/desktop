pkgname=xdebug
pkgver=2.6.0
pkgrel=1
pkgdesc="PHP debugging extension"
arch=('x86_64')
url="http://www.xdebug.org"
license=('GPL')
depends=('php' 're2c')
install="${pkgname}.install"
backup=('etc/php/conf.d/xdebug.ini')
categories=('programming')
source=(http://www.xdebug.org/files/$pkgname-$pkgver.tgz
	xdebug.ini)
sha512sums=('e81f771478eb45cdae5814037ac1b86bcb8c5616d22dcac436482c350187c53d3cc6d54be740a49dca6d38c58c086db6e4a68eb3004a0152d6215c97512538a2'
            '3b667dd97486df407662b80dfbef2a69a4eba964161ee9831556b9fa87823fa235323a5a3663fdfb08f649a9d98a5b5cae3de514f68a340158285abb4b50a4d4')

build() {
  local PHPVER=`php -r 'echo phpversion();'`

  cd $srcdir/$pkgname-$pkgver
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make

  cd debugclient
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/debugclient
  make DESTDIR=$pkgdir install

  cd $srcdir/$pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install

  install -D -m 744 modules/xdebug.so $pkgdir/usr/lib/php/modules/xdebug.so
  install -D -m 644 $srcdir/xdebug.ini $pkgdir/etc/php/conf.d/xdebug.ini
}
