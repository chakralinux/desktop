# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=frostwire
pkgver=5.7.0
pkgrel=1
pkgdesc="BitTorrent client with integrated search"
arch=('x86_64')
url="http://www.frostwire.com"
license=('GPL3')
depends=('java-runtime' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('mplayer: audio playback.'
            'vlc: video playback.')
makedepends=('libicns')
categories=('network')
source=(http://dl.frostwire.com/$pkgname/$pkgver/$pkgname-$pkgver.noarch.tar.gz
        $pkgname.png)
md5sums=('ab4bba116d2baaf041cf713e033dad6b'
         '3a9ca2d81ffed338556ce6feaaf57114')

package() {

  cd $srcdir/$pkgname-$pkgver.noarch

  # Install files.
  install -d $pkgdir/usr/share/$pkgname
  cp -r $pkgname *.jar $pkgdir/usr/share/$pkgname

  # Desktop integration.
  install -Dm644 $pkgname.desktop	$pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 "$srcdir/$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  
  # Execution script.
  install -dm755 $pkgdir/usr/bin
  echo '#!/bin/bash'					 > $pkgdir/usr/bin/$pkgname
  echo "cd /usr/share/$pkgname && ./$pkgname \$@"	>> $pkgdir/usr/bin/$pkgname
  chmod +x $pkgdir/usr/bin/$pkgname

  # Documentation.
  install -Dm664 changelog $pkgdir/usr/share/doc/$pkgname/ChangeLog

  # License.
  install -Dm664 COPYING $pkgdir/usr/share/licenses/$pkgname/license.txt
  install -Dm664 EULA.txt $pkgdir/usr/share/licenses/$pkgname/EULA.txt
}

