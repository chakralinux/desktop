#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=ktp-accounts-kcm
pkgver=0.5.0
pkgrel=1
pkgdesc="KCM Module for configuring Telepathy Instant Messaging Accounts"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kdelibs' 'ktp-common-internals' 'ktp-contact-list' )
makedepends=('cmake' 'automoc4')
optdepends=('telepathy-gabble: XMPP/Jabber/GTalk support'
            'telepathy-butterfly: MSN/Windows Live support'
            'telepathy-haze: all protocols supported by libpurple')
groups=('kde-telepathy')
conflicts=('telepathy-kde-accounts-kcm' 'telepathy-kde-accounts-kcm-plugins')
replaces=('telepathy-kde-accounts-kcm' 'telepathy-kde-accounts-kcm-plugins')
provides=('telepathy-kde-accounts-kcm')
source=("ftp://mirrors.mit.edu/kde/unstable/kde-telepathy/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('71b6b9e846034e2dfaef89b9f67deaed')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}