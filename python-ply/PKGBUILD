# Maintainer: Samir Benmendil <samir.benmendil@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marcin "avalan" Falkiewicz <avalatron@gmail.com>
# Contributor: C Anthony Risinger <anthony@xtfx.me>

pkgbase=python-ply
pkgname=(python3-ply python2-ply)
pkgver=3.8
pkgrel=1
pkgdesc='Implementation of lex and yacc parsing tools'
arch=('any')
url='http://www.dabeaz.com/ply/'
license=('BSD')
makedepends=('python3-setuptools' 'python2-setuptools')
source=("${url}ply-$pkgver.tar.gz"
        LICENSE)
sha256sums=('e7d1bdff026beb159c9942f7a17e102c375638d9478a7ecd4cc0c76afd8de0b8'
            '87c20dd0a774f4d4ff837b4f1555f7eb1ed1b8dc1e3223cd105e5c1e282d62bf')

prepare() {
  cp -r ${pkgbase#python-}-$pkgver{,-py2}
}

package_python3-ply() {
  depends=('python3')

  cd "${pkgbase#python-}-$pkgver"

  python3 setup.py install --root="$pkgdir"
  
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-ply() {
  depends=('python2')

  cd "${pkgbase#python-}-$pkgver-py2"

  python2 setup.py install --root="$pkgdir"
  
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
