pkgbase=python-httplib2
pkgname=('python2-httplib2' 'python3-httplib2')
_pkgname=httplib2
pkgver=0.10.3
pkgrel=1
pkgdesc="A comprehensive HTTP client library"
arch=('x86_64')
url="https://github.com/jcgregorio/httplib2"
license=('MIT')
makedepends=('python2-setuptools' 'python3-setuptools' 'python3' 'python2')
source=("git+https://github.com/httplib2/httplib2.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  # Prepare one build directory for each package
  cp -a "${pkgbase/python-}" "${pkgname[0]}"
  cp -a "${pkgbase/python-}" "${pkgname[1]}"
}

package_python3-httplib2() {
  depends=('python3')

  cd "$pkgname"
  python3 setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-httplib2() {
  depends=('python2')

  cd "$pkgname"
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
