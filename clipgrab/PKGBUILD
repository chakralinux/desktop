# Contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk/PKGBUILD?h=packages/clipgrab

pkgname=clipgrab
pkgver=3.4.11
pkgrel=1
pkgdesc='A video downloader and converter for YouTube, Vimeo, DailyMotion, MyVideo and many other'
arch=('x86_64')
url='http://clipgrab.de/en'
screenshot='http://download.clipgrab.de/img/screenshot_videosearch_x11.png'
license=('GPL3')
depends=('qtwebkit')
optdepends=('ffmpeg: for some encodings')
source=("http://download.clipgrab.de/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.desktop")
sha256sums=('48f7cbd9a503b19246ae2d31af8d2bba7c57945f5ba8ca4fcf7bf6b3a265bd83'
            'a12c0501d2f46b4a2a7103a0cce8a35d9b44245dee6cc12eb7d3e5c083cc5809')

build() {
  cd ${pkgname}-${pkgver}
  qmake clipgrab.pro
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 icon.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
