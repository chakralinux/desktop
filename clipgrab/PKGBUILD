pkgname=clipgrab
pkgver=3.7.1
pkgrel=1
pkgdesc='A video downloader and converter for YouTube, Vimeo, DailyMotion, MyVideo and many other'
arch=('x86_64')
url='https://clipgrab.org/'
screenshot='http://download.clipgrab.de/img/screenshot_videosearch_x11.png'
license=('GPL3')
depends=('qt5-webkit')
makedepends=('ffmpeg')
optdepends=('ffmpeg: for the conversion functionality')
source=("https://download.clipgrab.org/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop"
        https://gitlab.com/kikadf/clipgrab-qt5/raw/patches/patches/clipgrab-qt5-${pkgver}.patch)
sha256sums=('addf58c027f8f0e01acb0d1d6cdb9fd17c356d4fe5e2d768b94e531b599d1f2e'
            'a12c0501d2f46b4a2a7103a0cce8a35d9b44245dee6cc12eb7d3e5c083cc5809'
            '2efa3a821c352f4093315439365d600b9c93cc64562e2d2f8da9f67882b61cde')

prepare() {
  cd clipgrab-$pkgver
  patch -p1 -i ../clipgrab-qt5-${pkgver}.patch
} 

build() {
  cd ${pkgname}-${pkgver}
  qmake-qt5 clipgrab.pro
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 icon.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
