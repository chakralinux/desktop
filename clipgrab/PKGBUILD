pkgname=clipgrab
pkgver=3.8.0
pkgrel=1
pkgdesc='A video downloader and converter for YouTube, Vimeo, DailyMotion, MyVideo and many other'
arch=('x86_64')
url='https://clipgrab.org/'
screenshot='http://download.clipgrab.de/img/screenshot_videosearch_x11.png'
license=('GPL3')
depends=('qt5-webengine')
makedepends=('ffmpeg')
optdepends=('ffmpeg: for the conversion functionality')
source=("https://download.clipgrab.org/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
sha256sums=('e67485514e12ed4c4c7a1e70d1f9d51b093677088617ae44c2f68f729d4490d6'
            'a12c0501d2f46b4a2a7103a0cce8a35d9b44245dee6cc12eb7d3e5c083cc5809')

build() {
  cd ${pkgname}-${pkgver}
  qmake-qt5 \
    QMAKE_CFLAGS_RELEASE="$CPPFLAGS $CFLAGS" \
    QMAKE_CXXFLAGS_RELEASE="$CPPFLAGS $CXXFLAGS" \
    QMAKE_LFLAGS_RELEASE="$LDFLAGS" \
    clipgrab.pro
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 icon.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
