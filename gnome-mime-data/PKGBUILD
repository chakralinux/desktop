# Support packages for Desktop
# Maintainer: Daniele Cocca <daniele.cocca@gmail.com>
 
pkgname=gnome-mime-data
pkgver=2.18.0
pkgrel=5
pkgdesc="The base MIME and Application database for GNOME."
arch=(any)
license=('GPL')
depends=('shared-mime-info') 
makedepends=('intltool')
url="http://www.gnome.org"
groups=('gnome')
source=("ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.18/${pkgname}-${pkgver}.tar.bz2")
md5sums=('541858188f80090d12a33b5a7c34d42c')
 
build() {
  cd "${srcdir}"
 
  mkdir -p build
  cd build
 
  "${srcdir}/${pkgname}-${pkgver}/configure" \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
 
  make
}
 
package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
 
  install -d -m755 "${pkgdir}/usr/lib"
  mv "${pkgdir}/usr/share/pkgconfig" "${pkgdir}/usr/lib"
}
 
# vim:set ts=2 sw=2 et:
