pkgname=ardentryst
pkgver=1.7.1
_pkgver=1.71
pkgrel=4
pkgdesc="Ardentryst is an action/RPG sidescroller, focused not just on fighting, but on story, and character development."
arch=('any')
license=('GPL3' 'CCPL:by')
url="http://jordan.trudgett.com/"
depends=('python2-pygame')
categories=('games')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname$_pkgver.tar.gz"
        "$pkgname.sh")
sha1sums=('13fea2736b8289587c1cefa98ee4df2a76681e2e'
          '10fecc08847f9a26fc57b6e4cd7f6df7501df053')

package() {
  cd "$srcdir"/$pkgname$_pkgver

  # install data
  install -m755 -d "$pkgdir"/usr/share/$pkgname
  cp -rf \
    {Base,Data,Fonts,Levels,Music,Sounds,COPYING*} \
    *.{py,txt,png,xml,dig} \
    "$pkgdir"/usr/share/$pkgname/

  # set permissions
  find "$pkgdir"/usr/share/$pkgname -type d -exec chmod 755 {} +
  find "$pkgdir"/usr/share/$pkgname -type f -exec chmod 644 {} +

  # install icon
  install -Dm644 icon.png "$pkgdir"/usr/share/pixmaps/$pkgname.png

  # isntall .desktop file
  install -Dm644 ${pkgname^a}.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop

  # install launcher
  install -Dm755 ../$pkgname.sh "$pkgdir"/usr/bin/$pkgname
}
