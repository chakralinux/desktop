pkgname=sqliteman
pkgver=1.2.2
pkgrel=6
pkgdesc="The best developer's and/or admin's GUI tool for Sqlite3 in the world"
arch=('x86_64')
url="http://sqliteman.yarpen.cz/"
license=('GPL')
depends=('qscintilla-qt5')
makedepends=('cmake')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        0001_qt5.patch
        0002_lpthread.patch
        0003_icons.patch)
md5sums=('5ecdf710e23606e1b924f740ea69306d'
         '8e0fadd613f819d3814c24c28287c4e4'
         '8e24fc6b9231e7307bd6a86af8f4796a'
         '7976cef250c2a1fd49607cd5f1cd21d2')

prepare() {
    cd ${pkgname}-${pkgver}
    for patch in $(ls $srcdir| grep patch); do
      patch -p1 -i $srcdir/$patch
    done

    # remove bundled lib
    rm -rf $pkgname/qscintilla2

    mkdir $srcdir/build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
