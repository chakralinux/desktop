#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=libjpeg6
pkgver=6b
pkgrel=1
pkgdesc="Library of JPEG support functions"
arch=('i686' 'x86_64')
url="http://www.ijg.org/"
license=('custom')
depends=('glibc')
makedepends=('libtool')
options=(!libtool)
source=("ftp://ftp.debian.org/debian/pool/main/libj/libjpeg6b/libjpeg6b_6b.orig.tar.gz"
        "locale-fixes.patch")
md5sums=('dbd5f3b47ed13132f04c685d608a7547'
         'a0910de4ba7bb7526032d86bb0297de4')

build() {
  cd $srcdir/jpeg-$pkgver
  cp /usr/share/libtool/config/config.{guess,sub} .
  sed -i "s#./libtool#libtool#" configure
  patch -p1 -i "$srcdir/locale-fixes.patch"
  ./configure --prefix=/usr --enable-shared --enable-static
  make LIBTOOL="libtool --tag=CC" || return 1
  mkdir -p $pkgdir/usr/{bin,lib,include,share/man/man1}
  make prefix=$pkgdir/usr mandir=$pkgdir/usr/share/man/man1 install
  install -m644 jpegint.h $pkgdir/usr/include
  install -Dm644 README $pkgdir/usr/share/licenses/libjpeg6/README
  # Fix /usr/bin
  for fn in $pkgdir/usr/bin/*; do mv $fn ${fn}6; done
  # Fix /usr/lib
  rm $pkgdir/usr/lib/libjpeg.{a,so}
  # Fix /usr/share/man
  for fn in $pkgdir/usr/share/man/man1/*; do mv $fn ${fn%.1}6.1; done
  # Fix /usr/include
  mkdir -p $pkgdir/usr/include/libjpeg6
  mv $pkgdir/usr/include/*.h $pkgdir/usr/include/libjpeg6
}

# vim:set ts=2 sw=2 et:
