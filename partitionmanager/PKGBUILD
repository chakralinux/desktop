pkgname=partitionmanager
epoch=1
pkgver=3.3.0
pkgrel=1
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('x86_64')
url="https://sourceforge.net/projects/partitionman"
license=('GPL2')
depends=('kpmcore' 'kio' 'qt5-base' 'kconfig' 'kcrash' 'kde-cli-tools' 'kiconthemes'
         'kjobwidgets' 'kwindowsystem' 'kxmlgui' 'kwidgetsaddons' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'ntfs-3g' 'pkg-config' 'python3' 'kdoctools')
conflicts=('tribe-partitionmanager')
provides=('tribe-partitionmanager')
replaces=('tribe-partitionmanager<=1358163-8')
install=partitionmanager.install
options=('debug')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('0e559a231fe21d1f184313f384490c905517682e1710058a1f7138c14ec6074a')

build() {
  cd ${pkgname}-${pkgver}

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
