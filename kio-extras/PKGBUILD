# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/kio-extras
source ../kdeapps.conf

pkgname=kio-extras
pkgver=${_kdever}
pkgrel=1
pkgdesc="Additional components to increase the functionality of KIO"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/kio-extras'
license=('LGPL')
depends=('kdelibs4support' 'khtml' 'kdnssd' 'libssh' 'smbclient'
         'exiv2' 'openexr' 'openslp' 'libmtp' 'kpty' 'kactivities')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde' 'kde-applications' 'kdenetwork')
options=("debug")
install=${pkgname}.install
conflicts=('kio-mtp-git')
replaces=('kio-mtp-git')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"{,.sig})
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
                        'SKIP')
validpgpkeys=(${Avalidpgpkeys})

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver} \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
