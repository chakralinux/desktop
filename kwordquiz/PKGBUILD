# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdeedu-kwordquiz/ 
# Include global configuration

pkgname=kwordquiz
pkgver=18.07.90
pkgrel=1
pkgdesc="Flash card trainer"
url="http://kde.org/applications/education/kwordquiz/"
arch=('x86_64')
screenshot='https://www.kde.org/images/screenshots/kwordquiz.png'
license=('GPL' 'LGPL' 'FDL')
depends=('knotifyconfig' 'libkeduvocdocument' 'knewstuff' 'kdelibs4support' 'kdeclarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdeedu')
replaces=('kdeedu-kwordquiz')
conflicts=('kdeedu-kwordquiz')
options=('docs' 'debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d5c26c12992f382fa56b2e00193973a9a2c1143f4a745fa2380da4b3fb7002b9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
