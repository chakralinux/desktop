#
# Games Packages for Chakra, part of chakra-project.org
#
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Michal Bozon <michal.bozon__at__gmail.com>
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=yaml
pkgver=0.1.3
pkgrel=1
pkgdesc="YAML 1.1 library"
arch=('i686' 'x86_64')
url="http://pyyaml.org/wiki/LibYAML"
license=('MIT')
provides=('libyaml')
replaces=('libyaml')
conflicts=('libyaml')
source=(http://pyyaml.org/download/libyaml/yaml-$pkgver.tar.gz)
md5sums=('b8ab9064e8e0330423fe640de76608cd')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  # License
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/license.txt
}
