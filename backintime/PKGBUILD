pkgname=backintime
pkgver=1.1.14
pkgrel=1
pkgdesc="A simple backup system for Linux inspired from 'flyback project' and 'TimeVault'."
arch=('x86_64')
url="https://github.com/bit-team/backintime"
license="GPL"
depends=('rsync' 'cron' 'openssh' 'xorg-utils' 'icu' 'polkit' 'libnotify' 'python3' 'python3-pyqt4' 'python3-dbus' 'python3-keyring')
optdepends=('kompare: diff/patch frontend')
categories=('utils' 'backup')
provides=('backintime-kde')
conflicts=('backintime-kde')
replaces=('backintime-kde')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/bit-team/backintime/archive/v$pkgver.tar.gz"
	"${pkgname}.png")
sha256sums=('d97e1446434afc4694677fcb278fe84f9757f1ff62dcf71c5e9922ce0202f2b7'
            '984fd92d327d5939151cada4496e9fdff0d7b0a34e4e8653b43fa2350e9bdeea')

build() {
 
  cd "${srcdir}/${pkgname}-${pkgver}/common"
  ./configure --python3 --no-fuse-group
  make

  cd "${srcdir}/${pkgname}-${pkgver}/qt4"
  ./configure --python3
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}/common"
  make DESTDIR="${pkgdir}" install
  
  cd "${srcdir}/${pkgname}-${pkgver}/qt4"
  make DESTDIR="${pkgdir}" install
  
  install -D ../../${pkgname}.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
  
  sed -i s!"Icon=document-save"!"Icon=backintime"! \
  ${pkgdir}/usr/share/applications/${pkgname}-qt4-root.desktop
  sed -i s!"Icon=document-save"!"Icon=backintime"! \
  ${pkgdir}/usr/share/applications/${pkgname}-qt4.desktop
}
