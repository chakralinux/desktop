# Contributor: Andrea Scarpino <andrea@archlinux.org>

source ../frameworks.conf

pkgname=solid
pkgver=${KFVersion}
pkgrel=1
pkgdesc='A platform-independent hardware abstraction layer'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/solid'
license=('LGPL')
depends=('qt5-declarative' 'media-player-info' 'upower' 'udisks2' )
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
options=("debug")
source=("${KFServer}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=( $(getSum ${pkgname}) )

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=${KFBuildType} \
    -DCMAKE_INSTALL_PREFIX=${KFInstallPrefix} \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt5/qml \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt5/mkspecs/modules
  make
}

check() {
  cd build
  make test 
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
