#
# Apps SC Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# contributor kote martini438[at]gmail.com

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
# Default is RelWithDebInfo to help with debugging.

_buildtype="RelWithDebInfo"

##############################################################

pkgname=qtweetlib
pkgver=0.5
pkgrel=1
pkgdesc="C++ Qt based Twitter library"
arch=('i686' 'x86_64')
url="https://github.com/minimoog/QTweetLib"
license=('GPL2')
depends=('qjson')
makedepends=('cmake')
provides=('qtweetlib')
conflicts=('qtweetlib-git')
options=(!strip)
source=(https://github.com/downloads/minimoog/QTweetLib/QTweetLib-$pkgver.tar.gz)
md5sums=('bf1a7cf45eb63479bf5d81d807b0d518')

# Clean options array to strip pkg if release buildtype is chosen
if [[ ${_buildtype} == "Release" ]] || [[ ${_buildtype} == "release" ]]; then
  options=()
fi

build(){
  msg "Starting build..."

  rm -rf ${srcdir}/${pkgname}-build
  mv ${srcdir}/QTweetLib-${pkgver} ${srcdir}/${pkgname}-build
  cd ${srcdir}/${pkgname}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=${_buildtype}
  make
}

package() {
  cd ${srcdir}/${pkgname}-build
  make DESTDIR=${pkgdir} install
}
