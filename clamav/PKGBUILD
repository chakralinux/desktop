#
# Apps Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=clamav
pkgver=0.98
pkgrel=1
pkgdesc='Anti-virus toolkit for Unix'
arch=('i686' 'x86_64')
options=('!libtool')
install="$pkgname.install"
license=('GPL')
categories=('system')
backup=('etc/conf.d/clamav' 'etc/logrotate.d/clamav')
url='http://www.clamav.net/'
depends=('bzip2' 'zlib' 'libtool')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'clamav.confd'
        'clamav.logrotate'
        'config.patch'
        'service'
        'service.fresh'
        'tmpfiles.d')
sha256sums=('113450537f46ed47f010a179be333a0dcd79eac13f264dce26db7aac8d52b3b6'
            'f615973896b066307fcd9be2d250ecef975b62bf16d6a39871d23b8073a9c23e'
            'd0a118610f0f3a5e70633028d52741fcbbcf1b6cf89e4c73cd037169465b72b3'
            'b3225438ce3962641e9be05fe7a3b40b23a9e50de0dd8c36192676486d7d1a12'
            'cfeec88b227a60314826939512dd77586adabccdee7dfcf4d9106c867f41ead8'
            'dd5ff6c79ee360da5f2221c4d9110a2a8886d86293f6c93c16bf74fdb126593c'
            '0a61abee3b9bba94126afe3344e7d8e82da5120ca6dbd2b413b10f75da5b0b0d')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../config.patch
}
            
build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr \
	  --sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav \
		--disable-clamav
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install

	install -D -m644 ../service.fresh "${pkgdir}/usr/lib/systemd/system/freshclamd.service"
	install -D -m644 ../service "${pkgdir}/usr/lib/systemd/system/clamd.service"
	install -D -m644 ../tmpfiles.d "${pkgdir}/usr/lib/tmpfiles.d/clamav.conf"
	install -D -m644 ../clamav.logrotate "$pkgdir/etc/logrotate.d/clamav"
	install -D -m644 ../clamav.confd "$pkgdir/etc/conf.d/clamav"
	
	install -d -o 64 -g 64 "${pkgdir}"/run/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/log/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/lib/clamav
	
	# un-distribute databases to require freshclam
	#rm "$pkgdir"/var/lib/clamav/*.cvd
}
