#
# Apps Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=clamav
pkgver=0.97.6
pkgrel=3
pkgdesc='Anti-virus toolkit for Unix'
arch=('i686' 'x86_64')
depends=('bzip2' 'zlib' 'libtool')
options=(!libtool)
install="$pkgname.install"
license=('GPL')
categories=('system')
backup=('etc/clamav/clamd.conf' 'etc/clamav/freshclam.conf' 'etc/conf.d/clamav')
url='http://www.clamav.net/'
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        'clamav.confd'
        'clamav.logrotate'
        'config.patch'
        'service'
        'service.fresh'
        'tmpfiles.d')

sha1sums=('528f774b14b95fdfb8b377f8b41859c48b165e34'
          'cb116cdab49a810381a515cbcfb6a6c148547f07'
          'be3310d2b41a68ce06e33c84ab68ffe59fdce104'
          '701a61571788d10ff7af01597785835c6bfea918'
          'df522b0488f3901e491f148c9300f6bae348c605'
          'cda9a087e5593992150cb456e34c5f6f589aca82'
          'a224ea9b4d0f4f196827347d54bed51e11c197ea')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i ../config.patch
	./configure --prefix=/usr --sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav --disable-clamav
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	# make sure conf files get installed, cause make install
	# doesn't do that if clamav is already installed upon building.
	install -D -m644 etc/clamd.conf "$pkgdir/etc/clamav/clamd.conf"
	install -D -m644 etc/freshclam.conf "$pkgdir/etc/clamav/freshclam.conf"

	install -D -m644 ../service.fresh "${pkgdir}/usr/lib/systemd/system/freshclamd.service"
	install -D -m644 ../service "${pkgdir}/usr/lib/systemd/system/clamd.service"
	install -D -m644 ../tmpfiles.d "${pkgdir}/usr/lib/tmpfiles.d/clamav.conf"
	install -D -m644 ../clamav.logrotate "$pkgdir/etc/logrotate.d/clamav"
	install -D -m644 ../clamav.confd "$pkgdir/etc/conf.d/clamav"
	#install -D -m755 ../clamav "$pkgdir/etc/rc.d/clamav"

	# Test fix for http://www.chakra-project.org/bbs/viewtopic.php?pid=60823#p60823
	install -d -m755 "${pkgdir}/var/lib/clamav"

	# un-distribute databases to require freshclam
	#rm "$pkgdir"/var/lib/clamav/*.cvd
}
