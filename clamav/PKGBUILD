# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=clamav
pkgver=0.98.4
pkgrel=1
pkgdesc='Anti-virus toolkit for Unix'
arch=('x86_64')
options=('!libtool')
install="${pkgname}.install"
license=('GPL')
categories=('system')
backup=('etc/clamav/clamd.conf' 'etc/clamav/freshclam.conf' 'etc/logrotate.d/clamav')
url='http://www.clamav.net/'
depends=('bzip2' 'zlib' 'libtool')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'clamd.conf'
        'clamav.logrotate'
        'clamd.service'
        'freshclam.service'
        'tmpfiles.d'
        'freshclam.conf')
sha256sums=('d3006747abcf750f395a8bfa8267ab314c7b63196c4b41e7717948aaaafa3e1c'
            '03fe7fc57a72a618e0ccdc31e86050bd64f97a734ad7d47f1c8d68b2bd45fb9c'
            'dba0c3344a284374c3692c794891e673f3ad73e231efaa607ad3d51026e256d3'
            'a7722a9b93f1e23b5830a34e49087385551cf271f6584a47c7f8c550768c3a82'
            'dd5ff6c79ee360da5f2221c4d9110a2a8886d86293f6c93c16bf74fdb126593c'
            '0a61abee3b9bba94126afe3344e7d8e82da5120ca6dbd2b413b10f75da5b0b0d'
            'e16f1596201850fd4a63680b27f603cb64e67176159be3d8ed78a4403fdb1700')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/clamav \
    --with-dbdir=/var/lib/clamav \
    --disable-clamav
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR="${pkgdir}" install

  install -Dm644 ../freshclam.service "${pkgdir}/usr/lib/systemd/system/freshclam.service"
  install -Dm644 ../clamd.service "${pkgdir}/usr/lib/systemd/system/clamd.service"
  install -Dm644 ../tmpfiles.d "${pkgdir}/usr/lib/tmpfiles.d/clamav.conf"
  install -Dm644 ../clamav.logrotate "${pkgdir}/etc/logrotate.d/clamav"
  install -Dm644 ../clamd.conf "${pkgdir}/etc/clamav/clamd.conf"
  install -Dm644 ../freshclam.conf "${pkgdir}/etc/clamav/freshclam.conf"
	
  install -d -o 64 -g 64 "${pkgdir}"/var/log/clamav
  install -d -o 64 -g 64 "${pkgdir}"/var/lib/clamav
}
