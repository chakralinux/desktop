# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=1.9.9
pkgrel=1
pkgdesc="C library for finding and handling crystal symmetries"
arch=(x86_64)
url="https://atztogo.github.io/spglib/"
license=(custom)
depends=(glibc)
source=($pkgname-$pkgver.tar.gz::"https://github.com/atztogo/spglib/archive/v$pkgver.tar.gz")
md5sums=('6bbf2cb37a2294f26f2324e1142ab7d8')

build() {
  cd $pkgname-$pkgver
  touch INSTALL NEWS README AUTHORS
  autoreconf -vi
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
