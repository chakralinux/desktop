pkgname=links
pkgver=2.13
pkgrel=1
pkgdesc="A text WWW browser, similar to Lynx"
arch=('x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'libjpeg' 'libpng' 'libtiff' 'libx11' 'librsvg' 'libevent' 'xz' 'zlib' 'openssl' 'gpm')
screenshot='http://links.twibright.com/shots/shot5.jpg'
source=(http://links.twibright.com/download/${pkgname}-${pkgver}.tar.gz links.desktop)
sha1sums=('d4c5c41d7b181bd8cce7cd7e8c282d935e19b035'
          'f600e27c2a71184444f7dd07a10230aa44463a02')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" configure
}
          
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  (cd intl; ./gen-intl; ./synclang)
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-javascript \
              --enable-graphics \
              --with-x \
              --with-fb
  make
  mv links xlinks

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-javascript \
              --disable-graphics \
              --without-x  \
              --without-fb
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 xlinks "${pkgdir}/usr/bin/xlinks"
  ln -s links.1.gz "${pkgdir}/usr/share/man/man1/xlinks.1.gz"

  install -D -m644 "${srcdir}/links.desktop" "${pkgdir}/usr/share/applications/links.desktop"
  install -d "${pkgdir}/usr/share/pixmaps"
  install -m644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm "${pkgdir}/usr/share/pixmaps/"

  install -d "${pkgdir}/usr/share/doc/links/calibration"
  install -m644 doc/links_cal/* "${pkgdir}/usr/share/doc/links/calibration/"
}
