pkgname=links
pkgver=2.17
pkgrel=1
pkgdesc="A text WWW browser, similar to Lynx"
arch=('x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'libjpeg' 'libpng' 'libtiff' 'libx11' 'librsvg' 'libevent' 'xz' 'zlib' 'openssl' 'gpm' 'pcre')
screenshot='http://links.twibright.com/shots/shot5.jpg'
source=(http://links.twibright.com/download/$pkgname-$pkgver.tar.gz 
        links.desktop)
sha1sums=('ade8942f49a03708a6d3e47d0c4f569e8e94305e'
          'f600e27c2a71184444f7dd07a10230aa44463a02')

prepare() {
  cd $pkgname-$pkgver
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" configure
}
          
build() {
  cd $pkgname-$pkgver
  (cd intl; ./gen-intl; ./synclang)
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --disable-javascript \
              --enable-graphics \
              --with-x \
              --with-fb
  make
  mv links xlinks

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --disable-javascript \
              --disable-graphics \
              --without-x  \
              --without-fb
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install

  install -m755 xlinks $pkgdir/usr/bin/xlinks
  ln -s links.1.gz $pkgdir/usr/share/man/man1/xlinks.1.gz

  install -D -m644 ../links.desktop $pkgdir/usr/share/applications/links.desktop
  install -d $pkgdir/usr/share/pixmaps
  install -m644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm $pkgdir/usr/share/pixmaps/

  install -d $pkgdir/usr/share/doc/links/calibration
  install -m644 doc/links_cal/* $pkgdir/usr/share/doc/links/calibration/
}
