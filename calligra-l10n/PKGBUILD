pkgbase=calligra-l10n
pkgver=2.9.9
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  [en_GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  #[eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  #[nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  #[tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('bf2e2ee97e55c247c6f4f32809db312e'
         'e5ab031ba6d2e227c07f31de04d5cc4e'
         '38fe56a1b6e6c3af08760ec3684ece8d'
         'd4b3f271ef424769fafd69315d65b9a1'
         '6098d4858126f3896287cc230590658b'
         '2ebdfcd6a62e4d0fa90a874c5d188d22'
         '196d7793c1dda5b06d2a1b39a45de626'
         '09ef03a4d8430d1f639e41847e19fd51'
         '1c27da4af2aa456c54808890034a1b42'
         'f7f0eeb17293f880dbf1018706413024'
         '4b40bf19dbd10aac687d858b930e242e'
         '6687ba7d98572e971a952cd3fbd08dd6'
         '100d94cf660c1d5e9b1aeb34f9b2b5e2'
         'a7e261f6cb9792d021f07b8a646a85a7'
         'e35dbfb3350bd32813a81f08bd7a0366'
         'a215018540c7fe50e4f7652c254866fc'
         'a888fe4cf899a16f15f2a02d2882b9c9'
         '2f0561f2d258ef0474d4fd0161bb176b'
         'fe4458e20a35ca3ea3f38a72454a0765'
         '1ce7b6c8e8c8261b99f3711f71ad557f'
         'd8f7bf59c3312961b1756c892f0e0fe9'
         '79b1d8a00e000c35ce32ebec257a3905'
         '83d26fb27803f72c88ecd98f7a7a9d8a'
         '782b9c4dd030aff0f8806534392adb14'
         'ae1b0abc5c89a160f3a8fd1935e49ee0'
         'f19a444a7698890002b30dc1e3eb4afe'
         '9f83d2ee79379517f1f58e02eef12139'
         '963a4abba420cd380bdc115e7c428581')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$2]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
