pkgbase=calligra-l10n
pkgver=2.9.4
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  [en_GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  #[eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  #[nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  #[tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('48c9c77c73a36c3beaf0cf4f5792461e'
         'bc07c506e9e602fad8c0d2bd4fa74ef6'
         '37f6d873659aed7cd32fa134877d865f'
         '54748c2091e8e686e995c1a45c88b21c'
         '636c381ddb937c3ca1d4128f5547c782'
         '0a025604c602837ba7ff3ee438b47575'
         '54acdac0e6e2559f896ab8207b49341b'
         '6e8decc43bf0c2b7fcbd923c60ce6fc5'
         'b0143072e41a8b5de27157d4bff1fca8'
         '1f35e6863adc78931d9b1af2bbffba00'
         '32d89d0b7c006c366f79cc63d6554a8e'
         '9a0269924fbd19cc8f21a4afe13987aa'
         '1a295eef214dc59b8a09d86e9e03269b'
         '7d9c33670daa3d6faaa63158b678ac53'
         '083ee522c5649874d87c512ba468ff31'
         '108135435df5428c963509091cb02dc7'
         '96235bed627011d3d2ba91794a6ad659'
         '325487455521a3d0bfa22340a65cf91a'
         '71b04ece0bf73e0403fa6f318aa39f99'
         '29eac1e253f83206effa198a5d6862e8'
         '5c6459eb4dffe6bf9f75ad39f4648165'
         'f84bc429dcbf7c622338f68a0dd52773'
         'cb6f2273c4b7e7208c98e4f896e34fb2'
         '3085a1383b0698ba96ce81ab271f046b'
         '0562dc6650d96702ee4f24a69ee8b644'
         'ce531462a3e2af48353212f8a4062629'
         '3eb3b3c32ea80ed9a8e0c813438d4ca0'
         'a28cb1daa6acccd3b7a4981ef4e1ab85')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
