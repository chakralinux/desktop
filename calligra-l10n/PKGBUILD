# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>

pkgbase=calligra-l10n
pkgver=2.8.6
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  #[en-GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  [eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  [nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('830b1aa2406cad1a2ec792293cfae4bd'
         '104d637ab95d9bffc99ec80f11ad6bd6'
         'f174781032238ba12d9d244a2041c9f5'
         '624c96055805af8fe9a278c60ffe50d0'
         '7d77dff42771d094aeb71a87d6b6f589'
         '6522c4dd0878aee02b61c3f476868f5f'
         'f8db1a981502f606b966d894a8799b21'
         '76abe2255703a83e650b5b01022a7180'
         '6c72e3f1dcdcc5c1da33eb9924409909'
         '0eafe501f148a36bc9dae99e62e1e4d8'
         'd4e96354dbc20a2634eccd2364d1840b'
         '5ce2fe12b32edf70d8037a1b361b786a'
         'c21f6810bbb617d7b33da856e61c640a'
         'c01047568eca877d1a8c72aa8892e84b'
         'e12e540fc22282bdc14a4cb6d4bf3658'
         '0666f0d7882ac8b9e81eca548e9603e5'
         '179c4ae228c0a76b9cffcf69c8bb8bc0'
         'a3ed4138d02b1e9a79621471d23aeb76'
         '0ef1481ea15a1ae8e9e2aa1b70bc7839'
         '6227bc09f01cf7f34faaf06d6f306942'
         '290f526d54d7281d2514ed067b9c910c'
         '7da075702a77c8bcf9243e51b106f230'
         '03322f628af4b2bced1590e9555a81a7'
         '7642f2d18956e45e68622a687a0a1453'
         '052997ef78c2f65e8c55380c10f28865'
         '34fd5dadde42c535cece3e9ffb70a616'
         '976464eeff7edace845a10d1b0b11170'
         'eb074a01805019b0d72ac75ade50dbab'
         'b5f536fc33f34ed154c1598cbe6940d2')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
