# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>

pkgbase=calligra-l10n
pkgver=2.9.1
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  #[en-GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  #[eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  #[nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  [tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('790ea91505d5754564651a9797ad5b27'
         '7f93061b116d0e3bb8a42230d721f52b'
         'f085a670997dbc599dae1bd18ecfb6f3'
         'b2c3a57f77fd2d07b51bdddc5390a81f'
         '0747c067bbb212f1c5011e2b21ff3fff'
         '2f5092542e13c984a07fa06a13071de5'
         '462bdf3a1c357162947a2ac78193c0c8'
         '0b2f118ccb93256750e93da95af9922c'
         '23e37fd33625b3cb65864c86044cc307'
         '6f74a2430e93a8212b52ea4a7f3db80c'
         '2e1aa8454f058d60d6de027bf9e6013e'
         '653556c54ee517d685bfdd274610d00f'
         '35061f847c9df4a7d618dc2bfbd5e3d2'
         '686701808bb98526e93a3122d8a72211'
         'adf1689fb17cbe2bee3f00c8de68d26b'
         '333d06893bf797ae2f15374950431379'
         '2fb7ac4e427fb4f94b7a2f03b0df59db'
         '68627ac4b6594297baee20c1ae1a6313'
         'ed4fe0690d0f55776564066c469aff31'
         '732a10099b75a3068646afcb154bdeee'
         'cba77be979019517c35aeb1411061f74'
         'd2a492476166abe353b722d3be83f484'
         '86174201d1a67ee486699e39e792afe5'
         '9e2537324e6b896c9f174289d44d2401'
         'd94a51ff6dda4cbfe957d192775a738e'
         '462715b91ff71b696f6c592e2879b453'
         'cd144c35a06e68c7800235f82a34117b'
         '6cc374e9019fb4c2f4096ab167c31787')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
