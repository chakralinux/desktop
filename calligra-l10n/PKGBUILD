pkgbase=calligra-l10n
pkgver=2.9.5
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  [en_GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  #[eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  #[nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  #[tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('063b549bedbd8585692b360e92883899'
         '7b69efada2e36e31d20d8961de233438'
         '2a9cb06857094d8c4a9902dd9a9901b9'
         '38a937862107084556ea787f6db9f8be'
         'b5e8f3181ee6c1f2d18a444db377debd'
         'e96da4cb7453befebfe0820241ca8573'
         '2c9bcbdb02437f238d1ae05fe54a769c'
         '608ca8908074d36c12bf96f0e115f734'
         '11936cbbaf17beb526ddebbfe855af46'
         'd4a207fa1c93f8cc572ee4599b27c672'
         '5ba624e0e3d520cf70fff218d7418f97'
         '5ffbbe7d5cfd215d859692620266e095'
         'c88353d6b19bd32826fe3d909729387d'
         '45eaf444c0c6274ba8daa35977d3c272'
         'd158152c17d0c2bf32c3fc67e593e85e'
         'ddbfe294b28ad9ec36cd8a6532cd5dd1'
         'd8af0656cfe4c2a9b86413540bc3a400'
         'b5e67f03fdb78c91e9f6b5b0944626bf'
         '4fc7b21e03f1b8ead719b760f74cf958'
         '3579282b74438f6e05ac0dce7fb645c9'
         'f5cbe470cbeed07e720cf9a5449e9ee4'
         'ad959875d7682ca1ed9b0e52e4bb2187'
         'cee50520bfacc552a1dec77aa7071be2'
         'dae64fb4268f8d32d32204e87ee0d632'
         'da8f58f7b738c6aaf0a98819c52062b4'
         '8cdfb60763f95c936c62dd644f6ab129'
         'add12062068f73231ba11fe70aa3e9c7'
         '6276bfafa16a6f6601b2d509acb61711')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
