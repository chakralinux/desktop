# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>

pkgbase=calligra-l10n
pkgver=2.9.0
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  #[en-GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  #[eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  #[nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  [tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('7d21a5e68e7da704491253c61ef27616'
         '2249f929ffe2166d38dfacddf8f2176b'
         '143959752de38f2ab69a8a8fe1cc5fab'
         'a80c21e2eb73aebf8da032ee55bb7331'
         '0418789bc27c2c18a8292bcfbaaccbdd'
         '53260957880666794cd384be04bb73d3'
         '98cea4fbe71b503d241792707fc54adf'
         '9d2127e6828d561310b1ea2080955074'
         'feee9e3d9196481e316365d8b91fc981'
         '4390d3af0d141f0d0621a2f0d732ac71'
         '0a113fb12c35df2723f6399f401e7b97'
         '7b08d95c2f16e8ad8be3480d80ce1d8d'
         '9f746f3db36c40ded63b95e9f9fe43f9'
         '50093946ae7838df0cef810d5b3ef851'
         'c52043127faf5f6c98b4ed9dec417da1'
         '1357d9d620c6a70e69937c843e99d4b8'
         'ab46b1def7a9425ec3e5d49d6a66113f'
         'ba73bc0c2efcaac20276e11ccb3dc43b'
         'd47f782ade639791b44a5dbd5f3fad42'
         '5d6f8793ca8f7b870927e5dc85307970'
         '8c5bd1835ce57c6c78befad6fe7fb855'
         'e4e24f20a7bf0ecc038f4ba7392bcdc0'
         'af9c4ed7f06689e2166fa30c22f93027'
         'a1cfcfb48adf532223c00d8277e914d6'
         '4344da1de1a6eb4f5deed2891ded8e85'
         '28620fc585d63b8b897ae71d8e29fbda'
         '94b4380e385134e816019862b6e8db37'
         'ebcc25709d27fca6d0cf4e79711e360f')


build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
