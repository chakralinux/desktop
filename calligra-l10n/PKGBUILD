pkgbase=calligra-l10n
pkgver=2.9.2
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  [en_GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  #[eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  #[nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  [tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('2070b40dfd4ecf860a761a327f3463fc'
         'b3ce8fc10b4be2d7ccad9205522b69c3'
         '7d8475a9a1d1ab4469089902f4460534'
         '5a53ebae95d6dd082f606489c4073467'
         '4be303ea33737e8bb082bc55cf481b06'
         '3c77bf246bb7e4663e260d714a8d4920'
         'b732a3c8a46268b02c393f7da6983651'
         '1e72fb67933711140d822af999b3a483'
         'e5b63067ab038df231197a7ebed6ce68'
         '48059f6475940f96e89554ed3a5fa4ce'
         '6343ff6c408f89fb8a54fb284c0f6653'
         '2626d400c350627494b4d5937765ccb5'
         'e66bdc04698f29d074bd874ba2ac713b'
         'd08e35dedf029b2ad6343697a227db33'
         '0df375c87b322eaf59bc463c741d4fc6'
         'effa0b3806dcb230294ca9e9e5d64c42'
         'fa9b7fb496b948845462d87b82c12362'
         'f86648ecee94c2c6ed006a3dbd8a88f8'
         '8be7e15d3c49ce2f1a23f98215e76fe0'
         'da5f5eaaa8c0e4694a7d5620349300ca'
         '71749e6c0d1e2c8262bd1e871fdab090'
         '46abda11730b2855396da5c49be8118d'
         '5dfd8a63a543ccc031d6691300fc14d9'
         '12d6a77988ec5776d935c0278c464f6f'
         '77ae0624ee424f99b1248c429ce1d8af'
         '0034bffa695f030a54ead482e651b934'
         '04fc1e7c595e34dc12d008ad5b70bda6'
         '7a4dc57842926e644cdaf462a3b8bddf'
         'f9240d55cb05a1af64f50bcabc2d6866')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
