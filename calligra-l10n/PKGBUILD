# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>

pkgbase=calligra-l10n
pkgver=2.8.7
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  #[en-GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  [eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  [nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  [tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('4cb22f814b8c00040246b7d3e16fea29'
         '70e9b04b66aa74e836a23c1c606d5280'
         '09011b1829c1eafddb1987080b1e627d'
         'dfe2b5d1ee93ad60c361cfd01b4f2057'
         '2400e204e613a9710a09d57a08eb2631'
         'd8139eb50b743144852e328278d39164'
         'd17be021229e6ccc84582da889abedb8'
         'a45b9e10b511c83fd2a49f03dee42241'
         'd275542a37bf6e696123ff52e2c70345'
         '743d02e730421d2caa802276f6917a6a'
         '46e06d980692bdd86f114d16ff71187d'
         'a8dcfa38b9c3e8ca0eb8fbc68a7b21b8'
         '20357723a567fddf2fcf12dd399b88a8'
         'ed20dba78078a127feb206afaa0490cc'
         'aa141334488455d590b01fc6104e7ea8'
         '719acbca3e86c5ddc88ab0232d3e973b'
         'de076e3e38ccc698288420dc719c9357'
         '0a50e809722ad150dba660bd43b75db7'
         '5c5f0499d26545e26cdd9a8aa21a5928'
         '96232bbe66f1ee60a86bcf023670ced8'
         'd8c7d2da35097e355a139c7a346c5d7a'
         'd40b53e9266d0381fd6951daae0fbc69'
         '42dc9dd95b709ebfcb1f00cf69cf094f'
         'a8d94ba46a914a9b6d63fa71a89ecf8a'
         '867aa2c3351bd67acecad6a0b4642a4c'
         'a135128f4afe4125f4e82c166fa9b17d'
         'ca3546b5449ac81334b9228c88f4172f'
         '10e5faad6c7ad0b9084978840cc75603'
         '50a3272af6c6a62bf4f5978832b96181'
         'e1ec962ac680cd9dce72e48dd9c754e6')


build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
