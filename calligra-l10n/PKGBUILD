# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>

pkgbase=calligra-l10n
pkgver=2.8.5
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  #[en-GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  [eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  [nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('9d08b8ac9c180c0c50daedcb2760bc01'
         'cb1133e030919aec576325244cd1ad2f'
         '73a404be3560eff3a843da5334b542b8'
         'fb1bc7b5cb71e19105e701faa163a8b8'
         '8e96bb8799a5abd18f89d729af4aa8ca'
         '11e1759fb6d0742f0cddc24569cdb951'
         'acf75a2b936e67fe0eb2a1216758b795'
         '2f2d8debc2fcff5295af8fe806487084'
         '86c8c447269021d15e3e0fa704309199'
         '1cf0c858f5f15bfc464ebc939b2e792b'
         '724edbba5e7ab65f8d8640db211d1851'
         '0f2680760434b0ed6b9b13beab93819a'
         '89880793fe955edd7a9f1ebfd9058d69'
         '76e7e3785c2e44139f3cb4fc01346b62'
         '8988c584329251bc1a0c72b340d8060e'
         '88bde2beb644ab49d16cc258cc7d1902'
         '9466967f0c1632a10aec4f98a36c4c29'
         '5716557badd043f33f1b7b7d156e39c6'
         '93777d79e35eddc650b542826cc0c1af'
         '695c99ae5e407882c6c0e8f41e92e071'
         'ca78faf826f91c4b71a8d16e71dc7963'
         'c1a930ee854cf4dd637b826c55b958ce'
         '75a309c5c50af569ade85fecde31353e'
         'c541e41ef345dc4a4265056a32670afa'
         '49733484cb599484ca8ce36a2963e651'
         '7d603066961cd1f0531194f0c69ce762'
         'aa956e05b86accb890d727bee7521017'
         'b7cef30fe4d362b389782ef174a4fa96'
         'c52bd9a46c27a4106aaf0f688eec4525')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
