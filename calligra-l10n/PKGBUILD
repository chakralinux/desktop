pkgbase=calligra-l10n
pkgver=2.9.6
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  [en_GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  #[eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  #[nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  #[tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('14d76da4021541686dd5d793f70d9029'
         '446a54dfbb7b45712fc2b8981e127926'
         '8c81a876eb06a89c93094b5d392f52c0'
         '41d4a7b398cd4fe1dd63cf898c997501'
         'ef9c45f9dfb25307b5f2cbbd46975ab6'
         '9e0c1531241f0774c70c9dcd0836abb8'
         '715a52f56c41b776acf870bcf0df3591'
         'f39485124287adc007dab3642b57eb9a'
         '7d8f994234911e475d81f667cca9dd4a'
         'cd48ca919ffb1ee0152325eec19f4706'
         '2a0583e77368e5feee5c6bed3c2de89f'
         '324cb2be2ba0b9e1e1aa69e3315da683'
         'a4aa2f21f565eea3e31f84e9c2d9b8f9'
         'faf4f6e78684e57865106822148b8c3b'
         '6b40753e50a400200182250f056bee56'
         '6491144954bcdb75c3438ebf47797220'
         '1a915d0f6df72c8a1fde8e34d658b6e4'
         'a874dd9c4edb591a4f84fe00c120bab0'
         '58a1b0b2d524b270457096803f2bded5'
         '28eec40b0065f056de5d44bc06c229c1'
         '15d8651b6175d198f7a9f3e1c90ae8fd'
         '0befb2ddfc294427e8e35ffee290a956'
         'fc15c2255d182eb869fc28a1603efa04'
         '0eeab69fef10dae98c921c5ad127812c'
         'fbc4dbdce3a80d6ae5582c42272fcad6'
         '51dbdd45beda57deae4b30f2b0d49fe4'
         'b8f729b6ebba19cb6a796513a21d6ba0'
         '215fc821eac11759802c68c2b385f9b6')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
