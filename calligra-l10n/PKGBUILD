pkgbase=calligra-l10n
pkgver=2.9.11
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  [en_GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  #[eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  #[nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  #[tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('23be50c6e72729526fb649a697215e69'
         'cf743c38b3bc4d886f3fa709df8aabb9'
         '830cb6222edb3ad805ec21a9466f4e81'
         '7c07d0a8e4722286cd75bd669e141ed2'
         '6af6cfbae29929718e37b73f4e574b8a'
         'f7f539b7a32710502176b0a157aa5057'
         '99f7c7354f593147a384850b4f365a8b'
         '20a2d9f1ceb5e1b31faee72bf5fb4d0f'
         '83760e705c0dfeaa2f4f098305da3f79'
         '90ac8f87fb1f76692e19532d9e7246e6'
         '02a50ae334a195845fbd9b58730c8a50'
         '1d6acdd91f14331b465c2365272d0a2d'
         'c3b5b8c568920fdfe9719b5da524a101'
         '6d54da69aa0d5b79f37edae7b8edb726'
         '89080fa5d2448f84b6c406a5c206261c'
         '3042e50532305db27624be2cc4d78394'
         '306843e947b62056dca378c5898fd68c'
         '8029eee21ad33758a9b705f1c987d83b'
         'e644cdb5e8694b9dfd1937877d461710'
         'f3e020a8daefa5f26485e01ffa8a4520'
         'f87a80d5f9ef18e5bce4f9b546ac54f2'
         'a2d73c39ee84ee2f69842509da092f0b'
         '946784625e39f505330bf9cf9895cd32'
         'ef1fd56d9871a5235d66083bb5132968'
         '3598ca1fdcadbee772bbfbfd2e61836b'
         '5058af16b0a81e1087bd2a9226850ce8'
         '5755ce1e4d163fc07de6b4bb2abcb6e2'
         '602972907def3acfd0023fd3019a1512')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
