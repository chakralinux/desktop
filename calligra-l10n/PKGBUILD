# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: A Boersma (abveritas) <abveritas@chakra-project.org>

pkgbase=calligra-l10n
pkgver=2.8.3
pkgrel=2
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  #[en-GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  [nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  [sl]='Slovenian'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('fef9b587e40ec9fc72d6dcc4df115dfd'
         '7a5ab9a78c7d7500f824cd2c7b6c8e0d'
         'fc8a0b06ef5ff1afcd0efc685687c0e8'
         'c9c95f33c5e37eba1509e26a1ed0450d'
         '9e3369d49a4cb7f63cc18dbb83498f92'
         '7bf598efcfa57594af105322d361b405'
         'e18326c7e1f09041d9e3aca5d7269f92'
         '639e851b6f23e913db8784adfdd1dc10'
         'e4f126bdd41913cf34106fd53c9e6441'
         '77091516cad7f88db527e61265c58369'
         '78cfd014eda79d4ff0a4d2cd9b25860d'
         '22051f599d3ac702c1a43a046bd2a3de'
         'a8552b153de91f72636f8dd924100f9f'
         '8b3b5e730da093ddb93c982c7bbd7a42'
         '4dcfc63baeba2fc8ec5cc5ef97489dd0'
         'fbcb9b613cdf672026cece530d509874'
         '8f1bcadedf7082912bef37025b7e4b7a'
         'bb274e56c2e4011a35b9e1b4163fa350'
         'f52279cbb4bd5bab813f2d310be7520b'
         '694dc45c1df24b3afdba9b35e0920cdb'
         '278c791eda6c61261f5797c14c1a6c7c'
         '2cd028cdd51e469ffda4199cdb74edc7'
         '2b1dc6e34bfab4963672d20959bd313b'
         'dec926755041db16bc11e934cb5c2e14'
         'cc3f810547ec00b0544dd26739a3f471'
         '65e113d8bedad04c91dc36d4b1eac143'
         'e91409ee00da4943fe2e9ee9f478a5bc')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="${_languages[$1]} localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
