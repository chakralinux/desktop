pkgbase=calligra-l10n
pkgver=2.9.10
pkgrel=1
depends=("calligra-libs>=$pkgver")
arch=('any')
url="www.calligra-suite.org/"
license=('LGPL')
makedepends=('cmake'
             'automoc4'
             'kdelibs'
             'docbook-xsl')

declare -A _languages
_languages=(
  [bs]='Bosnian'
  [ca]='Catalan'
  [ca@valencia]='Catalan (Valencia)'
  [cs]='Czech'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  [en_GB]='English (GB)'
  #[en-ZA]='English (ZA)'
  [es]='Spanish'
  [et]='Estonian'
  #[eu]='Basque'
  [fi]='Finnish'
  [fr]='French'
  [gl]='Galician'
  [hu]='Hungarian'
  [it]='Italian'
  [ja]='Japanese'
  [kk]='Kazakh'
  [nb]='Norwegian Bokmal'
  #[nds]='Low Saxon'
  [nl]='Dutch'
  [pl]='Polish'
  [pt]='Portuguese'
  [pt_BR]='Portuguese (Brasil)'
  [ru]='Russian'
  [sk]='Slovak'
  #[sl]='Slovenian'
  [sv]='Swedish'
  #[tr]='Turkish'
  [uk]='Ukrainian'
  [zh_CN]='Chinese (simplified)'
  [zh_TW]='Chinese (traditional)'
)

md5sums=('11182cbb7674a943a8a252984c354516'
         'ea50351dfba0cdd17e75634d02449c8f'
         '14cb19f90363aac2480cc8066a2e63d8'
         'fa7abf2083d4231f7542991c0d9ce8c9'
         'e8733f18c40e8f7222f5a65c12097f75'
         'e733e18d868daf21fcce25a51c7a5e91'
         'b6f19901e757daf001b8a460be405623'
         '6cf57001aa50f271963225913f01bf66'
         '9c09329ba22abaeff0d1e0843fd411ee'
         '491d4f6ed22592f9225a0e56c7b0c97a'
         '4ab575c1b8a0a002f768a0845b8c085e'
         '22d58139eb77bd7ebaf5d0a63252bacd'
         'b84f8e7316c7b53b22d6fd7e0afc406e'
         '25b6bdff2ffa0baa923a68caa7cf0049'
         'fbad9672641fa06561fd7449a54d8933'
         'd035c3c4d21d2f78fbc6da6cc1209f4b'
         'c3069c9ff0f5276653c0454a4d306091'
         '9b6e39f3197e075ca2824540791a3a20'
         '6381d28e18e39e36555b94245588c7d3'
         '387e24e7d3a6f2d6816536fbb39678e9'
         '946b55e7e1894661252145c81292fbdc'
         'ed5a064e79de5c8d2bcc2a631ea9d29d'
         'f85080d10d014c248bf5e0cd9b4afdae'
         '4392181cea4097115d0e1c26e674931a'
         '00a7fdb2a24fcc7181b83fbd73d90315'
         'e7f4470428e16da3b5efefc48ff775b4'
         '4a762d6f1bd5396755eae7007641eb4d'
         'b2f5d6ebc787ea1d75773d4d601645f6')

build() {
    for i in ${pkgname[@]}; do
        _pkglang=${i#*-}
        msg ${_pkglang} " compiling..."
        install -d build/calligra-${_pkglang}
        pushd build/calligra-${_pkglang}
        cmake ${srcdir}/calligra-${_pkglang}-${pkgver} \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr
        make 
        popd
    done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  groups=('localization' 'calligra-l10n')

  cd $srcdir/build/calligra-l10n-$1 
  make DESTDIR=$pkgdir install
}

for lang in ${!_languages[@]}; do
  _pkgname=calligra-l10n-$lang
  source+=("http://download.kde.org/stable/calligra-${pkgver}/calligra-l10n/calligra-l10n-${lang}-${pkgver}.tar.xz")

  pkgname+=($_pkgname)
  eval "package_${_pkgname}() {
    _package $lang
  }"
done
