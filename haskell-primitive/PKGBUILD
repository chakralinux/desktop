# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=primitive
pkgname=haskell-primitive
pkgver=0.5.0.1
pkgrel=3
pkgdesc="Wrappers for primitive array operations"
url="http://hackage.haskell.org/package/primitive"
license=('custom:BSD3')
arch=('x86_64' 'i686')
makedepends=()
depends=('ghc=7.6.3-1' 'haskell-base>=4.0' 'haskell-ghc-prim')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/primitive/0.5.0.1/primitive-0.5.0.1.tar.gz")
install="haskell-primitive.install"
sha512sums=('f52ea7c3b3c8e01949dfb55e158b770375c718151bdd65b0123278334d21fe219711c0d207318d248958b4f39a93a62c66ca77737c7775e3fe3b053dc20b34a8')

build() {
    cd "${srcdir}/primitive-0.5.0.1"
    runhaskell Setup configure \
        -O \
        ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } \
        --enable-split-objs \
        --enable-shared \
        --prefix="/usr" \
        --docdir="/usr/share/doc/haskell-primitive" \
        --libsubdir='$compiler/site-local/$pkgid'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/primitive-0.5.0.1"
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/haskell-primitive/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/haskell-primitive/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/haskell-primitive/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/primitive"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/haskell-primitive/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/haskell-primitive/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}
