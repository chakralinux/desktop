#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=performous
_pkgname=Performous
pkgver=0.6.1
pkgrel=1
pkgdesc='A free game like Singstar™, Rockband™ or Stepmania™.'
arch=('i686' 'x86_64')
url="http://$pkgname.org/"
license=('GPL2' 'custom: Menu song: CC:BY-SA.')
depends=('boost' 'sdl' 'jack' 'imagemagick' 'ffmpeg' 'glew' 'libxml++' 'portaudio' 'portmidi'
         'opencv' 'librsvg' 'libjpeg' 'libpng' 'cairo')
makedepends=('cmake' 'help2man')
optdepends=('performous-songs: some songs for Performous.')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$_pkgname-$pkgver-Source.tar.bz2)
md5sums=('451a759de77984b5a699e91107fe52e2')

build() {
  cd $srcdir/$_pkgname-$pkgver-Source

  mkdir build && cd build

  # Fix config loading with libxml++:
  export LDFLAGS=${LDFLAGS/-Wl,--as-needed/}

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    ..
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver-Source/build

  make DESTDIR="$pkgdir" install

  # Licensing information:
  install -Dm644 $srcdir/$_pkgname-$pkgver-Source/docs/Authors.txt \
	  $pkgdir/usr/share/licenses/$pkgname/Authors.txt
  install -Dm644 $srcdir/$_pkgname-$pkgver-Source/docs/License.txt \
	  $pkgdir/usr/share/licenses/$pkgname/License.txt
}
