pkgname=pass
_pkgname=password-store
pkgver=1.7.1
pkgrel=1
pkgdesc='Stores, retrieves, generates, and synchronizes passwords securely'
arch=('x86_64')
url='https://www.passwordstore.org/'
license=('GPL2')
depends=('xclip' 'bash' 'gnupg' 'tree')
checkdepends=('git')
optdepends=('git: for Git support'
            'dmenu: for passmenu'
            'qrencode: for QR code support')
source=("https://git.zx2c4.com/password-store/snapshot/$_pkgname-$pkgver.tar.xz")
sha256sums=('f6d2199593398aaefeaa55e21daddfb7f1073e9e096af6d887126141e99d9869')

check() {
  cd $_pkgname-$pkgver
  make test
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR=$pkgdir WITH_ALLCOMP=yes install

  cd contrib/dmenu
  install -Dm0755 passmenu $pkgdir/usr/bin/passmenu
}
