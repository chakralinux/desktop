#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=blender-beta
pkgver=2.56a
pkgrel=2
pkgdesc="Blender beta version build from source"
arch=('i686' 'x86_64')
url="http://blender.org/"
depends=('libpng' 'libgl' 'mesa' 'openexr' 'gettext' 'python3' 'desktop-file-utils' 'libsamplerate'
         'hicolor-icon-theme' 'ffmpeg' 'fftw' 'lcms' 'lame' 'openal' 'libsndfile' 'jack')
makedepends=('python3' 'yasm' 'cmake')
provides=('blender')
conflicts=('blender')
license=('GPL')
install=blender.install
#source=(http://download.blender.org/source/blender-$pkgver-beta.tar.gz blender.desktop)
source=(http://rp.oldsch00l.com/tmp/source/blender-$pkgver-beta-source.tar.bz2 blender.desktop)
md5sums=('55624e1a5ebbb2b2ef919f6f09b3d862'
         '634b532b58d5ef51bcbb7859b318e81d')

extractedSrcDir="blender-$pkgver-beta-source"

build() {
  msg "Starting make..."
  cd "$srcdir"/$extractedSrcDir

  ##########
  [ -e "$srcdir"/blender-$pkgver-beta-build ] || mkdir -p "$srcdir"/blender-$pkgver-beta-build
  cd "$srcdir"/blender-$pkgver-beta-build
  cmake -DWITH_CODEC_SNDFILE:BOOL=ON -DWITH_CODEC_FFMPEG:BOOL=ON -DCMAKE_INSTALL_PREFIX=/usr "$srcdir"/$extractedSrcDir
  make
  ##########
  # now compile some plugins
  cp -rf "$srcdir"/$extractedSrcDir/release/plugins/* \
    "$srcdir"/$extractedSrcDir/source/blender/blenpluginapi/
  cd "$srcdir"/$extractedSrcDir/source/blender/blenpluginapi
  chmod 755 bmake
  sed -i 's/\r$//' bmake
  make
}

package() {
  cd "$srcdir"/blender-$pkgver-beta-build
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/blender.desktop \
    "$pkgdir"/usr/share/applications/blender.desktop

  # install plugins
  install -d -m755 "$pkgdir"/usr/share/blender/*/plugins/{sequence,texture}
  cp "$srcdir"/$extractedSrcDir/source/blender/blenpluginapi/sequence/*.so \
    "$pkgdir"/usr/share/blender/*/plugins/sequence/
  cp "$srcdir"/$extractedSrcDir/source/blender/blenpluginapi/texture/*.so \
    "$pkgdir"/usr/share/blender/*/plugins/texture/
}
