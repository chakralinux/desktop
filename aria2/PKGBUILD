pkgname=aria2
pkgver=1.19.0
pkgrel=1
pkgdesc='Download utility that supports HTTP(S), FTP, BitTorrent, and Metalink.'
arch=('x86_64')
url='http://aria2.sourceforge.net/'
license=('GPL3')
depends=('ca-certificates' 'c-ares' 'gnutls' 'libxml2' 'sqlite3')
checkdepends=('cppunit')
categories=('network')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e2ea59fc2351951a50408561a43fb4d7f0afeab5b606f0cdbfe2be745575a476')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
    
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  
    # add bash completion (aria2 automatically installs to a temporary target directory)
  install -d "${pkgdir}/usr/share/bash-completion/completions"
  install -m644 "${pkgdir}/usr/share/doc/aria2/bash_completion/aria2c" \
    "${pkgdir}/usr/share/bash-completion/completions"
  rm -rf "${pkgdir}/usr/share/doc/aria2/bash_completion"
}
