pkgname=redis
pkgver=3.0.7
pkgrel=1
pkgdesc="Advanced key-value store"
arch=('x86_64')
url="http://redis.io/"
license=('BSD')
depends=('bash')
makedepends=('gcc' 'make' 'pkgconfig')
backup=("etc/redis.conf"
        "etc/logrotate.d/redis")
install=${pkgname}.install
source=("http://download.redis.io/releases/${pkgname}-${pkgver}.tar.gz"
        "redis.service"
        "redis.logrotate"
        "redis.tmpfiles.d")
sha256sums=('58966575cedd1b1bb90a9251a13913a866c4dc34c4a26f194be20deca9c0b59e'
            'ca4d3cceeb39b4115d85516ab5a8eabd0b646b7a33ed718160ec335fe220261a'
            '8b4c2caabb4f54157ad91ca472423112b1803685ad18ed11b60463d78494df13'
            '7600554eb70af3c89e090fc768e7209a381b909bd941fcfc34c5e45a73f51948')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|# bind 127.0.0.1|bind 127.0.0.1|' redis.conf
  sed -i 's|daemonize no|daemonize yes|' redis.conf
  sed -i 's|dir \./|dir /var/lib/redis/|' redis.conf
  sed -i 's|pidfile .*|pidfile /run/redis/redis.pid|' redis.conf
  sed -i 's|logfile stdout|logfile /var/log/redis.log|' redis.conf
}

build() {
  cd ${pkgname}-${pkgver}
  #make MALLOC=libc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  mkdir -p "${pkgdir}/usr/bin"
  make INSTALL_BIN="${pkgdir}/usr/bin" PREFIX=/usr install

  install -D -m755 COPYING "${pkgdir}/usr/share/licenses/redis/COPYING"
  install -Dm644 "${srcdir}/redis.service" "${pkgdir}/usr/lib/systemd/system/redis.service"
  install -Dm644 "${srcdir}/redis.logrotate" "${pkgdir}/etc/logrotate.d/redis"
  install -D -m644 redis.conf "${pkgdir}/etc/redis.conf"
  install -Dm644 "${srcdir}/redis.tmpfiles.d" "${pkgdir}/usr/lib/tmpfiles.d/redis.conf"
}
