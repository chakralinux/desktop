pkgname=mpc
pkgver=0.30
pkgrel=1
pkgdesc="Minimalist command line interface to MPD"
arch=('x86_64')
url='http://www.musicpd.org/clients/mpc/'
license=('GPL2')
depends=('libmpdclient')
makedepends=('meson' 'python3-sphinx')
options=('!emptydirs')
source=("http://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('65fc5b0a8430efe9acbe6e261127960682764b20ab994676371bdc797d867fce')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    chakra-meson . build
    ninja -C build
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    DESTDIR="${pkgdir}" ninja -C build install

    # Install bash completion file
    install -Dm644 contrib/mpc-completion.bash "${pkgdir}/usr/share/bash-completion/completions/mpc"
}
 
