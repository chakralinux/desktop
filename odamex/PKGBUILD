#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=odamex
pkgver=0.5.3
pkgrel=1
pkgdesc='A free client/server multiplayer engine for the classic FPS Doom.'
arch=('i686' 'x86_64')
url='http://odamex.net/'
license=('GPL')
depends=('sdl_mixer>=1.2.6')
provides=('doom')
categories=('games')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-src-$pkgver.tar.bz2")
md5sums=('8779b0b5ab6d4c15f0e05cd4b250c886')

build() {
  cd $srcdir/$pkgname-src-$pkgver

  sed 's|getenv("DOOMWADPATH")|"/usr/share/doom"|g' -i {client,server}/src/d_main.cpp

  make CFLAGS="${CXXFLAGS} -DUNIX -DLINUX -DNOASM -Icommon" odamex odasrv
}

package() {
  cd $srcdir/$pkgname-src-$pkgver

  install -Dm755 odamex $pkgdir/usr/bin/odamex
  install -Dm755 odasrv $pkgdir/usr/bin/odasrv
  install -Dm644 odamex.wad $pkgdir/usr/share/doom/odamex.wad
  install -Dm644 media/icon_odamex_96.png $pkgdir/usr/share/pixmaps/odamex.png
  install -Dm644 media/icon_odasrv_96.png $pkgdir/usr/share/pixmaps/odasrv.png
  install -Dm644 installer/arch/odamex.desktop $pkgdir/usr/share/applications/odamex.desktop
  install -Dm644 installer/arch/odasrv.desktop $pkgdir/usr/share/applications/odasrv.desktop
}
