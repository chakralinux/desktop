# Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor: laloch <laloch@atlas.cz>

pkgname=qt-gstreamer
pkgver=0.10.3
pkgrel=2
pkgdesc="Qt bindings for GStreamer"
arch=('x86_64')
url="http://gstreamer.freedesktop.org/modules/qt-gstreamer.html"
license=('LGPL2.1')
depends=('qt' 'gstreamer0.10-base' 'mesa')
makedepends=('cmake' 'automoc4' 'boost' 'flex' 'bison' 'doxygen')
conflicts=(qtgstreamer-git)
replaces=(qtgstreamer-git)
provides=(qtgstreamer-git)
optdepends=('libgles: required for OpenGLES rendering support in qtvideosink (embedded only)')
source=("http://gstreamer.freedesktop.org/src/qt-gstreamer/${pkgname}-$pkgver.tar.bz2")
md5sums=('1dfbca4ffa924b0896dadb42221600e2')

build() {
  rm -rf "$srcdir/$pkgname-build"
  mkdir "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  cmake "$srcdir/${pkgname}-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DQTGSTREAMER_EXAMPLES=OFF \
    -DQTGSTREAMER_TESTS=OFF     
  make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
