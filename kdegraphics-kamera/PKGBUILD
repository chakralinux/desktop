# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdegraphics-kamera
_pkgname=kamera
pkgver=${_kdever}
pkgrel=1
pkgdesc='KDE integration for gphoto2 cameras'
arch=('x86_64')
url='http://kde.org/applications/graphics/kamera/'
license=('GPL'
         'LGPL'
         'FDL')
depends=("kde-runtime>=${_kdever}"
         'libgphoto2')
makedepends=('cmake'
             'automoc4')
groups=('kde'
        'kdegraphics'
        'kde-uninstall')
categories=('graphics')
options=('docs'
         '!header'
         'debug'
         'log')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE4_BUILD_TESTS=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
