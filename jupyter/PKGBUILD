pkgbase=jupyter
pkgname=('jupyter' 'ipython2-notebook')
pkgver=4.2.0
pkgrel=1
pkgdesc="A language-agnostic web-based interactive shell/notebook server."
arch=('any')
url="http://jupyter.org/"
license=('BSD')
depends=('ipython3>4.0.0' 'python3-ipykernel' 'python3-setuptools'
         'python3-jinja' 'python3-pyzmq' 'python3-tornado' 'python3-jsonschema'
         'python3-terminado' 'python3-mistune' 'python3-pygments')
makedepends=('python3-setuptools' 'python2-setuptools' 'npm' 'git')
optdepends=('qt5-svg: jupyter-qtconsole'
            'python3-pyqt5: jupyter-qtconsole'
            'python3-sip: jupyter-qtconsole')

# some of these will update rapidly, others never
# so _vers will be phased in as new updates are released
_notev=4.2.3  # notebook
_jcliv=4.4.0
_ipywv=5.2.2  # ipywidgets
_qtcv=4.2.1   # qtconsole
_jnbfv=4.1.0
_jconv=5.0.0  # jupyter_console
_pyver=3.5

source=("jupyter_core-$pkgver.tgz::https://github.com/jupyter/jupyter_core/archive/$pkgver.tar.gz"
        "ipywidgets-$_ipywv.tgz::https://github.com/ipython/ipywidgets/archive/$_ipywv.tar.gz"
        "nbformat-$_jnbfv.tar.gz::https://github.com/jupyter/nbformat/archive/$_jnbfv.tar.gz"
        "jupyter_console-$_jconv.tgz::https://github.com/jupyter/jupyter_console/archive/$_jconv.tar.gz"
        "jupyter_client-$_jcliv.tgz::https://github.com/jupyter/jupyter_client/archive/$_jcliv.tar.gz"
        "notebook-$_notev.tgz::https://github.com/jupyter/notebook/archive/$_notev.tar.gz"
        "https://pypi.python.org/packages/source/q/qtconsole/qtconsole-$_qtcv.tar.gz")
sha1sums=('9ad9f27c553cff61a4429f71419981e928369571'
          'a4e18ed6a91bca345ee074af8d77b2bebc4a1e96'
          '378c57a9e89fcd3e76e7a3446d41343168d77f59'
          '60cbc801c72cd42370a962a26c29c996734d7822'
          '84e097a5a1434b9ea3c4cef220c73a8cd4c284e5'
          'cd42c2013047d2722d3990db4787820cff51cbc8'
          '4a0ea706c88721e3c6d2cfeb46fcf10c9bd1b4dc')

prepare() {
  cd "$srcdir"
  cp -r jupyter_core-$pkgver python2-jupyter_core-$pkgver
  cp -r jupyter_client-$_jcliv python2-jupyter_client-$_jcliv
  cp -r qtconsole-$_qtcv      python2-qtconsole-$_qtcv
}

build() {
  cd "$srcdir"

  cd "$srcdir/notebook-$_notev"
  # needs node, downloads a lot of stuff
  python3 setup.py build

  cd "$srcdir/ipywidgets-$_ipywv"
  # needs node, downloads a lot of stuff
  python3 setup.py build
}

package_jupyter() {
  provides=('ipython3-notebook')
  replaces=('ipython3-notebook')

  cd "$srcdir/jupyter_core-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipywidgets-$_ipywv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/notebook-$_notev"
  # needs node
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  # FS45999 use system mathjax
  ln -s /usr/share/mathjax \
  "$pkgdir/usr/lib/python$_pyver/site-packages/notebook/static/components/MathJax"
  
  cd "$srcdir/nbformat-$_jnbfv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_console-$_jconv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_client-$_jcliv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/qtconsole-$_qtcv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 examples/jupyter-qtconsole.desktop "$pkgdir/usr/share/applications/org.ipython.qtconsole.desktop"
  sed -i s!gnome-netstatus-idle!JupyterConsole! \
  "$pkgdir/usr/share/applications/org.ipython.qtconsole.desktop"
  install -D $pkgdir/usr/lib/python$_pyver/site-packages/qtconsole/resources/icon/JupyterConsole.svg $pkgdir/usr/share/pixmaps/JupyterConsole.svg
  
  # LICENSE
  cd "$srcdir"
  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_ipython2-notebook() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('ipython2>4.0.0' 'python2-ipykernel' 'python2-pyzmq')
  optdepends=('python2-pyqt5: jupyter-qtconsole-py2'
              'qt5-svg: jupyter-qtconsole-py2'
              'python2-sip: jupyter-qtconsole-py2')

  cd "$srcdir/python2-jupyter_core-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-jupyter_client-$_jcliv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-qtconsole-$_qtcv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$pkgdir/usr/bin"
  rm jupyter
  rm jupyter-kernelspec
  rm jupyter-migrate
  mv jupyter-qtconsole jupyter-qtconsole-py2

  cd "$srcdir"
  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
