pkgbase=jupyter
pkgname=('jupyter' 'ipython2-notebook')
pkgver=4.1.0
pkgrel=1
pkgdesc="A language-agnostic web-based interactive shell/notebook server."
arch=('any')
url="http://jupyter.org/"
license=('BSD')
depends=('ipython3>4.0.0' 'python3-setuptools' 'mathjax'
         'python3-jinja' 'python3-pyzmq' 'python3-tornado' 'python3-jsonschema'
         'python3-terminado' 'python3-mistune' 'python3-pygments')
makedepends=('python3-setuptools' 'python2-setuptools' 'npm' 'git')
optdepends=('mathjax: latex equation rendering'
            'qt5-svg: jupyter-qtconsole'
            'python3-pyqt5: jupyter-qtconsole'
            'python3-sip: jupyter-qtconsole')

# some of these will update rapidly, others never
# so _vers will be phased in as new updates are released
_ipykv=4.3.1  # ipykernel
_ipywv=5.0.0  # ipywidgets
_qtcv=4.1.1   # qtconsole
_notev=4.2.0  # notebook
_nbcv=4.2.0
_nbfv=4.0.1
_jpcsv=4.1.1  # jupyter_console
_jpclv=4.2.2
_pyver=3.5

source=("jupyter_core.tgz::https://github.com/jupyter/jupyter_core/archive/$pkgver.tar.gz"
        "ipykernel.tgz::https://github.com/ipython/ipykernel/archive/$_ipykv.tar.gz"
        "ipywidgets.tgz::https://github.com/ipython/ipywidgets/archive/v$_ipywv.tar.gz"
        "nbconvert.tgz::https://github.com/jupyter/nbconvert/archive/$_nbcv.tar.gz"
        "nbformat.tar.gz::https://github.com/jupyter/nbformat/archive/$_nbfv.tar.gz"
        "jupyter_console.tgz::https://github.com/jupyter/jupyter_console/archive/$_jpcsv.tar.gz"
        "jupyter_client.tgz::https://github.com/jupyter/jupyter_client/archive/$_jpclv.tar.gz"
        "notebook.tgz::https://github.com/jupyter/notebook/archive/$_notev.tar.gz"
        "https://pypi.python.org/packages/source/q/qtconsole/qtconsole-$_qtcv.tar.gz")
sha256sums=('80b78d215399760f4678cc1512118257543e17b48316254d1e1dbddfafa2dffc'
            'f38b366053567c36464ae6d04d72ed72d74f77e63d49a6fa38826278ed7848fd'
            '0eb192bc2b33a5ab1adfcf5dd120b876630149d6b930ea43b3ff68299a3f8d81'
            '32394be5a20f39f99fabfb9b2f2625df17f1c2a6699182ca41598e98e7cc9610'
            '5c46c21349f29379fa55bf19e4359afcd605fd4b5693a56807355874a2e87f78'
            '0bb06a1f878d0c44c2f6f66406a80f949bcd86f3508035500af7dceffb9cc7dc'
            'bf3e8ea4c44f07dbe2991e41031f6dab242734be424f4d40b72cc58a12c7d2ca'
            'edb7ef8153e5ca781f21c7feb50b3316d515b0d8861ba0e45a00280166214a7f'
            '741906acae9e02c0df9138ac88b621ef22e438565aa96d783a9ef88faec3de46')

# notebook optdeps ?
# 'haskell-pandoc: ipython notebook conversion'
# 'texlive-bin: notebook pdf export'

# qtconsole deps?
# python-pyqt4 python-sip python-pygments python-pyzmq
# is qt5 now?

prepare() {
  cd "$srcdir"
  cp -r jupyter_core-$pkgver python2-jupyter_core-$pkgver
  cp -r jupyter_client-$_jpclv python2-jupyter_client-$_jpclv
  cp -r ipykernel-$_ipykv    python2-ipykernel-$_ipykv
  cp -r qtconsole-$_qtcv      python2-qtconsole-$_qtcv
}

build() {
  cd "$srcdir"

  # FS45999 disable bundled mathjax
  cd "$srcdir/notebook-$_notev"
  #sed -i 's/^.*MathJax.*$//' bower.json 
  #sed -i '/Trim mathjax/,/^                static_data.append/d' setupbase.py
  #sed -i 's/mj(/#mj(/' setupbase.py
  # needs node, downloads a lot of stuff
  python3 setup.py build

  cd "$srcdir/ipywidgets-$_ipywv"
  # needs node, downloads a lot of stuff
  python3 setup.py build
}

package_jupyter() {
  provides=('ipython3-notebook')
  replaces=('ipython3-notebook')

  cd "$srcdir/jupyter_core-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipykernel-$_ipykv"
  # this is in the ipython package
  #python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipywidgets-$_ipywv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/notebook-$_notev"
  # needs node
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  # FS45999 use system mathjax
  ln -s /usr/share/mathjax \
  "$pkgdir/usr/lib/python$_pyver/site-packages/notebook/static/components/MathJax"

  cd "$srcdir/nbconvert-$_nbcv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/nbformat-$_nbfv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_console-$_jpcsv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_client-$_jpclv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/qtconsole-$_qtcv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 examples/jupyter-qtconsole.desktop "$pkgdir/usr/share/applications/ipython-qtconsole.desktop"

  cd "$srcdir"
  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_ipython2-notebook() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('ipython2>4.0.0' 'jupyter' 'python2-pyzmq')
  optdepends=('python2-pyqt5: jupyter-qtconsole-py2'
              'qt5-svg: jupyter-qtconsole-py2'
              'python2-sip: jupyter-qtconsole-py2')

  cd "$srcdir/python2-ipykernel-$_ipykv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-jupyter_core-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-jupyter_client-$_jpclv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-qtconsole-$_qtcv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$pkgdir/usr/bin"
  rm jupyter
  rm jupyter-kernelspec
  rm jupyter-migrate
  mv jupyter-qtconsole jupyter-qtconsole-py2

  cd "$srcdir"
  install -d "$pkgdir/usr/share/jupyter/kernels/python2"
  echo '{"display_name": "Python 2", "language": "python2", "argv": ["python2", "-c", "from IPython.kernel.zmq.kernelapp import main; main()", "-f", "{connection_file}"],"codemirror_mode": {"version": 2, "name": "ipython2"}}' > "$pkgdir/usr/share/jupyter/kernels/python2/kernel.json"

  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
