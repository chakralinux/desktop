pkgname=lighttpd
pkgver=1.4.37
pkgrel=1
pkgdesc='A secure, fast, compliant and very flexible web-server.'
license=('custom')
arch=('x86_64')
url="http://www.lighttpd.net/"
depends=('pcre' 'bzip2' 'libldap' 'util-linux')
makedepends=('fcgi' 'libmysqlclient' 'lua' 'libxml2' 'e2fsprogs' 'sqlite3' 'gdbm' 'pkgconfig')
optdepends=('libxml2: mod_webdav'
            'lua: mod_cml/mod_magnet'
            'libmysqlclient: mod_mysql_vhost'
            'sqlite3: mod_webdav')
categories=('network')
backup=('etc/lighttpd/lighttpd.conf' 'etc/logrotate.d/lighttpd')
options=('!libtool' 'emptydirs')
install=${pkgname}.install
source=("http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.gz"
        'lighttpd.tmpfiles'
        'lighttpd.service'
        'lighttpd.logrotate.d' 
        'lighttpd.conf')
sha512sums=('66895bef05cc0cefb8807a7a753d6bd5fba0486c2187334f223a5b8431f789154a42ea3927e8a2a5ad09796433a47dd4ba8851400b9d76743202d42181a98bff'
            '0079d6fd80ca42fa5384838b3df68d25000fb7a5a92ea9fb02f6a18cdfb85b23279c78b881e21270e7e450ee3ae28ea839932f3268b826b52478079e510ecd33'
            'e9cb6f83e19366f8243a1ac1962d957f11801dbf20b2ebdaecd96d8422f794fd9061725dab648a6fd1bc7b74f59601340970f05ab4288e5c9ff92b065fbf1bf4'
            '6ad0213cb50e9325b0cbf8a58365387868f9798c775ce3b68b032fc4046ea637eb31f1fde38af7bb5efc5aac6a6e3443a8a620fd1c55f3beeece35300dec0fbd'
            '000f2ec18c52aa0d828355aec68ff85639e64161883c7567d23159d752b5f33054cca1a82177be58543fc880f165e797b2d262eea318053593b656c659addc6b')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr \
		--libexecdir=/usr/lib/lighttpd/modules \
		--libdir=/usr/lib \
                --sysconfdir=/etc/lighttpd \
		--with-mysql \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-webdav-props \
		--with-webdav-locks \
		--with-gdbm \
		--with-memcache \
		--with-lua
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -D -m644 "${srcdir}/lighttpd.logrotate.d" "${pkgdir}/etc/logrotate.d/lighttpd"
	install -D -m644 "${srcdir}/lighttpd.conf" "${pkgdir}/etc/lighttpd/lighttpd.conf"
	install -d -m755 -o http -g http "${pkgdir}"/{run/lighttpd,var/{log,cache}/lighttpd/}
	install -D -m644 "${srcdir}/lighttpd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/lighttpd.conf"
	install -D -m644 "${srcdir}/lighttpd.service" "${pkgdir}/usr/lib/systemd/system/lighttpd.service"

	pushd doc/config >/dev/null
	find . -type f ! -name 'Makefile*' -exec install -D -m644 {} "${pkgdir}"/usr/share/doc/lighttpd/config/{} \;
	popd >/dev/null

	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
