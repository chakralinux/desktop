pkgname=kid3
pkgver=3.4.1
pkgrel=1
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor for KDE"
arch=('x86_64')
url="http://kid3.sourceforge.net/"
license=('GPL')
depends=('id3lib' 'libmp4v2' 'chromaprint' 'ffmpeg' 'qt5-multimedia' 'plasma-framework'
	 'phonon-qt5' 'taglib' 'kio' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('cmake' 'docbook-xsl' 'docbook-xml' 'qt5-tools' 'extra-cmake-modules' 'kdoctools')
categories=('multimedia')
conflicts='kid3-kf5'
replaces='kid3-kf5'
screenshot=('http://pkgs.org/images/linux-software/kid3/kid3-screenshot.png')
install=${pkgname}.install
source=("http://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha1sums=('5b0fbafdf4757d60a0974523127b788047ab76a4')


build() {
  mkdir -p ${srcdir}/build
  cd ${srcdir}/build
  cmake ../${pkgname}-${pkgver} \
    -DWITH_QT5=ON \
    -DWITH_QML=ON  \
    -DWITH_FFMPEG=ON \
    -DWITH_MP4V2=ON \
    -DWITH_GSTREAMER=ON \
    -DWITH_APPS="CLI;KDE" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release 
  make

}

package() {
  cd ${srcdir}/build
  make DESTDIR="${pkgdir}" install
}
