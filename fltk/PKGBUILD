
pkgname=fltk
pkgver=1.3.4
_pkgver=2
pkgrel=2
pkgdesc="Graphical user interface toolkit for X."
arch=('x86_64')
screenshot="http://www.fltk.org/images/fltk-widgets-std.png"
license=('custom:LGPL')
url="http://www.fltk.org/"
depends=('libjpeg-turbo' 'libpng' 'libxft' 'libxinerama' 'hicolor-icon-theme'
         'desktop-file-utils' 'xdg-utils' 'libgl')
makedepends=('mesa' 'doxygen' 'alsa-lib')
options=('!docs')
categories=('system')
source=("http://fltk.org/pub/${pkgname}/${pkgver}/${pkgname}-${pkgver}-${_pkgver}-source.tar.gz")
md5sums=('b8e291343357e49dd81a22408744e400')

prepare() {
  cd ${pkgname}-${pkgver}-${_pkgver}

  sed -e 's/x-fluid.desktop/fluid.desktop/' \
      -i fluid/Makefile
}

build() {
  cd ${pkgname}-${pkgver}-${_pkgver}

  sed -i -e 's/$(LINKFLTK)/$(LINKSHARED)/' \
         -e 's/$(LINKFLTKIMG)/$(LINKSHARED)/' test/Makefile

  ./configure --prefix=/usr \
              --enable-shared \
              --enable-threads \
              --enable-xft \
              --enable-gl

  make
  cd documentation && make html
}

package() {
  cd ${pkgname}-${pkgver}-${_pkgver}
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C fluid install install-linux
  
  chmod 644 "${pkgdir}/usr/lib"/*.a
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
