# Contributor: kote
# Maintainer: Daniele Cocca <jmc@chakra-project.org>

pkgname=fltk
pkgver=1.3.0
pkgrel=2
pkgdesc="Graphical user interface toolkit for X."
arch=('x86_64')
screenshot="http://www.fltk.org/images/fltk-widgets-std.png"
license=('custom:LGPL')
url="http://www.fltk.org/"
depends=('libjpeg-turbo' 'libpng' 'libxft' 'libxinerama' 'hicolor-icon-theme'
         'desktop-file-utils' 'xdg-utils' 'libgl')
makedepends=('mesa' 'doxygen' 'alsa-lib')
options=('!docs')
categories=('system')
install="fltk.install"
source=("http://ftp.easysw.com/pub/${pkgname}/${pkgver}/${pkgname}-${pkgver}-source.tar.gz")
md5sums=('44d5d7ba06afdd36ea17da6b4b703ca3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e 's/$(LINKFLTK)/$(LINKSHARED)/' \
         -e 's/$(LINKFLTKIMG)/$(LINKSHARED)/' test/Makefile

  ./configure --prefix=/usr \
              --enable-shared \
              --enable-threads \
              --enable-xft \
              --enable-gl

  make
  cd documentation && make html
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  ( cd fluid && make DESTDIR="${pkgdir}" install install-linux )

  chmod 644 "${pkgdir}/usr/lib"/*.a
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
