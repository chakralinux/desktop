#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Frenández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_hkgname=mmap
pkgname=haskell-mmap
pkgver=0.5.7
pkgrel=1
pkgdesc="Memory mapped files for POSIX and Windows"
url="http://hackage.haskell.org/package/mmap"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc=6.12.3' sh)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/mmap/$pkgver/mmap-$pkgver.tar.gz)
install=haskell-mmap.install
md5sums=('1f74b3f59617332b06e277f5212423c1')

build() {
    cd ${srcdir}/mmap-$pkgver
    runhaskell Setup configure --prefix=/usr -p --enable-shared --enable-split-objs --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
}

package() {
    cd ${srcdir}/mmap-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
