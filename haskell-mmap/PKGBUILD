# Maintainer: UtG <utg[dot]chakra.linux[at]gmail[dot]com>
# Contributors  H W TovetjÃ¤rn (totte) <totte@tott.es>
#		Stephen McIntosh <stephenmac7[at]gmail[dot]com>
#               Thomas Dziedzic <gostrc@gmail.com>
#               Vesa Kaihlavirta <vesa@archlinux.org>
#               Arch Haskell Team <arch-haskell@haskell.org>

## NOTE: when you update Haskell packages you'll have to update the entire stack to not break it ##

_hkgname=mmap
pkgname=haskell-mmap
pkgver=0.5.9
pkgrel=2
pkgdesc="Memory mapped files for POSIX and Windows"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc' 'haskell-bytestring')
options=('strip' 'staticlibs')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('625301d54b9789e890174e38028c1bcb')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
   runhaskell Setup configure \
        -O \
        -p \
        --enable-split-objs \
        --enable-shared \
        --prefix=/usr \
        --docdir=/usr/share/doc/haskell-mmap \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh \
        "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install -m744 unregister.sh \
        "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"

    # Documentation
    ln -s "/usr/share/doc/${pkgname}/html" \
        "${pkgdir}/usr/share/doc/ghc/html/libraries/mmap"

    # License
    install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    runhaskell Setup copy --destdir="${pkgdir}"
}

