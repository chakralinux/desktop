# contributions from ArchLinux: https://www.archlinux.org/packages/community/x86_64/handbrake-cli/

pkgname=handbrake-cli
pkgver=1.0.7
pkgrel=2
pkgdesc="Multithreaded video transcoder (CLI)"
arch=('x86_64')
url="http://handbrake.fr/"
license=('GPL')
depends=('bzip2' 'gcc-libs' 'zlib' 'fribidi' 'libass' 'lame' 'libxml2'
         'libtheora' 'libsamplerate' 'ffmpeg' 'libx264' 'x264' 'x265' 'libvpx'
         'libdvdnav' 'jansson')
makedepends=('intltool' 'python2' 'yasm' 'wget' 'libnotify' 'gst-plugins-base' 'dbus-glib' 'cmake')
source=("https://download.handbrake.fr/releases/$pkgver/HandBrake-$pkgver.tar.bz2")
categories=('multimedia')
sha256sums=('ffdee112f0288f0146b965107956cd718408406b75db71c44d2188f5296e677f')

prepare() {
  cd "$srcdir/HandBrake-$pkgver"

  # Use more system libs
  # Bundled libbluray is kept because it is patched locally
  # Our ffmpeg is a liitle old so keep using bundled version
  for _lib in libvpx libdvdnav libdvdread; do
    sed -i "/MODULES += contrib\/$_lib/d" make/include/main.defs
  done
}    
build() {
  cd "$srcdir/HandBrake-$pkgver"
  
  ./configure --prefix=/usr \
              --force \
              --disable-gtk \
              --disable-gtk-update-checks
  cd build
  make
}

package() {
  cd "$srcdir/HandBrake-$pkgver/build"
  
  install -D HandBrakeCLI "$pkgdir/usr/bin/handbrake-cli"
}
