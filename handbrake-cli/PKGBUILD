# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# cpntributions from ArchLinux: https://www.archlinux.org/packages/community/x86_64/handbrake-cli/
pkgname=handbrake-cli
pkgver=0.9.9
pkgrel=2
arch=('x86_64')
url="http://handbrake.fr/"
license=('GPL')
depends=('bzip2' 'gcc-libs' 'zlib' 'fribidi' 'ffmpeg')
makedepends=('intltool' 'python' 'yasm' 'wget' 'libnotify' 'gstreamer0.10-base' 'dbus-glib' 'libbluray' 'libass')
source=("http://downloads.sourceforge.net/project/handbrake/$pkgver/HandBrake-$pkgver.tar.bz2")
categories=('multimedia')
md5sums=('ee81337a46f8af76599d6038f193a2c1')

build() {
  cd "$srcdir/HandBrake-$pkgver"
  
  # Use more system libs
  # We had ffmpeg here as well but it broke PGS subtitle processing
  # https://forum.handbrake.fr/viewtopic.php?f=13&t=27581
  sed -i \
    -e '/MODULES += contrib\/libbluray/d' \
    -e '/MODULES += contrib\/x264/d' \
    make/include/main.defs
    

  ./configure --prefix=/usr \
              --force \
              --disable-gtk \
              --disable-gtk-update-checks \
              --enable-ff-mpeg2
  cd build
  make
}

package() {
  cd "$srcdir/HandBrake-$pkgver/build"
  
  install -D HandBrakeCLI "$pkgdir/usr/bin/HandBrakeCLI"
}

