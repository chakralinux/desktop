#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgbase=handbrake
pkgname=('handbrake-cli')
pkgver=0.9.6
pkgrel=1
arch=('i686' 'x86_64')
url="http://handbrake.fr/"
license=('GPL')
makedepends=('intltool' 'python' 'yasm' 'wget' 'bzip2' 'gcc-libs' 'libnotify' 'gstreamer0.10-base' 'dbus-glib' 'fribidi')
source=(http://downloads.sourceforge.net/project/handbrake/$pkgver/HandBrake-$pkgver.tar.bz2)
md5sums=('c75a0ca3ee74a3d67b25959937397cc4')

build() {
  cd "$srcdir/HandBrake-$pkgver"

  ./configure --prefix=/usr --force --disable-gtk --disable-gtk-update-checks
  cd build
  make
}


package_handbrake-cli() {
  pkgdesc="Multithreaded video transcoder (CLI)"
  depends=('bzip2' 'gcc-libs' 'zlib' 'fribidi')

  cd "$srcdir/HandBrake-$pkgver/build"
  install -D HandBrakeCLI "$pkgdir/usr/bin/HandBrakeCLI"
}

