# Maintainer:   Manuel Tortosa <manutortosa@chakra-project@org>
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname='kde-runtime'
arch=('x86_64')
pkgver=${_kdever}
pkgrel=3
pkgdesc='Plugins and applications necessary for the running of KDE applications'
url='http://www.kde.org'
license=('GPL'
         'LGPL'
         'FDL')
options=('docs'
         '!header'
         'debug'
         'log')
depends=("kdelibs>=${_kdever}"
         "nepomuk-core>=${_kdever}"
         "kactivities>=${_kdever}"
         'oxygen-icons'
         'xorg-xauth'
         'hicolor-icon-theme'
         'samba'
         'rarian'
         'libssh'
         'libpulse'
         'libssh'
         'libcanberra'
         'sound-theme-freedesktop')
makedepends=('pkgconfig'
             'cmake'
             'automoc4'
             "kdepimlibs>=${_kdever}"
             'openslp'
             'docbook-xsl'
             'networkmanager'
             'xine-lib'
             'libcanberra-pulse'
             'soprano')
optdepends=('htdig: to build the search index'
            'gdb: drkonq crash handler')
groups=('kde'
        'kde-minimal'
        'kde-uninstall')
replaces=('kdebase-runtime'
          'kde-runtime-doc')
provides=('kdebase-runtime'
          'kde-runtime-doc')
conflicts=('kdebase-runtime'
           'kde-runtime-doc')
install=kde-runtime.install
noextract=(branding.svgz)
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        'branding.svgz'
        'Remove-volume-control-from-KNotify.patch')
sha1sums=(`grep ${pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d" " -f1`
          'e9a8617448523aacd70dd66f2ffc62a827af027b'
          '4fd852d4ceabb98724f6ceb3df3edf9ae7c72736')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    rm -rf "${srcdir}/${pkgname}-${pkgver}/build"
    mkdir "${srcdir}/${pkgname}-${pkgver}/build"
    cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgname}-${pkgver}/desktoptheme/oxygen/widgets/
    cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgname}-${pkgver}/desktoptheme/air/widgets/

    msg "Applying patches..."
    patch -p1 -i ${srcdir}/Remove-volume-control-from-KNotify.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    cmake .. \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DWITH_PulseAudio=ON \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make DESTDIR=${pkgdir} install
    # put kdesu into the path
    cp ${pkgdir}/usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/kdesu
    # WORKAROUND, conflict with hicolor-icon-theme
    rm -rf ${pkgdir}/usr/share/icons/hicolor/index.theme
    rm -rf ${pkgdir}/usr/share/icons/default.kde4
}
