#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

#
# package info
#
pkgbase="kde-runtime"
pkgname=('kde-runtime' 
	 'kde-runtime-doc')
        
arch=('i686' 'x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="split package"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' '!splithdr' 'splitdbg' 'log')
makedepends=('pkgconfig' 'cmake' 'automoc4' "kdepimlibs>=${_kdever}" 'openslp' 'docbook-xml' 'docbook-xsl'
             'xine-lib' 'libpulse' 'libssh' 'ntrack' 'libcanberra' 'libcanberra-pulse' 'samba' 'soprano')
groups=('kde')

noextract=(branding.svgz)

source=($_mirror/${pkgbase}-$_kdever.tar.bz2
	branding.svgz
#	01_knetattach_use_sftp.patch
#	02_oxygenify_knetattach_icon.patch
	03_disable_logout_sound.patch
#	04_disable_nepomuk_popups.patch
#	http://chakra-project.org/sources/kde-runtime/kde-runtime-95f65b882149d398553e8934318658301c84dd29.diff
#	http://chakra-project.org/sources/kde-runtime/kde-runtime-ebaf3b457b94a8be7a894b8eed81940cb7bb818f.diff
        fix-empty-jobs.patch)

md5sums=(`grep ${pkgbase}-$_kdever.tar.bz2 ../kde-sc.md5 | cut -d" " -f1`
        '68cd8eb57db22c2f2ed554b721253bc2'  #  branding.svgz
#       '45fa12b8baf21c4fc845147afcea2bc1'  #  01_knetattach_use_sftp.patch
#       '1f0225f31d5f44e753e966fd3a032f00'  #  02_oxygenify_knetattach_icon.patch
        'a434bf282ee391b2f857184be59a3b6f'  #  03_disable_logout_sound.patch
#       'a3c844d7bbd9a31f39b1b877ae521340'  #  04_disable_nepomuk_popups.patch
#        '286b55c6f8d11ee960739443993bc292'  # kde-runtime-95f65b882149d398553e8934318658301c84dd29.diff
#        '23e58fefbe59872f346b2470e3c9973b'  # kde-runtime-ebaf3b457b94a8be7a894b8eed81940cb7bb818f.diff
         'bf39497d3a8ba27751d10c2d4b913a5d') # fix-empty-jobs.patch

     



#
# build function
#
build()
{
	cd ${pkgbase}-${pkgver}

	msg "applying main patchset ..."
	# patch -Np1 -i ${srcdir}/01_knetattach_use_sftp.patch
	# patch -Np1 -i ${srcdir}/02_oxygenify_knetattach_icon.patch
	patch -Np1 -i ${srcdir}/03_disable_logout_sound.patch
	#patch -Np1 -i ${srcdir}/04_disable_nepomuk_popups.patch

	msg "add hotfixes ..."

        # FIXME: remove this?
	# see: http://mail.kde.org/pipermail/release-team/2011-July/005149.html
	#patch -Np1 -i ${srcdir}/kde-runtime-ebaf3b457b94a8be7a894b8eed81940cb7bb818f.diff
	#patch -Np1 -i ${srcdir}/kde-runtime-95f65b882149d398553e8934318658301c84dd29.diff

        # see: http://www.afiestas.org/two-annoying-bugs-fixed-in-kdirwatch-and-kuiserver/
	patch -Np1 -i ${srcdir}/fix-empty-jobs.patch
        
	# copy our branding stuff
	cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgbase}-${pkgver}/desktoptheme/oxygen/widgets/
	cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgbase}-${pkgver}/desktoptheme/air/widgets/

	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DWITH_PulseAudio=ON \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make
}



#
# split-install functions
#
package_kde-runtime()
{
	pkgdesc="KDE Runtime"
	depends=("kdelibs>=${_kdever}" 'oxygen-icons' 'xorg-xauth' 'hicolor-icon-theme'
		 'smbclient' 'rarian' 'libssh' 'libpulse' 'libssh' 'ntrack' 'libcanberra' 'sound-theme-freedesktop')
	optdepends=('htdig: to build the search index' 'gdb: drkonq crash handler')
	groups=("kde" "kde-minimal" "kde-uninstall")
        replaces=('kdebase-runtime')
        provides=('kdebase-runtime')
	conflicts=('kdebase-runtime')
	install=kde-runtime.install

	cd ${srcdir}/${pkgbase}-${pkgver}/
	make DESTDIR=${pkgdir} install

	# also in kde-runtime-doc
	rm -rf ${pkgdir}/usr/share/man
	rm -rf ${pkgdir}/usr/share/doc

	# put kdesu into the path
	cp ${pkgdir}/usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/kdesu

	# use transparent panel svg's everywhere
	for n in default oxygen; do
		cp -f ${pkgdir}/usr/share/apps/desktoptheme/$n/widgets/panel-background.svgz ${pkgdir}/usr/share/apps/desktoptheme/$n/opaque/widgets/panel-background.svgz
	done

	# include our patches into the package
	ls -1 ${startdir}/*.patch &>/dev/null 2>&1
	if [ "$?" = "0" ]; then
		warning "incuding patches into package"
		mkdir -p ${pkgdir}/usr/share/chakra/patches/${pkgbase} &>/dev/null
		for i in ${startdir}/*.patch; do
			msg "$i"
			cp $i ${pkgdir}/usr/share/chakra/patches/${pkgbase}/ &>/dev/null
		done
	else
		warning "no patches found, skipping to include them into the package..."
	fi
	
	# WORKAROUND, conflict with hicolor-icon-theme
	rm -rf ${pkgdir}/usr/share/icons/hicolor/index.theme
	rm -rf ${pkgdir}/usr/share/icons/default.kde4
}

package_kde-runtime-doc()
{
	pkgdesc="KDE Runtime - Documentation"
	groups=("kde" "kde-uninstall" "kde-doc")
        replaces=('kdebase-runtime-doc')

	splitdirs="doc"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install
		done
}

