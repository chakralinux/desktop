# Include global configuration
source ../kdeapps.conf

pkgbase='kde-runtime'
pkgname=('kde-runtime' 'khelpcenter4')
arch=('x86_64')
pkgver=${_kdever}
pkgrel=2
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
options=('!docs' 'debug')
makedepends=('pkgconfig'
             'cmake'
             'automoc4'
             "kdepimlibs4"
             'openslp'
             'docbook-xsl'
             'networkmanager'
             'xine-lib'
             'libcanberra-pulse'
             "kdelibs>=${_libsver}"
             'libkactivities4'
             'oxygen-icons'
             'xorg-xauth'
             'hicolor-icon-theme'
             'samba'
             'rarian'
             'libssh'
             'libpulse'
             'libssh>=0.6.0'
             'libcanberra'
             'sound-theme-freedesktop'
             'libgcrypt'
             'boost')
groups=('kde' 'kde-minimal' 'kde-uninstall')
install=kde-runtime.install
noextract=(branding.svgz)
source=("$_mirror/${pkgbase}-$_kdever.tar.xz"
        'branding.svgz')
sha256sums=(`grep ${pkgbase}-$_kdever.tar.xz  ../checksums.txt | cut -d" " -f1`
            '586c5461d66ef13d5c43fb269e16c4da51812ae25a0d1b132f0bbbcdb22505e5')

prepare() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    rm -rf "${srcdir}/${pkgbase}-${pkgver}/build"
    mkdir "${srcdir}/${pkgbase}-${pkgver}/build"
    cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgbase}-${pkgver}/desktoptheme/oxygen/widgets/
    cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgbase}-${pkgver}/desktoptheme/air/widgets/
}

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}/build"
    cmake .. \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DWITH_PulseAudio=ON \
        -DCMAKE_SKIP_RPATH=ON \
        -DWITH_NepomukCore=OFF \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package_kde-runtime() {
    pkgdesc='Plugins and applications necessary for the running of KDE applications'
    depends=("kdelibs>=${_libsver}"
             'libkactivities4'
             'oxygen-icons'
             'xorg-xauth'
             'hicolor-icon-theme'
             'samba'
             'rarian'
             'libssh'
             'libpulse'
             'libssh>=0.6.0'
             'libcanberra'
             'sound-theme-freedesktop'
             'libgcrypt')
    optdepends=('kdepimlibs4: for kwalletd, and is needed by DrKonqi to send crash reports to KDE.org'
		'htdig: to build the search index'
                'gdb: drkonq crash handler')
    replaces=('kdebase-runtime'
              'kde-runtime-doc')
    provides=('kdebase-runtime'
              'kde-runtime-doc')
    conflicts=('kdebase-runtime'
               'kde-runtime-doc')

    cd "${srcdir}/${pkgbase}-${pkgver}/build"
    make DESTDIR=${pkgdir} install
    # put kdesu into the path
    cp ${pkgdir}/usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/kdesu
    # WORKAROUND, conflict with hicolor-icon-theme
    rm -rf ${pkgdir}/usr/share/icons/hicolor/index.theme
    rm -rf ${pkgdir}/usr/share/icons/default.kde4
    # split khelpcenter
    rm  "${pkgdir}"/usr/lib/kde4/libexec/khc_indexbuilder \
        "${pkgdir}"/usr/lib/libkdeinit4_khelpcenter.so \
        "${pkgdir}"/usr/bin/khelpcenter \
        "${pkgdir}"/usr/share/applications/kde4/Help.desktop \
        "${pkgdir}"/usr/share/config.kcfg/khelpcenter.kcfg \
        "${pkgdir}"/usr/share/kde4/services/khelpcenter.desktop \
        "${pkgdir}"/usr/share/dbus-1/interfaces/org.kde.khelpcenter.kcmhelpcenter.xml \
        "${pkgdir}"/usr/lib/kde4/libexec/khc_htdig.pl \
        "${pkgdir}"/usr/lib/kde4/libexec/khc_htsearch.pl \
        "${pkgdir}"/usr/lib/kde4/libexec/khc_mansearch.pl \
        "${pkgdir}"/usr/lib/kde4/libexec/khc_docbookdig.pl
    rm -r "${pkgdir}"/usr/share/apps/khelpcenter/
    # useless in plasma5
    rm -rf ${pkgdir}/usr/share/desktop-directories/
    rm -rf ${pkgdir}/etc/xdg/menus/
}

package_khelpcenter4() {
    pkgdesc="Application to show KDE Applications' documentation"
    provides=('khelpcenter')
    depends=("kde-runtime=${pkgver}")
    cd "${srcdir}/${pkgbase}-${pkgver}/build/khelpcenter"
    make DESTDIR=${pkgdir} install
}
