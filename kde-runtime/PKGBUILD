# Include global configuration
source ../kdeapps.conf

pkgname='kde-runtime'
pkgver=${_kdever}
pkgrel=2
pkgdesc="Plugins and applications necessary for the running of KDE applications"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('GPL' 'LGPL')
options=('debug')
depends=('libkactivities4' 'smbclient' 'libssh' 'libcanberra' 'oxygen-icons' 'xorg-xauth' 'libwebp' 'kdepimlibs4')
optdepends=('gdb: needed by DrKonqi to generate backtrace'
            'khelpcenter: to display applications documentation')
makedepends=('pkg-config' 'cmake' 'automoc4' 'openslp' 'doxygen' 'networkmanager' 'boost')

groups=('kde' 'kde-minimal' 'kde-uninstall')
install=kde-runtime.install
replaces=('kdebase-runtime')
provides=('kdebase-runtime')
conflicts=('kdebase-runtime')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"{,.sig})
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../kdeapps.sums | cut -d" " -f1`
	    'SKIP')
validpgpkeys=(${Avalidpgpkeys})

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kde-runtime-$pkgver \
    -DCMAKE_BUILD_TYPE=${_build_type} \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=${_installprefix} \
    -DWITH_QNtrack=OFF \
    -DSAMBA_INCLUDE_DIR=/usr/include/samba-4.0 \
    -DWITH_NepomukCore=OFF \
    -DBUILD_khelpcenter=OFF \
    -DBUILD_kglobalaccel=OFF \
    -DBUILD_kuiserver=OFF \
    -DBUILD_doc=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/share/icons/hicolor/index.theme
  # included in oxygen-icons
  rm "$pkgdir"/usr/share/icons/default.kde4
  ln -s /usr/lib/kde4/libexec/kdesu "$pkgdir"/usr/bin/kdesu4

  # FS#36668
  chown :nobody "$pkgdir"/usr/lib/kde4/libexec/kdesud
  chmod g+s "$pkgdir"/usr/lib/kde4/libexec/kdesud
}

