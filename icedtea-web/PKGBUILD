#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=icedtea-web
pkgdesc="Provides a Free Software web browser plugin running applets written in the Java programming language and an implementation of Java Web Start, originally based on the NetX project."
pkgver=1.4.1
pkgrel=1
arch=('x86_64')
url="http://icedtea.classpath.org/wiki/IcedTea-Web"
license=('GPL2')

_javaver=7
_jvmdir="/usr/lib/jvm/java-${_javaver}-openjdk"

makedepends=("java-environment=${_javaver}" 'zip' 'qt' 'npapi-sdk' 'rhino' 'junit' 'cups' 'autoconf')
depends=("java-environment=${_javaver}" 'qt' 'desktop-file-utils')
categories=('network')
install="${pkgname}.install"

source=("http://icedtea.classpath.org/download/source/${pkgname}-${pkgver}.tar.gz")
noextract="${pkgname}-$pkgver.tar.gz" # due to broken path names in the tarball that fails with LANG=C in our chroot
sha256sums=('09100207396a09784e04f2ad40d65212930c1d43839e7fa63b9dffaf8567d77b')

build() {
  cd "${srcdir}"
  LANG=en_US.UTF8 bsdtar -x -f "${srcdir}/${pkgname}-$pkgver.tar.gz"

  cd "${srcdir}"/icedtea-web*

  ./configure --prefix="${_jvmdir}" \
              --with-jdk-home="${_jvmdir}" \
              --datarootdir=/usr/share \
              --disable-docs

  make
}

check() {
  cd "${srcdir}"/icedtea-web*
  # as more tests have been added some are expectged to fail
  # see http://mail.openjdk.java.net/pipermail/distro-pkg-dev/2012-March/017566.html
  make -k check || return 0
}

package() {
  cd "${srcdir}"/icedtea-web*

  # possible make target (see bottom of Makefile.am: install-exec-local install-data-local
  make install-exec-local install-data-local DESTDIR="${pkgdir}"

  # Install desktop files.
  install -m755 -d "${pkgdir}/usr/share"/{applications,pixmaps}
  install -m644 javaws.png "${pkgdir}/usr/share/pixmaps"
  install -m644 {javaws,itweb-settings}.desktop "${pkgdir}/usr/share/applications"
  # remove splitted doc files
  rm -rf "${pkgdir}/usr/share/doc"

  # link binaries into /usr/bin + jre/bin
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/${_jvmdir}/jre/bin"
  pushd "${pkgdir}/${_jvmdir}/bin"
    for file in *; do
      ln -sf "${_jvmdir}/bin/${file}" "${pkgdir}/usr/bin"
      ln -sf "${_jvmdir}/bin/${file}" "${pkgdir}/${_jvmdir}/jre/bin"
    done
  popd

  # link the mozilla-plugin - test it here http://www.java.com/en/download/help/testvm.xml
  install -m755 -d "${pkgdir}/usr/lib/mozilla/plugins/"
  ln -sf "${_jvmdir}/lib/IcedTeaPlugin.so" "${pkgdir}/usr/lib/mozilla/plugins/"
}

# vim:set ts=2 sw=2 et:
