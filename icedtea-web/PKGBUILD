pkgname=icedtea-web
pkgver=1.6.2
pkgrel=1
arch=('x86_64')
pkgdesc="Provides a Free Software web browser plugin running applets written in the Java programming language and an implementation of Java Web Start, originally based on the NetX project."
url="http://icedtea.classpath.org/wiki/IcedTea-Web"
license=('GPL2')

_javaver=8
_jvmdir="/usr/lib/jvm/java-${_javaver}-openjdk"

depends=("java-environment=${_javaver}" 'qt' 'desktop-file-utils')
makedepends=('zip' 'npapi-sdk' 'rhino' 'junit' 'cups' 'autoconf')
categories=('network')
istall="${pkgname}.install"
source=("http://icedtea.classpath.org/download/source/${pkgname}-${pkgver}.tar.gz")
# due to broken path names in the tarball that fails with LANG=C in our chroot
noextract="${pkgname}-$pkgver.tar.gz"
md5sums=('ef9b7746dab933afa871428da73169f6')

prepare() {
  cd "${srcdir}"

  # if you encounter this error "bsdtar: Failed to set default locale"
  # you will need to set locale of your chroot   https://wiki.archlinux.org/index.php/Locale
  # do this (inside your chroot):
  # sudo nano /etc/locale.gen
  # then uncomment a locale (like en_US.UTF-8 for example)
  # save and type
  # sudo locale-gen

  LANG=en_US.UTF-8 bsdtar -x -f "${srcdir}/${pkgname}-$pkgver.tar.gz"
}

build() {
  cd "${srcdir}"/icedtea-web*

  ./configure --prefix="${_jvmdir}" \
              --with-jdk-home="${_jvmdir}" \
              --datarootdir=/usr/share \
              --disable-docs

  make
}

package() {
  cd "${srcdir}"/icedtea-web*

  # possible make target (see bottom of Makefile.am: install-exec-local install-data-local
  make install-exec-local install-data-local DESTDIR="${pkgdir}"

  # Install desktop files.
  install -dm755 "${pkgdir}/usr/share"/{applications,pixmaps}
  install -m644 javaws.png "${pkgdir}/usr/share/pixmaps"
  install -m644 {javaws,itweb-settings}.desktop "${pkgdir}/usr/share/applications"

  # link binaries into /usr/bin + jre/bin
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/${_jvmdir}/jre/bin"
  pushd "${pkgdir}/${_jvmdir}/bin"
    for file in *; do
      ln -sf "${_jvmdir}/bin/${file}" "${pkgdir}/usr/bin"
      ln -sf "${_jvmdir}/bin/${file}" "${pkgdir}/${_jvmdir}/jre/bin"
    done
  popd

  # link the mozilla-plugin - test it here http://www.java.com/en/download/help/testvm.xml
  install -dm755 "${pkgdir}/usr/lib/mozilla/plugins/"
  ln -sf "${_jvmdir}/lib/IcedTeaPlugin.so" "${pkgdir}/usr/lib/mozilla/plugins/"
  
  make DESTDIR="${pkgdir}" install-data-local
}
