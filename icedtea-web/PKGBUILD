#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=icedtea-web
pkgdesc="Provides a Free Software web browser plugin running applets written in the Java programming language and an implementation of Java Web Start, originally based on the NetX project."
pkgver=1.3
pkgrel=1
arch=('i686' 'x86_64')
url="http://icedtea.classpath.org/wiki/IcedTea-Web"
license=('GPL2')

_javaver=7
_jvmdir="/usr/lib/jvm/java-${_javaver}-openjdk"

makedepends=("java-environment=${_javaver}" 'zip' 'qt' 'npapi-sdk' 'rhino' 'junit' 'cups')
depends=("java-environment=${_javaver}" 'qt' 'desktop-file-utils')
categories=('network')
install="${pkgname}.install"

source=("http://icedtea.classpath.org/download/source/${pkgname}-${pkgver}.tar.gz"
        'replace-gtk-with-qt.patch'
        'include_unistd_h.patch')
sha256sums=('d46ec10700732cea103da2aae64ff01e717cb1281b83e1797ce48cc53280b49f'
            '8690ee5e0fdc56b68073ca3a99a9423a938c09d7c01431b1ac70613206eb28f9'
            'e2258177b59c51425dd67327f6c21785a5f49155624db3522ee25e5dfa8537fd')

build() {
  cd "${srcdir}"

  rm -rf build
  cp -r "${pkgname}-${pkgver}" build

  cd build

  patch -Np1 -i "${srcdir}/replace-gtk-with-qt.patch"
  patch -Np1 -i "${srcdir}/include_unistd_h.patch"

  ./configure --prefix="${_jvmdir}" \
              --with-jdk-home="${_jvmdir}" \
              --datarootdir=/usr/share \
              --disable-docs \
  || return 1

  make
}

check() {
  cd "${srcdir}/build"
  # as more tests have been added some are expectged to fail
  # see http://mail.openjdk.java.net/pipermail/distro-pkg-dev/2012-March/017566.html
  make -k check || return 0
}

package() {
  cd "${srcdir}/build"

  # possible make target (see bottom of Makefile.am: install-exec-local install-data-local
  make install-exec-local install-data-local DESTDIR="${pkgdir}"

  # Install desktop files.
  install -m755 -d "${pkgdir}/usr/share"/{applications,pixmaps}
  install -m644 javaws.png "${pkgdir}/usr/share/pixmaps"
  install -m644 {javaws,itweb-settings}.desktop "${pkgdir}/usr/share/applications"
  # remove splitted doc files
  rm -rf "${pkgdir}/usr/share/doc"

  # link binaries into /usr/bin + jre/bin
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/${_jvmdir}/jre/bin"
  pushd "${pkgdir}/${_jvmdir}/bin"
    for file in *; do
      ln -sf "${_jvmdir}/bin/${file}" "${pkgdir}/usr/bin"
      ln -sf "${_jvmdir}/bin/${file}" "${pkgdir}/${_jvmdir}/jre/bin"
    done
  popd

  # link the mozilla-plugin - test it here http://www.java.com/en/download/help/testvm.xml
  install -m755 -d "${pkgdir}/usr/lib/mozilla/plugins/"
  ln -sf "${_jvmdir}/lib/IcedTeaPlugin.so" "${pkgdir}/usr/lib/mozilla/plugins/"
}

# vim:set ts=2 sw=2 et:
