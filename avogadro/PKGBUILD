#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dog]org>


pkgname=avogadro
pkgver=1.1.0
pkgrel=3
pkgdesc="An advanced molecular editor based on Qt"
arch=('x86_64')
url="http://avogadro.openmolecules.net/wiki/Main_Page"
license=('GPL2')
depends=('eigen' 'openbabel' 'pyqt4-python2' 'boost-libs' 'glew' 'python-numpy')
makedepends=('cmake' 'boost')
categories=('education')
screenshot=('http://avogadro.openmolecules.net/w/images/7/75/Avogadro-unitcell.png')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2" "boost.patch")
md5sums=('f4fbef5ffc69cc312b2316426c36a78d'
         'aa2573f78d5dbd85b3cb79e08acde8ab')

prepare() {
  mkdir -p build
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/boost.patch
}

build() {
  cd ${srcdir}/build

  # use the cmake module in system to enable python 2.7 support
  rm ../${pkgname}-${pkgver}/cmake/modules/FindPythonLibs.cmake

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_MKSPECS_RELATIVE=share/qt/mkspecs
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
