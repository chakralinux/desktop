pkgname=avogadro
pkgver=1.2.0
pkgrel=1
pkgdesc="An advanced molecular editor based on Qt"
arch=('x86_64')
url="http://avogadro.cc/wiki/Main_Page"
license=('GPL2')
depends=('eigen2' 'openbabel' 'python2-pyqt4' 'boost-libs' 'glew' 'python2-numpy')
makedepends=('cmake' 'boost' 'doxygen' 'libgl')
categories=('education')
screenshot=('http://avogadro.openmolecules.net/w/images/7/75/Avogadro-unitcell.png')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "boost153.patch"
        "avogadro-fix-linking.patch")
sha256sums=('6453e36e8ae3e61655cbe062df6d6fa6b2409122c7b5abc0a6f1d410a181640b'
            '0aaddbac2ed6e9688d7923a303b68c229f435068372f3665d731b1b9fa1d8cfb'
            '3f7a236ec59684762514e81f8211cee625a44af843c8ce401e1ee002786e9d31')

prepare() {
  mkdir -p build
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/boost153.patch
  patch -p1 -i "${srcdir}"/avogadro-fix-linking.patch
}

build() {
  cd ${srcdir}/build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DQT_MKSPECS_RELATIVE=share/qt/mkspecs
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
