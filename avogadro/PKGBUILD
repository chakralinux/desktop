# Maintainer:
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               A Boersma (abveritas)
#               A Scarpino (bash) <andrea@archlinux.org>

pkgname=avogadro
pkgver=1.1.0
pkgrel=4
pkgdesc="An advanced molecular editor based on Qt"
arch=('x86_64')
url="http://avogadro.openmolecules.net/wiki/Main_Page"
license=('GPL2')
depends=('eigen2' 'openbabel' 'python2-pyqt4' 'boost-libs' 'glew' 'python2-numpy')
makedepends=('cmake' 'boost' 'doxygen' 'libgl')
categories=('education')
screenshot=('http://avogadro.openmolecules.net/w/images/7/75/Avogadro-unitcell.png')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        "boost153.patch")
sha512sums=('d621dd3ea9e9223d3a29c6b0c0eec89a935837d0c587fcaaa549f91274a76f78a8c60c2a68a67d2f3aa2e107826befb6658be601c46c5bc0760c95bf7284540a'
            'b8ebad1e70a91408be97554ac4af5872ca9ce62a387568ed32c40c50f0b29b3f6c3db38333408fda119b08f99531fd87e4dec6a5483e3492a1e8fe30b4e42d26')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/boost153.patch
  cd ${srcdir}
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DQT_MKSPECS_RELATIVE=share/qt/mkspecs
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
