#
# Chakra Packages for Chakra, part of chakra-project.org
#
# Maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>
#
 
 
pkgname=mendeleydesktop
pkgver=1.1.3
pkgrel=1
url="http://www.mendeley.com/"
pkgdesc="The desktop client for managing and sharing research papers."
license=('custom:mendeley_eula')
arch=(i686 x86_64)
_filearch=i486
md5sums=('55719984f706e4f71718a7f1674ef046')
[ "${CARCH}" == "x86_64" ] && _filearch=x86_64 && md5sums=('3fc090c8d4a61154f3d1d979ff1bf09e')
source=("http://s3.amazonaws.com/mendeley-desktop-download/linux/${pkgname}-${pkgver}-linux-${_filearch}.tar.bz2")
screenshot=('http://wstaw.org/m/2011/11/21/plasma-desktopcl2200.jpg')
 
build()
{
  cd "$pkgname-$pkgver-linux-${_filearch}"
 
  rm -f share/doc/mendeleydesktop/*.txt
  install -dm755 "$pkgdir/opt/$pkgname/"{,bin}
  mv lib share "$pkgdir/opt/$pkgname/"
  ln -s "../lib/mendeleydesktop/libexec/mendeleydesktop.${_filearch}" "$pkgdir/opt/$pkgname/bin/$pkgname"
 
  sed -i '1s@^#!/usr/bin/python$@&2@' bin/mendeleydesktop
  install -Dm755 "bin/mendeleydesktop" "$pkgdir/usr/bin/mendeleydesktop"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
 
  mkdir -p "$pkgdir"/usr/share/applications
  ln -s /opt/$pkgname/share/applications/mendeleydesktop.desktop "$pkgdir"/usr/share/applications/
  for size in 16 22 32 48 64 128;do
    mkdir -p "$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps
    ln -s /opt/$pkgname/share/icons/hicolor/${size}x${size}/apps/${pkgname}.png \
      "$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps
  done
}
