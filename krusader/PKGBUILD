pkgname=krusader
pkgver=2.7.1
pkgrel=1
epoch=1
pkgdesc="An alternative file manager for KDE resembling Midnight Commander"
url="https://krusader.org/"
arch=('x86_64')
license=('GPL')
depends=('karchive' 'kbookmarks' 'kcodecs' 'kcompletion' 'kcoreaddons' 'kconfig' 'kdoctools' 'ki18n'
         'kiconthemes' 'kitemviews' 'kio' 'knotifications' 'kparts' 'solid' 'ktextwidgets'
         'kwallet' 'kwidgetsaddons' 'kwindowsystem' 'kxmlgui' 'kguiaddons' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python3' 'acl')
optdepends=('acl: Access control list utilities')
categories=('system')
screenshot=('http://www.krusader.org/static/img/slideshow/krusader-twinpanel-thumb.png')
source=(https://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('d5c14d7e0698b84a1285efe7058074f760924d13d0823e1b5123fbe771907deb')

prepare() {
  mkdir -p build
      
  sed -i s!"Categories=FileManager;Utility;Qt;KDE;"!"Categories=FileManager;System;Qt;KDE;"! \
  $srcdir/${pkgname}-${pkgver}/${pkgname}/org.kde.krusader.desktop
}

build() {
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make 
}

package() {
  
  make -C build DESTDIR="${pkgdir}" install 
}
