# Include global configuration

pkgname=kgoldrunner
pkgver=18.04.2
pkgrel=1
pkgdesc="A game of action and puzzle-solving"
arch=('x86_64')
url='http://kde.org/applications/games/kgoldrunner/'
screenshot="https://www.kde.org/images/screenshots/kgoldrunner.png"
license=('GPL' 'LGPL' 'FDL')
groups=('kdegames' 'kde' 'kde-applications')
depends=(libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegames-kgoldrunner)
replaces=(kdegames-kgoldrunner)
provides=(kdegames-kgoldrunner)
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a47ec6249943acdf7e28ce7e6fb20c16abe8136b694cd321700369f76d7f56e5'
                        'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
