#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves gmail.com>

pkgname=usb-modeswitch
_pkgname=usb_modeswitch
pkgver=1.2.5
pkgrel=1
_pkgdata=20121109
pkgdesc="Activating switchable USB devices on Linux."
arch=('i686' 'x86_64')
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL3')
depends=('libusb-compat' 'tcl')
makedepends=('gcc' 'make')
conflicts=(usb_modeswitch)
replaces=(usb_modeswitch)
categories=('system')
backup=("etc/$_pkgname.conf")
source=("http://www.draisberghof.de/$_pkgname/$pkgname-$pkgver.tar.bz2"
	"http://www.draisberghof.de/$_pkgname/$pkgname-data-$_pkgdata.tar.bz2"
	"usb-modeswitch-${pkgver}-device_reference.txt::http://www.draisberghof.de/$_pkgname/device_reference.txt")
md5sums=('c393603908eceab95444c5bde790f6f0'
         'a7d23a03157871013a0d708ab2b1b6df'
         '9918bbdc0f68873410688604c80d25c4')

build() {
  cd $srcdir/usb-modeswitch-$pkgver/
  make clean
  make
}

package() {
  cd $srcdir/usb-modeswitch-$pkgver/

  mkdir -p $pkgdir/usr/lib/udev
  mkdir -p $pkgdir/etc
  mkdir -p $pkgdir/usr/share/man/man1

  make DESTDIR=${pkgdir} UDEVDIR=${pkgdir}/usr/lib/udev install

  cp $srcdir/usb-modeswitch-${pkgver}-device_reference.txt $pkgdir/etc/usb_modeswitch.setup

  cd $srcdir/usb-modeswitch-data-${_pkgdata}
  make DESTDIR=$pkgdir install RULESDIR=${pkgdir}/usr/lib/udev/rules.d
}
