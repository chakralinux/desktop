# Maintainer: UtG  <utg[dot]chakra.linux[at]gmail[dot]com>

pkgbase=python-keyring
pkgname=('python3-keyring' 'python2-keyring')
pkgver=5.3
pkgrel=1
pkgdesc="Store and access your passwords safely."
arch=('any')
url="http://pypi.python.org/pypi/keyring"
license=('PSF' 'MIT')
makedepends=('python3-setuptools' 'python2-setuptools')
checkdepends=('python3-pytest' 'python2-pytest' 'python3-mock' 'python2-mock' 'python3-crypto' 'python2-crypto'
              'python3-gobject' 'python2-gobject' 'libgnome-keyring'
              'kdebindings-python3' 'kdebindings-python2')
optdepends=('kdeutils-kwallet: supports the KDE Kwallet service.')	    
source=("http://pypi.python.org/packages/source/k/keyring/keyring-$pkgver.zip")
md5sums=('fd50a2be4a44a78efb09a7c046b6410d')

prepare() {
  cp -a keyring-$pkgver{,-py2}
}


build() {
  cd "$srcdir/keyring-$pkgver"
  python3 setup.py build

  cd "$srcdir/keyring-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/keyring-$pkgver"
  py.test-3.4

  cd "$srcdir/keyring-$pkgver-py2"
  py.test2
}

package_python3-keyring() {
  depends=('python3')
  optdepends=('libgnome-keyring: Gnome integration'
            'python3-gobject: Gnome integration'
            'kdebindings-python3: Kde integration'
            'python3-crypto: CLI keyring')

  cd "$srcdir/keyring-$pkgver"
  python3 setup.py install --root=$pkgdir --optimize=1
}

package_python2-keyring() {
  depends=('python2')
  optdepends=('libgnome-keyring: Gnome integration'
            'python2-gobject: Gnome integration'
            'kdebindings-python2: Kde integration'
            'python2-crypto: CLI keyring')

  cd "$srcdir/keyring-$pkgver-py2"
  python2 setup.py install --root=$pkgdir --optimize=1

  mv "$pkgdir"/usr/bin/keyring{,2}
}
