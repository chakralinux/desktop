# Maintainer: UtG  <utg[dot]chakra.linux[at]gmail[dot]com>

pkgbase=python-keyring
pkgname=('python3-keyring' 'python2-keyring')
pkgver=8.5.1
pkgrel=1
pkgdesc="Store and access your passwords safely."
arch=('any')
url="http://pypi.python.org/pypi/keyring"
license=('PSF' 'MIT')
makedepends=('python3-setuptools' 'python2-setuptools')
optdepends=('kdeutils-kwallet: supports the KDE Kwallet service.')	    
source=("http://pypi.python.org/packages/source/k/keyring/keyring-$pkgver.tar.gz")
md5sums=('f358ae5f121d169424d32fcd7bd309f7')

prepare() {
  cp -a keyring-$pkgver{,-py2}
}

build() {
  cd "$srcdir/keyring-$pkgver"
  python3 setup.py build

  cd "$srcdir/keyring-$pkgver-py2"
  python2 setup.py build
}

package_python3-keyring() {
  depends=('python3')
  optdepends=('libgnome-keyring: Gnome integration'
            'python3-gobject: Gnome integration'
            'kdebindings-python3: Kde integration'
            'python3-crypto: CLI keyring')

  cd "$srcdir/keyring-$pkgver"
  python3 setup.py install --root=$pkgdir --optimize=1
}

package_python2-keyring() {
  depends=('python2')
  optdepends=('libgnome-keyring: Gnome integration'
            'python2-gobject: Gnome integration'
            'kdebindings-python2: Kde integration'
            'python2-crypto: CLI keyring')

  cd "$srcdir/keyring-$pkgver-py2"
  python2 setup.py install --root=$pkgdir --optimize=1

  mv "$pkgdir"/usr/bin/keyring{,2}
}
