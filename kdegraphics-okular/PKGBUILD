# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegraphics-okular/
# Include global configuration
source ../kdeapps.conf

pkgname=kdegraphics-okular
_pkgname=okular
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc='Universal document viewer'
url='http://kde.org/applications/graphics/okular/'
license=('GPL' 'LGPL' 'FDL')
options=('docs' '!header' 'debug' 'log')
groups=('kde' 'kdegraphics' 'kde-uninstall')
provides=('okular')
categories=('office')
# note on libxxf86vm:
#       not detected by namcap because libgl depends on it
#       but nvidia providing libgl does not depend on libxxf86vm
depends=("kdelibs>=${_libsver}"
         "kde-runtime"
         "kdegraphics-libkdcraw>=${_kdever}"
         "kdegraphics-libkexiv2>=${_kdever}"
         'libxxf86vm' 'sane' 'lcms' 'qimageblitz' 'poppler-qt' 'chmlib' 'djvulibre' 'libspectre')
optdepends=('ebook-tools: epub support'
            'kdegraphics-mobipocket: mobi support')
makedepends=('pkg-config' 'cmake' 'automoc4' 'qca')
install=okular.install
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    CXXFLAGS="${CXXFLAGS} -I /usr/include/freetype2" cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE4_BUILD_TESTS=ON \
        -DWITH_ActiveApp=OFF \
        -DFREETYPE_INCLUDE_DIR=/usr/include/freetype2/freetype/config \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
