# Maintainer:   Manuel Tortosa <manutortosa@chakra-project@org>
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>
#               Anke Boersma (abveritas) <abveritas@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdegraphics-okular
_pkgname=okular
arch=('x86_64')
pkgver=${_kdever}
pkgrel=3
pkgdesc='Universal document viewer'
url='http://kde.org/applications/graphics/okular/'
license=('GPL'
         'LGPL'
         'FDL')
options=('docs'
         '!header'
         'debug'
         'log')
groups=('kde'
        'kdegraphics'
        'kde-uninstall')
provides=('okular')
categories=('office')
install=okular.install
# note on libxxf86vm:
#       not detected by namcap because libgl depends on it
#       but nvidia providing libgl does not depend on libxxf86vm
depends=("kdelibs>=${_kdever}"
         "kde-runtime>=${_kdever}"
         'libxxf86vm'
         'sane'
         'lcms'
         "kdegraphics-libkdcraw>=${_kdever}"
         "kdegraphics-libkexiv2>=${_kdever}"
         'qimageblitz'
         'poppler-qt'
         'chmlib'
         'djvulibre'
         'libspectre'
         'ebook-tools')
makedepends=('pkg-config'
             'cmake'
             'automoc4'
             'qca')
source=("$_mirror/${_pkgname}-$_kdever.tar.xz")
sha256sums=(`grep ${_pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE4_BUILD_TESTS=OFF \
        -DWITH_ActiveApp=OFF \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
sha256sums=('e7f9f3071a1a47261da6e2efed9df9653ba7d60a1f5b834c67e5f2d877f8bb15')
