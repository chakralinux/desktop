#
# Chakra Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=sigil
_pkgname=Sigil
pkgver=0.4.1
pkgrel=1
pkgdesc="A multi-platform WYSIWYG ebook editor. It is designed to edit books in ePub format. "
arch=('i686' 'x86_64') 
url="http://code.google.com/p/sigil/"
license=('GPL3')
depends=('qt')
makedepends=('cmake')
install=sigil.install
source=(http://${pkgname}.googlecode.com/files/${_pkgname}-${pkgver}-Code.zip
        ${pkgname}.desktop)
md5sums=('b0ae292aea366fe6eb3f3392232ba9a6'
         '3034d3948eb01804db7a71f89a3eba7d')

build() {
  mkdir build
  cd build
  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
    -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON ..

    make
}

package() {
  cd build
  make install DESTDIR="${pkgdir}"

  install -d -m 0755 "${pkgdir}"/usr/share/applications
  install -m 0644 "${srcdir}"/${pkgname}.desktop "${pkgdir}"/usr/share/applications

  for _pic in 16 32 48 128 256; do
    install -D -m 0644 ../src/Sigil/Resource_Files/icon/app_icon_${_pic}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${_pic}x${_pic}/apps/${pkgname}.png
  done
}
