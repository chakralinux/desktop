#
# Apps Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=sigil
_pkgname=Sigil
pkgver=0.5.0
pkgrel=1
pkgdesc="A multi-platform WYSIWYG ebook editor. It is designed to edit books in ePub format. "
arch=('i686' 'x86_64') 
url="http://code.google.com/p/sigil/"
license=('GPL3')
depends=('qt')
makedepends=('cmake')
install=sigil.install
source=(http://$pkgname.googlecode.com/files/$_pkgname-$pkgver-Code.zip
        $pkgname.desktop)
md5sums=('8deb8ae451d485698ab9330dccfe46e5'
         '83779d140d681fd18f3a8c917bfa7f2f')

build() {
  mkdir build && cd build
  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS:STRING="$CFLAGS" \
    -DCMAKE_CXX_FLAGS:STRING="$CXXFLAGS" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON ..
  make
}

package() {
  cd build
  make install DESTDIR=$pkgdir

  # Desktop integration.
  install -Dm0644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  for _pic in 16 32 48 128 256; do
    install -Dm0644 ../src/Sigil/Resource_Files/icon/app_icon_$_pic.png \
      $pkgdir/usr/share/icons/hicolor/$_picx$_pic/apps/$pkgname.png
  done
}
