#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# contributor Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=sigil
_pkgname=Sigil
pkgver=0.6.2
pkgrel=1
pkgdesc="A multi-platform WYSIWYG ebook editor. It is designed to edit books in ePub format. "
arch=('x86_64')
url="http://code.google.com/p/sigil/"
license=('GPL3')
depends=('qtwebkit' 'boost-libs')
makedepends=('cmake' 'gendesk' 'boost')
categories=('office')
install=sigil.install
screenshot=('http://1.bp.blogspot.com/-K2Q-tdVbPRI/UI08v7LhzHI/AAAAAAAAABY/vZ5LHjM6nb4/s1600/Screen+Shot+2012-10-28+at+10.07.03+AM.png')
source=("http://$pkgname.googlecode.com/files/$_pkgname-$pkgver-Code.zip")
md5sums=('78615dc007c12cc34271243d82783a36')


build() {
  gendesk -n

  mkdir build && cd build
  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS:STRING="$CFLAGS" \
    -DCMAKE_CXX_FLAGS:STRING="$CXXFLAGS" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON ..
  make
}

package() {
  cd build
  make install DESTDIR=$pkgdir
  install -d -m 0755 "${pkgdir}"/usr/share/applications

  # Desktop integration.
  install -Dm0644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  for _pic in 16 32 48 128 256; do
    install -Dm0644 ../src/Sigil/Resource_Files/icon/app_icon_$_pic.png \
      $pkgdir/usr/share/icons/hicolor/$_picx$_pic/apps/$pkgname.png
  done
   install -Dm644 ../src/Sigil/Resource_Files/icon/app_icon_128.png \
       "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
