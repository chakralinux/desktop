pkgname=sigil
_pkgname=Sigil
pkgver=0.9.10
pkgrel=1
pkgdesc="A multi-platform WYSIWYG ebook editor. It is designed to edit books in ePub format."
arch=('x86_64')
url="http://sigil-ebook.com/"
license=('GPL3')
depends=('qt5-webkit' 'hicolor-icon-theme' 'python3' 'python3-lxml>=3.5.0' 'python3-six' 'python3-html5lib' 'python3-chardet' 'python3-cssselect' 'python3-cssutils' 'python3-pillow' 'python3-regex')
makedepends=('qt5-multimedia' 'qt5-tools' 'qt5-svg' 'cmake')
categories=('office')
source=("${pkgname}-${pkgver}.zip::https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.tar.gz")
sha256sums=('294954c4c2b3f1f9348d4f1bff18edebaba44afae901be3900154778b2f877ed')

build() {
  cd $_pkgname-$pkgver
  mkdir -p build 
  cd build

  cmake -G "Unix Makefiles"  \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS:STRING="$CFLAGS" \
    -DCMAKE_CXX_FLAGS:STRING="$CXXFLAGS" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_SYSTEM_LIBS=OFF ..
  make
}

package() {
  cd $_pkgname-$pkgver
  cd build
  
  make DESTDIR=$pkgdir install
  
  install -Dm0644 $srcdir/$_pkgname-$pkgver/src/Resource_Files/freedesktop/$pkgname.desktop \
  $pkgdir/usr/share/applications/$pkgname.desktop
  
  for _pic in 16 32 48 128 256; do
    install -Dm0644 ../src/Resource_Files/icon/app_icon_$_pic.png \
      $pkgdir/usr/share/icons/hicolor/${_pic}x${_pic}/apps/$pkgname.png
  done
   install -Dm644 ../src/Resource_Files/icon/app_icon_512.png \
       "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
