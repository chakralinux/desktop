# Include global configuration
source ../kdeapps.conf

pkgbase=marble
pkgname=(marble libastro marble-data)
pkgver=${_kdever}
pkgrel=1
pkgdesc="Desktop Globe"
arch=(x86_64)
url="https://www.kde.org/applications/system/marble"
screenshot="http://www.kde.org/images/screenshots/marble.png"
license=(GPL)
makedepends=(extra-cmake-modules gpsd libwlocate phonon-qt5 qt5-webkit krunner kparts knewstuff qt5-tools) # quazip-qt5
options=('docs' 'debug')
source=("$_mirror/${pkgbase}-$_kdever.tar.xz"{,.sig})
sha256sums=(`grep ${pkgbase}-$_kdever.tar.xz  ../kdeapps.sums | cut -d " " -f1`
                        'SKIP')
validpgpkeys=(${Avalidpgpkeys})

prepare() {
  mkdir -p build

  # fix installation designer plugins path
  sed 's|LIB_SUFFIX}/plugins|LIB_SUFFIX}/qt/plugins|g' -i $pkgbase-$pkgver/CMakeLists.txt

  # only build the KDE app
  sed -e '/mobile/d' \
      -e '/qt/d'  \
      -e '/Quick/d' \
      -e '/touch/d' \
      -i $pkgbase-$pkgver/src/apps/CMakeLists.txt
}

build() {
  cd build
  cmake_kf5 ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DBUILD_MARBLE_TOOLS=ON \
    -DBUILD_MARBLE_TESTS=OFF \
    -DMOBILE=OFF
  make
}

package_libastro() {
  pkgdesc='Marble astronomy library'
  depends=(gcc-libs)
  conflicts=('kdeedu-marble<15.07')

  cd build/src/lib/astro
  make DESTDIR="$pkgdir" install
}

package_marble() {
  depends=(libastro marble-data qt5-webkit kparts knewstuff hicolor-icon-theme)
  optdepends=('gpsd: GPS based geolocation'
            'quazip-qt5: reading and displaying .kmz files'
            'libwlocate: WLAN based geolocation'
            'phonon-qt5: enables the use of audio and video content'
            'krunner: Krunner plugin')
  conflicts=('kdeedu-marble<15.04.3-1')
  replaces=('kdeedu-marble')
  groups=(kde-applications kdeedu)
  install=marble.install

  cd build
  make DESTDIR="$pkgdir" install

  # provided by libastro
  rm -r "$pkgdir"/usr/include/astro
  rm -r "$pkgdir"/usr/lib/{libastro.*,cmake/Astro}

  # provided by marble-data
  rm -r "$pkgdir"/usr/share/{icons,marble/data,mime}
}

package_marble-data() {
  pkgdesc='Data for Marble'
  conflicts=('kdeedu-marble<15.07')

  cd build/data
  make DESTDIR="$pkgdir" install
}
