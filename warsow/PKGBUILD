#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=warsow
pkgver=1.0
pkgrel=2
pkgdesc="A free online multiplayer competitive FPS based on the Qfusion engine"
url="http://www.warsow.net/"
license=('GPL')
arch=('i686' 'x86_64') 
depends=('curl' 'libjpeg' 'libvorbis' 'libxinerama' 'libxxf86dga' 'libxxf86vm' 'sdl' 'warsow-data')
makedepends=('mesa' 'openal' 'imagemagick' 'gendesk')
optdepends=('openal: for openal audio support')
categories=('games')
source=("https://launchpadlibrarian.net/111352130/${pkgname}_${pkgver}_sdk.tar.gz")
md5sums=('35b9a8f530b51cda15c660b3a73f377e')

build() {
  cd ${srcdir}
  gendesk -n

  # Compile Warsow.
  cd $srcdir/${pkgname}_${pkgver}_sdk/source/
  make -j1 # Error finding a library if -j is higher.
}

generateAndInstall() {
  echo "#!/bin/bash"             > $pkgdir/usr/bin/$1
  echo "cd /usr/share/$pkgname" >> $pkgdir/usr/bin/$1
  echo "./$1 \$*"               >> $pkgdir/usr/bin/$1
  echo "exit \$?"               >> $pkgdir/usr/bin/$1
  chmod +x $pkgdir/usr/bin/$1
}

package() {
  cd $srcdir/${pkgname}_${pkgver}_sdk/source/

  # Manual installation.
  install -d $pkgdir/usr/share/${pkgname}/
  cp -r $srcdir/${pkgname}_${pkgver}_sdk/source/release/* \
      $pkgdir/usr/share/${pkgname}

  # Execution scripts:
  install -d $pkgdir/usr/bin
  for script in ${pkgname} wsw_server wswtv_server
  do
    generateAndInstall ${script}
  done

  # Desktop integration:
  install -D -m 0644 $srcdir/${pkgname}.desktop $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 $srcdir/${pkgname}_${pkgver}_sdk/source/win32/${pkgname}.ico $pkgdir/usr/share/pixmaps/${pkgname}.png
}
