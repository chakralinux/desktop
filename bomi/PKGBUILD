pkgname=bomi
pkgver=0.9.11.r25.g90f18ff5
pkgrel=2
pkgdesc="Powerful and easy-to-use multimedia player"
arch=('x86_64')
url="http://bomi-player.github.io"
license=('GPL2')
screenshot='http://i.imgur.com/j76hzyd.png'
install=$pkgname.install
depends=('qt5-base' 'qt5-declarative' 'qt5-x11extras' 'qt5-quickcontrols' 'qt5-svg'
         'libdvdread' 'libdvdnav' 'libcdio-paranoia' 'libcdio' 'smbclient'
         'alsa-lib' 'libpulse' 'jack' 'libchardet' 'libbluray'
         'libva' 'libgl' 'fribidi' 'libass' 'ffmpeg')
makedepends=('mesa' 'gcc' 'pkg-config' 'python3' 'qt5-tools')
optdepends=('libaacs: AACS decryption for Blu-ray support'
            'libbdplus: BD+ decryption for Blu-ray support'
            'youtube-dl: streaming website support including YouTube'
            'yle-dl:  rtmpdump frontend for Yle servers'
            'mesa-vdpau: hardware acceleration for nvidia')
provides=('cmplayer')
replace=('cmplayer')
conflicts=('cmplayer' 'bomi-git')
source=(git+https://github.com/xylosper/${pkgname}.git
            'ffmpeg3.patch'
            'qt5.6.patch')
md5sums=('SKIP'
         '2c005f32510aa504d1eb65f8c83323b3'
         '44b2f48f40bffe6aa8070642031e5d35')

pkgver() {
    cd "$srcdir/$pkgname"
    echo $(git describe --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g' | cut -c2-)
}

prepare() {
 cd "$srcdir/$pkgname"
 
 # https://github.com/d-s-x/bomi/commit/944c3e196229f144e97f285f69355ddfc3f7b579
 patch -Np1 -i "$srcdir/ffmpeg3.patch" 
 # https://github.com/d-s-x/bomi/commit/39ee4c372ffb98a4485b61a11726d39060bb2994
 patch -Np1 -i "$srcdir/qt5.6.patch"
}

build() {
  cd "$srcdir/$pkgname"
    ./configure --prefix=/usr \
        --defaultskin=Breeze
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DEST_DIR=$pkgdir install
}
