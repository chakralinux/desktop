pkgname=bomi
pkgver=0.9.11
pkgrel=2
pkgdesc="Powerful and easy-to-use multimedia player"
arch=('x86_64')
url="http://bomi-player.github.io"
license=('GPL2')
screenshot='http://i.imgur.com/j76hzyd.png'
install=$pkgname.install
depends=('qt5-base' 'qt5-declarative' 'qt5-x11extras' 'qt5-quickcontrols' 'qt5-svg'
         'libdvdread' 'libdvdnav' 'libcdio-paranoia' 'libcdio' 'smbclient'
         'alsa-lib' 'libpulse' 'jack' 'libchardet' 'libbluray'
         'libva' 'libgl' 'fribidi' 'libass' 'ffmpeg')
makedepends=('mesa' 'gcc' 'pkg-config' 'python3' 'qt5-tools')
optdepends=('libaacs: AACS decryption for Blu-ray support'
            'libbdplus: BD+ decryption for Blu-ray support'
            'youtube-dl: streaming website support including YouTube'
            'yle-dl:  rtmpdump frontend for Yle servers'
            'mesa-vdpau: hardware acceleration for nvidia')
provides=('cmplayer')
replace=('cmplayer')
conflicts=('cmplayer' 'bomi-git')
source=(https://github.com/xylosper/bomi/archive/v$pkgver.tar.gz
            'ffmpeg2.8.patch')
md5sums=('543c592f588c68d6f0c3cf254c288f58'
         '7bb39aea99bb5c0f6607cda44edc11ae')


prepare() {
 cd "$srcdir/$pkgname-$pkgver"
 
 patch -Np0 -i "$srcdir/ffmpeg2.8.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DEST_DIR=$pkgdir install
}
