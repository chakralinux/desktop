# Maintainer Ram-Z <samir.benmendil[at]gmail[dot]com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=js
pkgver=24.2.0
pkgrel=1
pkgdesc="JavaScript interpreter and libraries"
arch=('x86_64')
url="https://developer.mozilla.org/En/SpiderMonkey/24"
license=('GPL2')
depends=('nspr' 'gcc-libs' 'readline' 'zlib')
makedepends=('python2' 'zip' 'libffi')
source=(http://ftp.mozilla.org/pub/mozilla.org/js/mozjs-${pkgver}.tar.bz2)
sha1sums=('ce779081cc11bd0c871c6f303fc4a0091cf4fe66')

prepare() {
  cd mozjs-$pkgver/js/src
  rm -rf editline
  rm -rf ctypes/libffi
}

build() {
  unset CPPFLAGS
  cd mozjs-$pkgver/js/src

  ./configure \
    --prefix=/usr \
    --with-system-nspr \
    --enable-system-ffi \
    --enable-readline \
    --enable-threadsafe

  make
}

check() {
  cd mozjs-$pkgver/js/src
  make check
}

package() {
  cd mozjs-$pkgver/js/src
  make DESTDIR="$pkgdir" install
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -x {} +
}

