# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdb
pkgver=3.1.0
pkgrel=1
pkgdesc="A database connectivity and creation framework for various database vendors"
arch=(x86_64)
url="http://www.kexi-project.org/"
license=(GPL2)
depends=(kcoreaddons)
makedepends=(extra-cmake-modules python2 libmariadbclient postgresql qt5-tools doxygen)
optdepends=('mariadb: MySQL plugin' 'postgresql: PostgreSQL plugin')
source=("https://download.kde.org/stable/$pkgname/src/$pkgname-$pkgver.tar.xz")
sha1sums=('5dc8275617998cb2c68526883a4670b0de518c55')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
