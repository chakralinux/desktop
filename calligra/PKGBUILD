# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa (manutortosa) <manuel.tortosa@chakra-project.org>
#               Andrea Scarpino <andrea@archlinux.org>
#               Ronald van Haren <ronald.archlinux.org>

pkgbase=calligra
pkgname=('calligra-braindump'
         'calligra-devtools'
         'calligra-extras'
         'calligra-flow'
         'calligra-handbook'
         'calligra-karbon'
         'calligra-kexi'
         'calligra-krita'
         'calligra-libs'
         'calligra-plan'
         'calligra-sheets'
         'calligra-stage'
         'calligra-words')
pkgver=2.8.7
pkgrel=2
arch=('x86_64')
url='http://www.calligra.org'
screenshot=("http://upload.wikimedia.org/wikipedia/commons/thumb/3/3e/Calligra_Stage_2.4.png/800px-Calligra_Stage_2.4.png")
license=('FDL1.2'
         'GPL2'
         'LGPL')
makedepends=('automoc4'
             'boost'
             'cmake'
             'create-resources'
             'docbook-xsl'
             'eigen2'
             'fftw'
             'freetds'
             'glew'
             'gsl'
             'icu'
             'kdeedu-marble'
             'kdegraphics-okular'
             'kdelibs'
             'kdepimlibs'
             'lcms2'
             'libdmtx'
             'libgsf'
             'libkdcraw'
             'libmariadbclient'
             'libodfgen'
             'libpqxx'
             'libspnav'
             'libvisio'
             'libwpd'
             'libwpg'
             'libwps'
             'opencolorio'
             'openjpeg'
             'poppler-qt'
             'pstoedit'
             'qrencode'
             'vc'
             'xbase'
             'freetype2')
options=("debug")
groups=('calligra')
source=("http://download.kde.org/stable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.xz")
sha512sums=('e09fcbb6708d73db3b31027c4393a138fbf5c47d1b777dd4a3b94f6e668d67ac963cfadb320350c63f1b2d1173edc67dc2100704899176c196865fb4d2ce7442')

prepare() {
    mkdir -p "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake "../${pkgbase}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DBUILD_active=OFF
    make
}

package_calligra-braindump() {
    pkgdesc="Notetaking and mindmapping application"
    depends=('calligra-libs'
             'kde-runtime')
    categories=('office')
    install=calligra-braindump.install

    cd build/braindump
    make DESTDIR="${pkgdir}" install
}

package_calligra-devtools() {
    pkgdesc="Developer utilities for Calligra"
    depends=('calligra-libs')

    cd build/devtools
    make DESTDIR="${pkgdir}" install
}

package_calligra-extras() {
    pkgdesc="User utilities for Calligra"
    depends=('calligra-libs')
    conflicts=('calligra-templates')
    replaces=('calligra-templates')
    install=calligra-extras.install

    cd build/extras
    make DESTDIR="${pkgdir}" install
}

package_calligra-flow() {
    pkgdesc="Flowchart drawing application"
    depends=('calligra-words'
             'kde-runtime')
    optdepends=('calligra-karbon: to export the charts in various formats')
    install=calligra-flow.install
    categories=('office')

    cd build/flow
    make DESTDIR="${pkgdir}" install
}

package_calligra-handbook() {
    pkgdesc="Documentation for Calligra"
    options=('docs')
    categories=('documentation')

    cd build/doc/calligra
    make DESTDIR="${pkgdir}" install
}

package_calligra-karbon() {
    pkgdesc="Vector graphics editor"
    depends=('calligra-libs'
             'hicolor-icon-theme'
             'pstoedit')
    optdepends=('calligra-extras: extra Calligra utilities')
    provides=('karbon')
    categories=('graphics')
    install=calligra-karbon.install

    cd build/karbon
    make DESTDIR="${pkgdir}" install
}

package_calligra-kexi() {
    pkgdesc="Visual database management frontend"
    depends=('calligra-libs'
             'fftw'
             'glew'
             'icu'
             'sqlite3')
    optdepends=('calligra-sheets'
                'freetds'
                'kdeedu-marble'
                'libmariadbclient'
                'libpqxx'
                'xbase')
    provides=('kexi')
    categories=('office')
    install=calligra-kexi.install

    cd build/kexi
    make DESTDIR="${pkgdir}" install
    cd ../doc/kexi
    make DESTDIR="${pkgdir}" install
}

package_calligra-krita() {
    pkgdesc="Digital painting application"
    depends=('calligra-libs'
             'fftw'
             'glew'
             'hicolor-icon-theme'
             'kde-runtime'
             'lcms2'
             'libkdcraw'
             'opencolorio'
             'openexr'
             'poppler-qt'
             'qimageblitz'
             'shared-mime-info'
             'exiv2>=0.24'
             'vc')
    provides=('krita')
    categories=('graphics')
    install=calligra-krita.install

    cd build/krita
    make DESTDIR="${pkgdir}" install
}

package_calligra-libs() {
    pkgdesc="Libraries for Calligra"
    depends=('create-resources'
             'icu'
             'kactivities'
             'kdeedu-marble'
             'kdepimlibs'
             'lcms2'
             'libspnav'
             'libvisio'
             'libodfgen')
    optdepends=('libetonyek: Apple Keynote document importer'
                'libgsf: Microsoft Word document importer'
                'libkdcraw: support for raw images'
                'libvisio: Microsoft Visio document importer'
                'libwpg: Corel WordPerfect Graphics image importer'
                'libwps: Microsoft Works document importer'
                'pstoedit: Karbon EPS import filter')
    conflicts=('calligra-3rdparty'
               'calligra-data'
               'calligra-filters'
               'calligra-interfaces'
               'calligra-pics'
               'calligra-plugins'
               'calligra-servicetypes')
    replaces=('calligra-3rdparty'
              'calligra-data'
              'calligra-filters'
              'calligra-interfaces'
              'calligra-pics'
              'calligra-plugins'
              'calligra-servicetypes')
    install=calligra-libs.install

    for d in 3rdparty data filters interfaces libs pics plugins servicetypes; do
        cd "${srcdir}"/build/${d}
        make DESTDIR="${pkgdir}" install
    done
}

package_calligra-plan() {
    pkgdesc="Project management application"
    depends=('calligra-words'
             'hicolor-icon-theme'
             'calligra-libs'
             'kdepimlibs')
    categories=('office')
    install=calligra-plan.install

    cd build/plan
    make DESTDIR="${pkgdir}" install
}

package_calligra-sheets() {
    pkgdesc="Spreadsheet application"
    depends=('calligra-libs'
             'gsl'
             'hicolor-icon-theme'
             'kde-runtime')
    categories=('office')
    install=calligra-sheets.install

    cd build/sheets
    make DESTDIR="${pkgdir}" install
}

package_calligra-stage() {
    pkgdesc="Presentation application"
    depends=('calligra-libs'
             'hicolor-icon-theme')
    categories=('office')
    install=calligra-stage.install

    cd build/stage
    make DESTDIR="${pkgdir}" install
}

package_calligra-words() {
    pkgdesc="Word processor"
    depends=('calligra-libs'
             'hicolor-icon-theme')
    categories=('office')
    install=calligra-words.install

    cd build/words
    make DESTDIR="${pkgdir}" install
}
