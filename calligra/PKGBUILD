# Contribution from Arch: https://git.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/calligra

pkgname=calligra
pkgdesc="A set of applications for productivity and creative usage"
pkgver=3.1.0
pkgrel=2
arch=('x86_64')
url='http://www.calligra.org'
license=('FDL1.2' 'GPL2' 'LGPL')
depends=('kcmutils' 'kdelibs4support' 'knotifyconfig' 'kross' 'ktexteditor' 'threadweaver' 'kactivities' 'kdiagram'
         'kreport' 'qt5-declarative' 'libspnav' 'gsl' 'openexr' 'kcontacts' 'qca-qt5' 'poppler-qt5' 'libodfgen'
         'qt5-webkit' 'cauchy')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3' 'kdesignerplugin' 'khtml' 'boost' 'openjpeg'
             'libwpg' 'okular' 'eigen3' 'marble' 'pstoedit' 'libvisio' 'libetonyek' 'libodfgen' 'libwps'
             'vc' 'akonadi-contacts' 'libgit2')
optdepends=('libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'libvisio: Microsoft Visio import filter'
            'libetonyek: Apple Keynote import filter'
            'pstoedit: EPS to SVG filter'
            'poppler: PDF to SVG filter'
            'khtml: HTML to ODS filter'
            'openjpeg: PDF to SVG filer'
            'libgit2: Calligra Gemini git plugin'
            'kirigami: for Calligra Gemini')
options=("debug")

# list of replaced packages
conflicts=('calligra-braindump'
         'calligra-devtools'
         'calligra-extras'
         'calligra-filters'
         'calligra-flow'
         'calligra-gemini'
         'calligra-handbook'
         'calligra-karbon'
         'karbon'
         'calligra-libs'
         'calligra-plugins'
         'calligra-sheets'
         'calligra-stage'
         'calligra-words'
         'calligra-l10n')
replaces=('calligra-braindump'
         'calligra-devtools'
         'calligra-extras'
         'calligra-filters'
         'calligra-flow'
         'calligra-gemini'
         'calligra-handbook'
         'calligra-karbon'
         'karbon'
         'calligra-libs'
         'calligra-plugins'
         'calligra-sheets'
         'calligra-stage'
         'calligra-words'
         'calligra-l10n')
for _lang in bs ca ca@valencia cs da de el en_GB es et fi fr gl hu it ja kk nb nl \
             pl pt pt_BR ru sk sv uk zh_CN zh_TW; do
  conflicts+=(calligra-l10n-$_lang)
  replaces+=(calligra-l10n-$_lang)
done

source=("http://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        calligra-qt5.11.patch)
sha256sums=('6818cd6e64136321be217eb57cc7d6ac7c7035191fdb1ee336ebe60bc114e870'
            'SKIP')
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD
              42A60D06F449E895F40F09C01638B8A616108B8A) # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  # Fix build with Qt 5.11
  patch -p1 -i ../calligra-qt5.11.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
