# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa (manutortosa) <manuel.tortosa@chakra-project.org>

pkgbase=calligra
pkgname=('calligra-3rdparty'
         'calligra-braindump'
         'calligra-extras'
         'calligra-filters'
         'calligra-flow'
         'calligra-handbook'
         'calligra-interfaces'
         'calligra-karbon'
         'calligra-kexi'
         'calligra-krita'
         'calligra-libs'
         'calligra-pics'
         'calligra-plan'
         'calligra-plugins'
         'calligra-servicetypes'
         'calligra-sheets'
         'calligra-stage'
         'calligra-words')
pkgver=2.7.90
pkgrel=1
arch=('x86_64')
url='http://www.calligra-suite.org'
screenshot=("http://upload.wikimedia.org/wikipedia/commons/thumb/3/3e/Calligra_Stage_2.4.png/800px-Calligra_Stage_2.4.png")
license=('FDL1.2'
         'GPL2'
         'LGPL')
makedepends=('automoc4'
             'boost'
             'cmake'
             'create-resources'
             'docbook-xsl'
             'eigen2'
             'fftw'
             'freetds'
             'glew'
             'gsl'
             'icu'
             'kdeedu-marble'
             'kdegraphics-okular'
             'kdelibs'
             'kdepimlibs'
             'lcms2'
             'libdmtx'
             'libgsf'
             'libkdcraw'
             'libmariadbclient'
             'libpqxx'
             'libqtgtl'
             'libspnav'
             'libvisio'
             'libwpd'
             'libwpg'
             'libwps'
             'opencolorio'
             'opengtl'
             'openjpeg'
             'poppler-qt'
             'pstoedit'
             'qrencode'
             'vc'
             'xbase')
options=("debug")
groups=('calligra')
source=("http://download.kde.org/stable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.xz")
sha512sums=('277a24f862b43de415f011890943e29b7759220b80274607de13afb2738f5dd8047e9ca27164883311200f05be6a3c17a3896e9bd9951abd6e6d526a89a2811c')

prepare() {
    mkdir -p "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake "../${pkgbase}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_active=OFF
    make
}

package_calligra-3rdparty() {
    pkgdesc="Third-party plugins for the Calligra office suite"
    depends=('kdepimlibs')

    cd build/3rdparty
    make DESTDIR="${pkgdir}" install
}

package_calligra-filters() {
    pkgdesc="Filters for the Calligra office suite"
    optdepends=('libgsf: Microsoft Word document importer'
                'libwps: WordPerfect document importer'
                'libwpg: Corel WordPerfect Graphics image importer'
                'pstoedit: Karbon EPS import filter'
                'libkdcraw: support for raw images')
    install=calligra-filters.install

    cd build/filters
    make DESTDIR="${pkgdir}" install
}

package_calligra-interfaces() {
    pkgdesc="Interfaces for the Calligra office suite"
    depends=('calligra-libs'
             'kactivities')

    cd build/interfaces
    make DESTDIR="${pkgdir}" install
}

package_calligra-libs() {
    pkgdesc="Libraries for the Calligra office suite"
    depends=('kdepimlibs'
             'lcms2'
             'calligra-3rdparty'
             'calligra-servicetypes' 
             'kdeedu-marble'
             'create-resources'
             'libspnav'
             'libvisio'
             'icu')

    cd build/libs
    make DESTDIR="${pkgdir}" install
}

package_calligra-plugins() {
    pkgdesc="Plugins for the Calligra office suite"
    depends=('calligra-libs')
    categories=('office')

    cd build/plugins
    make DESTDIR="${pkgdir}" install
}

package_calligra-pics() {
    pkgdesc="Icons for the Calligra office suite"
    depends=('calligra-libs')
    categories=('office')
    install=calligra.install

    cd build/pics
    make DESTDIR="${pkgdir}" install
}

package_calligra-servicetypes() {
    pkgdesc="Servicetypes for the Calligra office suite"
    depends=('kactivities')
    install=calligra.install

    cd build/servicetypes
    make DESTDIR="${pkgdir}" install
}

package_calligra-extras() {
    pkgdesc="Extras for the Calligra office suite"
    depends=('calligra-libs')
    install=calligra-extras.install

    cd build/extras
    make DESTDIR="${pkgdir}" install
}

package_calligra-karbon() {
    pkgdesc="Vector graphics editor"
    depends=('calligra-libs'
             'hicolor-icon-theme')
    provides=('karbon')
    categories=('graphics')
    install=calligra-karbon.install

    cd build/karbon
    make DESTDIR="${pkgdir}" install
}

package_calligra-kexi() {
    pkgdesc="Visual database management frontend"
    depends=('calligra-libs'
             'calligra-sheets'
             'sqlite3'
             'xbase'
             'icu')
    optdepends=('libmariadbclient'
                'postgresql')
    provides=('kexi')
    categories=('office')
    install=calligra-kexi.install

    cd build/kexi
    make DESTDIR="${pkgdir}" install
}

package_calligra-kounavail() {
    pkgdesc="Unavailable Calligra Document"
    depends=('calligra-libs')
    categories=('office')

    cd build/kounavail
    make DESTDIR="${pkgdir}" install
}

package_calligra-plan() {
    pkgdesc="Project management application"
    depends=('calligra-words'
             'hicolor-icon-theme'
             'calligra-libs'
             'kdepimlibs'
             'calligra-plugins')
    categories=('office')
    install=calligra-plan.install

    cd build/plan
    make DESTDIR="${pkgdir}" install
}

package_calligra-stage() {
    pkgdesc="Presentation application"
    depends=('calligra-libs'
             'hicolor-icon-theme')
    optdepends=('calligra-filters: import/export filters')
    categories=('office')
    install=calligra-stage.install

    cd build/stage
    make DESTDIR="${pkgdir}" install
}

package_calligra-krita() {
    pkgdesc="Digital painting application"
    depends=('calligra-libs'
             'calligra-plugins'
             'glew'
             'qimageblitz'
             'poppler-qt'
             'openexr'
             'opengtl'
             'hicolor-icon-theme'
             'shared-mime-info'
             'lcms2'
             'libqtgtl')
    optdepends=('calligra-filters: import/export filters'
                'vc: Required by the Krita for vectorization'
                'opencolorio: Required by the Krita LUT docker')
    provides=('krita')
    categories=('graphics')
    install=calligra-krita.install

    cd build/krita
    make DESTDIR="${pkgdir}" install
}

package_calligra-sheets() {
    pkgdesc="Spreadsheet application"
    depends=('calligra-libs'
             'calligra-plugins'
             'gsl'
             'hicolor-icon-theme')
    optdepends=('calligra-filters: import/export filters')
    categories=('office')
    install=calligra-sheets.install

    cd build/sheets
    make DESTDIR="${pkgdir}" install
}

package_calligra-words() {
    pkgdesc="Word processor"
    depends=('calligra-libs'
             'calligra-plugins'
             'calligra-pics'
             'hicolor-icon-theme')
    optdepends=('calligra-filters: import/export filters')
    categories=('office')
    install=calligra-words.install

    cd build/words
    make DESTDIR="${pkgdir}" install
}

package_calligra-handbook() {
    pkgdesc="Documentation for Calligra"
    categories=('documentation')

    cd build/doc/calligra
    make DESTDIR="${pkgdir}" install
}

package_calligra-thesaurus-doc() {
    pkgdesc="Documentation for Thesaurus"
    categories=('documentation')

    cd build/doc/thesaurus
    make DESTDIR="${pkgdir}" install
}

package_calligra-braindump() {
    pkgdesc="Notetaking and mindmapping application"
    depends=('calligra-libs')
    categories=('office')
    install=calligra-braindump.install

    cd build/braindump
    make DESTDIR="${pkgdir}" install
}

package_calligra-flow() {
    pkgdesc="Flowchart drawing application"
    depends=('calligra-words'
             'kde-runtime')
    install=calligra-flow.install
    categories=('office')

    cd build/flow
    make DESTDIR="${pkgdir}" install
}
