# Arch contribution https://www.archlinux.org/packages/community/x86_64/belle-sip/

pkgname=belle-sip
pkgver=1.4.1
pkgrel=1
pkgdesc="A Voice-over-IP phone"
arch=('x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('libantlr3c' 'gcc-libs' 'mbedtls')
makedepends=('java-runtime')
options=('!emptydirs')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/belle-sip/belle-sip-$pkgver.tar.gz"
	"antlr.jar::https://github.com/antlr/website-antlr3/blob/gh-pages/download/antlr-3.4-complete.jar?raw=true")
noextract=('antlr.jar')
sha512sums=('29b2b0d6997e8764976cdaa5d83e8e943d37f1a35cecbbd621d86c568e6b507c4dec5b38bf32130afabd2c88f6d0319f1a33a2a796189f466391e2fa09d7e101'
         '04be4dfba3a21f3ab9d9e439a64958bd8e844a9f151b798383bd9e0dd6ebc416783ae7cb1d1dbb27fb7288ab9756b13b8338cdb8ceb41a10949c852ad45ab1f2')

prepare() {
  cd $pkgname-$pkgver
  sed -i \
    -e "s#antlr_java_prefixes=.*#antlr_java_prefixes=$srcdir#" \
    -e "s|-Werror||g" \
    configure{,.ac}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr  --libexecdir=/usr/lib/$pkgname \
    --disable-tests --disable-static --enable-tls
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}