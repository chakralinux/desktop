# Arch contribution https://www.archlinux.org/packages/community/x86_64/belle-sip/

pkgname=belle-sip
pkgver=1.4.2
pkgrel=2
pkgdesc="A Voice-over-IP phone"
arch=('x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('libantlr3c' 'gcc-libs' 'mbedtls')
makedepends=('java-runtime')
options=('!emptydirs')
source=("https://github.com/BelledonneCommunications/belle-sip/archive/${pkgver}.tar.gz"
	"antlr.jar::https://github.com/antlr/website-antlr3/blob/gh-pages/download/antlr-3.4-complete.jar?raw=true")
noextract=('antlr.jar')
sha512sums=('39590df959d22dc2c8f122176653e4254c4994f8ebfcd3f9066fd207d727b2d84708e4a4aa26ead109061be4d3084fafc3c8a7c3c5b7e4a3eed9bb0679e17c6e'
         '04be4dfba3a21f3ab9d9e439a64958bd8e844a9f151b798383bd9e0dd6ebc416783ae7cb1d1dbb27fb7288ab9756b13b8338cdb8ceb41a10949c852ad45ab1f2')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
  sed -i \
    -e "s#antlr_java_prefixes=.*#antlr_java_prefixes=$srcdir#" \
    -e "s|-Werror||g" \
    configure{,.ac}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr  --libexecdir=/usr/lib/$pkgname \
    --disable-tests --disable-static --enable-tls
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}