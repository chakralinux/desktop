# Arch contribution https://www.archlinux.org/packages/community/x86_64/belle-sip/

pkgname=belle-sip
pkgver=1.6.1
pkgrel=1
pkgdesc="A Voice-over-IP phone"
arch=('x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('libantlr3c' 'gcc-libs' 'mbedtls' 'bctoolbox')
makedepends=('java-runtime')
options=('!emptydirs')
source=("https://github.com/BelledonneCommunications/belle-sip/archive/${pkgver}.tar.gz"
	"antlr.jar::https://github.com/antlr/website-antlr3/blob/gh-pages/download/antlr-3.4-complete.jar?raw=true")
noextract=('antlr.jar')
sha512sums=('5227dd12332914bb28ab2b48126f34613c56b161836b61cafd46ca22abf15e760d7818d81f1973aed5683b0122e914915a6b8dba7cd0425029c6b3de2211755b'
            '04be4dfba3a21f3ab9d9e439a64958bd8e844a9f151b798383bd9e0dd6ebc416783ae7cb1d1dbb27fb7288ab9756b13b8338cdb8ceb41a10949c852ad45ab1f2')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
  sed -i \
    -e "s#antlr_java_prefixes=.*#antlr_java_prefixes=$srcdir#" \
    -e "s|-Werror||g" \
    configure{,.ac}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr  --libexecdir=/usr/lib/$pkgname \
    --disable-tests --disable-static --enable-tls
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
