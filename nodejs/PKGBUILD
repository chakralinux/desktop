pkgname=nodejs
pkgver=0.12.6
_npmver=2.12.1
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('openssl' 'zlib')
provides=('nodejs-node-gyp' 'npm')
makedepends=('python2' 'git' 'procps-ng')
optdepends=('python2: for node-gyp')
checkdepends=('curl')
options=('!emptydirs')
source=("http://nodejs.org/dist/v$pkgver/node-v$pkgver.tar.gz"
        git+https://github.com/npm/npm.git#tag=v$_npmver)
sha512sums=('a7f1c88d8df0f65d986e283b859466a34459f8ebdd3232f4906f5ebd53c1a019a553eadcc50bcb5fe8252441c567692b9ada36e8b158ed818971cd927bf1b29f'
            'SKIP')

prepare() {
  cd node-v$pkgver

  # We need newer version of npm to fix bugs
 # rm -r deps/npm
 # ln -s "$srcdir/npm" deps
 # rm -r "$srcdir/npm/.git"

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd node-v$pkgver

  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --shared-openssl \
    --shared-zlib

  make

  cd ../npm
  ../node-v$pkgver/node cli.js install ronn marked
  PATH="$srcdir/node-v$pkgver:$PATH" make
  ../node-v$pkgver/node cli.js uninstall ronn marked marked-man
}

check() {
  cd node-v$pkgver
  make test || warning "Tests failed"
}

package() {
  cd node-v$pkgver

  make DESTDIR="$pkgdir" install

  # install docs as per user request
  install -d "$pkgdir"/usr/share/doc/nodejs
  cp -r doc/api/{*.html,assets} \
    "$pkgdir"/usr/share/doc/nodejs

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE

  install -d "$pkgdir"/usr/share/man/{man1,man3,man5,man7}
  for _dir in man1 man3 man5 man7; do
    cd "$pkgdir"/usr/lib/node_modules/npm/man/$_dir
    for _file in *; do
      ln -s /usr/lib/node_modules/npm/man/$_dir/$_file "$pkgdir"/usr/share/man/$_dir/
    done
  done

  ln -s /usr/lib/node_modules/npm/node_modules/node-gyp/bin/node-gyp.js "$pkgdir"/usr/bin/node-gyp
}
