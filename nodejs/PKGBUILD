# Maintainer: Daniele Cocca <jmc@chakra-project.org>
# Original CCR maintainer: Inkane <neoinkaneglade@aol.com>

pkgname=nodejs
pkgver=0.10.24
pkgrel=1
pkgdesc="Node.js is a platform built on Chrome's JavaScript runtime for easily building fast, scalable network applications."
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('python2' 'openssl')
checkdepends=('curl') # curl used for check()
options=('!emptydirs')
source=("http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz")
md5sums=('186726490c9a715cb7ee2ed37ccb5eb3')

build() {
  cd "node-v${pkgver}"
  
  export PYTHON=python2
  ./configure --prefix=/usr

  make
}

check() {
  cd "node-v${pkgver}"
  # Added true because the test are successfully done but exit with a fail
  make test || true 
}

package() {
  cd "node-v${pkgver}"

  make install DESTDIR="${pkgdir}"

  # install docs as per user request
  install -d "${pkgdir}/usr/share/doc/nodejs"
  cp -r doc/api/*.html \
        "${pkgdir}/usr/share/doc/nodejs"

  install -D -m644 LICENSE \
        "${pkgdir}/usr/share/licenses/nodejs/LICENSE"
}

