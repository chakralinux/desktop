#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=akonadi
pkgver=1.5.2
pkgrel=1
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('i686' 'x86_64')
url='http://www.akonadi-project.org'
license=('LGPL')
depends=('shared-mime-info' 'boost' 'mysql>=5.5.11' 'soprano>=2.6.0')
makedepends=('pkgconfig' 'cmake' 'automoc4')
install=$pkgname.install
source=("http://download.akonadi-project.org/${pkgname}-${pkgver}.tar.bz2"
        'qt-plugins-path.patch')
md5sums=('1c69a74474389e35709171a329c77bed'
        '9e8ad893d8e8d7af2c4f89429e32e367')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/qt-plugins-path.patch
  cd ${srcdir}
  mkdir -p build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
