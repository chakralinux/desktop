# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-benchmark
pkgname=('python3-pytest-benchmark' 'python2-pytest-benchmark')
pkgver=3.1.1
pkgrel=1
pkgdesc='A py.test fixture for benchmarking code'
arch=('any')
license=('BSD')
url='https://github.com/ionelmc/pytest-benchmark'
makedepends=('python3-pytest' 'python2-pytest' 'python2-statistics' 'python2-pathlib'
             'python3-py-cpuinfo' 'python2-py-cpuinfo')
#TODO I have stopped importing all the test infrastructure here, feel free to continue
#checkdepends=('python3-pytest-cov' 'python2-pytest-cov' 'python3-pygal' 'python2-pygal'
#              'python3-freezegun' 'python2-freezegun' 'mercurial' 'python3-aspectlib'
#              'python2-aspectlib' 'python3-pytest-xdist' 'python2-pytest-xdist'
#              'python3-pytest-runner' 'python2-pytest-runner' 'python3-elasticsearch'
#              'python2-elasticsearch' 'git')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/ionelmc/pytest-benchmark/archive/v$pkgver.tar.gz")
sha512sums=('4a037fc3133b93a5a5ae914c92bc03468812aaa883cfbfeca35877003ebad781ab0dd4fc941eb6d9c7c44d7bf5380585ce04392c77278c54b7b4ebe5911a09f8')

prepare() {
  cp -a pytest-benchmark-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-benchmark-$pkgver
  python3 setup.py build

  cd "$srcdir"/pytest-benchmark-$pkgver-py2
  python2 setup.py build
}

#check() {
#  # Hack entry points by installing it
#  # Test should be related to pytest 2.9
#
#  cd "$srcdir"/pytest-benchmark-$pkgver
#  python3 setup.py install --root="$PWD/tmp_install" --optimize=1
#  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" PATH="$PWD/tmp_install/usr/bin:$PATH" python3 setup.py pytest --addopts=tests || warning "Tests failed"
#
#  cd "$srcdir"/pytest-benchmark-$pkgver-py2
#  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
#  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" PATH="$PWD/tmp_install/usr/bin:$PATH" python2 setup.py pytest --addopts=tests || warning "Tests failed"
#}

package_python3-pytest-benchmark() {
  depends=('python3-pytest' 'python3-py-cpuinfo')

  cd "$srcdir"/pytest-benchmark-$pkgver
  python3 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-benchmark() {
  depends=('python2-pytest' 'python2-py-cpuinfo' 'python2-statistics' 'python2-pathlib')

  cd "$srcdir"/pytest-benchmark-$pkgver-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/pytest-benchmark{,2}
  mv "$pkgdir"/usr/bin/py.test-benchmark{,2}
}

# vim:set ts=2 sw=2 et:
