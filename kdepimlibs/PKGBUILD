# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: Manuel Tortosa <manutortosa@chakra-project.org>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="kdepimlibs"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=2
pkgdesc="KDE PIM libraries"
url="http://www.kde.org"
license=('GPL'
         'LGPL'
         'FDL')
install=${pkgname}.install
options=('docs'
         '!header'
         'debug'
         'log')
depends=("kdelibs>=${_kdever}"
         'akonadi>=1.9.80'
         'libical'
         'gpgme'
         'prison'
         'libdmtx'
         'qrencode'
         'qjson'
         'nepomuk-core')
makedepends=('pkg-config'
             'cmake>=2.8.9'
             'automoc4'
             'boost'
             'cyrus-sasl'
             'openldap'
             'prison'
             'docbook-xsl')
groups=('kde'
        'kde-uninstall'
        'kde-minimal')
source=($_mirror/${pkgname}-$_kdever.tar.xz)
sha256sums=(`grep ${pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d" " -f1`)

build() {
    cd ${srcdir}
    mkdir -p build
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd ${srcdir}/build
    make DESTDIR=${pkgdir} install
}
