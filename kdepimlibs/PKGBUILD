# Include global configuration
source ../kdeapps.conf

pkgbase=kdepimlibs
pkgname=(akonadi-client akonadi-contact akonadi-mime akonadi-notes akonadi-socialutils kio-pim)
pkgver=${_kdever}
pkgrel=2
pkgdesc="KDE PIM Libraries"
arch=('x86_64')
url='https://projects.kde.org/kdepimlibs'
license=(GPL LGPL)
makedepends=(extra-cmake-modules qt5-tools kcalcore kmbox kcontacts kldap akonadi prison-kf5 boost kdoctools)
options=('docs' 'debug')
source=("$_mirror/${pkgbase}-$_kdever.tar.xz")
sha256sums=(`grep ${pkgbase}-$_kdever.tar.xz  ../checksums.txt | cut -d " " -f1`)

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake_kf5 ../${pkgbase}-${pkgver}
  make
}

package_akonadi-client() {
  depends=(akonadi kdelibs4support)

  cd build/akonadi
  make DESTDIR="$pkgdir" install
}

package_akonadi-contact() {
  depends=(akonadi-client kmime kcontacts kcalcore prison-kf5)

  cd build/akonadi-contact
  make DESTDIR="$pkgdir" install
}

package_akonadi-mime() {
  depends=(akonadi-client kmime)
  install=akonadi-mime.install

  cd build/akonadi-mime
  make DESTDIR="$pkgdir" install
}

package_akonadi-notes() {
  depends=(kmime)

  cd build/akonadi-notes
  make DESTDIR="$pkgdir" install
}

package_akonadi-socialutils() {
  depends=(akonadi-client)
  install=akonadi-socialutils.install

  cd build/akonadi-socialutils
  make DESTDIR="$pkgdir" install
}

package_kio-pim() {
  pkgdesc='PIM kioslaves'
  depends=(kldap kio)

  cd build/kioslave
  make DESTDIR="$pkgdir" install
}