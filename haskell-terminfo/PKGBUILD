#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Frenández (Gallaecio) <adriyetichaves gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_hkgname=terminfo
pkgname=haskell-terminfo
pkgver=0.3.1.3
pkgrel=2.2
pkgdesc="Haskell bindings to the terminfo library."
url="http://hackage.haskell.org/cgi-bin/hackage-scripts/package/terminfo"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc=6.12.3' sh)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/terminfo/$pkgver/terminfo-$pkgver.tar.gz)
install=${pkgname}.install
md5sums=('8c0e052340f628b76f5687fa23379628')

build() {
    cd ${srcdir}/${_hkgname}-$pkgver
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared --prefix=/usr --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
