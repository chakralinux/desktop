# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=plasma5-applets-latte-dock
_pkgname=latte-dock
pkgver=0.8.2
pkgrel=1
pkgdesc='A dock based on Plasma Frameworks'
arch=(x86_64)
url='https://store.kde.org/p/1169519/'
license=(GPL)
depends=(kcrash kglobalaccel knewstuff kwayland plasma-framework hicolor-icon-theme)
makedepends=(extra-cmake-modules python3)
conflicts=(plasma5-applets-nowdock)
replaces=(plasma5-applets-nowdock)
source=("https://download.kde.org/stable/$_pkgname/$_pkgname-$pkgver.tar.xz")
sha256sums=('bdb98e9bad1762ec98b10bd671f713f0001bbd8f4095b752fcaa729d5e7f9ca9')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
