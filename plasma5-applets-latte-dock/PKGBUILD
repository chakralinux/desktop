# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=plasma5-applets-latte-dock
pkgver=0.7.1
pkgrel=1
pkgdesc='A dock based on Plasma Frameworks'
arch=(x86_64)
url='https://github.com/psifidotos/Latte-Dock'
license=(GPL)
depends=(kcrash kglobalaccel kwayland plasma-framework hicolor-icon-theme)
makedepends=(extra-cmake-modules python3)
conflicts=(plasma5-applets-nowdock)
replaces=(plasma5-applets-nowdock)
source=("$pkgname-$pkgver.tar.gz::https://github.com/psifidotos/Latte-Dock/archive/v$pkgver.tar.gz")
sha256sums=('b57a5495370fea6ae61f8045590d958c477efbe454690ea52d992d8ec76b80ea')

prepare() {
  mkdir -p Latte-Dock-$pkgver/build # out-of-tree build fails
}

build() {
  cd Latte-Dock-$pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd Latte-Dock-$pkgver/build

  make DESTDIR="$pkgdir" install
}
