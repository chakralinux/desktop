# Include global configuration

pkgname=ktouch
pkgver=18.12.2
pkgrel=1
pkgdesc="Touch Typing Tutor"
url="http://kde.org/applications/education/ktouch/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kqtquickcharts libxkbfile ktexteditor kcmutils knewstuff hicolor-icon-theme)
makedepends=(extra-cmake-modules mesa kdoctools python3)
conflicts=(kdeedu-ktouch)
replaces=(kdeedu-ktouch)
options=(docs debug)
groups=(kde kde-uninstall kde-applications kdeedu)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('bdcc2b9c1682b3eddaf5d7e2259a747428dbdecb6ec76f8697815ac4cede820d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
