pkgname=flacon
pkgver=2.1.1
pkgrel=1
pkgdesc="Tool to extract individual tracks from one big audio file."
arch=('x86_64')
url="http://flacon.github.io/"
license=('GPL3')
screenshot="http://wiki.flacon.googlecode.com/hg/imgs/flacon-1-small.png"
depends=('uchardet' 'shntool' 'qt5-base')
makedepends=('cmake' 'qt5-tools')
optdepends=('flac: for encoding and decoding FLAC files'
            'mac: for APE support'
            'ttaenc: for TrueAudio support'
            'lame: for MP3 support'
            'mp3gain: for MP3 Replay Gain support'
            'vorbis-tools: for OGG support'
            'vorbisgain: for OGG Replay Gain support'
            'wavpack: for WavPack support')
categories=('multimedia')
source=(https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('9ece812c0bd68828301d745fbdddaf9ff2d9e572cc205e20d2bbcb093de98a1b')

build() {
  cd $pkgname-$pkgver
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
	   -DBUILD_TESTS=Yes \
	   -DUSE_QT5=Yes
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
}
