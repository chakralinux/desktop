 
pkgname=libkcddb4
pkgver=16.08.3
pkgrel=1
pkgdesc="KDE4 CDDB library"
url='https://www.kde.org/applications/multimedia/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kdelibs libmusicbrainz5)
makedepends=(cmake automoc4)
replaces=('libkcddb<16.11')
conflicts=('libkcddb<16.11')
source=("http://download.kde.org/stable/applications/$pkgver/src/libkcddb-$pkgver.tar.xz")
sha1sums=('b77aa60310efd8946c7b0289cf1fb5fc7d5dc5ac')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libkcddb-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
