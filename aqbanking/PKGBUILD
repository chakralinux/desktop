# Arch contribution https://www.archlinux.org/packages/community/x86_64/aqbanking/

pkgname=aqbanking
pkgver=5.7.8
_dnrel=217
pkgrel=1
pkgdesc="A library for online banking and financial applications"
arch=('x86_64')
url="http://www.aquamaniac.de/aqbanking"
license=('GPL')
depends=('gwenhywfar' 'ktoblzcheck' 'libofx')
options=('!makeflags')
replaces=('aqbanking-qt4')
provides=('aqbanking-qt4')
source=("$pkgname-$pkgver.tar.gz::https://www.aquamaniac.de/sites/download/download.php?package=03&release=${_dnrel}&file=02&dummy=$pkgname-$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.asc::https://www.aquamaniac.de/sites/download/download.php?package=03&release=${_dnrel}&file=01&dummy=$pkgname-$pkgver.tar.gz.asc")
validpgpkeys=('88491B1E68624A5AE5068262895C619C3CA35783') #Martin Preuss <martin@libchipcard.de>
sha256sums=('16f86e4cc49a9eaaa8dfe3206607e627873208bce45a70030c3caea9b5afc768'
            'SKIP')

build() {
  cd aqbanking-$pkgver
  export PKG_CONFIG=/usr/bin/pkg-config
  ./configure --prefix=/usr \
	--with-backends="aqhbci aqofxconnect" \
	--enable-gwenhywfar
  make
}

package() {
  cd aqbanking-$pkgver
  make DESTDIR=$pkgdir install
}
