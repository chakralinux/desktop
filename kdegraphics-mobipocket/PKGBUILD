# Maintainer:   Manuel Tortosa <manutortosa@chakra-project@org>
# Contributors: H W Tovetj√§rn (totte) <totte@tott.es>

# Include global configuration
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdegraphics-mobipocket
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc='A collection of plugins to handle mobipocket files'
url='https://projects.kde.org/projects/kde/kdegraphics/kdegraphics-mobipocket'
license=('GPL'
         'LGPL'
         'FDL')
options=('docs'
         '!header'
         'debug'
         'log')
groups=('kde'
        'kdegraphics'
        'kde-uninstall')
provides=('mobipocket')
depends=("kdegraphics-okular>=${_kdever}"
         'strigi')
makedepends=('automoc4')
source=("$_mirror/${pkgname}-$_kdever.tar.xz")
install=${pkgname}.install
sha1sums=(`grep ${pkgname}-$_kdever.tar.xz  ../checksums.txt | cut -d" " -f1`)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
