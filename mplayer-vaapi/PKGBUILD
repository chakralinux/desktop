#
# Apps Packages for Chakra, part of chakra-project.org
#
# contributor (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=mplayer-vaapi
pkgver=34357
pkgrel=1
pkgdesc="A movie player, compiled with vaapi support"
arch=('i686' 'x86_64')
url="http://www.splitted-desktop.com/~gbeauchesne/mplayer-vaapi/"
license=('GPL')
depends=('libxxf86dga' 'libxxf86vm' 'libmad' 'cdparanoia' 'libxinerama' 'sdl'
         'lame' 'libtheora' 'xvidcore' 'libmng' 'libxss' 'libgl' 'smbclient'
         'aalib' 'jack' 'libcaca' 'x264' 'faac' 'faad2' 'lirc-utils' 'ttf-dejavu'
         'libxvmc' 'enca' 'opencore-amr' 'libdca' 'a52dec' 'schroedinger'
         'libvpx' 'libpulse' 'mesa' 'libva')
makedepends=('unzip' 'live-media' 'yasm' 'ladspa')
provides=("mplayer=$pkgver")
conflicts=('mplayer')
categories=('multimedia')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(http://pkgbuild.com/~foutrelis/mplayer-vaapi-$pkgver.tar.xz
        mplayer.desktop
        mplayer.png)
install=mplayer.install
md5sums=('eb239c50b099c807524d4c38f2445c1c'
         '647b9f4ab5284a7fef3f84f992214e77'
         'd00874ccc644b7f43d6ef1c942fcef28')

build() {
  cd "$srcdir/mplayer-vaapi-$pkgver"

  ./configure --prefix=/usr \
      --enable-runtime-cpudetection \
      --disable-gui \
      --disable-arts \
      --disable-liblzo \
      --disable-speex \
      --disable-openal \
      --disable-fribidi \
      --disable-libdv \
      --disable-musepack \
      --disable-esd \
      --disable-mga \
      --enable-xvmc \
      --disable-vdpau \
      --enable-vaapi \
      --language=all \
      --confdir=/etc/mplayer

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make
}

package() {
  cd "$srcdir/mplayer-vaapi-$pkgver"

  make -j1 DESTDIR="$pkgdir" install-mplayer install-mplayer-man
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} "$pkgdir/etc/mplayer/"
  install -dm755 "$pkgdir/usr/share/mplayer/"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "$pkgdir/usr/share/mplayer/subfont.ttf"
  rm -rf "$pkgdir/usr/share/mplayer/font"
  # Desktop file (FS#14770)
  install -Dm644 "$srcdir/mplayer.desktop" "$pkgdir/usr/share/applications/mplayer.desktop"
  install -Dm644 "$srcdir/mplayer.png" "$pkgdir/usr/share/pixmaps/mplayer.png"
}
