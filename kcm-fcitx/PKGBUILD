
pkgname=kcm-fcitx
pkgver=0.5.4
pkgrel=1
pkgdesc="KDE Config Module for Fcitx"
arch=('x86_64')
url="https://github.com/fcitx/kcm-fcitx"
license=('GPL')
depends=('fcitx-qt5' 'kcoreaddons' 'kwidgetsaddons' 'kcmutils' 'kitemviews' 'ki18n' 'kio' 'knewstuff')
makedepends=('extra-cmake-modules' 'make' 'cmake')
source=(http://download.fcitx-im.org/$pkgname/$pkgname-${pkgver}.tar.xz{,.sig})
sha512sums=('349fe50303e027f44b53d954c088e441450aad07dac3a1162a5dcf9c445ca9d04bd3c36475c17dab91ed2df9c3c71031d7582e39b85ad0465a816f7fc24ac0bd'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9')

prepare() {
  mkdir build
}

build() {
  cd build
    
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
