
pkgname=kcm-fcitx
pkgver=0.5.3
pkgrel=1
pkgdesc="KDE Config Module for Fcitx"
arch=('x86_64')
url="https://github.com/fcitx/kcm-fcitx"
license=('GPL')
depends=('fcitx-qt5' 'kcoreaddons' 'kwidgetsaddons' 'kcmutils' 'kitemviews' 'ki18n' 'kio' 'knewstuff')
makedepends=('extra-cmake-modules' 'make' 'cmake')
source=(http://download.fcitx-im.org/$pkgname/$pkgname-${pkgver}.tar.xz{,.sig})
sha512sums=('19d172cbf1d2a13f289b64b67488ddc5ba83a9d26503438045271c85df3775715614951652dc6f8da52a3b9d692d6e2138a760fc51d1e7f0f8bb0bd467265496'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9')

prepare() {
  mkdir build
}

build() {
  cd build
    
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
