#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
# Contributor: nqn1976@gmail.com

pkgname=opensc-opendnie
_pkgname=opensc
pkgver=0.12.2
_code=1332
pkgrel=2
pkgdesc="Access smart cards that support cryptographic operations (with open DNIe driver)"
arch=('i686' 'x86_64')
url="http://forja.cenatic.es/projects/opendnie/"
license=("LGPL")
depends=('openssl' 'ccid' 'pinentry')
provides=('opensc')
conflicts=('opensc')
source=(https://forja.cenatic.es/frs/download.php/file/${_code}/$pkgname-$pkgver.tar.gz)
md5sums=('5e9db0ecaeec8748fcfa8e149b9ff236')

build() {
  cd $srcdir/$_pkgname-$pkgver
  ./bootstrap
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  # workaround broken make install
  install -d $pkgdir/{etc,usr/lib/pkcs11}

  cd $srcdir/$_pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
