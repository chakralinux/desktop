#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=k3b
pkgver=2.0.1
pkgrel=4
depends=('kdebase-runtime' 'kdemultimedia-common' 'libsamplerate' 'libmad'
         'ffmpeg>=0.5' 'taglib>=1.4' 'libmpcdec>=1.2.5' 'libdvdread>=4.1.3'
         'cdrdao' 'cdrkit' 'shared-mime-info' 'libxft' 'musicbrainz')
makedepends=('cmake' 'pkgconfig' 'automoc4' )
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=("i686" "x86_64")
license=('GPL')
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/k3b/${pkgname}-${pkgver}.tar.bz2
        http://chakra-project.org/sources/k3b/fix-crash-with-qt-4.7.0.patch)
url="http://k3b.sourceforge.net/"
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager:    for vcd burning support'
            'transcode:    for advanced mpeg conversion support'
            'emovix:       for bootable multimedia cd/dvd support')
options=(!libtool)
md5sums=('4e5b6665d91d4984cbfa814b797257cb'
         'd4f0b447d8054eaa927f2bc65026cbf5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i ${srcdir}/fix-crash-with-qt-4.7.0.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}

