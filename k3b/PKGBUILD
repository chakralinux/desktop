#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=k3b
pkgver=2.0.2
pkgrel=4
depends=('kde-runtime' 'kdemultimedia-common' 'libsamplerate' 'libmad'
         'ffmpeg>=0.5' 'taglib>=1.4' 'libmpcdec>=1.2.5' 'libdvdread>=4.1.3'
         'cdrdao' 'cdrkit' 'shared-mime-info' 'libxft' 'musicbrainz' 'docbook-xsl')
makedepends=('cmake' 'pkgconfig' 'automoc4' 'dvd+rw-tools')
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=("i686" "x86_64")
license=('GPL')
install=$pkgname.install
url="http://k3b.sourceforge.net/"
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager:    for vcd burning support'
            'transcode:    for advanced mpeg conversion support'
            'emovix:       for bootable multimedia cd/dvd support')
categories=('multimedia')
options=(!libtool)
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2 \
        k3b-2.0.2-ffmpeg.patch)
sha1sums=('8b30a4d07942e82559b01bc07dea6bcf2defd532'
          '48f494363e7f423ee5d32f979d48e3d5731fe2ae')

build() {
  cd "${srcdir}"
  (cd ${pkgname}-${pkgver}; patch -p1 -i "${srcdir}/k3b-2.0.2-ffmpeg.patch")
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}

