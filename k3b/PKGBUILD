# Include global configuration

pkgname=k3b
pkgver=18.07.80
pkgrel=1
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=("x86_64")
url="http://sourceforge.net/projects/k3b/"
license=('GPL')
depends=('libkcddb' 'kcmutils' 'knotifyconfig' 'libmad' 'kfilemetadata' 'knewstuff'
         'libmpcdec' 'libdvdread' 'libburn' 'qt5-webkit' 'hicolor-icon-theme' 'taglib' 'libsamplerate')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager: for vcd burning support'
            'transcode: for advanced mpeg conversion support'
            'emovix: for bootable multimedia cd/dvd support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for cd ripping support')
options=('debug')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0c8b27a0f65b4e531783edfadb96528dd1af5a792f9b817a3ccde94f6556465a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>
          
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DK3B_ENABLE_PERMISSION_HELPER=ON
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
