# Include global configuration
source ../kdeapps.conf

_pkgname=baloo
pkgbase=baloo4
pkgname=('baloo4' 'baloo4-akonadi' 'libbaloo4')
arch=('x86_64')
pkgver=${_oldkdever}
pkgrel=3
pkgdesc="The core baloo system"
url="http://www.kde.org"
license=('GPL'
         'LGPL'
         'FDL')
options=('docs'
         'debug')
makedepends=('cmake' 'automoc4' 'boost' 'kdepimlibs' 'xapian-core' 'kfilemetadata4' 'libakonadi-qt4')
optdepends=("nepomuk-core: To migrate nepomuk metadata to baloo")
source=("http://download.kde.org/stable/${pkgver}/src/baloo-${pkgver}.tar.xz")
sha1sums=('bf90846545c5b5a0b0badab25dc3e839dc0c6574')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
}

build() {
    rm -rf build 
    mkdir -p build && cd build
    cmake ../${_pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix}
    make
}

package_baloo4() {
  pkgdesc='A KDE4 framework for searching and managing metadata'
  depends=('libbaloo4' 'baloo4-akonadi')
  conflicts=('baloo<=4.14.3-1')
  provides=('baloo_file')
  install=${pkgname}.install

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm -r "${pkgdir}"{/usr/bin/akonadi_baloo_indexer,/usr/include,/usr/lib,/usr/share/akonadi,/usr/share/kde4}
}

package_baloo4-akonadi() {
  pkgdesc='Baloo resource for Akonadi'
  depends=('libbaloo4')
  conflicts=('baloo<=4.14.3-1')

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm -r "${pkgdir}"{/etc,/usr/bin/baloo*,/usr/include,/usr/lib,/usr/share/{kde4,autostart,dbus-1,icons,polkit-1}}
}

package_libbaloo4() {
  pkgdesc='Libraries for baloo4'
  depends=('kdepimlibs' 'xapian-core' 'kfilemetadata4' 'baloo_file')
  conflicts=('baloo<=4.14.3-1')
  replaces=('baloo<=4.14.3-1')
  provides=("baloo=${pkgver}-${pkgrel}")

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm -r "${pkgdir}"{/etc,/usr/bin,/usr/share/{akonadi,autostart,dbus-1,icons,polkit-1}}
}
