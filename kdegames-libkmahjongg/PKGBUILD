# Include global configuration
source ../kdeapps.conf

pkgname=kdegames-libkmahjongg
_pkgname=libkmahjongg
pkgver=14.12.3
pkgrel=2
pkgdesc="Library used for loading and rendering of Mahjongg tilesets"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdegames/libmahjongg'
license=('GPL' 'LGPL' 'FDL')
depends=("kdelibs>=${_libsver}" "kdegames-libkdegames=${pkgver}")
makedepends=('pkg-config' 'cmake' 'automoc4')
groups=("kde" "kdegames" "kde-uninstall")
options=('docs' 'debug')
source=("http://download.kde.org/stable/applications/${pkgver}/src/libkmahjongg-${pkgver}.tar.xz")
sha1sums=('511983c1b92e391e24909b04d3ae2817f720e661')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    cmake . \
        -DCMAKE_BUILD_TYPE=${_build_type} \
        -DCMAKE_INSTALL_PREFIX=${_installprefix} \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
