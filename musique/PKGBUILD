pkgname=musique
pkgver=1.5
pkgrel=1
pkgdesc="Just another music player, only better"
arch=('x86_64')
url="http://flavio.tordini.org/musique"
license=('GPL3')
depends=('qt5-base' 'desktop-file-utils' 'hicolor-icon-theme' 'phonon-qt5' 'taglib' 'sqlite3')
makedepends=('qt5-tools')
optdepends=('gstreamer0.10-plugins: for gstreamer backend'
            'phonon-qt5-backend-gstreamer: gstreamer backend'
            'phonon-qt5-backend-vlc: vlc backend')
conflicts=('musique-git')
replaces=('minitunes' 'minitunes-git')
categories=('multimedia')
source=("$pkgname-$pkgver.tar.gz::https://github.com/flaviotordini/musique/archive/$pkgver.tar.gz")
sha256sums=('82600f4e3d58900df1cc11acd77efbf11e8eac94f8c7382d57f953cadcd49d54')

prepare() {
  cd ${pkgname}-$pkgver
  # Disable GTK integration (will not build in Chakra otherwise).
  sed -e "/Fall back to gconf/,+3d" -i src/main.cpp
}

build() {
  cd ${pkgname}-$pkgver
  qmake-qt5 PREFIX=/usr
}

package() {
  cd ${pkgname}-$pkgver
  make INSTALL_ROOT="${pkgdir}" install
  # install icon
  install -Dm644 "data/${pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
}
