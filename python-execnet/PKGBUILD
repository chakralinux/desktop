pkgbase=python-execnet
pkgname=('python3-execnet' 'python2-execnet')
pkgver=1.5.0
pkgrel=1
pkgdesc="Rapid multi-Python deployment"
arch=('any')
license=('MIT')
url="http://codespeak.net/execnet"
makedepends=('python3-setuptools' 'python2-setuptools' 'python3-apipkg' 'python2-apipkg')
checkdepends=('python3-pytest-timeout' 'python2-pytest-timeout' 'procps-ng' 'openssh' 'pypy')
source=("https://pypi.io/packages/source/e/execnet/execnet-$pkgver.tar.gz")
sha512sums=('ca2b571fafdf8f68b3cc7a04ee326e3255828d4cde28ead65d0cef325569c3a6dee09359e525152248038de65326ffc6b75c8362f53aa5c0b3f736eb596cb2d9')

prepare() {
  cp -a execnet-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/execnet-${pkgver}"
  python3 setup.py build

  cd "$srcdir/execnet-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/execnet-${pkgver}"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test testing

  cd "$srcdir/execnet-${pkgver}-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2 testing
}

package_python3-execnet() {
  depends=('python3-apipkg')

  cd execnet-${pkgver}
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-execnet() {
  depends=('python2-apipkg')

  cd execnet-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

