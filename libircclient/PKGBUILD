#
# Games Packages for Chakra, part of chakra-project.org
#
# Maintainer:  Adrin Chaves Fernndez <adriyetichaves@gmail.com>
# Contributor: Bartomiej Piotrowski <barthalion@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Marcel Wysocki <maci@satgnu.net>
# Contributor: coolkehon <coolkehon at g m a i l>

pkgname=libircclient
pkgver=1.9
pkgrel=1
pkgdesc="A small but powerful library, which implements client-server IRC protocol."
arch=('i686' 'x86_64')
url="http://www.ulduzsoft.com/libircclient/"
depends=('glibc')
license=('GPL')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('783c48fe9153ed55a5565c818a178d67')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Add fPIC flag for x86_64.
  [ "$CARCH" = x86_64 ] && export CFLAGS="$CFLAGS -fPIC"

  ## Makefile.in fixes:
  #  invalid `lib` requisite for `install` target
  #  install shared lib instead of static
  #  headers in /usr/include/$pkgname
  sed -e "/install/s/lib/all/" \
      -e "/DESTDIR/s/\.a/\.so/g" \
      -e "s/@\/include/&\/$pkgname/" \
      -i src/Makefile.in

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-shared

  cd src &&  make CFLAGS="$CFLAGS"
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"

  make DESTDIR="$pkgdir/" install
}
