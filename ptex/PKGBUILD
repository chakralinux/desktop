pkgname=ptex
pkgver=2.1.28
pkgrel=1
pkgdesc="Per-Face Texture Mapping for Production Rendering"
arch=(i686 x86_64)
url="http://ptex.us/"
license=('BSD')
depends=(zlib)
makedepends=(git cmake doxygen python2)
options=(!makeflags)
source=("https://github.com/wdas/ptex/archive/v${pkgver}.tar.gz")
md5sums=('ce4eb665f686f8391968fa137113bc69')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i "s/python/python2/g" src/tests/run_tests.py
  mkdir -p build
}

build() {
  cd "$pkgname-$pkgver"/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib

  make
  make doc
}

check() {
  cd "$pkgname-$pkgver"/build
  make test
}

package() {
  cd "$pkgname-$pkgver"/build

  make DESTDIR="$pkgdir" install
  # install -d ${pkgdir}/usr/share
  #
  # cp -r install/{bin,include,lib} ${pkgdir}/usr
  # cp -r install/doc ${pkgdir}/usr/share/
  #
  cd ..
  install -Dm644 src/doc/License.txt ${pkgdir}/usr/share/licenses/${pkgname}/License.txt
}
