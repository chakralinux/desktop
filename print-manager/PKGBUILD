# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdeutils-print-manager/
# Include global configuration

pkgname=print-manager
pkgver=18.12.1
pkgrel=1
pkgdesc="A KDE tool for managing print jobs and printers"
url="https://projects.kde.org/projects/playground/base/print-manager"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libcups' 'kcmutils' 'plasma-framework')
makedepends=('extra-cmake-modules' 'python3' 'kdoctools' 'qt5-tools')
optdepends=('system-config-printer: auto-detect the printer driver')
groups=("kde" "kdeadmin" "kde-uninstall")
replaces=('kdeadmin-print-manager')
conflicts=('kdeadmin-print-manager')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b9c39b1ea916e83c66a666f7ee94dc19286f9977a1b9da15c7398bf8442a7306'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
