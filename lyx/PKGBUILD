#
# Apps Packages for Chakra, part of chakra-project.org
#
# Maintainer: Ernesto Manriquez <alejandronova@gmail.com>

pkgname=lyx
pkgver=2.0.7
pkgrel=1
pkgdesc="An advanced open-source document processor"
arch=('x86_64')
url="http://www.lyx.org"
depends=('qt' 'texlive-core' 'python2' 'perl' 'imagemagick' 'aspell' 'boost-libs' 'icu')
makedepends=('boost')
optdepends=('texlive-most: TeX Live goodies')
categories=('office')
license=('GPL')
source=("ftp://ftp.lyx.org/pub/lyx/stable/2.0.x/$pkgname-$pkgver.tar.xz" 'lyx.desktop')
screenshot=('http://www.lyx.org/images/about/main_window.png')
md5sums=('e5e16f3a4d2133c671caf5bf0f8857e7'
         'a511db621c56ff56383496533db3701a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --with-frontend=qt4 --without-included-boost
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install desktop entry
  install -Dm644 "${srcdir}/lyx.desktop" "${pkgdir}/usr/share/applications/lyx.desktop"
}
