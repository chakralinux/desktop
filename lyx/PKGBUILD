pkgname=lyx
pkgver=2.1.4
pkgrel=2
pkgdesc="An advanced open-source document processor"
arch=('x86_64')
url="http://www.lyx.org"
screenshot=('http://www.lyx.org/images/about/main_window.png')
depends=('qt' 'texlive-core' 'python2' 'perl' 'imagemagick' 'enchant' 'libmythes' 'boost-libs' 'icu')
makedepends=('boost' 'bc')
optdepends=('texlive-most: TeX Live goodies'
            'kbibtex: BibTeX reference manager')
license=('GPL')
source=("ftp://ftp.lyx.org/pub/lyx/stable/2.1.x/$pkgname-$pkgver.tar.xz"
        'lyx.desktop'
        'lyxrc.dist')
md5sums=('33f6906e5447a193b27c7231986e9f46'
         '3827d343670a38d97bdc7e002ff1e4a1'
         'b70fdeab95ba4a6c3ff72410dfa61823')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --with-frontend=qt4 \
              --without-included-boost \
              --without-included-mythes
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install desktop entry
  install -Dm644 "${srcdir}/lyx.desktop" "${pkgdir}/usr/share/applications/lyx.desktop"
  install -Dm644 "lib/images/lyx.png" "${pkgdir}/usr/share/pixmaps/lyx.png"
  
  # install default config file
  install -Dm644 "${srcdir}/lyxrc.dist" "${pkgdir}/usr/share/lyx/lyxrc.dist"
}
