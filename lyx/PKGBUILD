#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

pkgname=lyx
pkgver=2.0.5.1
pkgrel=2
pkgdesc="An advanced open-source document processor."
arch=('x86_64')
url="http://www.lyx.org"
depends=('qt' 'texlive-core' 'python2' 'perl' 'imagemagick' 'aspell' 'boost-libs' 'icu')
makedepends=('boost')
optdepends=('texlive-latex3: pdf export')
categories=('office')
license=('GPL')
source=("ftp://ftp.lyx.org/pub/lyx/stable/2.0.x/$pkgname-$pkgver.tar.xz" \
	'lyx.desktop')
screenshot=('http://www.lyx.org/images/about/main_window.png')
md5sums=('6db0968c9491d0730e0b957984343699'
         '0a7a76d3d9682f8d18ef0ed002b6d6e8')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --with-frontend=qt4 --without-included-boost
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install desktop entry
  install -Dm644 "${srcdir}/lyx.desktop" "${pkgdir}/usr/share/applications/lyx.desktop"
}
