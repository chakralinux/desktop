pkgname=lyx
pkgver=2.2.1
pkgrel=1
pkgdesc="An advanced open-source document processor"
arch=('x86_64')
url="http://www.lyx.org"
screenshot=('http://www.lyx.org/images/about/main_window.png')
depends=('qt5-base' 'qt5-svg' 'texlive-core' 'python2' 'perl' 'imagemagick' 'enchant' 'libmythes' 'boost-libs' 'icu')
makedepends=('boost' 'bc')
optdepends=('texlive-most: TeX Live goodies'
            'kbibtex: BibTeX reference manager')
license=('GPL')
source=("ftp://ftp.lyx.org/pub/lyx/stable/2.2.x/$pkgname-$pkgver.tar.xz"
        'lyx.desktop'
        'lyxrc.dist')
md5sums=('7805b9943eb386a9c27ab0a7541fca48'
         '3827d343670a38d97bdc7e002ff1e4a1'
         'b70fdeab95ba4a6c3ff72410dfa61823')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --enable-qt5 \
              --without-included-boost \
              --without-included-mythes
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install desktop entry
  install -Dm644 "${srcdir}/lyx.desktop" "${pkgdir}/usr/share/applications/lyx.desktop"
  install -Dm644 "lib/images/lyx.png" "${pkgdir}/usr/share/pixmaps/lyx.png"
  
  # install default config file
  install -Dm644 "${srcdir}/lyxrc.dist" "${pkgdir}/usr/share/lyx/lyxrc.dist"
}
