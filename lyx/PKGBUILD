pkgname=lyx
pkgver=2.3.1
pkgrel=1
pkgdesc="An advanced open-source document processor"
arch=('x86_64')
url="http://www.lyx.org"
screenshot=('http://www.lyx.org/images/about/main_window.png')
depends=('qt5-base' 'qt5-svg' 'texlive-core' 'python2' 'perl' 'imagemagick' 'enchant' 'libmythes' 'boost-libs' 'icu' 'hunspell')
makedepends=('boost' 'bc')
optdepends=('texlive-most: TeX Live goodies'
            'kbibtex: BibTeX reference manager'
            'python3: support for python scripts')
license=('GPL')
source=("ftp://ftp.lip6.fr/pub/lyx/stable/2.3.x/$pkgname-$pkgver-2.tar.xz"
        'lyxrc.dist')
md5sums=('b09bea675883779b268c4d5e1f5773d8'
         'b70fdeab95ba4a6c3ff72410dfa61823')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  export CXXFLAGS="$CXXFLAGS -fpermissive"
  ./configure --prefix=/usr \
              --enable-qt5 \
              --without-included-boost \
              --without-included-mythes
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # install default config file
  install -Dm644 "${srcdir}/lyxrc.dist" "${pkgdir}/etc/lyx/lyxrc.dist"
  ln -sf /etc/lyx/lyxrc.dist "${pkgdir}/usr/share/lyx/lyxrc.dist"
}
