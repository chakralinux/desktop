# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegames-kjumpingcube/
# Include global configuration

pkgname=kjumpingcube
pkgver=18.12.2
pkgrel=1
pkgdesc="Territory-capture game"
arch=('x86_64')
url='http://kde.org/applications/games/kjumpingcube/'
screenshot='https://www.kde.org/images/screenshots/kjumpingcube.png'
license=('GPL' 'LGPL' 'FDL')  
depends=('knotifyconfig' 'libkdegames' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools' 'python3')
groups=('kde' 'kde-uninstall' 'kde-applications' 'kdegames')
replaces=('kdegames-kjumpingcube')
conflicts=('kdegames-kjumpingcube')     
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a35c2de93a7fe3c1f8e3c82dd4f6cf82df00e4e69745e506a7f87e4a1b429292'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
