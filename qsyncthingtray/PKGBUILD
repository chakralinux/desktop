# Arch contribution https://aur4.archlinux.org/packages/qsyncthingtray/

pkgname=qsyncthingtray
pkgver=0.5.2
pkgrel=1
pkgdesc="Tray app for syncthing"
arch=('x86_64')
url="https://github.com/sieren/QSyncthingTray"
license=('LGPL2.1')
depends=('qt5-webengine' 'qt5-webchannel' 'qt5-location' 'syncthing')
makedepends=('cmake')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/sieren/QSyncthingTray/archive/$pkgver.tar.gz"
        "$pkgname.desktop")
sha1sums=('bb25aa7301461d1d1ba786a028212630b02a2446'
          'bfd5a49e55a34cf2f45641797a8acdd45122042d')


_pkgname=QSyncthingTray

build() {
  cd "$_pkgname-$pkgver"
  mkdir -p build
  cd build
  QTDIR=/usr/lib/qt5 cmake ..
  make
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm755 "build/$_pkgname" "$pkgdir/usr/bin/$_pkgname"

  # install .desktop file
  install -Dm755 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm755 "resources/images/Icon1024.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
