# Maintainer: Daniele Cocca <jmc@chakra-project.org>

_pkgname=ZynAddSubFX
pkgname=zynaddsubfx
pkgver=2.4.1
pkgrel=2
pkgdesc="ZynAddSubFX is a open source software synthesizer capable of making a countless number of instruments, from some common heard from expensive hardware to interesting sounds that you'll boost to an amazing universe of sounds."
arch=('i686' 'x86_64')
url="http://zynaddsubfx.sourceforge.net/"
license=('GPL2')
depends=('fftw' 'mxml' 'zlib' 'fltk')
makedepends=('oss' 'jack')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.bz2"
        'gentoo-zynaddsubfx-2.4.1-docs.patch'
        'gentoo-zynaddsubfx-2.4.1-fltk.patch'
        'gentoo-zynaddsubfx-2.4.1-fltk13.patch')
md5sums=('59eb69ce24d6f8c605f8ba43958d0526'
         '1cc79aea2033fab472f35a5fec60167e'
         'eb95d339ff5deac8c6b54965f04a8c60'
         '49464a33ab9c4250520eda0df7705663')

build() {
  # Patch it first
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/gentoo-zynaddsubfx-2.4.1-docs.patch"
  patch -Np1 -i "${srcdir}/gentoo-zynaddsubfx-2.4.1-fltk.patch"
  patch -Np1 -i "${srcdir}/gentoo-zynaddsubfx-2.4.1-fltk13.patch"

  # Then, prepare the build directory
  cd "${srcdir}"
  
  [ ! -d build ] && mkdir -p build

  # Then, actually build it
  cd build
  
  cmake "${srcdir}/${_pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DGuiModule=fltk
  
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
