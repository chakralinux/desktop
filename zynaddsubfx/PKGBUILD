# Maintainer: Daniele Cocca <jmc@chakra-project.org>
# Contributor: Ernesto Manr√≠quez <alejandronova@gmail.com>

_pkgname=ZynAddSubFX
pkgname=zynaddsubfx
pkgver=2.4.3
pkgrel=1
pkgdesc="Open source additive and substractive software synthesizer, with an integrated effect engine."
arch=('x86_64')
url="http://zynaddsubfx.sourceforge.net/"
license=('GPL2')
depends=('fftw' 'mxml' 'zlib' 'fltk' 'lash' 'dssi')
makedepends=('oss' 'jack')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.bz2"
        'gentoo-zynaddsubfx-2.4.1-docs.patch')
categories=('multimedia')
sha256sums=('b84b595660ccec3ebc364c29c2d29def7d28a6947fc1dfb26bd8c34ca0e7f54d'
            '379c61a8f911d4794f3369f5b12b46142b46fb771d2cd2877e220c33e6891ec2')

build() {
  # Patch it first
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/gentoo-zynaddsubfx-2.4.1-docs.patch"

  # Then, prepare the build directory
  cd "${srcdir}"
  
  [ ! -d build ] && mkdir -p build

  # Then, actually build it
  cd build
  
  cmake "${srcdir}/${_pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DGuiModule=fltk \
        -DDssiEnable=true
  
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  # Also copy the data files...
  install -d -m755 "${pkgdir}/usr/share/${pkgname}"
  cp -a "${srcdir}/${_pkgname}-${pkgver}/instruments/banks" "${pkgdir}/usr/share/${pkgname}"
  cp -a "${srcdir}/${_pkgname}-${pkgver}/instruments/examples" "${pkgdir}/usr/share/${pkgname}"
}

# vim:set ts=2 sw=2 et:
