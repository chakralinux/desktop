pkgname=zynaddsubfx
pkgver=3.0.1
pkgrel=1
pkgdesc="Open source additive and substractive software synthesizer, with an integrated effect engine."
arch=('x86_64')
url="http://zynaddsubfx.sourceforge.net/"
license=('GPL2')
depends=('fftw' 'mxml' 'jack' 'fltk' 'lash' 'dssi' 'fftw' 'libxpm' 'gcc-libs' 'portaudio' 'liblo')
makedepends=('mesa' 'ladspa' 'cmake' 'gendesk' 'setconf')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
categories=('multimedia')
sha256sums=('a8017f382be5af9e46e6e8771fb7d7c1fd2e480cf099bbb0c29606b217dd32e2')

prepare() {
  cd "$srcdir"
  gendesk -f $startdir/PKGBUILD
  setconf "$pkgname.desktop" Icon "$pkgname.svg"
  
  cd "zynaddsubfx-$pkgver"
  [[ -e build ]] && rm -rf build
  mkdir -p build
}

build() {
  cd "$srcdir/zynaddsubfx-$pkgver/build"
  
  cmake "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DDefaultOutput=jack \
        -DDefaultInput=jack \
        -DGuiModule=fltk \
        -DDssiEnable=true
  make

  # build external programs
  cd ${srcdir}/${pkgname}-${pkgver}/ExternalPrograms/Spliter && make
  cd ../Controller && sed -i "s|Box\.h|Box\.H|" ControllerUI.fl && make
}

package() {
  cd "$srcdir/zynaddsubfx-$pkgver/build"
  
  make DESTDIR="${pkgdir}" install

  
  # install external programs
  install -Dm755 ../ExternalPrograms/Spliter/spliter "$pkgdir/usr/bin/spliter"
  install -Dm755 ../ExternalPrograms/Controller/controller "$pkgdir/usr/bin/controller"
  install -Dm644 ../ExternalPrograms/Spliter/readme.txt "$pkgdir/usr/share/doc/$pkgname/SPLITER.txt"

  # banks and examples
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a ../instruments/banks "$pkgdir/usr/share/$pkgname"
  cp -a ../instruments/examples "$pkgdir/usr/share/$pkgname"

  # desktop file and icon
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/$pkgname.svg" "$pkgdir/usr/share/icons/$pkgname.svg"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
