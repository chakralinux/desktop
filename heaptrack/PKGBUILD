# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=heaptrack
pkgver=1.1.0
pkgrel=1
arch=(x86_64)
pkgdesc="A heap memory profiler for Linux"
url="http://milianw.de/tag/heaptrack"
license=(GPL)
depends=(kdiagram threadweaver kitemmodels kio boost-libs hicolor-icon-theme)
makedepends=(extra-cmake-modules boost sparsehash)
source=(http://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('120f8b276f772aa3409fc3d628dbe7c4fa9d0d98cd710982b6007e6113e13f3b'
            'SKIP')
validpgpkeys=(3D8568869097C65C98F8D2760C0EB212CD1D1393
              C51B45A532F17FA401D099A0A0C6B72C4F1C5E7C) # Milian Wolff <mail@milianw.de>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DBUILD_TESTING=OFF
  make
}

package(){
  cd build
  make DESTDIR="$pkgdir" install
}

