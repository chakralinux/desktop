#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=qtcreator
pkgver=3.0.0
pkgrel=1
pkgdesc="Lightweight, cross-platform integrated development environment."
arch=('x86_64')
url="http://qt.digia.com/"
license=('LGPL')
makedepends=('qt-private-headers>=4.8.4' 'git')
depends=('qt>=4.8.4')
optdepends=('qt-docs: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project suppport'
            'ksshaskpass: for ssh support'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support'
            'valgrind: for analyze support')
categories=('programming')
screenshot=("http://qt.nokia.com/images/products/qt-creator-breakdown-screenshot")
options=(docs)
#source=("http://download.qt-project.org/official_releases/qtcreator/2.8/${pkgver}/qt-creator-${pkgver}-src.tar.gz"
source=("http://ftp.fau.de/qtproject/official_releases/${pkgname}/3.0/${pkgver}/qt-creator-opensource-src-${pkgver}.tar.gz"
        'qtcreator.desktop')
sha256sums=('db14c3706803917c8fb8aa578a67e8d5e2bcfabeba968f7669141df795410d99'
            '1f70efa02ea457a4df21243d3e8d68b6c66ad194400beb1f26f954536d4e7c90')
        

build() {
  cd "${srcdir}"
  [ -d build ] && rm -r build
  mkdir build
  cd build

  qmake "${srcdir}/qt-creator-opensource-src-${pkgver}/qtcreator.pro"
  make
  make docs -j1
}

package() {
  cd "${srcdir}/build"

  make INSTALL_ROOT="${pkgdir}/usr" install
  make INSTALL_ROOT="${pkgdir}/usr/" install_docs

  install -Dm644 "${srcdir}/qtcreator.desktop" \
    "${pkgdir}/usr/share/applications/qtcreator.desktop"
  install -Dm644 "${srcdir}/qt-creator-opensource-src-${pkgver}/LGPL_EXCEPTION.TXT" \
    "${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT"
}
