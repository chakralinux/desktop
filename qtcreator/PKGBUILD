#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=qtcreator
pkgver=2.6.0
pkgrel=1
_qtver=4.8.3
pkgdesc="Lightweight, cross-platform integrated development environment."
arch=('i686' 'x86_64')
url="http://trolltech.com/developer/qt-creator"
license=('LGPL')
makedepends=('qt-private-headers>=4.8.3' 'git')
depends=('qt>=4.8.3')
optdepends=('qt-docs: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project suppport'
            'ksshaskpass: for ssh support'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support')
categories=('programming')
screenshot=("http://qt.nokia.com/images/products/qt-creator-breakdown-screenshot")
options=(docs)
source=("http://releases.qt-project.org/qtcreator/${pkgver}/qt-creator-${pkgver}-src.tar.gz"
        qtcreator.desktop)
md5sums=('9bf01098f84a0fe930b2718d11124204'
         '457956849ca2658072a6ce4c56ed1b36')
         

build() {
  cd "${srcdir}"
  [ -d build ] && rm -r build
  mkdir build
  cd build

  qmake "${srcdir}/qt-creator-${pkgver}-src/qtcreator.pro"
  make 
}

package() {
  cd "${srcdir}/build"

  make INSTALL_ROOT="${pkgdir}/usr" install

  install -Dm644 "${srcdir}/qtcreator.desktop" \
    "${pkgdir}/usr/share/applications/qtcreator.desktop"
  install -Dm644 "${srcdir}/qt-creator-${pkgver}-src/LGPL_EXCEPTION.TXT" \
    "${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT"
}
