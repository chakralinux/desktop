# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/kdegraphics-thumbnailers/
# Include global configuration

pkgname=kdegraphics-thumbnailers
pkgver=18.04.3
pkgrel=1
pkgdesc='Thumbnailers for various graphics file formats'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdegraphics/kdegraphics-thumbnailers'
license=(GPL LGPL FDL)
depends=(libkexiv2 libkdcraw kio)
makedepends=(extra-cmake-modules)
groups=('kde' 'kdegraphics' 'kde-uninstall')
options=('docs' 'debug')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('aae09cd604c0a18ceca00555254972d4816e00bd6c427531ed7a447860750ddd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
    make
}

package() {
    cd build
    make DESTDIR=${pkgdir} install
}
