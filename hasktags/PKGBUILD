# Apps Packages for Chakra, part of www.chakra-project.org
# Maintainer: H W Tovetjärn (totte) <totte@tott.es>
# Contributor: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>

pkgname=hasktags
pkgver=0.68.2
pkgrel=1
pkgdesc='Produces ctags “tags” and etags “TAGS” files for Haskell software'
url='http://hackage.haskell.org/package/$pkgname'
license=(BSD)
arch=(i686 x86_64)
makedepends=(ghc)
source=(http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('9d3fcaad59c34d15c92051c74accf5ac')

build() {
    cd $srcdir/$pkgname-$pkgver
    runhaskell Setup configure \
        -O \
        --prefix=/usr
    runhaskell Setup build
}

package() {
    cd $srcdir/$pkgname-$pkgver
    runhaskell Setup copy \
        --destdir=$pkgdir

    # Documentation
    install -Dm644 README \
        $pkgdir/usr/share/doc/$pkgname/usage.txt
    rm -R $pkgdir/usr/share/doc/$pkgname-$pkgver

    # License
    install -Dm644 LICENSE \
        $pkgdir/usr/share/licenses/$pkgname/license.txt
}
