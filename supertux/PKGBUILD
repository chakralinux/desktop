pkgname=supertux
pkgver=0.5.1
pkgrel=1
pkgdesc="A classic 2D jump'n run sidescroller game in a style similar to the original SuperMario games"
arch=('x86_64')
url="http://super-tux.sourceforge.net/"
license=('GPL')
depends=('sdl2_image' 'curl' 'openal' 'libvorbis' 'glew')
makedepends=('cmake' 'boost' 'mesa' 'optipng' 'git')
source=(https://github.com/SuperTux/$pkgname/releases/download/v${pkgver}/SuperTux-v${pkgver}-Source.tar.gz)
sha1sums=('f2a40ed73ca81951e17e23edc914e2ed480f9ca1')

build() {
  cd "SuperTux-v${pkgver}-Source"

  cmake \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DINSTALL_SUBDIR_BIN=bin \
  -DUSE_SYSTEM_PHYSFS=0
  make
}

package() {
  cd "SuperTux-v${pkgver}-Source"

  make DESTDIR="${pkgdir}" install
}
